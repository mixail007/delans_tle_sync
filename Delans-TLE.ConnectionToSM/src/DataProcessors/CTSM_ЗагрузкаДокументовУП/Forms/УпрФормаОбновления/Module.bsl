// Процедура - обработчик события формы "ПриОткрытии"
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	ЕстьОбновление = Ложь;
	
	СтрокаЗапроса = "/adl42/hs/api_v1/DocLoaderFiles/GetUpdatesTab?Version=" + ВладелецФормы.ВерсияПрограммы;
	ПараметрыОтвета = ВладелецФормы.ВыполнитьЗапрос("ESDLADLHTTP", СтрокаЗапроса);
	Если ПараметрыОтвета.КодОтвета = 200 Тогда
		МассивСтруктурСтрокТаблицаОбновляемыхФайлов = ПараметрыОтвета.СтруктураОтвета.TableUpdateFiles;  
		Если МассивСтруктурСтрокТаблицаОбновляемыхФайлов <> "" И МассивСтруктурСтрокТаблицаОбновляемыхФайлов.Количество() > 0 Тогда
			ЕстьОбновление = Истина;
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЕстьОбновление Тогда
		Элементы.Декорация1.Заголовок = "Проверить на наличие обновлений";
		Элементы.Декорация2.Заголовок = "Установленная версия программы " + ВладелецФормы.ИмяПрограммы + " является самой последней.";
		Элементы.Загрузить.Видимость = Ложь;
		Элементы.Закрыть.Заголовок = "ОК";
		Элементы.Закрыть.КнопкаПоУмолчанию = Истина;
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик команды формы "Загрузить"
//
&НаКлиенте
Процедура Загрузить(Команда)
	
	СтрокаЗапроса = "/adl42/hs/api_v1/DocLoaderFiles/GetTheUpdateFile?DocLoaderFileID=DocLoader";
	ПараметрыОтвета = ВладелецФормы.ВыполнитьЗапрос("ESDLADLHTTP", СтрокаЗапроса,,,,Истина);
	Если ПараметрыОтвета.КодОтвета = 200 Тогда
		ДопПараметры = Новый Структура("filename, ДвоичныеДанные", ПараметрыОтвета.СтруктураПараметровContentDisposition.filename, ПараметрыОтвета.СтруктураОтвета.ДвоичныеДанные);
		Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = "Выберите папку для сохранения";
		ОписаниеОповещения = Новый ОписаниеОповещения("ПапкаОбработкаВыбора", ЭтаФорма, ДопПараметры);
		ДиалогОткрытияФайла.Показать(ОписаниеОповещения);
	Иначе
		Элементы.Декорация1.Заголовок = "Проверить на наличие обновлений";
		Элементы.Декорация2.Заголовок = "Ошибка при получении файла обновления: " + ПараметрыОтвета.СтруктураОтвета.Description;
		Элементы.Загрузить.Видимость = Ложь;
		Элементы.Закрыть.Заголовок = "Закрыть";
		Элементы.Закрыть.КнопкаПоУмолчанию = Истина;
	КонецЕсли;

КонецПроцедуры

// Процедура обработки оповещения при загрузке приложения
//
&НаКлиенте
Процедура ПапкаОбработкаВыбора(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		ПолныйПутьКФайлу = ВыбранныеФайлы[0] + "\" + ДопПараметры.filename;
		Файл = Новый Файл(ПолныйПутьКФайлу);
		ИмяБезРасширения = Файл.ИмяБезРасширения;
		Число = 1;
		Пока Файл.Существует() Цикл
			ПолныйПутьКФайлу = Файл.Путь + "\" + ИмяБезРасширения + "(" + Число + ").exe";
			Файл = Новый Файл(ПолныйПутьКФайлу);
			Число = Число + 1;
		КонецЦикла;
		ДопПараметры.ДвоичныеДанные.Записать(ПолныйПутьКФайлу);	
		Элементы.Декорация1.Заголовок = "Новая версия программы " + ВладелецФормы.ИмяПрограммы + " готова к установке";
		Элементы.Декорация2.Заголовок = "Внимание! Для установки новой версии требуются права администратора.";
		Элементы.Загрузить.Видимость = Ложь;
		//Элементы.Установить.Видимость = Истина;
		//Элементы.Установить.КнопкаПоУмолчанию = Истина;
		Элементы.Закрыть.Заголовок = "Ок";
	КонецЕсли;
	
КонецПроцедуры



// Процедура - обработчик события формы "ОбработкаОповещения"
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УпрОсновнаяФормаБудетЗакрыта"  И ЭтаФорма.Открыта() Тогда
		
		ЭтаФорма.Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры
