// Механизм расчета статусов оформления документов ВЕТИС.
//

#Область ПрограммныйИнтерфейс

// Позволяет переопределить имена реквизитов документа-основания для документа ВЕТИС.
//
// Параметры:
//	МетаданныеДокументаОснования - ОбъектМетаданных:Документ - метаданные документа из ОпределяемыйТип.Основание<Имя документа ВЕТИС>
//	МетаданныеДокументаВЕТИС 	 - ОбъектМетаданных:Документ - метаданные документа из ОпределяемыйТип.ДокументыВЕТИСПоддерживающиеСтатусыОформления
//	Реквизиты 					 - Структура - имена реквизитов
//		Ключ 	 - служебное имя реквизита в ВЕТИС
//		Значение - имя реквизита документа-основания, которое при необходимости надо переопределить
//			Подробнее см. описание РасчетСтатусовОформленияВЕТИС.СтруктураРеквизитовДляРасчетаСтатусаОформленияДокументовВЕТИС	
//
Процедура ПриОпределенииИменРеквизитовДокументаДляРасчетаСтатусаОформленияДокументаВЕТИС(
			Знач МетаданныеДокументаОснования, Знач МетаданныеДокументаВЕТИС, Реквизиты) Экспорт
	
	// Зададим имена реквизитов по умолчанию.
	Реквизиты.Контрагент             			= "Организация";
	Реквизиты.ТорговыйИлиПроизводственныйОбъект = "СтруктурнаяЕдиница";
	
	// Определим имя реквизита "Ответственный".
	Если МетаданныеДокументаОснования.Реквизиты.Найти("Ответственный") <> Неопределено Тогда
		Реквизиты.Ответственный = "Ответственный";
	Иначе
		Реквизиты.Ответственный = "Автор";
	КонецЕсли;
		
	// Уточним имена реквизитов для конкретных документов.
	Если МетаданныеДокументаОснования = Метаданные.Документы.ПеремещениеЗапасов Тогда
		
		Если МетаданныеДокументаВЕТИС = Метаданные.Документы.ИсходящаяТранспортнаяОперацияВЕТИС Тогда
			
			Реквизиты.ТорговыйИлиПроизводственныйОбъект = "СтруктурнаяЕдиница";
			
		ИначеЕсли МетаданныеДокументаВЕТИС = Метаданные.Документы.ВходящаяТранспортнаяОперацияВЕТИС Тогда
			
			Реквизиты.ТорговыйИлиПроизводственныйОбъект = "СтруктурнаяЕдиницаПолучатель";
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ЗаказПокупателя Тогда
		
		Реквизиты.ТорговыйИлиПроизводственныйОбъект = "СтруктурнаяЕдиницаПродажи";
		
	КонецЕсли;
	
КонецПроцедуры

// Позволяет переопределить текст запроса выборки данных из документов-основания для расчета статуса оформления.
//
// Параметры:
//	МетаданныеДокументаОснования - ОбъектМетаданных:Документ - метаданные документа из ОпределяемыйТип.Основание<Имя документа ВЕТИС>
//	МетаданныеДокументаВЕТИС 	 - ОбъектМетаданных:Документ - метаданные документа из ОпределяемыйТип.ДокументыВЕТИСПоддерживающиеСтатусыОформления
//	ТекстЗапроса 				 - Строка - текст запроса выборки данных, который надо переопределить
//		Требования к тексту запроса:
//		- результат запроса обязательно должен содержать следующие поля:
//			- Ссылка 			- ОпределяемыйТип.Основание<Имя документа ВЕТИС> - ссылка на документ-основание
//			- ЭтоДвижениеПриход - Булево - вид движения ТМЦ (Истина - приход, Ложь - расход)
//			- Номенклатура 		- ОпределяемыйТип.Номенклатура - номенклатура
//			- Характеристика 	- ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика номенклатуры
//			- Серия 			- ОпределяемыйТип.СерияНоменклатуры - серия номенклатуры
//			- Количество 		- Число - количество номенклатуры в ее основной единице измерения
//		- в результат запроса нужно включать только подконтрольную номенклатуру ВЕТИС
//		- для отбора документов-основания в запросе нужно использовать отбор "В (&МассивДокументов)"
//		- выбранные данные необходимо поместить во временную таблицу,
//			см. РасчетСтатусовОформленияВЕТИС.ИмяВременнойТаблицыДляВыборкиДанныхДокумента
//		Если данные из документа выбирать не требуется, то данному параметру надо присвоить значение "" (пустая строка).
//	ДополнительныеПараметрыЗапроса - Структура - дополнительные параметры запроса,
//	 	требуемые для выполнения запроса конкретного документа; при необходимости можно дополнить данную структуру
//		- Ключ 	   - имя параметры
//		- Значение - значение параметра
//
Процедура ПриОпределенииТекстаЗапросаДляРасчетаСтатусаОформленияДокументаВЕТИС(
			Знач МетаданныеДокументаОснования, Знач МетаданныеДокументаВЕТИС, ТекстЗапроса, ДополнительныеПараметрыЗапроса) Экспорт
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.ОприходованиеЗапасов Тогда
		
		Если МетаданныеДокументаВЕТИС = Метаданные.Документы.ИнвентаризацияПродукцииВЕТИС Тогда
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ОприходованиеЗапасовЗапасы.Ссылка КАК Ссылка,
			|	ИСТИНА КАК ЭтоДвижениеПриход,
			|	ОприходованиеЗапасовЗапасы.Номенклатура КАК Номенклатура,
			|	ОприходованиеЗапасовЗапасы.Характеристика КАК Характеристика,
			|	ОприходованиеЗапасовЗапасы.Партия КАК Серия,
			|	ОприходованиеЗапасовЗапасы.Количество КАК Количество
			|ПОМЕСТИТЬ %ВТ%
			|ИЗ
			|	Документ.ОприходованиеЗапасов.Запасы КАК ОприходованиеЗапасовЗапасы
			|ГДЕ
			|	ОприходованиеЗапасовЗапасы.Ссылка В (&МассивДокументов)
			|	И ОприходованиеЗапасовЗапасы.Номенклатура.ПодконтрольнаяПродукцияВЕТИС";
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПеремещениеЗапасов Тогда
		
		Если МетаданныеДокументаВЕТИС = Метаданные.Документы.ВходящаяТранспортнаяОперацияВЕТИС Тогда
			
			ТекстЗапроса = ""; // не формируем - будет получен через обмен
			
		ИначеЕсли МетаданныеДокументаВЕТИС = Метаданные.Документы.ИсходящаяТранспортнаяОперацияВЕТИС Тогда
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ПеремещениеЗапасовЗапасы.Ссылка КАК Ссылка,
			|	ЛОЖЬ КАК ЭтоДвижениеПриход,
			|	ПеремещениеЗапасовЗапасы.Номенклатура КАК Номенклатура,
			|	ПеремещениеЗапасовЗапасы.Характеристика КАК Характеристика,
			|	ПеремещениеЗапасовЗапасы.Партия КАК Серия,
			|	ПеремещениеЗапасовЗапасы.Количество КАК Количество
			|ПОМЕСТИТЬ %ВТ%
			|ИЗ
			|	Документ.ПеремещениеЗапасов.Запасы КАК ПеремещениеЗапасовЗапасы
			|ГДЕ
			|	ПеремещениеЗапасовЗапасы.Ссылка В (&МассивДокументов)
			|	И ПеремещениеЗапасовЗапасы.Номенклатура.ПодконтрольнаяПродукцияВЕТИС";
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПересортицаЗапасов Тогда
		
		Если МетаданныеДокументаВЕТИС = Метаданные.Документы.ИнвентаризацияПродукцииВЕТИС Тогда
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ПересортицаЗапасовЗапасы.Ссылка КАК Ссылка,
			|	ЛОЖЬ КАК ЭтоДвижениеПриход,
			|	ПересортицаЗапасовЗапасы.Номенклатура КАК Номенклатура,
			|	ПересортицаЗапасовЗапасы.Характеристика КАК Характеристика,
			|	ПересортицаЗапасовЗапасы.Партия КАК Серия,
			|	ПересортицаЗапасовЗапасы.Количество КАК Количество
			|ПОМЕСТИТЬ %ВТ%
			|ИЗ
			|	Документ.ПересортицаЗапасов.Запасы КАК ПересортицаЗапасовЗапасы
			|ГДЕ
			|	ПересортицаЗапасовЗапасы.Ссылка В (&МассивДокументов)
			|	И ПересортицаЗапасовЗапасы.Номенклатура.ПодконтрольнаяПродукцияВЕТИС
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПересортицаЗапасовЗапасы.Ссылка,
			|	ИСТИНА,
			|	ПересортицаЗапасовЗапасы.НоменклатураОприходование,
			|	ПересортицаЗапасовЗапасы.ХарактеристикаОприходование,
			|	ПересортицаЗапасовЗапасы.ПартияОприходование,
			|	ПересортицаЗапасовЗапасы.Количество
			|ИЗ
			|	Документ.ПересортицаЗапасов.Запасы КАК ПересортицаЗапасовЗапасы
			|ГДЕ
			|	ПересортицаЗапасовЗапасы.Ссылка В (&МассивДокументов)
			|	И ПересортицаЗапасовЗапасы.НоменклатураОприходование.ПодконтрольнаяПродукцияВЕТИС";
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.РасходнаяНакладная Тогда
		
		Если МетаданныеДокументаВЕТИС = Метаданные.Документы.ИсходящаяТранспортнаяОперацияВЕТИС Тогда
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	РасходнаяНакладнаяЗапасы.Ссылка КАК Ссылка,
			|	ЛОЖЬ КАК ЭтоДвижениеПриход,
			|	РасходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
			|	РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
			|	РасходнаяНакладнаяЗапасы.Партия КАК Серия,
			|	РасходнаяНакладнаяЗапасы.Количество КАК Количество
			|ПОМЕСТИТЬ %ВТ%
			|ИЗ
			|	Документ.РасходнаяНакладная.Запасы КАК РасходнаяНакладнаяЗапасы
			|ГДЕ
			|	РасходнаяНакладнаяЗапасы.Ссылка В (&МассивДокументов)
			|	И НЕ РасходнаяНакладнаяЗапасы.Ссылка.СпособПродажиГИСМ = ""роз""
			|	И РасходнаяНакладнаяЗапасы.Номенклатура.ПодконтрольнаяПродукцияВЕТИС";
			
		КонецЕсли;
		
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОтчетОПереработке Тогда
		
		Если МетаданныеДокументаВЕТИС = Метаданные.Документы.ИсходящаяТранспортнаяОперацияВЕТИС Тогда
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ОтчетОПереработкеПродукция.Ссылка КАК Ссылка,
			|	ЛОЖЬ КАК ЭтоДвижениеПриход,
			|	ОтчетОПереработкеПродукция.Номенклатура КАК Номенклатура,
			|	ОтчетОПереработкеПродукция.Характеристика КАК Характеристика,
			|	ОтчетОПереработкеПродукция.Партия КАК Серия,
			|	ОтчетОПереработкеПродукция.Количество КАК Количество
			|ПОМЕСТИТЬ %ВТ%
			|ИЗ
			|	Документ.ОтчетОПереработке.Продукция КАК ОтчетОПереработкеПродукция
			|ГДЕ
			|	ОтчетОПереработкеПродукция.Ссылка В (&МассивДокументов)
			|	И ОтчетОПереработкеПродукция.Номенклатура.ПодконтрольнаяПродукцияВЕТИС";
			
		КонецЕсли;

	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ЗаказПокупателя Тогда
		
		Если МетаданныеДокументаВЕТИС = Метаданные.Документы.ИсходящаяТранспортнаяОперацияВЕТИС Тогда
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ЗаказПокупателяЗапасы.Ссылка КАК Ссылка,
			|	ЛОЖЬ КАК ЭтоДвижениеПриход,
			|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
			|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
			|	ЗаказПокупателяЗапасы.Партия КАК Серия,
			|	ЗаказПокупателяЗапасы.Количество КАК Количество
			|ПОМЕСТИТЬ %ВТ%
			|ИЗ
			|	Документ.ЗаказПокупателя.Запасы КАК ЗаказПокупателяЗапасы
			|ГДЕ
			|	ЗаказПокупателяЗапасы.Ссылка В (&МассивДокументов)
			|	И ЗаказПокупателяЗапасы.Номенклатура.ПодконтрольнаяПродукцияВЕТИС
			|	И ЗаказПокупателяЗапасы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)";
			
		КонецЕсли;
			
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.СписаниеЗапасов Тогда
		
		Если МетаданныеДокументаВЕТИС = Метаданные.Документы.ИнвентаризацияПродукцииВЕТИС Тогда
			
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	СписаниеЗапасовЗапасы.Ссылка КАК Ссылка,
			|	ЛОЖЬ КАК ЭтоДвижениеПриход,
			|	СписаниеЗапасовЗапасы.Номенклатура КАК Номенклатура,
			|	СписаниеЗапасовЗапасы.Характеристика КАК Характеристика,
			|	СписаниеЗапасовЗапасы.Партия КАК Серия,
			|	СписаниеЗапасовЗапасы.Количество КАК Количество
			|ПОМЕСТИТЬ %ВТ%
			|ИЗ
			|	Документ.СписаниеЗапасов.Запасы КАК СписаниеЗапасовЗапасы
			|ГДЕ
			|	СписаниеЗапасовЗапасы.Ссылка В (&МассивДокументов)
			|	И СписаниеЗапасовЗапасы.Номенклатура.ПодконтрольнаяПродукцияВЕТИС";
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.СборкаЗапасов Тогда
		
		Если МетаданныеДокументаВЕТИС = Метаданные.Документы.ПроизводственнаяОперацияВЕТИС Тогда
			
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	СборкаЗапасовПродукция.Ссылка КАК Ссылка,
			|	ИСТИНА КАК ЭтоДвижениеПриход,
			|	СборкаЗапасовПродукция.Номенклатура КАК Номенклатура,
			|	СборкаЗапасовПродукция.Характеристика КАК Характеристика,
			|	СборкаЗапасовПродукция.Партия КАК Серия,
			|	СборкаЗапасовПродукция.Количество КАК Количество
			|ПОМЕСТИТЬ %ВТ%
			|ИЗ
			|	Документ.СборкаЗапасов.Продукция КАК СборкаЗапасовПродукция
			|ГДЕ
			|	СборкаЗапасовПродукция.Ссылка В (&МассивДокументов)
			|	И СборкаЗапасовПродукция.Номенклатура.ПодконтрольнаяПродукцияВЕТИС
			|	И СборкаЗапасовПродукция.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСборкаЗапасов.Сборка)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СборкаЗапасовЗапасы.Ссылка,
			|	ЛОЖЬ,
			|	СборкаЗапасовЗапасы.Номенклатура,
			|	СборкаЗапасовЗапасы.Характеристика,
			|	СборкаЗапасовЗапасы.Партия,
			|	СборкаЗапасовЗапасы.Количество
			|ИЗ
			|	Документ.СборкаЗапасов.Запасы КАК СборкаЗапасовЗапасы
			|ГДЕ
			|	СборкаЗапасовЗапасы.Ссылка В (&МассивДокументов)
			|	И СборкаЗапасовЗапасы.Номенклатура.ПодконтрольнаяПродукцияВЕТИС
			|	И СборкаЗапасовЗапасы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСборкаЗапасов.Сборка)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СборкаЗапасовПродукция.Ссылка,
			|	ЛОЖЬ,
			|	СборкаЗапасовПродукция.Номенклатура,
			|	СборкаЗапасовПродукция.Характеристика,
			|	СборкаЗапасовПродукция.Партия,
			|	СборкаЗапасовПродукция.Количество
			|ИЗ
			|	Документ.СборкаЗапасов.Продукция КАК СборкаЗапасовПродукция
			|ГДЕ
			|	СборкаЗапасовПродукция.Ссылка В (&МассивДокументов)
			|	И СборкаЗапасовПродукция.Номенклатура.ПодконтрольнаяПродукцияВЕТИС
			|	И СборкаЗапасовПродукция.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСборкаЗапасов.Разборка)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СборкаЗапасовЗапасы.Ссылка,
			|	ИСТИНА,
			|	СборкаЗапасовЗапасы.Номенклатура,
			|	СборкаЗапасовЗапасы.Характеристика,
			|	СборкаЗапасовЗапасы.Партия,
			|	СборкаЗапасовЗапасы.Количество
			|ИЗ
			|	Документ.СборкаЗапасов.Запасы КАК СборкаЗапасовЗапасы
			|ГДЕ
			|	СборкаЗапасовЗапасы.Ссылка В (&МассивДокументов)
			|	И СборкаЗапасовЗапасы.Номенклатура.ПодконтрольнаяПродукцияВЕТИС
			|	И СборкаЗапасовЗапасы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСборкаЗапасов.Разборка)";
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИмяВременнойТаблицы = РасчетСтатусовОформленияВЕТИС.ИмяВременнойТаблицыДляВыборкиДанныхДокумента();
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ВТ%", ИмяВременнойТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
