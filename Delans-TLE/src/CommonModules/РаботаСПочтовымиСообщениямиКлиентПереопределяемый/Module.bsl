///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается перед открытием формы нового письма.
// Открытие формы может быть отменено изменением параметра СтандартнаяОбработка.
//
// Параметры:
//  ПараметрыОтправки    - Структура - см. описание в РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо;
//  ОбработчикЗавершения - ОписаниеОповещения - описание процедуры, которая будет вызвана после завершения
//                                              отправки письма.
//  СтандартнаяОбработка - Булево - признак продолжения открытия формы нового письма после выхода из этой
//                                  процедуры. Если установить Ложь, форма письма открыта не будет.
Процедура ПередОткрытиемФормыОтправкиПисьма(ПараметрыОтправки, ОбработчикЗавершения, СтандартнаяОбработка) Экспорт
	Перем Отправитель, Получатель, Вложения, Тема, Текст, УдалятьФайлыПослеОтправки, ДокументыОснования;
	
	СтандартнаяОбработка = Ложь;
	
	Если ПараметрыОтправки.Свойство("Шаблон") И Не ЗначениеЗаполнено(ПараметрыОтправки.Шаблон)
		И ПараметрыОтправки.Свойство("Предмет") И ЗначениеЗаполнено(ПараметрыОтправки.Предмет) Тогда
		
		// В форме выбора шаблона сообщения пользователь выбрал <Без шаблона>, нужно продолжить с открытием выбора печатных форм
		// см. общую команду ОтправитьПоЭлектроннойПочте
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИмяФормыОбъектаПечати", ПараметрыОтправки.ИмяФормыИсточникаСообщения);
		ПараметрыФормы.Вставить("ОбъектыОтправки", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыОтправки.Предмет));
		ОткрытьФорму("ОбщаяФорма.ВыборПечатныхФормДляОтправки", ПараметрыФормы);
		Возврат;
	КонецЕсли;
	
	ПараметрыОтправки.Свойство("Отправитель", Отправитель);
	ПараметрыОтправки.Свойство("Получатель", Получатель);
	ПараметрыОтправки.Свойство("Тема", Тема);
	ПараметрыОтправки.Свойство("Текст", Текст);
	ПараметрыОтправки.Свойство("Вложения", Вложения);
	ПараметрыОтправки.Свойство("УдалятьФайлыПослеОтправки", УдалятьФайлыПослеОтправки);
	ПараметрыОтправки.Свойство("ДокументыОснования", ДокументыОснования);
	
	Если ПараметрыОтправки.Свойство("Предмет") И ЗначениеЗаполнено(ПараметрыОтправки.Предмет) Тогда
		ДокументыОснования = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыОтправки.Предмет);
	КонецЕсли;
	
	// Используем форму документа Событие с типом "Электронное письмо"
	УправлениеНебольшойФирмойКлиент.ОткрытьФормуОтправкиПочтовогоСообщения(Отправитель, Получатель,
		Тема, Текст, Вложения, ДокументыОснования, УдалятьФайлыПослеОтправки, ОбработчикЗавершения);
	
КонецПроцедуры

#КонецОбласти
