
#Область ОбработчикиСобытийОбъектов

// Обработчик события вызывается на сервере при получении стандартной управляемой формы.
// Если требуется переопределить выбор открываемой формы, необходимо установить в параметре <ВыбраннаяФорма>
// другое имя формы или объект метаданных формы, которую требуется открыть, и в параметре <СтандартнаяОбработка>
// установить значение Ложь.
//
// Параметры:
//  ИмяСправочника - Строка - имя справочника, для которого открывается форма,
//  ВидФормы - Строка - имя стандартной формы,
//  Параметры - Структура - параметры формы,
//  ВыбраннаяФорма - Строка, УправляемаяФорма - содержит имя открываемой формы или объект метаданных Форма,
//  ДополнительнаяИнформация - Структура - дополнительная информация открытия формы,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки события.
//
Процедура ПриПолученииФормыСправочника(ИмяСправочника, ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Обработчик события вызывается на сервере при получении стандартной управляемой формы.
// Если требуется переопределить выбор открываемой формы, необходимо установить в параметре <ВыбраннаяФорма>
// другое имя формы или объект метаданных формы, которую требуется открыть, и в параметре <СтандартнаяОбработка>
// установить значение Ложь.
//
// Параметры:
//  ИмяДокумента - Строка - имя документа, для которого открывается форма,
//  ВидФормы - Строка - имя стандартной формы,
//  Параметры - Структура - параметры формы,
//  ВыбраннаяФорма - Строка, УправляемаяФорма - содержит имя открываемой формы или объект метаданных Форма,
//  ДополнительнаяИнформация - Структура - дополнительная информация открытия формы,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки события.
//
Процедура ПриПолученииФормыДокумента(ИмяДокумента, ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

// Обработчик события вызывается на сервере при получении стандартной управляемой формы.
// Если требуется переопределить выбор открываемой формы, необходимо установить в параметре <ВыбраннаяФорма>
// другое имя формы или объект метаданных формы, которую требуется открыть, и в параметре <СтандартнаяОбработка>
// установить значение Ложь.
//
// Параметры:
//  ИмяРегистра - Строка - имя регистра сведений, для которого открывается форма,
//  ВидФормы - Строка - имя стандартной формы,
//  Параметры - Структура - параметры формы,
//  ВыбраннаяФорма - Строка, УправляемаяФорма - содержит имя открываемой формы или объект метаданных Форма,
//  ДополнительнаяИнформация - Структура - дополнительная информация открытия формы,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки события.
//
Процедура ПриПолученииФормыРегистраСведений(ИмяРегистра, ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

// Возникает на сервере при создании формы.
//
// Параметры:
//  Форма - УправляемаяФорма - создаваемая форма,
//  Отказ - Булево - признак отказа от создания формы,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	//Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Объект, "Ссылка") Тогда
	//	
	//	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("СправочникСсылка.ХозяйствующиеСубъектыВЕТИС") Тогда
	//		Форма.Элементы.Организация.ТолькоПросмотр = Справочники.Организации.КоличествоОрганизаций() < 2; 
	//	КонецЕсли; 
	//	
	//КонецЕсли; 
	
	//ToDo: Пока не реализовываем
	//Для каждого ЭлементФормы Из Форма.Элементы Цикл
	//	Если СтрНайти(ЭлементФормы.Имя, "Сери") <> 0 Тогда
	//		Если ЗначениеЗаполнено(ЭлементФормы.Заголовок) Тогда
	//			ЭлементФормы.Заголовок = СтрЗаменить(ЭлементФормы.Заголовок, "Сери", "Парти");
	//		Иначе	
	//		    ЭлементФормы.Заголовок = СтрЗаменить(ЭлементФормы.Имя, "Сери", "Парти");
	//		КонецЕсли; 
	//		
	//	КонецЕсли; 
	//КонецЦикла;
	//
	//Возврат;
	
	ИнтеграцияВЕТИСУНФВызовСервера.ЗаполнитьПризнакиИспользованияХарактеристикВЕТИС(Форма);
	
	ИнтеграцияВЕТИСУНФКлиентСервер.УстановитьВидимостьКомандыГенерацииСерии(Форма);
	
КонецПроцедуры

// Вызывается при чтении объекта на сервере.
//
// Параметры:
//  Форма - УправляемаяФорма - форма читаемого объекта,
//  ТекущийОбъект - ДокументОбъект, СправочникОбъект - читаемый объект.
//
Процедура ПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт
	
	ИнтеграцияВЕТИСУНФВызовСервера.ЗаполнитьПризнакиИспользованияХарактеристикВЕТИС(Форма);
	
	ИнтеграцияВЕТИСУНФКлиентСервер.УстановитьВидимостьКомандыГенерацииСерии(Форма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// Выполняет действия при изменении номенклатуры в таблице Товары.
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыЗаполнения - Структура - см. функцию СобытияФормЕГАИСКлиент.СтруктураПараметрыЗаполнения.
//	ПараметрыУказанияСерий	- Структура						- Состав полей определен в функции 
//																НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Процедура ПриИзмененииНоменклатуры(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения, ПараметрыУказанияСерий = Неопределено) Экспорт
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Номенклатура", ТекущаяСтрока.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", ТекущаяСтрока.Характеристика);
	
	// Характеристики
	СтруктураДанные.Вставить("ИспользоватьХарактеристики", Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики", Ложь);
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) и СтруктураДанные.Номенклатура.ИспользоватьХарактеристики
		Тогда
		ЗначенияПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура);
		
		Если Не ЗначенияПоУмолчанию = Неопределено
			Тогда
			ХарактеристикаПоУмолчанию = ЗначенияПоУмолчанию;
		КонецЕсли;
		
		СтруктураДанные.Характеристика = ?(ЗначениеЗаполнено(СтруктураДанные.Характеристика), СтруктураДанные.Характеристика, ХарактеристикаПоУмолчанию);
		
		СтруктураДанные.Вставить("ИспользоватьХарактеристики", Истина);
		СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики", СтруктураДанные.Номенклатура.ПроверятьЗаполнениеХарактеристики);
	КонецЕсли; 
	// Конец Характеристики
	
	//Партии
	СтруктураДанные.Вставить("ИспользоватьПартии", Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеПартий", Ложь);
	СтруктураДанные.Вставить("Партия", Неопределено);
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) и СтруктураДанные.Номенклатура.ИспользоватьПартии
		Тогда
		
		Если ТекущаяСтрока.Свойство("СтатусУказанияСерий") Тогда
			ТекущаяСтрока.СтатусУказанияСерий = 1;
		КонецЕсли;
		
		ЗначенияПартииПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура);
		
		ПартияПоУмолчанию = Справочники.ПартииНоменклатуры.ПустаяСсылка();
		
		Если Не ЗначенияПартииПоУмолчанию = Неопределено
			Тогда
			ПартияПоУмолчанию = ЗначенияПартииПоУмолчанию;
		КонецЕсли;
		
		СтруктураДанные.Партия = ?(ЗначениеЗаполнено(СтруктураДанные.Партия), СтруктураДанные.Партия, ПартияПоУмолчанию);
		
		СтруктураДанные.ПроверятьЗаполнениеПартий = СтруктураДанные.Номенклатура.ПроверятьЗаполнениеПартий;
		СтруктураДанные.ИспользоватьПартии = Истина;
	
	КонецЕсли;
	// Конец Партии
	
	
	
	Если СтруктураДанные.ИспользоватьХарактеристики
		Тогда
		ТекущаяСтрока.Характеристика = СтруктураДанные.Характеристика;
	КонецЕсли;
	
	Если СтруктураДанные.ИспользоватьПартии
		Тогда
		ТекущаяСтрока.Серия = СтруктураДанные.Партия;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет действия при изменении количества ВетИС в таблице Товары.
//
// Параметры:
//	Форма					- УправляемаяФорма				- Форма, в которой произошло событие.
//	ТекущаяСтрока			- ДанныеФормыЭлементКоллекции 	- Строка таблицы товаров.
//	КэшированныеЗначения	- Структура						- Сохраненные значения параметров, используемых при обработке строки таблицы.
//	ПараметрыЗаполнения		- Структура						- См. функцию ИнтеграцияВЕТИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//
Процедура ПриИзмененииКоличестваВЕТИС(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	ИнтеграцияВЕТИСУНФВызовСервера.ПересчитатьКоличествоЕдиниц(ТекущаяСтрока.КоличествоВЕТИС, ТекущаяСтрока.Номенклатура, ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС, Ложь, КэшированныеЗначения);
	
КонецПроцедуры

// Выполняет действия при изменении количества в таблице Товары.
//
// Параметры:
//	Форма					- УправляемаяФорма				- Форма, в которой произошло событие.
//	ТекущаяСтрока			- ДанныеФормыЭлементКоллекции 	- Строка таблицы товаров.
//	КэшированныеЗначения	- Структура						- Сохраненные значения параметров, используемых при обработке строки таблицы.
//	ПараметрыЗаполнения		- Структура						- См. функцию ИнтеграцияВЕТИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//
Процедура ПриИзмененииКоличества(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения) Экспорт
	
	ИнтеграцияВЕТИСУНФВызовСервера.ПересчитатьКоличествоЕдиницВЕТИС(ТекущаяСтрока.Количество, ТекущаяСтрока.Номенклатура, ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС, Ложь, КэшированныеЗначения);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Заполняет табличную часть Товары подобранными товарами.
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой производится подбор,
//  ВыбранноеЗначение - Произвольный - данные, содержащие подобранную пользователем номенклатуру,
//  ПараметрыЗаполнения - Структура - см. функцию СобытияФормЕГАИСКлиент.СтруктураПараметрыЗаполнения.
//
Процедура ОбработкаРезультатаПодбораНоменклатуры(Форма, ВыбранноеЗначение, ПараметрыЗаполнения) Экспорт
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресТоваровВХранилище);
	
	
	ТекущаяСтрока = Неопределено;
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		ТекущаяСтрока = Форма.Объект.Товары.Добавить();
		
		СписокСвойств = "Номенклатура, Характеристика";
		Если ТекущаяСтрока.Свойство("КоличествоУпаковок") Тогда
			СписокСвойств = СписокСвойств + ", КоличествоУпаковок";
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара, СписокСвойств);
		
	КонецЦикла;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		Форма.Элементы.Товары.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает условное оформление для поля "Характеристика".
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой нужно установить условное оформление,
//  ИмяПоляВводаХарактеристики - Строка - имя элемента формы "Характеристика",
//  ПутьКПолюОтбора - Строка - полный путь к реквизиту "Характеристики используются".
//
Процедура УстановитьУсловноеОформлениеХарактеристикНоменклатуры(Форма,
	                                                            ИмяПоляВводаХарактеристики = "ТоварыХарактеристика",
	                                                            ПутьКПолюОтбора = "Объект.Товары.ХарактеристикиИспользуются") Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
		Возврат;
	КонецЕсли;
																
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаХарактеристики].Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<характеристики не используются>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	ИмяТЧ = "";
	
	Если ИмяПоляВводаХарактеристики = "ТоварыХарактеристика" Тогда
		ПутьКТЧ = "Объект.Товары";
		ИмяТЧ = "Товары";
		
	ИначеЕсли ИмяПоляВводаХарактеристики = "СырьеХарактеристика" Тогда
		ПутьКТЧ = "Объект.Сырье";
		ИмяТЧ = "Сырье";
	Иначе Возврат;
	КонецЕсли;
	
	НовыеРеквизиты = Новый Массив;
	НовыеРеквизиты.Добавить(Новый РеквизитФормы("ПроверятьЗаполнениеХарактеристики", Новый ОписаниеТипов("Булево"), ПутьКТЧ, "ПроверятьЗаполнениеХарактеристики", Истина));
	Форма.ИзменитьРеквизиты(НовыеРеквизиты);

	НоваяКолонка = Форма.Элементы.Добавить(ИмяТЧ+"ПроверятьЗаполнениеХарактеристики", Тип("ПолеФормы"), Форма.Элементы[ИмяТЧ]);
	НоваяКолонка.Вид = ВидПоляФормы.ПолеВвода; 
	НоваяКолонка.ПутьКДанным = ПутьКТЧ + "." + "ПроверятьЗаполнениеХарактеристики";
	НоваяКолонка.Видимость = Ложь;
	
	ИмяПоляПроверятьЗаполнениеХарактеристики = "Объект."+ИмяТЧ+ ".ПроверятьЗаполнениеХарактеристики";
	
	ИмяПоляХарактеристики = ИмяПоляВводаХарактеристики;
	
	НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляПроверятьЗаполнениеХарактеристики, Истина, ВидСравненияКомпоновкиДанных.Равно);
	
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляХарактеристики);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Истина);
	
	НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект."+ИмяТЧ+".Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка(), ВидСравненияКомпоновкиДанных.НеРавно);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляХарактеристики);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

// Устанавливает условное оформление для поля "Серия".
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой нужно установить условное оформление,
//
Процедура УстановитьУсловноеОформлениеСерийНоменклатуры(Форма,
	                                                    ИмяПоляВводаСерии = "ТоварыСерия",
	                                                    ПутьКПолюОтбораСтатусУказанияСерий = "Объект.Товары.СтатусУказанияСерий",
	                                                    ПутьКПолюОтбораТипНоменклатуры = "Объект.Товары.ТипНоменклатуры") Экспорт
		
		ПутьКТЧ = "Объект.Товары";
		ИмяТЧ = "Товары";
		
		Если ИмяПоляВводаСерии = "СырьеСерия" Тогда
			ПутьКТЧ = "Объект.Сырье";
			ИмяТЧ = "Сырье";
		КонецЕсли;
		
		НовыеРеквизиты = Новый Массив;
		НовыеРеквизиты.Добавить(Новый РеквизитФормы("ИспользоватьПартии", Новый ОписаниеТипов("Булево"), ПутьКТЧ, "ИспользоватьПартии", Истина));
		НовыеРеквизиты.Добавить(Новый РеквизитФормы("ПроверятьЗаполнениеПартий", Новый ОписаниеТипов("Булево"), ПутьКТЧ, "ПроверятьЗаполнениеПартий", Истина));
		Форма.ИзменитьРеквизиты(НовыеРеквизиты);
		
		НоваяКолонка = Форма.Элементы.Добавить(ИмяТЧ+"ИспользоватьПартии", Тип("ПолеФормы"), Форма.Элементы[ИмяТЧ]);
		НоваяКолонка.Вид = ВидПоляФормы.ПолеВвода; 
		НоваяКолонка.ПутьКДанным = ПутьКТЧ + "." + "ИспользоватьПартии";
		НоваяКолонка.Видимость = Ложь;
		
		НоваяКолонка = Форма.Элементы.Добавить(ИмяТЧ+"ПроверятьЗаполнениеПартий", Тип("ПолеФормы"), Форма.Элементы[ИмяТЧ]);
		НоваяКолонка.Вид = ВидПоляФормы.ПолеВвода; 
		НоваяКолонка.ПутьКДанным = ПутьКТЧ + "." + "ПроверятьЗаполнениеПартий";
		НоваяКолонка.Видимость = Ложь;
		
		ИмяПоляИспользоватьПартии = "Объект."+ИмяТЧ+".ИспользоватьПартии";
		ИмяПоляПроверятьЗаполнениеПартии ="Объект."+ИмяТЧ+ ".ПроверятьЗаполнениеПартий";
		
		ИмяПоляПартии = ИмяПоляВводаСерии;
		
		ЭлементУсловногоОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы[ИмяПоляВводаСерии].Имя);
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Истина);
		
		НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляИспользоватьПартии, Ложь, ВидСравненияКомпоновкиДанных.Равно);
		
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляПартии);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Текст", НСтр("ru = '<Не используется>'"));
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
		
		НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляПроверятьЗаполнениеПартии, Истина, ВидСравненияКомпоновкиДанных.НеРавно);
		
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляПартии);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
		
		НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект."+ИмяТЧ+".Серия", Справочники.ПартииНоменклатуры.ПустаяСсылка(), ВидСравненияКомпоновкиДанных.НеРавно);
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляПартии);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

#КонецОбласти
