///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Позволяет изменить работу интерфейса при встраивании.
//
// Параметры:
//  НастройкиРаботыИнтерфейса - Структура - содержит свойство:
//   * ИспользоватьВнешнихПользователей - Булево - начальное значение Ложь,
//     если установить Истина, тогда даты запрета можно будет настраивать для внешних пользователей.
//
Процедура НастройкаИнтерфейса(НастройкиРаботыИнтерфейса) Экспорт
	
	НастройкиРаботыИнтерфейса.ИспользоватьВнешнихПользователей = Ложь;
	
КонецПроцедуры

// Заполняет разделы дат запрета изменения, используемые при настройке дат запрета.
// Если не указать ни одного раздела, тогда будет доступна только настройка общей даты запрета.
//
// Параметры:
//  Разделы - ТаблицаЗначений - с колонками:
//   * Имя - Строка - имя, используемое в описании источников данных в
//       процедуре ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения.
//
//   * Идентификатор - УникальныйИдентификатор - идентификатор ссылки элемента плана видов характеристик.
//       Чтобы получить идентификатор, нужно в режиме 1С:Предприятие выполнить метод платформы:
//       "ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.ПолучитьСсылку().УникальныйИдентификатор()".
//       Не следует указывать идентификаторы, полученные любым другим способом,
//       так как это может нарушить их уникальность.
//
//   * Представление - Строка - представляет раздел в форме настройки дат запрета.
//
//   * ТипыОбъектов  - Массив - типы ссылок объектов, в разрезе которых можно настроить даты запрета,
//       например Тип("СправочникСсылка.Организации"); если не указано ни одного типа,
//       то даты запрета будут настраиваться только с точностью до раздела.
//
Процедура ПриЗаполненииРазделовДатЗапретаИзменения(Разделы) Экспорт
	
	Раздел = Разделы.Добавить();
	Раздел.Имя  = "УправленческийУчет";
	Раздел.Идентификатор = Новый УникальныйИдентификатор("9b115778-61b1-47db-afc0-1c92191545cd");
	Раздел.Представление = НСтр("ru = 'Управленческий учет'");
	Раздел.ТипыОбъектов.Добавить(Тип("СправочникСсылка.Организации"));
	
	Раздел = Разделы.Добавить();
	Раздел.Имя  = "ЗаказыПоставщиков";
	Раздел.Идентификатор = Новый УникальныйИдентификатор("70411f9a-cdb8-48ed-adee-c3f0368a09c7");
	Раздел.Представление = НСтр("ru = 'Заказы поставщиков'");
	Раздел.ТипыОбъектов.Добавить(Тип("СправочникСсылка.СостоянияЗаказовПоставщикам"));
	
	Раздел = Разделы.Добавить();
	Раздел.Имя  = "ЗаказыПокупателей";
	Раздел.Идентификатор = Новый УникальныйИдентификатор("5fcbf40d-10b5-4019-a07d-c62cfa1ba3b2");
	Раздел.Представление = НСтр("ru = 'Заказы покупателей'");
	Раздел.ТипыОбъектов.Добавить(Тип("СправочникСсылка.СостоянияЗаказовПокупателей"));
	
	Раздел = Разделы.Добавить();
	Раздел.Имя  = "ЗаказНаПроизводство";
	Раздел.Идентификатор = Новый УникальныйИдентификатор("0e5919e2-ea5d-4fa4-aa5c-ed4b1b4b9fbe");
	Раздел.Представление = НСтр("ru = 'Заказ на производство'");
	Раздел.ТипыОбъектов.Добавить(Тип("СправочникСсылка.СостоянияЗаказовНаПроизводство"));
	
	Раздел = Разделы.Добавить();
	Раздел.Имя  = "ЗаказНаряд";
	Раздел.Идентификатор = Новый УникальныйИдентификатор("e61bbaff-fd48-4140-bb9e-25141219d52e");
	Раздел.Представление = НСтр("ru = 'Заказ-наряд'");
	Раздел.ТипыОбъектов.Добавить(Тип("СправочникСсылка.СостоянияЗаказНарядов"));
	
КонецПроцедуры

// Позволяет задать таблицы и поля объектов для проверки запрета изменения данных.
// Для добавления нового источника в ИсточникиДанных см. ДатыЗапретаИзменения.ДобавитьСтроку.
//
// Вызывается из процедуры ИзменениеЗапрещено общего модуля ДатыЗапретаИзменения,
// используемой в подписке на событие ПередЗаписью объекта для проверки наличия
// запретов и отказа от изменений запрещенного объекта.
//
// Параметры:
//  ИсточникиДанных - ТаблицаЗначений - с колонками:
//   * Таблица     - Строка - полное имя объекта метаданных,
//                   например Метаданные.Документы.ПриходнаяНакладная.ПолноеИмя().
//   * ПолеДаты    - Строка - имя реквизита объекта или табличной части,
//                   например: "Дата", "Товары.ДатаОтгрузки".
//   * Раздел      - Строка - имя раздела дат запрета, указанного в
//                   процедуре ПриЗаполненииРазделовДатЗапретаИзменения (см. выше).
//   * ПолеОбъекта - Строка - имя реквизита объекта или реквизита табличной части,
//                   например: "Организация", "Товары.Склад".
//
Процедура ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения(ИсточникиДанных) Экспорт
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АвансовыйОтчет", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АктВыполненныхРабот",			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АмортизацияВА", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Бюджет", 						"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВводНачальныхОстатков",			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Взаимозачет", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВыработкаВА", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Доверенность", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ДополнительныеРасходы",			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗаданиеНаРаботу", 				"Дата", "УправленческийУчет", "Организация");
	// "Документ.ЗаказНаПроизводство" - вынесен в отдельный раздел
	// "Документ.ЗаказПокупателя" - вынесен в отдельный раздел
	// "Документ.ЗаказПоставщику" - вынесен в отдельный раздел
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗакрытиеМесяца", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ИзменениеПараметровВА", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ИнвентаризацияЗапасов", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КадровоеПеремещение", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НачислениеЗарплаты", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НачислениеНалогов", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Операция", 						"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОприходованиеЗапасов", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетКомиссионера", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетКомитенту", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетОПереработке", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетОРозничныхПродажах",		"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетПереработчика", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПередачаВА", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПеремещениеДС", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПеремещениеДСПлан", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПеремещениеЗапасов", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПеремещениеПоЯчейкам", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПереоценкаВРозницеСуммовойУчет", "Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПродаж", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПлатежнаяВедомость", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПлатежноеПоручение", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПоступлениеВКассу", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПоступлениеДСПлан", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПоступлениеНаСчет", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриемНаРаботу", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПринятиеКУчетуВА", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриходнаяНакладная", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриходныйОрдер", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПрочиеРасходы", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РаспределениеЗатрат", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходДСПлан", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходИзКассы", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходнаяНакладная", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходныйОрдер", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходСоСчета", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходыПриИмпорте", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РезервированиеЗапасов", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СборкаЗапасов", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СдельныйНаряд", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СписаниеВА", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СписаниеЗапасов", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СчетНаОплату", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СчетНаОплатуПоставщика",			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СчетФактура", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СчетФактураПолученный", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Табель", 						"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Увольнение", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.УчетВремени", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЧекККМ", 						"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЧекККМВозврат", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОперацияПоПлатежнымКартам",		"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ДоговорКредитаИЗайма",			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НачисленияПоКредитамИЗаймам",	"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КорректировкаПоступления",		"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КорректировкаРеализации",		"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПересортицаЗапасов",				"Дата", "УправленческийУчет", "Организация");
	
	// Дополнительные разделы для документов Заказ покупателя, Заказ поставщику
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗаказНаПроизводство", 			"Дата", "ЗаказНаПроизводство",	"СостояниеЗаказа");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗаказПокупателя", 				"Дата", "ЗаказыПокупателей",	"СостояниеЗаказа");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗаказПокупателя", 				"Дата", "ЗаказНаряд",			"СостояниеЗаказа");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗаказПоставщику", 				"Дата", "ЗаказыПоставщиков",	"СостояниеЗаказа");
	
КонецПроцедуры

// Позволяет переопределить выполнение проверки запрета изменения произвольным образом.
//
// Если проверка выполняется в процессе записи документа, то в свойстве ДополнительныеСвойства документа Объект
// имеется свойство РежимЗаписи.
//  
// Параметры:
//  Объект       - СправочникОбъект,
//                 ДокументОбъект,
//                 ПланВидовХарактеристикОбъект,
//                 ПланСчетовОбъект,
//                 ПланВидовРасчетаОбъект,
//                 БизнесПроцессОбъект,
//                 ЗадачаОбъект,
//                 ПланОбменаОбъект, 
//                 РегистрСведенийНаборЗаписей,
//                 РегистрНакопленияНаборЗаписей,
//                 РегистрБухгалтерииНаборЗаписей,
//                 РегистрРасчетаНаборЗаписей - проверяемый элемент данных или набор записей 
//                 (который передается из обработчиков ПередЗаписью и ПриЧтенииНаСервере).
//
//  ПроверкаЗапретаИзменения    - Булево - установить в Ложь, чтобы пропустить проверку запрета изменения данных.
//  УзелПроверкиЗапретаЗагрузки - ПланыОбменаСсылка, Неопределено - установить в Неопределено, чтобы 
//                                пропустить проверку запрета загрузки данных.
//  ВерсияОбъекта               - Строка - установить "СтараяВерсия" или "НоваяВерсия", чтобы
//                                выполнить проверку только старой (в базе данных) 
//                                или только новой версии объекта (в параметре Объект).
//                                По умолчанию содержит значение "" - проверяются обе версии объекта сразу.
//
Процедура ПередПроверкойЗапретаИзменения(Объект,
                                         ПроверкаЗапретаИзменения,
                                         УзелПроверкиЗапретаЗагрузки,
                                         ВерсияОбъекта) Экспорт
	
	Если Объект.Метаданные().ПолноеИмя() = "Документ.ЗаказПокупателя" Тогда
		
		ПроверкаЗапретаИзменения = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйИнтерфейсУНФ

// Копия с изменениями метода ДатыЗапретаИзмененияСлужебный.ЗапретИзмененияДанныхНеИспользуется()
Функция ЗапретИзмененияДанныхНеИспользуется(ПроверкаЗапретаИзменения, УзелПроверкиЗапретаЗагрузки)
	
	Если ОбновлениеИнформационнойБазы.НеобходимоОбновлениеИнформационнойБазы() Тогда
		Возврат Истина;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДействующиеДатыЗапрета = ДатыЗапретаИзмененияСлужебный.ДействующиеДатыЗапрета();
	
	Если Не ДействующиеДатыЗапрета.ЗапретИзмененияИспользуется
	   И Не ДействующиеДатыЗапрета.ЗапретЗагрузкиИспользуется Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Копия с изменениями метода ДатыЗапретаИзмененияСлужебный.ПропуститьПроверкуДатЗапрета()
//
Функция ПропуститьПроверкуДатЗапрета(Объект, ПроверкаЗапретаИзменения, УзелПроверкиЗапретаЗагрузки, ВерсияОбъекта) Экспорт
	
	Если ТипЗнч(Объект) <> Тип("УдалениеОбъекта")
	   И Объект.ДополнительныеСвойства.Свойство("ПропуститьПроверкуЗапретаИзменения") Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Если ПараметрыСеанса.ПропуститьПроверкуЗапретаИзменения Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ЗапретИзмененияДанныхНеИспользуется(ПроверкаЗапретаИзменения, УзелПроверкиЗапретаЗагрузки) Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Если ОбновлениеИнформационнойБазы.ВыполняетсяОбновлениеИнформационнойБазы()
	 Или ОбновлениеИнформационнойБазы.ЭтоВызовИзОбработчикаОбновления() Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Для документов типа Заказ покупателя и Заказ-наряд выполнение проверки происходит по индивидуальному правилу.
//
Процедура ПроверитьДатуЗапретаРедактированияОбъектаЗаказПокупателя(Форма, ТекущийОбъект, ОписаниеОшибки = "", УзелПроверкиЗапретаЗагрузки = Неопределено, Отказ = Ложь) Экспорт
	
	Если ПропуститьПроверкуДатЗапрета(ТекущийОбъект, Истина, УзелПроверкиЗапретаЗагрузки, "") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЭтоЗаказНаряд		= (ТекущийОбъект.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд);
	
	ИмяРаздела			= ?(ЭтоЗаказНаряд, "ЗаказНаряд", "ЗаказыПокупателей");
	ДатаПроверки		= ?(ЭтоЗаказНаряд, ТекущийОбъект.Финиш, ТекущийОбъект.Дата);
	
	ДанныеДляПроверки	= ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	
	НоваяСтрока			= ДанныеДляПроверки.Добавить();
	НоваяСтрока.Дата	= ДатаПроверки;
	Новаястрока.Раздел	= ИмяРаздела;
	НоваяСтрока.Объект	= ТекущийОбъект.СостояниеЗаказа;
	
	НайденныеЗапреты	= Неопределено;
	СтандартнаяОбработка= Истина;
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("НоваяВерсия", Истина);
	ОписаниеДанных.Вставить("Данные", ТекущийОбъект.Ссылка);
	
	РезультатПроверки = ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ДанныеДляПроверки, ОписаниеДанных, ОписаниеОшибки, УзелПроверкиЗапретаЗагрузки);
	Если Форма <> Неопределено Тогда 
		
		Форма.ТолькоПросмотр = РезультатПроверки;
		
	ИначеЕсли НЕ ПустаяСтрока(ОписаниеОшибки) Тогда
		
		Отказ = Истина;
		
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Даты запрета изменения. Найдены запреты изменения'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ОписаниеОшибки,
			РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьДатуЗапретаРедактированияОбъектаЗаказПокупателя()

#КонецОбласти