
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Безналичные;
	Элементы.Касса.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ЕстьОшибкиВЗаполненииПолей() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	
	Результат.Вставить("ТипДенежныхСредств", ТипДенежныхСредств);
	
	Если ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные") Тогда
		Результат.Вставить("БанковскийСчетКасса", Касса);
	ИначеЕсли ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные") Тогда
		Результат.Вставить("БанковскийСчетКасса", БанковскийСчет);
	Иначе
		Результат.Вставить("БанковскийСчетКасса", Неопределено);
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипДенежныхСредствПриИзменении(Элемент)
	
	Элементы.БанковскийСчет.Видимость = ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные");
	Элементы.Касса.Видимость 		  = ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные");
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьОшибкиВЗаполненииПолей()
	
	Результат = Ложь;
	
	Если ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные") И НЕ ЗначениеЗаполнено(Касса) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Не выбрана касса!'");
		Сообщение.Поле = "Касса";
		Сообщение.Сообщить();
		Результат = Истина;
	КонецЕсли;
	
	Если ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные") И НЕ ЗначениеЗаполнено(БанковскийСчет) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Не выбрана банковский счет!'");
		Сообщение.Поле = "БанковскийСчет";
		Сообщение.Сообщить();
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
