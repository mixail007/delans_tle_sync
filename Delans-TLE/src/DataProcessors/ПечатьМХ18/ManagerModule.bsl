
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ServiceAPI

Функция ИдентификаторПечатнойФормы() Экспорт
	
	Возврат "МХ18";
	
КонецФункции

Функция ПредставлениеПФ() Экспорт
	
	Возврат НСтр("ru ='МХ18 (Накладная на передачу готовой продукции)'");
	
КонецФункции

Функция КлючПараметровПечати() Экспорт
	
	Возврат "ПАРАМЕТРЫ_ПЕЧАТИ_Универсальные_МХ18";
	
КонецФункции

Функция ПолныйПутьКМакету() Экспорт
	
	Возврат "Обработка.ПечатьМХ18.ПФ_MXL_МХ18";
	
КонецФункции

Функция СформироватьПФ(ОписаниеПечатнойФормы, ДанныеОбъектовПечати, ОбъектыПечати, ВключаяУслуги) Экспорт
	Перем Ошибки, ПервыйДокумент, НомерСтрокиНачало;
	
	Макет				= УправлениеПечатью.МакетПечатнойФормы(ОписаниеПечатнойФормы.ПолныйПутьКМакету);
	ТабличныйДокумент	= ОписаниеПечатнойФормы.ТабличныйДокумент;
	ДанныеПечати		= Новый Структура;
	СтруктураИтогов		= Новый Структура;
	
	СтруктураОбластейМакета = Новый Структура;
	СтруктураОбластейМакета.Вставить("ОбластьШапка", 			ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Шапка", "", Ошибки));
	СтруктураОбластейМакета.Вставить("ОбластьЗаголовокТаблицы", ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "ЗаголовокТаблицы", "", Ошибки));
	СтруктураОбластейМакета.Вставить("НомерСтраницы",			ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "НомерСтраницы", "", Ошибки));
	СтруктураОбластейМакета.Вставить("ОбластьСтрока", 			ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Строка", "", Ошибки));
	СтруктураОбластейМакета.Вставить("ОбластьИтоговПоСтранице", ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "ИтогиПоСтранице", "", Ошибки));
	СтруктураОбластейМакета.Вставить("ОбластьВсего",			ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Всего", "", Ошибки));
	СтруктураОбластейМакета.Вставить("ОбластьПодвал",			ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Подвал", "", Ошибки));
	
	Для каждого ДанныеОбъекта Из ДанныеОбъектовПечати Цикл
		
		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало, ДанныеПечати);
		
		//:::Шапка
		СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ДанныеОбъекта.Организация, ДанныеОбъекта.ДатаДокумента);
		
		ДанныеПечати.Вставить("ДатаДокумента", ДанныеОбъекта.ДатаДокумента);
		ДанныеПечати.Вставить("ОрганизацияПоОКПО", СведенияОбОрганизации.КодПоОКПО);
		ДанныеПечати.Вставить("ПредставлениеОрганизации", УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации));
		ДанныеПечати.Вставить("Отправитель", ДанныеОбъекта.Отправитель);
		ДанныеПечати.Вставить("Получатель", ДанныеОбъекта.Получатель);
		ДанныеПечати.Вставить("КорСчет", ДанныеОбъекта.КорСчет);
		
		НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ДанныеОбъекта.ДатаДокумента, ДанныеОбъекта.Номер, ДанныеОбъекта.Префикс);
		ДанныеПечати.Вставить("НомерДокумента", НомерДокумента);
		
		СтруктураОбластейМакета.ОбластьШапка.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(СтруктураОбластейМакета.ОбластьШапка);
		
		//:::Таблица документа
		СтруктураИтогов.Очистить();
		СтруктураИтогов.Вставить("НомерСтраницы",				1);
		СтруктураИтогов.Вставить("НомерСтроки",					0);
		СтруктураИтогов.Вставить("КоличествоСтрок",				ДанныеОбъекта.ТаблицаЗапасы.Количество());
		СтруктураИтогов.Вставить("ОбработаноСтрок",				0);
		СтруктураИтогов.Вставить("КоличествоМестПоСтранице",	0);
		СтруктураИтогов.Вставить("КоличествоНаСтранице",		0);
		СтруктураИтогов.Вставить("ИтогКоличествоМест",			0);
		СтруктураИтогов.Вставить("ИтогоКоличество",				0);
		СтруктураИтогов.Вставить("СуммаНаСтранице",				0);
		СтруктураИтогов.Вставить("ИтогСумма",					0);
		
		
		ПредставлениеДокумента = СтрШаблон(НСтр("ru ='Накладная на передачу готовой продукции в места хранения №%1 от %2'"), НомерДокумента, Формат(ДанныеОбъекта.ДатаДокумента,"ДЛФ=DD"));
		СтруктураИтогов.Вставить("ПредставлениеДокумента", ПредставлениеДокумента);
		
		ДанныеПечати.Вставить("НомерСтраницы", НСтр("ru ='Страница '") + СтруктураИтогов.НомерСтраницы);
		СтруктураОбластейМакета.ОбластьЗаголовокТаблицы.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(СтруктураОбластейМакета.ОбластьЗаголовокТаблицы);
		
		ПараметрыНоменклатуры = Новый Структура;
		Для каждого СтрокаЗапасы Из ДанныеОбъекта.ТаблицаЗапасы Цикл
			
			ДанныеПечати.Очистить();
			
			СтруктураИтогов.НомерСтроки = СтруктураИтогов.НомерСтроки + 1;
			
			Если СтруктураИтогов.НомерСтроки <> 0 
				И СтрокаКорректноРазмещаетсяНаСтранице(ТабличныйДокумент, СтруктураОбластейМакета, СтруктураИтогов) = Ложь Тогда
				
				ДобавитьНовуюСтраницуДокумента(ТабличныйДокумент, СтруктураОбластейМакета, СтруктураИтогов);
				
			КонецЕсли;
			
			ПараметрыНоменклатуры.Очистить();
			ПараметрыНоменклатуры.Вставить("ПредставлениеНоменклатуры", СтрокаЗапасы.ПредставлениеНоменклатуры);
			ПараметрыНоменклатуры.Вставить("ПредставлениеХарактеристики", СтрокаЗапасы.Характеристика);
			
			ДанныеПечати.Вставить("ПредставлениеНоменклатуры",		ПечатьДокументовУНФ.ПредставлениеНоменклатуры(ПараметрыНоменклатуры));
			ДанныеПечати.Вставить("ПредставлениеКодаНоменклатуры",	ПечатьДокументовУНФ.ПредставлениеКодаНоменклатуры(СтрокаЗапасы));
			ДанныеПечати.Вставить("ЕдиницаИзмеренияНаименование",	СтрокаЗапасы.ЕдиницаИзмеренияНаименование);
			ДанныеПечати.Вставить("ЕдиницаИзмеренияКодПоОКЕИ",		СтрокаЗапасы.ЕдиницаИзмеренияКодПоОКЕИ);
			ДанныеПечати.Вставить("Количество",						СтрокаЗапасы.Количество);
			ДанныеПечати.Вставить("ВидУпаковки",					СтрокаЗапасы.ВидУпаковки);
			ДанныеПечати.Вставить("КоличествоМест",					СтрокаЗапасы.КоличествоМест);
			ДанныеПечати.Вставить("КоличествоВОдномМесте",			СтрокаЗапасы.КоличествоВОдномМесте);
			ДанныеПечати.Вставить("Цена",							СтрокаЗапасы.Цена);
			ДанныеПечати.Вставить("Сумма",							СтрокаЗапасы.Сумма);
			
			СтруктураОбластейМакета.ОбластьСтрока.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(СтруктураОбластейМакета.ОбластьСтрока);
			
			СтруктураИтогов.КоличествоНаСтранице = СтруктураИтогов.КоличествоНаСтранице + СтрокаЗапасы.Количество;
			СтруктураИтогов.ИтогоКоличество  = СтруктураИтогов.ИтогоКоличество  + СтрокаЗапасы.КоличествоМест;
			СтруктураИтогов.КоличествоМестПоСтранице = СтруктураИтогов.КоличествоМестПоСтранице + СтрокаЗапасы.КоличествоМест;
			СтруктураИтогов.ИтогКоличествоМест = СтруктураИтогов.ИтогКоличествоМест + СтрокаЗапасы.КоличествоМест;
			СтруктураИтогов.СуммаНаСтранице = СтруктураИтогов.СуммаНаСтранице + СтрокаЗапасы.Сумма;
			СтруктураИтогов.ИтогСумма = СтруктураИтогов.ИтогСумма + СтрокаЗапасы.Сумма;
			
			СтруктураИтогов.ОбработаноСтрок = СтруктураИтогов.ОбработаноСтрок + 1;
			
		КонецЦикла;
		
		СтруктураОбластейМакета.ОбластьИтоговПоСтранице.Параметры.Заполнить(СтруктураИтогов);
		ТабличныйДокумент.Вывести(СтруктураОбластейМакета.ОбластьИтоговПоСтранице);
		
		СтруктураОбластейМакета.ОбластьВсего.Параметры.Заполнить(СтруктураИтогов);
		ТабличныйДокумент.Вывести(СтруктураОбластейМакета.ОбластьВсего);
		
		//:::Подвал документа
		ДанныеПечати.Очистить();
		ДанныеПечати.Вставить("ДолжностьКладовщикаОтпустил",			ДанныеОбъекта.ДолжностьКладовщикаОтпустил);
		ДанныеПечати.Вставить("РасшифровкаПодписиКладовщикаОтпустил",	ДанныеОбъекта.РасшифровкаПодписиКладовщикаОтпустил);
		ДанныеПечати.Вставить("ДолжностьКонтролера",					ДанныеОбъекта.ДолжностьКонтролера);
		ДанныеПечати.Вставить("РасшифровкаПодписиКонтролера",			ДанныеОбъекта.РасшифровкаПодписиКонтролера);
		ДанныеПечати.Вставить("ДолжностьКладовщикаПринял",				ДанныеОбъекта.ДолжностьКладовщикаПринял);
		ДанныеПечати.Вставить("РасшифровкаПодписиКладовщикаПринял",		ДанныеОбъекта.РасшифровкаПодписиКладовщикаПринял);
		ДанныеПечати.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", УправлениеНебольшойФирмойСервер.КоличествоПрописью(СтруктураИтогов.НомерСтроки));
		ДанныеПечати.Вставить("СуммаПрописью",							УправлениеНебольшойФирмойСервер.СформироватьСуммуПрописью(СтруктураИтогов.ИтогСумма, ДанныеОбъекта.ВалютаДокумента));
		
		СтруктураОбластейМакета.ОбластьПодвал.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(СтруктураОбластейМакета.ОбластьПодвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеОбъекта.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

Функция СтрокаКорректноРазмещаетсяНаСтранице(ТабличныйДокумент, ОбластиМакета, СтруктураИтогов)
	
	ЕстьВсеОбласти = Истина;
	Для каждого ЭлементСтруктуры Из ОбластиМакета Цикл
		
		Если ЭлементСтруктуры.Значение = Неопределено Тогда
			
			ЕстьВсеОбласти = Ложь;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЕстьВсеОбласти Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	МассивОбластейМакета = Новый Массив;
	
	МассивОбластейМакета.Добавить(ОбластиМакета.ОбластьСтрока);
	МассивОбластейМакета.Добавить(ОбластиМакета.ОбластьИтоговПоСтранице);
	
	Если СтруктураИтогов.ОбработаноСтрок = СтруктураИтогов.КоличествоСтрок - 1 Тогда
		
		МассивОбластейМакета.Добавить(ОбластиМакета.ОбластьВсего);
		МассивОбластейМакета.Добавить(ОбластиМакета.ОбластьПодвал);
		
	КонецЕсли;
	
	Возврат ТабличныйДокумент.ПроверитьВывод(МассивОбластейМакета)
	
КонецФункции

Процедура ДобавитьНовуюСтраницуДокумента(ТабличныйДокумент, ОбластиМакета, СтруктураИтогов)
	
	Если ОбластиМакета.ОбластьИтоговПоСтранице <> Неопределено Тогда
		
		ОбластиМакета.ОбластьИтоговПоСтранице.Параметры.Заполнить(СтруктураИтогов);
		ТабличныйДокумент.Вывести(ОбластиМакета.ОбластьИтоговПоСтранице);
		
	КонецЕсли;
	
	ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	СтруктураИтогов.КоличествоНаСтранице		= 0;
	СтруктураИтогов.КоличествоМестПоСтранице	= 0;
	СтруктураИтогов.СуммаНаСтранице				= 0;
	СтруктураИтогов.НомерСтраницы				= СтруктураИтогов.НомерСтраницы + 1;
	
	Если ОбластиМакета.НомерСтраницы <> Неопределено Тогда
		
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("ПредставлениеДокумента", СтруктураИтогов.ПредставлениеДокумента);
		ДанныеПечати.Вставить("ПредставлениеСтраницы", НСтр("ru ='Страница '") + СтруктураИтогов.НомерСтраницы);
		
		ОбластиМакета.НомерСтраницы.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластиМакета.НомерСтраницы);
		
	КонецЕсли;
	
	Если ОбластиМакета.ОбластьЗаголовокТаблицы <> Неопределено Тогда
		
		ТабличныйДокумент.Вывести(ОбластиМакета.ОбластьЗаголовокТаблицы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли