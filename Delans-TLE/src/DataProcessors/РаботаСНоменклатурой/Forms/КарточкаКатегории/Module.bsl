#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("ИдентификаторКатегории", ИдентификаторКатегории) Тогда
		ВызватьИсключение НСтр("ru = 'Не задан идентификатор категории'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СформироватьПредставлениеКатегории();
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьПредставлениеКатегории()
	
	ПараметрыЗавершения = Новый Структура;
	ПараметрыЗавершения.Вставить("ИдентификаторЗадания", Неопределено);
	
	СформироватьПредставлениеКатегорииЗавершение = Новый ОписаниеОповещения("СформироватьПредставлениеКатегорииЗавершение",
		ЭтотОбъект, ПараметрыЗавершения);
		
		
	РаботаСНоменклатуройКлиент.СформироватьПредставлениеКарточкиКатегории(СформироватьПредставлениеКатегорииЗавершение,
		ИдентификаторКатегории, ЭтотОбъект, Неопределено, Элементы.КарточкаКатегории.ОтображениеСостояния);
		
	Если Не Элементы.КарточкаКатегории.ОтображениеСостояния.Картинка.Вид = ВидКартинки.Пустая Тогда 
		Элементы.КарточкаКатегории.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
		Элементы.КарточкаКатегории.ОтображениеСостояния.Текст = НСтр("ru = 'Карточка категории загружается...'");
		Элементы.КарточкаКатегории.ОтображениеСостояния.Видимость = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредставлениеКатегорииЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	
	Элементы.КарточкаКатегории.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
	Элементы.КарточкаКатегории.ОтображениеСостояния.Видимость = Ложь;
	Элементы.КарточкаКатегории.ОтображениеСостояния.Текст = "";
	
	Если ЭтоАдресВременногоХранилища(Результат.АдресРезультата) Тогда 
		ТабличныйДокумент = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ТабличныйДокумент = Неопределено Тогда 
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
	КарточкаКатегории = ТабличныйДокумент;
	
КонецПроцедуры

&НаКлиенте
Процедура КарточкаКатегорииОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	РаботаСНоменклатуройКлиент.
		ОбработкаРасшифровкиПредставленияКатегории(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти