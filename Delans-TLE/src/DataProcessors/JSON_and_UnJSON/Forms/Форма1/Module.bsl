
&НаКлиенте
Процедура Пример1(Команда)
	Массив1=Новый Массив;
	Массив1.Добавить("Элемент 1 массива 1");
	Массив1.Добавить("Элемент 2 массива 1");
	Массив1.Добавить("Элемент 3 массива 1");
	
	Массив2=Новый Массив;
	Массив2.Добавить("Элемент 1 массива 2");
	Массив2.Добавить("Элемент 2 массива 2");
	Массив2.Добавить("Элемент 3 массива 2");
	
	Массив3=Новый Массив;
	Массив3.Добавить(Массив1);
	Массив3.Добавить(Массив2);
	
	ПолеJSON1=JSON(Массив3);
	
КонецПроцедуры

&НаКлиенте
Процедура Пример2(Команда)
	Массив1=Новый Массив;
	Соответствие=Новый Соответствие;
	Массив2=Новый Массив;
	Массив2.Добавить("array with 1 element");
	Соответствие.Вставить("object with 1 member",Массив2);
	Массив1.Добавить(Соответствие);
	Массив1.Добавить(Новый Структура);
	Массив1.Добавить(Новый Массив);
	Структура=Новый Структура;
	Структура.Вставить("foo","bar");
	Структура.Вставить("bar",1);
	Массив3=Новый Массив;
	Массив3.Вставить(42);
	Структура.Вставить(Массив3);
	ПолеJSON1=JSON(Массив1,Истина);
КонецПроцедуры

&НаКлиенте
Процедура Пример3(Команда)	
	Пример3НаСервере();
КонецПроцедуры

&НаСервере
Процедура Пример3НаСервере()
	Структура=Новый Структура;
	Структура.Вставить("Ключ1","Значение 1");
	Структура.Вставить("Ключ2","Значение 2");
	Массив1=Новый Массив;
	Массив1.Добавить(11);
	Массив1.Добавить(" 22 ");
	Массив1.Добавить(33);
	Структура.Вставить("Массив",Массив1);
	Структура.Вставить("Ключ3",12345);
	Структура.Вставить("Ключ4","12345");
	ПолеJSON1=JSON(Структура);
КонецПроцедуры


&НаКлиенте
Процедура Пример4(Команда)
	Пример4НаСервере();
КонецПроцедуры

&НаСервере
Процедура Пример4НаСервере()
	ТЗ1=Новый ТаблицаЗначений;
	ТЗ1.Колонки.Добавить("Код", Новый ОписаниеТипов("Число"));
	ТЗ1.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ТЗ1.Колонки.Добавить("ЭтоГруппа", Новый ОписаниеТипов("Булево"));
	ТЗ1.Колонки.Добавить("Комментарий");
	СтрокаТЗ=ТЗ1.Добавить();
	СтрокаТЗ.Код=1;
	СтрокаТЗ.Наименование="Группа";
	СтрокаТЗ.ЭтоГруппа=Истина;
	СтрокаТЗ.Комментарий="Наверное это действительно группа";
	СтрокаТЗ=ТЗ1.Добавить();
	СтрокаТЗ.Код=2;
	СтрокаТЗ.Наименование="Не группа";
	СтрокаТЗ.ЭтоГруппа=Ложь;
	СтрокаТЗ.Комментарий="Есть мнение, что это не группа";
	ПолеJSON1=JSON(ТЗ1);
КонецПроцедуры


&НаКлиенте
Процедура Пример5(Команда)
	//Структура=Новый Структура;
	//ТЗ2=Новый ТаблицаЗначений;
	//ТЗ2.Колонки.Добавить("Инструмент");
	//ТЗ2.Колонки.Добавить("Примечание");
	//СтрокаТЗ=ТЗ2.Добавить();
	//СтрокаТЗ.Инструмент="Вилы";
	//СтрокаТЗ.Примечание="Средство самообороны на садовом участке";
	//СтрокаТЗ=ТЗ2.Добавить();
	//СтрокаТЗ.Инструмент="Грабли";
	//СтрокаТЗ.Примечание="Применяются для сбора опавших листьев";
	//Структура.Вставить("СадовыеИнструменты",ТЗ2);
	//Массив=Новый Массив;
	//Массив.Добавить(123);
	//Массив.Добавить(456);
	//Массив.Добавить(789);
	//Структура.Вставить("Массив",Массив);
	//Структура.Вставить("Автор","BigB");
	//Структура.Вставить("Примечание","Парсер полностью отлажен.
	//|Дальнейшие действия будут направлены на оптимизацию алгоритма.
	//|Если понравилось Тогда плюсаните - буду рад.");
	//
	//ПолеJSON1=JSON(Структура);
КонецПроцедуры

&НаСервер
Процедура Пример5НаСервере()
	
	//Структура=Новый Структура;
	//ТЗ2=Новый ТаблицаЗначений;
	//ТЗ2.Колонки.Добавить("Инструмент");
	//ТЗ2.Колонки.Добавить("Примечание");
	//СтрокаТЗ=ТЗ2.Добавить();
	//СтрокаТЗ.Инструмент="Вилы";
	//СтрокаТЗ.Примечание="Средство самообороны на садовом участке";
	//СтрокаТЗ=ТЗ2.Добавить();
	//СтрокаТЗ.Инструмент="Грабли";
	//СтрокаТЗ.Примечание="Применяются для сбора опавших листьев";
	//Структура.Вставить("СадовыеИнструменты",ТЗ2);
	//Массив=Новый Массив;
	//Массив.Добавить(123);
	//Массив.Добавить(456);
	//Массив.Добавить(789);
	//Структура.Вставить("Массив",Массив);
	//Структура.Вставить("Автор","BigB");
	//Структура.Вставить("Примечание","Парсер полностью отлажен.
	//|Дальнейшие действия будут направлены на оптимизацию алгоритма.
	//|Если понравилось Тогда плюсаните - буду рад.");
	//
	//ПолеJSON1=JSON(Структура);
	
КонецПроцедуры



&НаКлиенте
Процедура Пример6(Команда)
	//ТекстовыйДокумент = ПолучитьМакет("Пример6");
	//ПолеJSON1=ТекстовыйДокумент.ПолучитьТекст();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//ТекстовыйДокумент=ПолучитьМакет("Тест_JSON");
	//ПолеJSON1=ТекстовыйДокумент.ПолучитьТекст();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработку(Команда)
	ХХ=UnJSON(ПолеJSON1);
	//ПолеJSON2=JSON(ХХ,Истина);
	ПолеJSON2=ХХ;

КонецПроцедуры

/// ========================================

//Не рекомендую объединять фунции ФорматированныйJSON и НеФорматированныйJSON
//в одну, так как функция ФорматированныйJSON обычно используется только в 
//отладочных целях. В реальной работе требуется быстродействие, а не красивость.

Функция JSON(Значение,Форматировать=Ложь) Экспорт
	Если Форматировать Тогда
		Возврат ФорматированныйJSON(Значение)
	КонецЕсли;
	Возврат НеФорматированныйJSON(Значение)
КонецФункции

//Функция имеет недостаток, она не проводит валидацию JSON-строки,
//поэтому если строка "битая" или неправильная, то результат непредсказуем. :-)
//В смысле — в результате работы парсинга будет возвращена какая-нибудь лабуда.
Функция UnJSON(СтрJSON) Экспорт
	Перем Значение;
	Если ПолучитьЗначениеJSON(СтрJSON,Значение)=0 Тогда
		Возврат Значение
	КонецЕсли;
	Возврат Неопределено
КонецФункции

//В функции конструкции условий расставлены по частоте их использования.
//Если кому нибудь удастся более оптимально (в смысле быстродействия) написать,
//то просьба сообщить мне на bigb.forum@gmail.com
Функция НеФорматированныйJSON(Значение)
	Разделитель="";
	
	ТипЗн=ТипЗнч(Значение);

	Если ТипЗн=Тип("Строка") Тогда
		Стр=""""+Экранировать(Значение)+""""

	ИначеЕсли ТипЗн=Тип("Число") ИЛИ ТипЗнч(Значение)=Тип("Булево") Тогда
		Стр=XMLСтрока(Значение)

	ИначеЕсли ТипЗн=Тип("Дата") Тогда
		Стр=""""+?(ЗначениеЗаполнено(Значение),XMLСтрока(Значение),"")+""""

	ИначеЕсли ТипЗн=Тип("Структура") ИЛИ ТипЗн=Тип("Соответствие") Тогда
		Стр="{";
		Для Каждого Параметр Из Значение Цикл
			Стр=Стр+Разделитель+Символы.ПС+""""+Параметр.Ключ+""":"+НеФорматированныйJSON(Параметр.Значение);
			Разделитель=","
		КонецЦикла;
		Стр=Стр+Символы.ПС+"}";

	ИначеЕсли ТипЗн=Тип("Массив") Тогда
		Стр="[";
		Для Каждого Элемент Из Значение Цикл
			Стр=Стр+Разделитель+Символы.ПС+НеФорматированныйJSON(Элемент);
			Разделитель=","
		КонецЦикла;
		Стр=Стр+Символы.ПС+"]";

	ИначеЕсли ТипЗн=Тип("ТаблицаЗначений") Тогда
		Колонки=Значение.Колонки;
		Массив=Новый Массив;
		Для Каждого СтрокаТЗ Из Значение Цикл
			Структура=Новый Структура;
			Для Каждого Колонка Из Колонки Цикл
				Структура.Вставить(Колонка.Имя,СтрокаТЗ[Колонка.Имя])
			КонецЦикла;
			Массив.Добавить(Структура);
		КонецЦикла;
		Стр=НеФорматированныйJSON(Массив)

	ИначеЕсли Значение=Неопределено Тогда
		Стр="null"

	Иначе
		Стр=""""+Экранировать(Значение)+""""
	КонецЕсли;

	Возврат Стр
КонецФункции

//Функция более медленная, чем НеФорматированныйJSON,
//но дает более структурированное представление данных.
//Рекомендуется использовать её только в отладочных целях.
Функция ФорматированныйJSON(Значение,Отступ="")
	Разделитель="";
	
	ТипЗн=ТипЗнч(Значение);

	Если ТипЗн=Тип("Строка") Тогда
		Стр=""""+Экранировать(Значение)+""""

	ИначеЕсли ТипЗн=Тип("Число") ИЛИ ТипЗнч(Значение)=Тип("Булево") Тогда
		Стр=XMLСтрока(Значение)

	ИначеЕсли ТипЗн=Тип("Дата") Тогда
		Стр=""""+?(ЗначениеЗаполнено(Значение),XMLСтрока(Значение),"")+""""

	ИначеЕсли ТипЗн=Тип("Структура") ИЛИ ТипЗн=Тип("Соответствие") Тогда
		Стр="{";
		Для Каждого Параметр Из Значение Цикл
			Стр=Стр+Разделитель+Символы.ПС+Отступ+""""+Параметр.Ключ+""":"+ФорматированныйJSON(Параметр.Значение,Отступ+Символы.Таб);
			Разделитель=","
		КонецЦикла;
		Стр=Стр+Символы.ПС+Отступ+"}";

	ИначеЕсли ТипЗн=Тип("Массив") Тогда
		Стр="[";
		Для Каждого Элемент Из Значение Цикл
			Стр=Стр+Разделитель+Символы.ПС+Отступ+ФорматированныйJSON(Элемент,Отступ);
			Разделитель=","
		КонецЦикла;
		Стр=Стр+Символы.ПС+Отступ+"]";

	ИначеЕсли ТипЗн=Тип("ТаблицаЗначений") Тогда
		Колонки=Значение.Колонки;
		Массив=Новый Массив;
		Для Каждого СтрокаТЗ Из Значение Цикл
			Структура=Новый Структура;
			Для Каждого Колонка Из Колонки Цикл
				Структура.Вставить(Колонка.Имя,СтрокаТЗ[Колонка.Имя])
			КонецЦикла;
			Массив.Добавить(Структура);
		КонецЦикла;
		Стр=ФорматированныйJSON(Массив)

	ИначеЕсли Значение=Неопределено Тогда
		Стр="null"

	Иначе
		Стр=""""+Экранировать(Значение)+""""
	КонецЕсли;

	Возврат Стр
КонецФункции

//Экранирует недопустимые символы
Функция Экранировать(Стр)
	Х=СтрЗаменить(Стр,"""","\""");
	//Х=СтрЗаменить(Х,"'","\'"); //??? Не уверен в этой строке. Кто нибудь может подтвердить или опровергнуть нужность этой строки.
	Х=СтрЗаменить(Х,"\","\\");
	Х=СтрЗаменить(Х,"/","\/"); //Тут тоже не уверен, но пока оставлю.
	Х=СтрЗаменить(Х,Символ(8),"\b");
	Х=СтрЗаменить(Х,Символы.ПФ,"\f");
	Х=СтрЗаменить(Х,Символы.ПС,"\n");
	Х=СтрЗаменить(Х,Символы.ВК,"\r");
	Х=СтрЗаменить(Х,Символы.ВТаб,"\t");
	Возврат Х
КонецФункции

Функция ПолучитьЗначениеJSON(СтрJSON,Значение,Позиция=1,Ключ="")
	Перем Кавычка;

	ЗначениеВСтроке="";
	Кавычек=0;
	Комментарий=Ложь;
	Строка=Ложь;

	Пока Позиция<=СтрДлина(СтрJSON) Цикл
		ХХ=Сред(СтрJSON,Позиция,2);
		Х=Лев(ХХ,1);
		Позиция=Позиция+1;

		Если Х>" " ИЛИ Строка Тогда //Отсекаем всякий хлам

			Если Комментарий Тогда
				//Это комментарий. Крутимся в цикле пока не встретится конец комментария
				Если ХХ="*/" Тогда
					//Комментарий закончился
					Комментарий=Ложь;
					Позиция=Позиция+1;
				КонецЕсли;

			ИначеЕсли Х="\" Тогда
				Позиция=Позиция+1;
				ХХ=ВРег(ХХ);
				Если ХХ="\""" Тогда ЗначениеВСтроке=ЗначениеВСтроке+""""
				ИначеЕсли ХХ="\\" Тогда ЗначениеВСтроке=ЗначениеВСтроке+"\"
				ИначеЕсли ХХ="\/" Тогда ЗначениеВСтроке=ЗначениеВСтроке+"/"
				ИначеЕсли ХХ="\B" Тогда ЗначениеВСтроке=ЗначениеВСтроке+Символ(8)
				ИначеЕсли ХХ="\F" Тогда ЗначениеВСтроке=ЗначениеВСтроке+Символы.ПФ //перевод формы (страницы)
				ИначеЕсли ХХ="\N" Тогда ЗначениеВСтроке=ЗначениеВСтроке+Символы.ПС //перевод строки
				ИначеЕсли ХХ="\R" Тогда ЗначениеВСтроке=ЗначениеВСтроке+Символы.ВК //возврат каретки
				ИначеЕсли ХХ="\T" Тогда ЗначениеВСтроке=ЗначениеВСтроке+Символы.ВТаб //символ вертикальной табуляции
				ИначеЕсли ХХ="\U" Тогда
					ЗначениеВСтроке=ЗначениеВСтроке+Символ(Hex2Число(Сред(СтрJSON,Позиция,4))); //шестнадцатиричное число
					Позиция=Позиция+4
				КонецЕсли;

			ИначеЕсли Строка Тогда
				//Если строка не закончилась - то пропускаем управляющие символы
				Если Х=Кавычка Тогда
					//Закончилась строка
					Строка=Ложь;
					Кавычек=Кавычек+1;
				Иначе
					ЗначениеВСтроке=ЗначениеВСтроке+Х;
				КонецЕсли;

			ИначеЕсли ХХ="/*" Тогда
				//Начался комментарий
				Комментарий=Истина;
				Позиция=Позиция+1;

			ИначеЕсли Найти("""'{}[]:,",Х)>0 Тогда
				Если Х="""" ИЛИ Х="'" Тогда
					//Началась строка
					//Строка - коллекция нуля или больше символов Unicode, заключенная в
					//двойные кавычки, используя "\" в качестве символа экранирования.
					//Символ представляется как односимвольная строка.
					//Похожий синтаксис используется в C и Java.
					Строка=Истина;
					Кавычка=Х;
					Кавычек=Кавычек+1;

				ИначеЕсли Х="{" Тогда
					//Начался объект
					//Объект - неупорядоченный набор пар ключ/значение.
					//Объект начинается с "{" и заканчивается "}".
					//Каждое имя сопровождается ":", пары ключ/значение разделяются ",".
					ОбъектСоотв=Новый Соответствие;
					//Объект=Новый Структура;
					Пока Истина Цикл
						//Получим ключ и значение
						Ключ="";
						Режим=ПолучитьЗначениеJSON(СтрJSON,Значение,Позиция,Ключ);
						//0 - есть значение и не конец объекта (запятая)
						//1 - есть значение и конец объекта
						//2 - нет значения и не конец объекта (запятая)
						//3 - нет значения и конец объекта
						Если Режим=0 Тогда
							ОбъектСоотв.Вставить(Ключ,Значение);
						ИначеЕсли Режим=1 Тогда
							ОбъектСоотв.Вставить(Ключ,Значение);
							Прервать
						ИначеЕсли Режим=3 Тогда
							Прервать
						КонецЕсли;
					КонецЦикла;
					Значение=ОбъектСоотв;
					Возврат 0

				ИначеЕсли Х="[" Тогда
					//Начался массив
					//Массив - упорядоченная коллекция значений.
					//Массив начинается с "[" и заканчивается "]".
					//Значения разделены ",".
					Массив=Новый Массив;
					Пока Истина Цикл
						Режим=ПолучитьЗначениеJSON(СтрJSON,Значение,Позиция);
						//0 - есть значение и не конец массива (запятая)
						//1 - есть значение и конец массива
						//2 - нет значения и не конец массива (запятая)
						//3 - нет значения и конец массива
						Если Режим=0 Тогда
							Массив.Добавить(Значение);
						ИначеЕсли Режим=1 Тогда
							Массив.Добавить(Значение);
							Прервать
						ИначеЕсли Режим=3 Тогда
							Прервать
						КонецЕсли;
					КонецЦикла;
					Значение=Массив;
					Возврат 0

				ИначеЕсли Х="]" ИЛИ Х="}" Тогда
					//Закончился массив/объект
					Если ЗначениеВСтроке="" И Кавычек=0 Тогда
						Возврат 3 //нет значения и конец массива/объекта
					Иначе
						Значение=ПолучитьЗначениеИзСтроки(ЗначениеВСтроке,Кавычек);
						Возврат 1 //есть значение и конец массива/объекта
					КонецЕсли;

				ИначеЕсли Х=":" Тогда
					Ключ=ЗначениеВСтроке;
					Возврат ПолучитьЗначениеJSON(СтрJSON,Значение,Позиция);

				Иначе
					// запятая
					Прервать
				КонецЕсли;

			Иначе
				ЗначениеВСтроке=ЗначениеВСтроке+Х;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если Кавычек>0 Тогда
		Значение=ЗначениеВКавычках(ЗначениеВСтроке);
	Иначе
		Если ЗначениеВСтроке="" Тогда
			Возврат 2
		Иначе
			Значение=ЗначениеБезКавычек(ЗначениеВСтроке)
		КонецЕсли;
	КонецЕсли;
	Возврат 0
КонецФункции

Функция Hex2Число(Hex) Экспорт
	Стр=ВРег(СокрЛП(Hex));
	Dec=0;
	Для Х=1 По СтрДлина(Стр) Цикл
		Dec=Dec+Найти("123456789ABCDEF",Сред(Стр,Х,1))*Pow(16,СтрДлина(Стр)-Х)
	КонецЦикла;
	Возврат Dec
КонецФункции

Функция ПолучитьЗначениеИзСтроки(ЗначениеВСтроке,Кавычек)
	Если Кавычек>0 Тогда
		Возврат ЗначениеВКавычках(ЗначениеВСтроке)
	ИначеЕсли ЗначениеВСтроке="" Тогда
		Возврат Неопределено
	КонецЕсли;
	Возврат ЗначениеБезКавычек(ЗначениеВСтроке)
КонецФункции

Функция ЗначениеВКавычках(ЗначениеВСтроке)
	//Это или строка или дата.
	//Дата пока не обрабатывается (потом надо дописать)
	//Пока всегда возвращаем просто строку
	Возврат ЗначениеВСтроке
КонецФункции

Функция ЗначениеБезКавычек(ЗначениеВСтроке)
	//Это число, булево или null.
	//Хотя здесь могут быть и строки. Например: {Code:123}
	Стр=ВРег(ЗначениеВСтроке);
	Если Стр="TRUE" Тогда Возврат Истина
	ИначеЕсли Стр="FALSE" Тогда Возврат Ложь
	ИначеЕсли Стр="NULL" Тогда Возврат Неопределено
	КонецЕсли;

	//Пробежимся по предполагаемому "числу"
	Экспонента=Ложь;
	ХХ=" ";
	Для Индекс=1 По СтрДлина(Стр) Цикл
		Х=Сред(Стр,Индекс,1);
		Если Найти("0123456789.+-",Х) Тогда
			ХХ=ХХ+Х
		ИначеЕсли Х="E" Тогда
			Экспонента=Истина;
			УУ=XMLЗначение(Тип("Число"),ХХ);
			ХХ=" ";
		Иначе
			Возврат ЗначениеВСтроке //Это точно не число, а строка
		КонецЕсли;
	КонецЦикла;

	ХХ=XMLЗначение(Тип("Число"),ХХ);
	Если Экспонента Тогда
		ХХ=УУ*Pow(10,ХХ)
	КонецЕсли;

	Возврат ХХ
КонецФункции




							







