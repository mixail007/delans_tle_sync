
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ServiceAPI

Функция ИдентификаторПечатнойФормы(Подробно = Ложь, ИспользоватьФаксимиле = Ложь) Экспорт
	
	Возврат ?(Подробно, "АктОбОказанииУслугПодробно", "АктОбОказанииУслуг") + ?(ИспользоватьФаксимиле, "Факсимиле", "");
	
КонецФункции

Функция ПредставлениеПФ(Подробно = Ложь, ИспользоватьФаксимиле = Ложь) Экспорт
	
	ПредставлениеПФ = НСтр("ru ='Акт об оказании услуг'");
	
	ДополнительноеПредставление = "";
	Если Подробно Тогда
		
		ДополнительноеПредставление = НСтр("ru ='подробно'");
		
	КонецЕсли;
	
	Если ИспользоватьФаксимиле Тогда
		
		ДополнительноеПредставление = ДополнительноеПредставление + ?(Подробно, ", ", "") + НСтр("ru ='факсимиле'");
		
	КонецЕсли;
	
	Если Подробно
		ИЛИ ИспользоватьФаксимиле Тогда
		
		ДополнительноеПредставление = НСтр("ru =' ('") + ДополнительноеПредставление + НСтр("ru =')'");
		
	КонецЕсли;
	
	Возврат ПредставлениеПФ + ДополнительноеПредставление;
	
КонецФункции

Функция МатрицаВозможныхВариантов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"Выбрать Ложь Подробно, Ложь ИспользоватьФаксимиле
	|Объединить
	|Выбрать Ложь, Истина
	|Объединить
	|Выбрать Истина, Ложь
	|Объединить
	|Выбрать Истина, Истина";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция КлючПараметровПечати() Экспорт
	
	Возврат "ПАРАМЕТРЫ_ПЕЧАТИ_Универсальные_АктОбОказанииУслуг";
	
КонецФункции

Функция ПолныйПутьКМакету(Подробно = Ложь) Экспорт
	
	Возврат ?(Подробно, "Обработка.ПечатьАктаОбОказанииУслуг.ПФ_MXL_АктОбОказанииУслугПодробно", "Обработка.ПечатьАктаОбОказанииУслуг.ПФ_MXL_АктОбОказанииУслуг");
	
КонецФункции

Функция СформироватьПФ(ОписаниеПечатнойФормы, ДанныеОбъектовПечати, ОбъектыПечати, Подробно) Экспорт
	Перем Ошибки, ПервыйДокумент, НомерСтрокиНачало;
	
	Макет				= УправлениеПечатью.МакетПечатнойФормы(ОписаниеПечатнойФормы.ПолныйПутьКМакету);
	ТабличныйДокумент	= ОписаниеПечатнойФормы.ТабличныйДокумент;
	ДанныеПечати		= Новый Структура;
	ЕстьТЧРаботыУслуги	= (ДанныеОбъектовПечати.Колонки.Найти("ТаблицаРаботыУслуги") <> Неопределено);
	ПредставлениеСкидки = Константы.ПредставлениеСкидкиВПечатнойФорме.Получить();
	
	Для каждого ДанныеОбъекта Из ДанныеОбъектовПечати Цикл
		
		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало, ДанныеПечати);
		
		СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ДанныеОбъекта.Организация, ДанныеОбъекта.ДатаДокумента, ,);
		СведенияОбКонтрагенте = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ДанныеОбъекта.Контрагент, ДанныеОбъекта.ДатаДокумента, ,);
		
		ЛоготипЗаполнен = ЗначениеЗаполнено(ДанныеОбъекта.ФайлЛоготип);
		ИмяМакета = ?(ЛоготипЗаполнен, "ЗаголовокСЛоготипом", "Заголовок");
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяМакета, "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ШаблонЗаголовка = Нстр("ru ='Акт об оказании услуг № %1 от %2'");
			НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ДанныеОбъекта.ДатаДокумента, ДанныеОбъекта.Номер, ДанныеОбъекта.Префикс);
			ДанныеПечати.Вставить("ПредставлениеДокумента", СтрШаблон(ШаблонЗаголовка, НомерДокумента, Формат(ДанныеОбъекта.ДатаДокумента, "ДЛФ=DD")));
			
			Если ЛоготипЗаполнен Тогда
				
				ДанныеКартинки = ПрисоединенныеФайлы.ПолучитьДвоичныеДанныеФайла(ДанныеОбъекта.ФайлЛоготип);
				Если ЗначениеЗаполнено(ДанныеКартинки) Тогда
					
					ОбластьМакета.Рисунки.Логотип.Картинка = Новый Картинка(ДанныеКартинки);
					
				КонецЕсли;
				
			КонецЕсли;
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Поставщик", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ДанныеПечати.Вставить("ПредставлениеПоставщика", УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
			ДанныеПечати.Вставить("ОснованиеПечати", ДанныеОбъекта.ОснованиеПечати);
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Покупатель", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ДанныеПечати.Вставить("ПредставлениеПолучателя", УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбКонтрагенте, "ПолноеНаименование,ИНН,КПП,РегистрационныйНомер,ЮридическийАдрес,Телефоны,"));
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ЕстьСкидки = (ДанныеОбъекта.ТаблицаРаботыУслуги.Итог("ЕстьСкидка") <> 0);
		
		ИмяОбласти = ?(ЕстьСкидки, "ШапкаТаблицыСоСкидкой", "ШапкаТаблицы");
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяОбласти, "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ИмяОбласти = ?(ЕстьСкидки, "СтрокаСоСкидкой", "Строка");
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяОбласти, "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			СтруктураИтогов = Новый Структура("Сумма, СуммаНДС, Всего, Количество, НомерСтроки, ПредставлениеСкидки, ЕстьСкидки, Подробно, ЕстьСтавкаНольПроцентов", 0, 0, 0, 0, 0, ПредставлениеСкидки, ЕстьСкидки, Подробно, Ложь);
			// Наборы
			СтруктураИтогов.Вставить("ЕстьНаборы", ДанныеОбъекта.ТаблицаРаботыУслуги.Колонки.Найти("НоменклатураНабора")<>Неопределено);
			Если СтруктураИтогов.ЕстьНаборы Тогда
				// Наборы неподходящего типа разворачиваем до составляющих
				РазвернутьНабор = Ложь;
				Для каждого СтрокаРаботыУслуги Из ДанныеОбъекта.ТаблицаРаботыУслуги Цикл
					Если СтрокаРаботыУслуги.ЭтоНабор 
						И СтрокаРаботыУслуги.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Услуга 
						И СтрокаРаботыУслуги.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Работа Тогда
						РазвернутьНабор = Истина;
						Продолжить;
					КонецЕсли; 
					Если РазвернутьНабор 
						И НЕ ЗначениеЗаполнено(СтрокаРаботыУслуги.НоменклатураНабора) Тогда
						РазвернутьНабор = Ложь;
					КонецЕсли;
					Если РазвернутьНабор Тогда
						СтрокаРаботыУслуги.НоменклатураНабора = Неопределено;
						СтрокаРаботыУслуги.ХарактеристикаНабора = Неопределено;
						СтрокаРаботыУслуги.НеобходимоВыделитьКакСоставНабора = Ложь;
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли; 
			// Конец Наборы
			
			ПараметрыНоменклатуры = Новый Структура;
			
			Для каждого СтрокаРаботыУслуги Из ДанныеОбъекта.ТаблицаРаботыУслуги Цикл
				
				Если СтрокаРаботыУслуги.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Услуга
					И СтрокаРаботыУслуги.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Работа Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаРаботыУслуги, ДанныеПечати, ПараметрыНоменклатуры, СтруктураИтогов);
				ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				// Наборы
				Если СтруктураИтогов.ЕстьНаборы Тогда
					НаборыСервер.УчестьОформлениеСтрокиНабора(ТабличныйДокумент, ОбластьМакета, СтрокаРаботыУслуги);
				КонецЕсли;
				// Конец Наборы
			
			КонецЦикла;
			
		КонецЕсли;
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Итого", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ДанныеПечати.Вставить("Всего", УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.Сумма));
			ДанныеПечати.Вставить("ЗаголовокНДС", ПечатьДокументовУНФ.ПредставлениеЗаголовкаНДС(СтруктураИтогов.СуммаНДС, ДанныеОбъекта.СуммаВключаетНДС, Ложь, СтруктураИтогов.ЕстьСтавкаНольПроцентов));
			ДанныеПечати.Вставить("ВсегоНДС", ?(СтруктураИтогов.СуммаНДС = 0 И НЕ СтруктураИтогов.ЕстьСтавкаНольПроцентов, "-", УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.СуммаНДС, , "0,00")));
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "СуммаПрописью", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ШаблонИтоговойСтроки = НСтр("ru ='Всего наименований %1, на сумму %2'");
			ДанныеПечати.Вставить("ИтоговаяСтрока", СтрШаблон(ШаблонИтоговойСтроки, Строка(СтруктураИтогов.Количество), УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.Всего, ДанныеОбъекта.ВалютаДокумента)));
			ДанныеПечати.Вставить("СуммаПрописью", РаботаСКурсамиВалют.СформироватьСуммуПрописью(СтруктураИтогов.Всего, ДанныеОбъекта.ВалютаДокумента));
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ИмяОбласти = ?(ДанныеОбъекта.ИспользоватьФаксимиле = Перечисления.ДаНет.Да, "ПодписиСФаксимиле", "ПодписиБезФаксимиле");
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяОбласти, , Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ОбластьМакета.Параметры.Заполнить(ДанныеОбъекта);
			
			Если ДанныеОбъекта.ИспользоватьФаксимиле = Перечисления.ДаНет.Да Тогда
				
				ПодписиИФаксимиле = Новый Соответствие; // Ключ - имя каринки в области, Значение - имя реквизита
				ПодписиИФаксимиле.Вставить("ПодписьРуководителя", "ФаксимилеРуководителя");
				ПодписиИФаксимиле.Вставить("ПечатьОрганизации", "ФаксимилеПечати");
				
				ПодписьДокументовУНФ.ЗаполнитьФаксимилеВОбластиМакета(ОбластьМакета, ДанныеОбъекта, ПодписиИФаксимиле, Ошибки);
				
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеОбъекта.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

Процедура ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаТабличнойЧасти, ДанныеПечати, ПараметрыНоменклатуры, СтруктураИтогов)
	
	ДанныеПечати.Очистить();
	
	Если СтруктураИтогов.ЕстьНаборы И СтрокаТабличнойЧасти.ЭтоНабор=Истина Тогда
		НомерСтроки = 0;
	Иначе
		СтруктураИтогов.НомерСтроки = СтруктураИтогов.НомерСтроки+1;
		НомерСтроки = СтруктураИтогов.НомерСтроки;
	КонецЕсли;
	ДанныеПечати.Вставить("НомерСтроки", НомерСтроки);
	
	ПараметрыНоменклатуры.Очистить();
	ПараметрыНоменклатуры.Вставить("Содержание", СтрокаТабличнойЧасти.Содержание);
	ПараметрыНоменклатуры.Вставить("ПредставлениеНоменклатуры", СтрокаТабличнойЧасти.ПредставлениеНоменклатуры);
	ПараметрыНоменклатуры.Вставить("ПредставлениеХарактеристики", СтрокаТабличнойЧасти.Характеристика);
	// Наборы
	Если СтруктураИтогов.ЕстьНаборы Тогда
		ПараметрыНоменклатуры.Вставить("НеобходимоВыделитьКакСоставНабора", СтрокаТабличнойЧасти.НеобходимоВыделитьКакСоставНабора);
	КонецЕсли; 
	
	ДанныеПечати.Вставить("ПредставлениеНоменклатуры", ПечатьДокументовУНФ.ПредставлениеНоменклатуры(ПараметрыНоменклатуры));
	ДанныеПечати.Вставить("Количество", СтрокаТабличнойЧасти.Количество);
	ДанныеПечати.Вставить("ЕдиницаИзмерения", СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	ДанныеПечати.Вставить("Цена", СтрокаТабличнойЧасти.Цена);
	
	Если СтруктураИтогов.Подробно Тогда
		
		ДанныеПечати.Вставить("Время", СтрокаТабличнойЧасти.Время);
		ДанныеПечати.Вставить("Кратность", СтрокаТабличнойЧасти.Кратность);
		ДанныеПечати.Вставить("Коэффициент", СтрокаТабличнойЧасти.Коэффициент);
		
	КонецЕсли;
	
	Если СтруктураИтогов.ЕстьСкидки Тогда
		
		ДанныеПечати.Вставить("ПредставлениеСкидки", ПечатьДокументовУНФ.ПредставлениеСкидки(СтрокаТабличнойЧасти, СтруктураИтогов));
		
	КонецЕсли;
	
	ДанныеПечати.Вставить("Сумма", СтрокаТабличнойЧасти.Сумма);
	
	Если СтруктураИтогов.ЕстьСтавкаНольПроцентов = Ложь // Нет смысла проверять каждую строку, если уже нашли...
		И СтрокаТабличнойЧасти.Владелец().Колонки.Найти("СтавкаНДС") <> Неопределено
		И СтрокаТабличнойЧасти.СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль() Тогда
		
		СтруктураИтогов.ЕстьСтавкаНольПроцентов = Истина;
		
	КонецЕсли;

	
	Если НЕ СтруктураИтогов.ЕстьНаборы ИЛИ НЕ СтрокаТабличнойЧасти.ЭтоНабор Тогда
		СтруктураИтогов.Количество = СтруктураИтогов.Количество + 1;
		СтруктураИтогов.Сумма		= СтруктураИтогов.Сумма		+ СтрокаТабличнойЧасти.Сумма;
		СтруктураИтогов.СуммаНДС	= СтруктураИтогов.СуммаНДС	+ СтрокаТабличнойЧасти.СуммаНДС;
		СтруктураИтогов.Всего		= СтруктураИтогов.Всего 	+ СтрокаТабличнойЧасти.Всего;
	КонецЕсли; 
	
КонецПроцедуры
 
#КонецЕсли