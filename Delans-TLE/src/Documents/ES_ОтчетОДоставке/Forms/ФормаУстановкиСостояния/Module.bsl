
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивЗаказов") Тогда
		ЗаполнитьТаблицуЗаказами(Параметры.МассивЗаказов);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЗаказами(МассивЗаказов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокЗаказПокупателя.Ссылка
		|ПОМЕСТИТЬ ВТ_Заказы
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ДокЗаказПокупателя
		|ГДЕ
		|	ДокЗаказПокупателя.Ссылка В(&МассивЗаказов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ  РАЗРЕШЕННЫЕ
		|	ВТ_Заказы.Ссылка КАК ДокЗаказ,
		|	ES_СостоянияЗаказовПокупателяСрезПоследних.СостояниеЗаказа,
		|	ES_СтатусыЗаказовСрезПоследних.СтатусЗаказа
		|ИЗ
		|	ВТ_Заказы КАК ВТ_Заказы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ES_СтатусыЗаказов.СрезПоследних(
		|				,
		|				ЗаказДоставки В
		|					(ВЫБРАТЬ
		|						ВТ_Заказы.Ссылка
		|					ИЗ
		|						ВТ_Заказы КАК ВТ_Заказы)) КАК ES_СтатусыЗаказовСрезПоследних
		|		ПО ВТ_Заказы.Ссылка = ES_СтатусыЗаказовСрезПоследних.ЗаказДоставки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ES_СостоянияЗаказовПокупателя.СрезПоследних(
		|				,
		|				ЗаказПокупателя В
		|					(ВЫБРАТЬ
		|						ВТ_Заказы.Ссылка
		|					ИЗ
		|						ВТ_Заказы КАК ВТ_Заказы)) КАК ES_СостоянияЗаказовПокупателяСрезПоследних
		|		ПО ВТ_Заказы.Ссылка = ES_СостоянияЗаказовПокупателяСрезПоследних.ЗаказПокупателя";
	
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НовСтр = СписокЗаказов.Добавить();
			НовСтр.Отметка = Истина;
			НовСтр.ЗаказНаДоставку = Выборка.ДокЗаказ;
			НовСтр.ТекущееСостояние = Выборка.СостояниеЗаказа;
			НовСтр.СтатусЗаказа = Выборка.СтатусЗаказа;
		КонецЦикла;
	
	КонецЕсли; 

	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояние(Команда)
	
	Если НЕ ЗначениеЗаполнено(ВыбранноеСостояние) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выберите состояние";
		Сообщение.Поле = "ВыбранноеСостояние";
		Сообщение.Сообщить(); 
		Возврат;
	КонецЕсли; 
	
	УстановитьСостояниеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеНаСервере()
	
	ЗаказыДляУстановкиСостояния = Новый Массив;
	Для каждого Строка Из СписокЗаказов Цикл
		Если Строка.Отметка Тогда
			ЗаказыДляУстановкиСостояния.Добавить(Строка.ЗаказНаДоставку);
		КонецЕсли; 
	КонецЦикла;
	
	Если ЗаказыДляУстановкиСостояния.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Ответственный = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнойОтветственный");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ES_СостоянияЗаказовПокупателяСрезПоследних.ЗаказПокупателя,
		|	ES_СостоянияЗаказовПокупателяСрезПоследних.СостояниеЗаказа
		|ИЗ
		|	РегистрСведений.ES_СостоянияЗаказовПокупателя.СрезПоследних(, ЗаказПокупателя В (&ЗаказыДляУстановкиСостояния)) КАК ES_СостоянияЗаказовПокупателяСрезПоследних
		|ГДЕ
		|	ES_СостоянияЗаказовПокупателяСрезПоследних.СостояниеЗаказа <> &СостояниеЗаказа";
	
	Запрос.УстановитьПараметр("ЗаказыДляУстановкиСостояния", ЗаказыДляУстановкиСостояния);
	Запрос.УстановитьПараметр("СостояниеЗаказа", ВыбранноеСостояние);
	
	ПериодДляЗаписи = ТекущаяДата();
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДокОбъект = Выборка.ЗаказПокупателя.ПолучитьОбъект();
			ДокОбъект.СостояниеЗаказа = ВыбранноеСостояние;
			
			РежимЗаписи = ?(ДокОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
			
			Попытка
				ДокОбъект.Записать(РежимЗаписи);
			Исключение
			    Сообщить("Не удалось установить состояние " + ДокОбъект.Ссылка);
			КонецПопытки;
			
			//НаборЗаписей = РегистрыСведений.ES_СостоянияЗаказовПокупателя.СоздатьНаборЗаписей();
			//НаборЗаписей.Отбор.ЗаказПокупателя.Установить(Выборка.ЗаказПокупателя);
			//НаборЗаписей.Отбор.Период.Установить(ПериодДляЗаписи);
			//НаборЗаписей.Прочитать();
			
			//НоваяЗапись = НаборЗаписей.Добавить();
			//НоваяЗапись.Период 				= ПериодДляЗаписи;
			//НоваяЗапись.ЗаказПокупателя 	= Выборка.ЗаказПокупателя;
			//НоваяЗапись.СостояниеЗаказа 	= ВыбранноеСостояние;
			//НаборЗаписей[0].Ответственный 		= Ответственный;
			//НаборЗаписей[0].ИнфаОРегистраторе 	= "Обработка ""Распределение заказов""";
			
			//НаборЗаписей.Записать();
		КонецЦикла;
		
		Сообщить("Новое состояние установлено");
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для каждого Строка Из СписокЗаказов Цикл
		Строка.Отметка = Истина;
	КонецЦикла; 
	         
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Для каждого Строка Из СписокЗаказов Цикл
		Строка.Отметка = Ложь;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаказовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаказовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры
