
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	УстановитьПериод();
	УстновитьНоменклатуруИХарактеристику();
	ЗаполнитьСписокЗаказовНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПериод()
	
	Период.ДатаНачала = НачалоНедели(ТекущаяДата());
	Период.ДатаОкончания = КонецНедели(ТекущаяДата());
	
КонецПроцедуры

&НаСервере
Процедура УстновитьНоменклатуруИХарактеристику()
	
	Номенклатура = ES_ОбщегоНазначения.ПолучитьСтартовуюНастройку(Перечисления.ES_ВидыСтартовыхНастроек.УслугаДоставки);
	Характеристика = ES_ОбщегоНазначения.ПолучитьСтартовуюНастройку(Перечисления.ES_ВидыСтартовыхНастроек.Характеристика);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокЗаказовНаСервере()
	
	СписокЗаказов.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПокупателяЗапасы.Ссылка,
		|	ЗаказПокупателяЗапасы.Номенклатура,
		|	ЗаказПокупателяЗапасы.Характеристика,
		|	ЕСТЬNULL(ЗаказПокупателяЗапасы.Цена, 0) КАК Цена
		|ПОМЕСТИТЬ ВТ_Запасы
		|ИЗ
		|	Документ.ЗаказПокупателя.Запасы КАК ЗаказПокупателяЗапасы
		|ГДЕ
		|	ЗаказПокупателяЗапасы.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
		|	И ЗаказПокупателяЗапасы.Ссылка.Контрагент = &Контрагент
		|	И ЗаказПокупателяЗапасы.Номенклатура = &Номенклатура
		|	И ЗаказПокупателяЗапасы.Характеристика = &Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказПокупателяЗапасы.Ссылка,
		|	ЗаказПокупателяЗапасы.Номенклатура,
		|	ЗаказПокупателяЗапасы.Характеристика,
		|	ЕСТЬNULL(ЗаказПокупателяЗапасы.Сумма, 0) КАК Сумма
		|ПОМЕСТИТЬ ВТ_ЗапасыБезДоставки
		|ИЗ
		|	Документ.ЗаказПокупателя.Запасы КАК ЗаказПокупателяЗапасы
		|ГДЕ
		|	ЗаказПокупателяЗапасы.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
		|	И ЗаказПокупателяЗапасы.Номенклатура <> &Номенклатура
		|	И ЗаказПокупателяЗапасы.Ссылка.Контрагент = &Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказПокупателя.Ссылка КАК Заказ,
		|	ЗаказПокупателя.Номер КАК Номер,
		|	ЗаказПокупателя.Дата КАК Дата,
		|	ЗаказПокупателя.ES_НомерНакладной КАК НомерНакладной,
		|	ВТ_Запасы.Номенклатура КАК Номенклатура,
		|	ВТ_Запасы.Характеристика КАК Характеристика,
		|	ВТ_ЗапасыБезДоставки.Номенклатура КАК ДругаяНоменклатура,
		|	ВТ_ЗапасыБезДоставки.Характеристика КАК ДругаяХарактеристика,
		|	ВТ_ЗапасыБезДоставки.Сумма КАК ДругаяСумма,
		|	ВТ_Запасы.Цена КАК Цена
		|ИЗ
		|	ВТ_Запасы КАК ВТ_Запасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|		ПО ВТ_Запасы.Ссылка = ЗаказПокупателя.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗапасыБезДоставки КАК ВТ_ЗапасыБезДоставки
		|		ПО ВТ_Запасы.Ссылка = ВТ_ЗапасыБезДоставки.Ссылка
		|ИТОГИ
		|	МАКСИМУМ(ДругаяНоменклатура),
		|	МАКСИМУМ(ДругаяХарактеристика),
		|	СУММА(ДругаяСумма)
		|ПО
		|	Заказ";
		
	Запрос.УстановитьПараметр("Контрагент", Заказчик);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(Период.ДатаОкончания));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаЗаказ = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Пока ВыборкаЗаказ.Следующий() Цикл
		
		НовСтр = СписокЗаказов.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаЗаказ);
		//НовСтр.Заказ = ВыборкаЗаказ.Заказ;
		//НовСтр.Дата = ВыборкаЗаказ.Дата;
		//НовСтр.Номер = ВыборкаЗаказ.Номер;
		//НовСтр.НомерНакладной = ВыборкаЗаказ.НомерНакладной;
		
		Выборка = ВыборкаЗаказ.Выбрать();
		мДругиеУслуги = "";
		мСтоимостьДругихУслуг = 0;
		мНоменклатура = Справочники.Номенклатура.ПустаяСсылка();
		мХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		мТекущаяЦена = 0;
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.Номенклатура) Тогда
				мНоменклатура = Выборка.Номенклатура;
			КонецЕсли;
			Если ЗначениеЗаполнено(Выборка.Характеристика) Тогда
				мХарактеристика = Выборка.Характеристика;
			КонецЕсли;
			Если ЗначениеЗаполнено(Выборка.Цена) Тогда
				мТекущаяЦена = Выборка.Цена;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.ДругаяНоменклатура) Тогда
				мДругиеУслуги = мДругиеУслуги + " " + Выборка.ДругаяНоменклатура;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.ДругаяСумма) Тогда
				мСтоимостьДругихУслуг = мСтоимостьДругихУслуг + Выборка.ДругаяСумма;
			КонецЕсли;
			
			
		КонецЦикла;
		НовСтр.Номенклатура = мНоменклатура;
		НовСтр.Характеристика = мХарактеристика;
		НовСтр.ТекущаяЦена = мТекущаяЦена;
		НовСтр.ДругиеУслуги = мДругиеУслуги;
		НовСтр.СтоимостьДргуихУслуг = мСтоимостьДругихУслуг;
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура НоваяЦенаПриИзменении(Элемент)
	
	Если СписокЗаказов.Количество() > 0 Тогда
		Для каждого Стр Из СписокЗаказов Цикл
			Стр.НоваяЦена = НоваяЦена;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикПриИзменении(Элемент)
	ЗаполнитьСписокЗаказовНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	 ЗаполнитьСписокЗаказовНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ЗаполнитьСписокЗаказовНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	
	ЗаполнитьСписокЗаказовНаСервере();
	
КонецПроцедуры


&НаСервере
Процедура КорректировкаЦенНаСервере()
	
	Для каждого Стр Из СписокЗаказов Цикл
		мОбъект = Стр.Заказ.ПолучитьОбъект();
		ПараметрыОтбора = Новый Структура();
		ПараметрыОтбора.Вставить("Номенклатура", Стр.Номенклатура);
		ПараметрыОтбора.Вставить("Характеристика", Стр.Характеристика);
		НайденныеСтроки = мОбъект.Запасы.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].Цена = Стр.НоваяЦена;
			НайденныеСтроки[0].Сумма = Стр.НоваяЦена * НайденныеСтроки[0].Количество;
			НайденныеСтроки[0].Всего = НайденныеСтроки[0].Сумма;
			СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(НайденныеСтроки[0].СтавкаНДС); 
			НайденныеСтроки[0].СуммаНДС = НайденныеСтроки[0].Сумма*СтавкаНДС/100;  
		КонецЕсли;
		
		Если мОбъект.Проведен Тогда
			Попытка
				мОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				мОбъект.Записать();
				Сообщить("Не удалось провести " + Стр.Заказ);
			КонецПопытки;
		Иначе
			мОбъект.Записать();
		КонецЕсли;
	КонецЦикла;
	
	
КонецПроцедуры


&НаКлиенте
Процедура КорректировкаЦен(Команда)
	КорректировкаЦенНаСервере();
	ЗаполнитьСписокЗаказовНаСервере();
КонецПроцедуры

