
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("АдресДанных", АдресДанных) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ОбновитьПараметрыФормы();
	
	УстановитьУсловноеОформление();
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(Параметры.АдресДанных);
	СтруктураДанных.Свойство("ТолькоПросмотр", ТолькоПросмотр);
	СтруктураДанных.Свойство("Ссылка", ЗаказСсылка);
	СтруктураДанных.Свойство("ТекущийВариантКП", ТекущийВариантКП);
	ЗаполнитьЗначенияСвойств(ДанныеЗаказа, СтруктураДанных.ДанныеЗаказа);
	Если НЕ ЗначениеЗаполнено(ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции) Тогда
		ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции = Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоПоследнейЦенеЗакупки;
	ИначеЕсли ТипЗнч(ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции)=Тип("СправочникСсылка.ВидыЦен") Тогда
		ВидЦен = ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции;
		ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции = Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры;
	КонецЕсли;
	Если ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов Тогда
		ВидыЦенКонтрагентов.ЗагрузитьЗначения(СтруктураДанных.ВидыЦен);
	ИначеЕсли ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры И СтруктураДанных.ВидыЦен.Количество()>0 Тогда
		ВидЦен = СтруктураДанных.ВидыЦен[0];
		ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции = ВидЦен;
	КонецЕсли; 
	СтруктураДанных.Свойство("СкидкаПроцент", СкидкаПроцент);
	СтруктураДанных.Свойство("СкидкаСумма", СкидкаСумма);
	ДанныеЗаказа.Запасы.Загрузить(СтруктураДанных.Запасы);
	ДанныеЗаказа.Калькуляция.Загрузить(СтруктураДанных.Калькуляция);
	ДанныеЗаказа.ПараметрыДоставки.Загрузить(СтруктураДанных.ПараметрыДоставки);
	
	// Калькуляция
	Если ДанныеЗаказа.КалькуляцияРассчитана И НЕ ТолькоПросмотр И ДанныеЗаказа.КоличествоВариантовКП>1 Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("НомерВариантаКП", 0);
		Строки = ДанныеЗаказа.Калькуляция.НайтиСтроки(СтруктураОтбора);
		Если Строки.Количество()>0 Тогда
			СтрокаСообщения = НСтр("ru = 'Добавлена возможность расчета отдельных калькуляций для каждого варианта коммерческого предложения. Необходимо обновить текущую калькуляцию.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
			Для каждого СтрокаТабличнойЧасти Из Строки Цикл
				СтрокаТабличнойЧасти.НомерВариантаКП = ТекущийВариантКП;
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли;
	// Конец Калькуляция
	
	Если ДанныеЗаказа.КоличествоВариантовКП<=1 Тогда
		Элементы.ТекущийВариантКП.Видимость = Ложь;
	Иначе
		Элементы.ТекущийВариантКП.СписокВыбора.Очистить();
		Для ВариантКП = 1 По ДанныеЗаказа.КоличествоВариантовКП Цикл
			Если ВариантКП = ДанныеЗаказа.ОсновнойВариантКП Тогда
				ПостфиксВарианта = НСтр("ru='(основной)'");
			Иначе
				ПостфиксВарианта = "";
			КонецЕсли;
			ИмяВарианта = СтрШаблон(НСтр("ru='Вариант %1 %2'"), ВариантКП, ПостфиксВарианта);
			Элементы.ТекущийВариантКП.СписокВыбора.Добавить(ВариантКП, ИмяВарианта);
		КонецЦикла; 
	КонецЕсли; 
	
	СкрытьКнопкуПечати = Ложь;
	СтруктураДанных.Свойство("ПечатьКалькуляцииСкрыта", СкрытьКнопкуПечати);
	Если СкрытьКнопкуПечати Тогда
		Элементы.ФормаПечать.Видимость = Ложь;
	КонецЕсли; 
	
	ЗаполнитьВидыЦенКалькуляции();
	СостояниеОтображатьСебестоимость = ХранилищеСистемныхНастроек.Загрузить("ЗаказПокупателя", "ОтображатьСебестоимость");
	ОтображатьСебестоимость = (СостояниеОтображатьСебестоимость=Истина ИЛИ СостояниеОтображатьСебестоимость=Неопределено);
	ОтобразитьСебестоимостьСервер();
	
	ПрочитатьДанныеШаблона();
	ЗаполнитьСписокИзделий();
	ТекущаяСтрокаСостава = -1;
	Если НЕ ДанныеЗаказа.КалькуляцияРассчитана Тогда
		ОбновитьКалькуляциюСервер();
	Иначе
		ОбновитьФактическиеДанные();
		ВывестиКалькуляцию();
	КонецЕсли;
	ОбновитьКомментарииСпецификаций();
	
	// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	ЗагрузкаДанныхИзВнешнегоИсточника.ПриСозданииНаСервере(Метаданные.Документы.ЗаказПокупателя.ТабличныеЧасти.Калькуляция, НастройкиЗагрузкиДанных, ЭтотОбъект, Ложь);
	// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	
	// ПодборНоменклатурыВДокументах
	ПодборНоменклатурыВДокументах.НазначитьФормуПодбора(ПараметрыОткрытияПодбора, ДанныеЗаказа.Ссылка.Метаданные().Имя, "Калькуляция");
	// Конец ПодборНоменклатурыВДокументах
	
	// МобильныйКлиент
	УправлениеНебольшойФирмойСервер.НастроитьФормуОбъектаМобильныйКлиент(Элементы, "КалькуляцияНаФорме");
	// Конец МобильныйКлиент
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ДанныеЗаказа.КалькуляцияРассчитана Тогда
		ПересчитатьФормулыПоШаблону();
	КонецЕсли; 
	
	ВывестиКомментарий();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПодборПроизведен" 
		И ЗначениеЗаполнено(Параметр) 
		//Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор
		Тогда
			
		АдресЗапасовВХранилище	= Параметр;
		ЕстьХарактеристики 		= Истина;
		ЕстьПартии			= Ложь;
		
		Если МаркерПодбора = "Калькуляция" Тогда
			
			Если НЕ ПустаяСтрока(ТекстОшибкиЖурналаРегистрации) Тогда
				ЗаписатьОшибкуЧтенияДанныхИзХранилища();
			КонецЕсли;
			
			ИмяТабличнойЧасти	= "Калькуляция";
			ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, ЕстьХарактеристики, ЕстьПартии);
			
			ПересчитатьФормулыПоШаблону();
			Модифицированность = Истина;
			ОбновитьНомераСтрок(КалькуляцияНаФорме);
			
		КонецЕсли;
		
		МаркерПодбора = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ТолькоПросмотр И Модифицированность Тогда
		Модифицированность = Ложь;
	КонецЕсли; 
	
	Если Модифицированность И ЗавершениеРаботы Тогда
		Отказ = Истина;
		ТекстПредупреждения = НСтр("ru = 'При закрытии программы все несохраненные изменения будут утеряны.'");
	ИначеЕсли Модифицированность Тогда
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(
		Оповещение, 
		НСтр("ru = 'Сохранить измерения калькуляции?'"), 
		РежимДиалогаВопрос.ДаНетОтмена, , 
		КодВозвратаДиалога.Да, НСтр("ru = 'Калькуляция'"));
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ=КодВозвратаДиалога.Да Тогда
		ПоместитьДанныеКалькуляцииВХранилище();
		СтруктураЗакрытия = Новый Структура;
		СтруктураЗакрытия.Вставить("АдресДанных", АдресДанных); 
		Закрыть(СтруктураЗакрытия);
	ИначеЕсли Ответ=КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли; 		
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СкидкаПроцентПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаказа.Запасы Цикл
		Если СтрокаТабличнойЧасти.ЭтоРазделитель ИЛИ СтрокаТабличнойЧасти.НомерВариантаКП<>ТекущийВариантКП Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = СкидкаПроцент;
		РассчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТабличнойЧасти);
	КонецЦикла; 
	
	Если ТекущаяСтрокаСостава<0 Тогда
		ОбновитьСкидкиСуммыНаФорме();
	КонецЕсли;
	
	РассчитатьСуммуИПроцентСкидки(ДанныеЗаказа.Запасы, СкидкаСумма, СкидкаПроцент, ТекущийВариантКП);
	ПересчитатьФормулыПоШаблону();
	
КонецПроцедуры

&НаКлиенте
Процедура СкидкаСуммаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	РаспределитьСуммуНаСкидки(СкидкаСумма);
	РассчитатьСуммуИПроцентСкидки(ДанныеЗаказа.Запасы, СкидкаСумма, СкидкаПроцент, ТекущийВариантКП);
	ПересчитатьФормулыПоШаблону();
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийВариантКППриИзменении(Элемент)
	
	ТекущаяСтрокаСостава = -1;
	ЗаполнитьСписокИзделий();
	ВывестиКалькуляциюКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИзделийПриИзменении(Элемент)
	
	Если ТекущаяСтрокаСостава=-1 Тогда
		ВывестиКалькуляциюКлиент();
	Иначе
		ВывестиКалькуляциюКлиент(ТекущаяСтрокаСостава);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СпособРасчетаСебестоимостиКалькуляцииПриИзменении(Элемент)
	
	Модифицированность = Истина;
	СпособРасчетаСебестоимостиКалькуляцииПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура СпособРасчетаСебестоимостиКалькуляцииПриИзмененииСервер()
	
	УстановитьВидимостьИДоступность();
	ОбновитьКалькуляциюСервер(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособРасчетаСебестоимостиКалькуляцииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.СпособРасчетаСебестоимостиКалькуляции.Подсказка = "";
	
	Если ВыбранноеЗначение=ПредопределенноеЗначение("Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов") Тогда
		СтандартнаяОбработка = Ложь;
		СтруктураОткрытия = Новый Структура;
		СтруктураОткрытия.Вставить("РежимВыбора", Истина);
		СтруктураОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
		СтруктураОткрытия.Вставить("ВидыЦен", ВидыЦенКонтрагентов.ВыгрузитьЗначения());
		ОткрытьФорму("Справочник.ВидыЦенКонтрагентов.Форма.ФормаМножественногоВыбора", СтруктураОткрытия, Элементы.СпособРасчетаСебестоимостиКалькуляции);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение)=Тип("СправочникСсылка.ВидыЦен") Тогда
		ВидЦен = ВыбранноеЗначение;
	КонецЕсли; 
	
	Если ТипЗнч(ВыбранноеЗначение)=Тип("Массив") Тогда
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
		ВидыЦенКонтрагентов.ЗагрузитьЗначения(ВыбранноеЗначение);
		ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции = ПредопределенноеЗначение("Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов");
		ОбновитьКалькуляциюСервер(Ложь);
		ЭлементВыбора = Элементы.СпособРасчетаСебестоимостиКалькуляции.СписокВыбора.НайтиПоЗначению(ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции);
		Если НЕ ЭлементВыбора=Неопределено Тогда
			ЭлементВыбора.Представление = ПредставлениеЭлементаВидЦенКонтрагента(ВидыЦенКонтрагентов);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовТаблицыФормыКалькуляция

&НаКлиенте
Процедура КалькуляцияНаФормеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
	Если ТекущаяСтрокаСостава=-1
		И СтрокаТабличнойЧасти.Источник=ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Запас")
		И (Поле=Элементы.КалькуляцияНаФормеНоменклатура
		ИЛИ Поле=Элементы.КалькуляцияНаФормеХарактеристика
		ИЛИ Поле=Элементы.КалькуляцияНаФормеПартия) Тогда
		СтрокаЗапасов = СтрокаПоКлючу(ДанныеЗаказа.Запасы, СтрокаТабличнойЧасти.КлючСвязи);
		Если СтрокаЗапасов=Неопределено Тогда
			Возврат;
		КонецЕсли;
		Идентификатор = СтрокаЗапасов.ПолучитьИдентификатор();
		Если Элементы.ТекущаяСтрокаСостава.СписокВыбора.НайтиПоЗначению(Идентификатор)=Неопределено Тогда
			Возврат;
		КонецЕсли; 
		СтандартнаяОбработка = Ложь;
		ТекущаяСтрокаСостава = Идентификатор;
		ВывестиКалькуляциюКлиент(ТекущаяСтрокаСостава);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если НЕ Копирование Тогда
		Отказ = Истина;	
		НачатьДобавлениеНоменклатуры();
	КонецЕсли;
	
	СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
	Если Копирование И СтрокаТабличнойЧасти.Источник=ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Запас") Тогда
		Отказ = Истина;	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение)=Тип("СправочникСсылка.Номенклатура") Тогда
		СтрокаТабличнойЧасти = КалькуляцияНаФорме.Добавить();
		СтрокаТабличнойЧасти.Номенклатура = ВыбранноеЗначение;
		СтрокаТабличнойЧасти.Количество = 1;
		СтруктураДанные = СтруктураДанныхШапки();
		СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
		СтруктураДанные.Вставить("Характеристика", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));
		
		СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
		
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтруктураДанные, "Характеристика, Спецификация, ЕдиницаИзмерения, СпособПополнения, ТипНоменклатуры, ИспользоватьХарактеристики, ИспользоватьПартии");
		СтрокаТабличнойЧасти.СебестоимостьЕдиницы = СтруктураДанные.СебестоимостьЕдиницы;
		ПересчитатьСебестоимость(СтрокаТабличнойЧасти);
		ВнестиИзмененияВКалькуляцию();
		ПересчитатьФормулыПоШаблону();
		Если СтруктураДанные.ИспользоватьХарактеристики Тогда
			ЭлементТаблицы = Элементы.КалькуляцияНаФормеХарактеристика;
		ИначеЕсли СтруктураДанные.ИспользоватьПартии Тогда
			ЭлементТаблицы = Элементы.КалькуляцияНаФормеПартия;
		ИначеЕсли СтруктураДанные.СпособПополнения=ПредопределенноеЗначение("Перечисление.СпособыПополненияЗапасов.Производство") Тогда
			ЭлементТаблицы = Элементы.КалькуляцияНаФормеСпецификация;
		Иначе
			ЭлементТаблицы = Элементы.КалькуляцияНаФормеКоличество;
		КонецЕсли; 
	ИначеЕсли ТипЗнч(ВыбранноеЗначение)=Тип("ПланСчетовСсылка.Управленческий") Тогда 
		СтрокаТабличнойЧасти = КалькуляцияНаФорме.Добавить();
		СтрокаТабличнойЧасти.Номенклатура = ВыбранноеЗначение;
		ВнестиИзмененияВКалькуляцию();
		ЭлементТаблицы = Элементы.КалькуляцияНаФормеСебестоимость;
	Иначе
		Возврат;
	КонецЕсли;
	СтрокаТабличнойЧасти.Источник = ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Прочее");
	
	Элементы.КалькуляцияНаФорме.ТекущаяСтрока = СтрокаТабличнойЧасти.ПолучитьИдентификатор();
	Элементы.КалькуляцияНаФорме.ТекущийЭлемент = ЭлементТаблицы;
	Элементы.КалькуляцияНаФорме.ИзменитьСтроку();
	
	ОбновитьНомераСтрок(КалькуляцияНаФорме);
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
	Если НоваяСтрока Тогда
		СтрокаТабличнойЧасти.КлючСвязи = 0;
		СтрокаТабличнойЧасти.Источник = ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Прочее");
		Если НЕ Копирование Тогда
			Если ТекущаяСтрокаСостава=-1 И ТипЗнч(СтрокаТабличнойЧасти.Номенклатура)<>Тип("ПланСчетовСсылка.Управленческий") Тогда
				СтрокаТабличнойЧасти.Номенклатура = ПредопределенноеЗначение("ПланСчетов.Управленческий.ПустаяСсылка");
			ИначеЕсли ТекущаяСтрокаСостава>=0 И ТипЗнч(СтрокаТабличнойЧасти.Номенклатура)<>Тип("СправочникСсылка.Номенклатура") Тогда 
				СтрокаТабличнойЧасти.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
			КонецЕсли; 
		КонецЕсли; 
		ОбновитьНомераСтрок(КалькуляцияНаФорме);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
	СтрокаТабличнойЧасти.НеСохранять = Ложь;
	ВнестиИзмененияВКалькуляцию();
	ПересчитатьФормулыПоШаблону();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеПослеУдаления(Элемент)
	
	ВнестиИзмененияВКалькуляцию();
	ПересчитатьФормулыПоШаблону();
	Модифицированность = Истина;
	ОбновитьНомераСтрок(КалькуляцияНаФорме);
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеНоменклатураОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтрокаТабличнойЧасти = Элементы.КалькуляцияНаФорме.ТекущиеДанные;
	Если ТекущаяСтрокаСостава=-1 Тогда
		СтрокаТабличнойЧасти.Номенклатура = ПредопределенноеЗначение("ПланСчетов.Управленческий.ПустаяСсылка");
	Иначе 
		СтрокаТабличнойЧасти.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
	КонецЕсли;
	СтрокаТабличнойЧасти.Себестоимость = 0;
	СтрокаТабличнойЧасти.СебестоимостьЕдиницы = 0;
	СтрокаТабличнойЧасти.Прибыль = СтрокаТабличнойЧасти.Сумма;
	СтрокаТабличнойЧасти.РучноеИзменение = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.КалькуляцияНаФорме.ТекущиеДанные;
	Если СтрокаТабличнойЧасти=Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура)<>Тип("СправочникСсылка.Номенклатура") Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураДанные = СтруктураДанныхШапки();
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтруктураДанные, "Характеристика, Спецификация, ЕдиницаИзмерения, СпособПополнения, ТипНоменклатуры, ИспользоватьХарактеристики, ИспользоватьПартии, СебестоимостьЕдиницы");
	СтрокаТабличнойЧасти.РучноеИзменение = Ложь;
	ПересчитатьСебестоимость(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеХарактеристикаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.КалькуляцияНаФорме.ТекущиеДанные;
	Если СтрокаТабличнойЧасти=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанные = СтруктураДанныхШапки();
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	СтруктураДанные.Вставить("ЕдиницаИзмерения", СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтруктураДанные, "Спецификация, СебестоимостьЕдиницы");
	СтрокаТабличнойЧасти.РучноеИзменение = Ложь;
	ПересчитатьСебестоимость(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеСпецификацияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.КалькуляцияНаФорме.ТекущиеДанные;
	Если СтрокаТабличнойЧасти=Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаменитьСпецификацию(СтрокаТабличнойЧасти.ПолучитьИдентификатор());
	
	СтрокаКомментарий = СтрокаПоКлючу(КомментарииСпецификаций, СтрокаТабличнойЧасти.КлючСвязи);
	Если СтрокаКомментарий<>Неопределено Тогда
		КомментарииСпецификаций.Удалить(СтрокаКомментарий);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеЕдиницаИзмеренияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.КалькуляцияНаФорме.ТекущиеДанные;
	Если СтрокаТабличнойЧасти=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанные = СтруктураДанныхШапки();
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	СтруктураДанные.Вставить("Спецификация", СтрокаТабличнойЧасти.Спецификация);
	СтруктураДанные.Вставить("ЕдиницаИзмерения", СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.СебестоимостьЕдиницы = СтруктураДанные.СебестоимостьЕдиницы;
	СтрокаТабличнойЧасти.РучноеИзменение = Ложь;
	ПересчитатьСебестоимость(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеКоличествоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.КалькуляцияНаФорме.ТекущиеДанные;
	Если СтрокаТабличнойЧасти=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПересчитатьСебестоимость(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеСебестоимостьЕдиницыПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.КалькуляцияНаФорме.ТекущиеДанные;
	Если СтрокаТабличнойЧасти=Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтрокаТабличнойЧасти.Себестоимость = СтрокаТабличнойЧасти.СебестоимостьЕдиницы * СтрокаТабличнойЧасти.Количество;
	СтрокаТабличнойЧасти.Прибыль = СтрокаТабличнойЧасти.Сумма-СтрокаТабличнойЧасти.Себестоимость;
	СтрокаТабличнойЧасти.РучноеИзменение = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеСебестоимостьПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.КалькуляцияНаФорме.ТекущиеДанные;
	Если СтрокаТабличнойЧасти=Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтрокаТабличнойЧасти.Прибыль = СтрокаТабличнойЧасти.Сумма-СтрокаТабличнойЧасти.Себестоимость;
	СтрокаТабличнойЧасти.РучноеИзменение = Истина;
	Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
		Если СтрокаТабличнойЧасти.Количество=0 Тогда
			СтрокаТабличнойЧасти.Количество = 1;
		КонецЕсли; 
		СтрокаТабличнойЧасти.СебестоимостьЕдиницы = СтрокаТабличнойЧасти.Себестоимость/СтрокаТабличнойЧасти.Количество;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеПроцентСкидкиНаценкиПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.КалькуляцияНаФорме.ТекущиеДанные;
	Если СтрокаТабличнойЧасти=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСкидкуЗапасов(СтрокаТабличнойЧасти);
	РассчитатьСуммуИПроцентСкидки(ДанныеЗаказа.Запасы, СкидкаСумма, СкидкаПроцент, ТекущийВариантКП);

КонецПроцедуры

&НаКлиенте
Процедура КалькуляцияНаФормеПередУдалением(Элемент, Отказ)
	
	СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
	Если СтрокаТабличнойЧасти.Источник=ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Запас")
		ИЛИ СтрокаТабличнойЧасти.Источник=ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Доставка") Тогда
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	ПриИзмененииКомментария();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("КомментарийЗавершениеВвода", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(Оповещение, Элементы.Комментарий.ТекстРедактирования, НСтр("ru='Комментарий'"));
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КалькуляцияОтображениеСебестоимости(Команда)
	
	ОтображатьСебестоимость = НЕ ОтображатьСебестоимость;
	КалькуляцияОтображениеСебестоимостиСервер();
	
КонецПроцедуры

&НаСервере
Процедура КалькуляцияОтображениеСебестоимостиСервер()
	
	ОтобразитьСебестоимостьСервер();
	Если ТекущаяСтрокаСостава<0 Тогда
		ВывестиКалькуляцию();
	Иначе
		ВывестиКалькуляцию(ТекущаяСтрокаСостава);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНоменклатуру(Команда)
	
	НачатьДобавлениеНоменклатуры();	
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьДобавлениеНоменклатуры()
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	
	СтруктураОтбора = Новый Структура;
	ТипНоменклатуры = Новый СписокЗначений;
	Для каждого ЭлементМассива Из Элементы.КалькуляцияНаФормеНоменклатура.ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
				Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
					ТипНоменклатуры.Добавить(ЭлементФиксМассива);
				КонецЦикла; 
			Иначе
				ТипНоменклатуры.Добавить(ЭлементМассива.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	СтруктураОтбора.Вставить("ТипНоменклатуры", ТипНоменклатуры);
	СтруктураОтбора.Вставить("ЭтоНабор", Ложь);
	
	ПараметрыОткрытия.Вставить("Отбор", СтруктураОтбора);
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыОткрытия, Элементы.КалькуляцияНаФорме,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРасход(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	СтруктураОтбора = Новый Структура;
	ТипыСчетов = Новый Массив;
	ТипыСчетов.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСчетов.Расходы"));
	ТипыСчетов.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСчетов.ПрочиеРасходы"));
	ТипыСчетов.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСчетов.НезавершенноеПроизводство"));
	СтруктураОтбора.Вставить("ТипСчета", Новый ФиксированныйМассив(ТипыСчетов));
	ПараметрыОткрытия.Вставить("Отбор", СтруктураОтбора);
	ОткрытьФорму("ПланСчетов.Управленческий.ФормаВыбора", ПараметрыОткрытия, Элементы.КалькуляцияНаФорме,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКалькуляцию(Команда)
	
	Модифицированность = Истина;
	ОбновитьКалькуляциюСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ТолькоПросмотр Тогда
		Модифицированность = Ложь;
		Закрыть();
	Иначе
		ПоместитьДанныеКалькуляцииВХранилище();
		СтруктураЗакрытия = Новый Структура;
		СтруктураЗакрытия.Вставить("АдресДанных", АдресДанных); 
		Закрыть(СтруктураЗакрытия);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблону(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьПоШаблонуЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	Если ЗначениеЗаполнено(ДанныеЗаказа.ШаблонКалькуляции) Тогда
		ПараметрыОткрытия.Вставить("ТекущаяСтрока", ДанныеЗаказа.ШаблонКалькуляции);
	КонецЕсли; 
	ОткрытьФорму("Справочник.ШаблоныКалькуляций.ФормаВыбора", ПараметрыОткрытия, ЭтотОбъект, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблонуЗавершение(Шаблон, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Шаблон) Тогда
		ДанныеЗаказа.ШаблонКалькуляции = Шаблон;
		ЗаполнитьПоШаблонуЗавершениеСервер();
		Модифицированность = Истина;
	КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоШаблонуЗавершениеСервер()
	
	ЗапрещеноИзменениеКалькуляции = Ложь;
	ЗаполнитьПоШаблонуСервер();
	Если ТекущаяСтрокаСостава<0 Тогда
		ВывестиКалькуляцию();
	Иначе
		ВывестиКалькуляцию(ТекущаяСтрокаСостава);
	КонецЕсли; 
	
КонецПроцедуры
 
&НаКлиенте
Процедура Печать(Команда)
	
	СтруктураЗакрытия = Новый Структура;
	Если НЕ ТолькоПросмотр И (Модифицированность ИЛИ НЕ ДанныеЗаказа.КалькуляцияРассчитана) Тогда
		ПоместитьДанныеКалькуляцииВХранилище();
		СтруктураЗакрытия.Вставить("АдресДанных", АдресДанных); 
	КонецЕсли;
	СтруктураЗакрытия.Вставить("Распечатать", Истина);
	ВладелецФормы.ПриИзмененииКалькуляции(СтруктураЗакрытия, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяККалькуляцииЗаказа(Команда)
	
	ТекущаяСтрокаСостава = -1;
	ВывестиКалькуляциюКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуНоменклатуры(Команда)
	
	СтрокаТабличнойЧасти = Элементы.КалькуляцияНаФорме.ТекущиеДанные;
	Если СтрокаТабличнойЧасти=Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
		ПоказатьЗначение(, СтрокаТабличнойЧасти.Номенклатура);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЦены(Команда)
	
	Модифицированность = Истина;
	ОбновитьЦеныСервер();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПараметрыФормы()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ДоступныПолныеПрава", РольДоступна(Метаданные.Роли.ПолныеПрава) ИЛИ РольДоступна(Метаданные.Роли.ЧтениеДанныхУНФ));
	СтруктураПараметров.Вставить("ИспользоватьХарактеристики", ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики"));
	СтруктураПараметров.Вставить("ИспользоватьПартии", ПолучитьФункциональнуюОпцию("ИспользоватьПартии") И ЗаполненРеквизитТЧ(ДанныеЗаказа.Запасы, "Партия"));
	СтруктураПараметров.Вставить("ИспользоватьЕдиницыИзмерения", ПолучитьФункциональнуюОпцию("УчетВРазличныхЕдиницахИзмерения"));
	ПараметрыФормы = Новый ФиксированнаяСтруктура(СтруктураПараметров);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Шрифт", Новый Шрифт(Новый Шрифт, , , Истина));
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "КалькуляцияНаФорме.РучноеИзменение", Истина);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСебестоимостьЕдиницы.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСебестоимость.Имя);
	
	// УО запасов
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "КалькуляцияНаФорме.ВложенныйСостав", Истина);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСебестоимостьЕдиницы.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСебестоимость.Имя);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "КалькуляцияНаФорме.ИспользоватьХарактеристики", Ложь);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеХарактеристика.Имя);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "КалькуляцияНаФорме.ИспользоватьПартии", Ложь);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеПартия.Имя);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "КалькуляцияНаФорме.СпособПополнения", Перечисления.СпособыПополненияЗапасов.Производство, ВидСравненияКомпоновкиДанных.НеРавно);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСпецификация.Имя);
	
	// УО услуг
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "КалькуляцияНаФорме.ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Услуга);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСпецификация.Имя);
	
	// УО расходов
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "КалькуляцияНаФорме.ТипНоменклатуры", Перечисления.ТипыНоменклатуры.ПустаяСсылка());
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСпецификация.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеХарактеристика.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеПартия.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеКоличество.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеЕдиницаИзмерения.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСебестоимостьЕдиницы.Имя);
	
	// Строки запасов
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "КалькуляцияНаФорме.Источник", Перечисления.ИсточникиСтрокКалькуляции.Запас);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеНоменклатура.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеХарактеристика.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеПартия.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеКоличество.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеЕдиницаИзмерения.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеПроцентАвтоматическойСкидки.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСумма.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеПрибыль.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСебестоимостьЕдиницыФакт.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСебестоимостьФакт.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеСтоимостьФакт.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеПрибыльФакт.Имя);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "КалькуляцияНаФорме.Источник", Перечисления.ИсточникиСтрокКалькуляции.Запас, ВидСравненияКомпоновкиДанных.НеРавно);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеПроцентСкидкиНаценки.Имя);
	
	// Строки доставки
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "КалькуляцияНаФорме.Источник", Перечисления.ИсточникиСтрокКалькуляции.Доставка);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеНоменклатура.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеХарактеристика.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеПартия.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеКоличество.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеЕдиницаИзмерения.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.КалькуляцияНаФормеПроцентАвтоматическойСкидки.Имя);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	РежимПолнойКалькуляции = (ТекущаяСтрокаСостава<0);
	ЕстьФактическиеДанные = (ФактическиеДанные.Количество()>0);
	Элементы.СпособРасчетаСебестоимостиКалькуляции.Видимость = ОтображатьСебестоимость;
	Элементы.КалькуляцияНаФормеХарактеристика.Видимость = ПараметрыФормы.ИспользоватьХарактеристики;
	Элементы.КалькуляцияНаФормеПартия.Видимость = РежимПолнойКалькуляции И ПараметрыФормы.ИспользоватьПартии;
	Элементы.КалькуляцияНаФормеЕдиницаИзмерения.Видимость = ПараметрыФормы.ИспользоватьЕдиницыИзмерения;
	Элементы.КалькуляцияНаФормеСебестоимостьЕдиницы.Видимость = ОтображатьСебестоимость;
	Элементы.КалькуляцияНаФормеСебестоимость.Видимость = ОтображатьСебестоимость;
	Элементы.КалькуляцияНаФормеСумма.Видимость = РежимПолнойКалькуляции;
	Элементы.КалькуляцияНаФормеПроцентСкидкиНаценки.Видимость = РежимПолнойКалькуляции;
	Элементы.КалькуляцияНаФормеПроцентАвтоматическойСкидки.Видимость = РежимПолнойКалькуляции;
	Элементы.КалькуляцияНаФормеПрибыль.Видимость = РежимПолнойКалькуляции И ОтображатьСебестоимость;
	Элементы.КалькуляцияНаФормеДобавитьРасход.Видимость = РежимПолнойКалькуляции;
	Элементы.КалькуляцияНаФормеЗаполнитьПоШаблону.Видимость = РежимПолнойКалькуляции;
	Элементы.КалькуляцияНаФормеСебестоимостьЕдиницыФакт.Видимость = РежимПолнойКалькуляции И ЕстьФактическиеДанные И ОтображатьСебестоимость;
	Элементы.КалькуляцияНаФормеСебестоимостьФакт.Видимость = РежимПолнойКалькуляции И ЕстьФактическиеДанные И ОтображатьСебестоимость;
	Элементы.КалькуляцияНаФормеПрибыльФакт.Видимость = РежимПолнойКалькуляции И ЕстьФактическиеДанные И ОтображатьСебестоимость;
	Элементы.КалькуляцияНаФормеСтоимостьФакт.Видимость = РежимПолнойКалькуляции И ЕстьФактическиеДанные;
	
	Элементы.ГруппаПодвалСкидки.Видимость = РежимПолнойКалькуляции;
	Элементы.ДекорацияЗаголовокСебестоимость.Видимость = ОтображатьСебестоимость;
	Элементы.ИтогоСебестоимость.Видимость = ОтображатьСебестоимость;
	Элементы.ИтогоСебестоимостьФакт.Видимость = ОтображатьСебестоимость;
	Элементы.ДекорацияЗаголовокПрибыль.Видимость = ОтображатьСебестоимость;
	Элементы.ДекорацияОтступИтогоПрибыль.Видимость = ОтображатьСебестоимость;
	Элементы.ДекорацияОтступИтогоПрибыльФакт.Видимость = ОтображатьСебестоимость;
	Элементы.ИтогоПрибыль.Видимость = ОтображатьСебестоимость;
	Элементы.ИтогоПрибыльФакт.Видимость = ОтображатьСебестоимость;
	
	Элементы.КалькуляцияОтображениеСебестоимости.Картинка = ?(ОтображатьСебестоимость, БиблиотекаКартинок.ВидимостьРазрешена, БиблиотекаКартинок.ВидимостьЗапрещена);
	
	Элементы.КалькуляцияНаФорме.ТолькоПросмотр = ЗапрещеноИзменениеКалькуляции ИЛИ ТолькоПросмотр;
	Элементы.КалькуляцияНаФормеДобавитьНоменклатуру.Доступность = НЕ ЗапрещеноИзменениеКалькуляции И НЕ ТолькоПросмотр;
	Элементы.КалькуляцияНаФормеДобавитьНоменклатуру.Заголовок = ?(ТекущаяСтрокаСостава<0, НСтр("ru = 'Добавить номенклатуру'"), НСтр("ru = 'Добавить'"));
	Элементы.КалькуляцияНаФормеДобавитьРасход.Доступность = НЕ ЗапрещеноИзменениеКалькуляции И НЕ ТолькоПросмотр;
	Элементы.КалькуляцияНаФормеПодбор.Доступность = НЕ ЗапрещеноИзменениеКалькуляции И НЕ ТолькоПросмотр;
	Если Элементы.Найти("КалькуляцияНаФормеЗагрузкаДанныхИзВнешнегоИсточника")<>Неопределено Тогда
		Элементы.КалькуляцияНаФормеЗагрузкаДанныхИзВнешнегоИсточника.Доступность = НЕ ЗапрещеноИзменениеКалькуляции И НЕ ТолькоПросмотр;
	КонецЕсли; 
	Элементы.КалькуляцияНаФормеЗаполнитьПоШаблону.Доступность = НЕ ТолькоПросмотр;
	Элементы.КалькуляцияНаФорме.ЦветТекста = ?(ЗапрещеноИзменениеКалькуляции И НЕ ТолькоПросмотр, ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти, Новый Цвет);
	Элементы.СпособРасчетаСебестоимостиКалькуляции.Доступность = НЕ ТолькоПросмотр;
	Элементы.КалькуляцияНаФормеОбновитьКалькуляцию.Доступность = НЕ ТолькоПросмотр;
	Элементы.СкидкаПроцент.Доступность = НЕ ТолькоПросмотр;
	Элементы.СкидкаСумма.Доступность = НЕ ТолькоПросмотр;
	Элементы.Комментарий.ТолькоПросмотр = ТолькоПросмотр ИЛИ ЗапрещеноИзменениеКалькуляции;
	Элементы.СохранитьИзменения.Заголовок = ?(ТолькоПросмотр, НСтр("ru = 'Закрыть'"), НСтр("ru = 'Записать и закрыть'"));
	
	Элементы.ВернутьсяККалькуляцииЗаказа.Видимость = (ТекущаяСтрокаСостава>=0);
	
	Элементы.КалькуляцияНаФормеЗаполнитьПоШаблону.Заголовок =
	?(ЗначениеЗаполнено(ДанныеЗаказа.ШаблонКалькуляции),
	НСтр("ru = 'Использовать другой шаблон'"),
	НСтр("ru = 'Добавить по шаблону'"));
	
	Элементы.ГруппаИтогиФакт.Видимость = ЕстьФактическиеДанные;
	Элементы.ДекорацияФактЗаголовок.Видимость = ЕстьФактическиеДанные;
	//Элементы.ДекорацияСкидкиОтступ.Видимость = ЕстьФактическиеДанные;
	
	// Параметры выбора номенклатуры
	МассивТипов = Новый Массив();
	МассивТипов.Добавить(Перечисления.ТипыНоменклатуры.Запас);
	МассивТипов.Добавить(Перечисления.ТипыНоменклатуры.Услуга);
	МассивТипов.Добавить(Перечисления.ТипыНоменклатуры.Операция);
	Если ТекущаяСтрокаСостава<0 Тогда
		МассивТипов.Добавить(Перечисления.ТипыНоменклатуры.Работа);
	КонецЕсли;
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ТипНоменклатуры", Новый ФиксированныйМассив(МассивТипов)));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ЭтоНабор", Ложь));
	Элементы.КалькуляцияНаФормеНоменклатура.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьВидыЦенКалькуляции()
	
	Элементы.СпособРасчетаСебестоимостиКалькуляции.СписокВыбора.Очистить();
	Элементы.СпособРасчетаСебестоимостиКалькуляции.СписокВыбора.Добавить(Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоПоследнейЦенеЗакупки, НСтр("ru = 'последней цене закупки'"));
	
	Если ПолучитьФункциональнуюОпцию("УчетЦенКонтрагентов") Тогда
		Элементы.СпособРасчетаСебестоимостиКалькуляции.СписокВыбора.Добавить(
		Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов, 
		ПредставлениеЭлементаВидЦенКонтрагента(ВидыЦенКонтрагентов));
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыЦен.Ссылка,
	|	ВидыЦен.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ВидыЦен КАК ВидыЦен
	|ГДЕ
	|	НЕ ВидыЦен.ПометкаУдаления
	|	И НЕ ВидыЦен.РассчитыватьАвтоматически
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	Выборка  =Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Элементы.СпособРасчетаСебестоимостиКалькуляции.СписокВыбора.Добавить(Выборка.Ссылка, НСтр("ru = 'виду цен '")+""""+СокрЛП(Выборка.Наименование)+"""");	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеЭлементаВидЦенКонтрагента(ВидыЦенКонтрагентов)
	
	Результат = НСтр("ru = 'цене поставщиков'")+?(ВидыЦенКонтрагентов.Количество()=0, "", " ("+Строка(ВидыЦенКонтрагентов)+")");	
	Если СтрДлина(Результат)>60 Тогда
		Результат = Лев(Результат, 58)+"...";
	КонецЕсли;
	Возврат Результат;
	
КонецФункции 

&НаСервере
Процедура ЗаполнитьСписокИзделий()
	
	Элементы.ТекущаяСтрокаСостава.СписокВыбора.Очистить();
	Элементы.ТекущаяСтрокаСостава.СписокВыбора.Добавить(-1, НСтр("ru = 'Калькуляция всего заказа'"));
	
	ТаблицаЗапасов = Новый ТаблицаЗначений;
	ТаблицаЗапасов.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаЗапасов.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.Спецификации"));
	ТаблицаЗапасов.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(0)));
	ТаблицаЗапасов.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	ТаблицаЗапасов.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Для каждого Стр Из ДанныеЗаказа.Запасы Цикл
		Если Стр.ЭтоРазделитель ИЛИ Стр.НомерВариантаКП<>ТекущийВариантКП Тогда
			Продолжить;
		КонецЕсли; 
		ИдентификаторСтроки = Стр.ПолучитьИдентификатор();
		НоваяСтрока = ТаблицаЗапасов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		НоваяСтрока.Идентификатор = ИдентификаторСтроки;
		НоваяСтрока.Сумма = Стр.Всего;
		НоваяСтрока.Представление = УправлениеНебольшойФирмойСервер.ПолучитьПредставлениеНоменклатурыДляПечати(Стр.Номенклатура, Стр.Характеристика);
	КонецЦикла;                                                                                                                                                                                                      
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Запасы", ТаблицаЗапасов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Запасы.Идентификатор,
	|	Запасы.Сумма,
	|	Запасы.Представление,
	|	ВЫРАЗИТЬ(Запасы.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Запасы.Спецификация КАК Справочник.Спецификации) КАК Спецификация
	|ПОМЕСТИТЬ Запасы
	|ИЗ
	|	&Запасы КАК Запасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запасы.Идентификатор,
	|	Запасы.Сумма,
	|	Запасы.Представление,
	|	Запасы.Спецификация
	|ИЗ
	|	Запасы КАК Запасы
	|ГДЕ
	|	(Запасы.Номенклатура.СпособПополнения = ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
	|			ИЛИ НЕ Запасы.Спецификация = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка))";
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат.Получить(1).Выбрать();
	Пока Выборка.Следующий() Цикл
		Элементы.ТекущаяСтрокаСостава.СписокВыбора.Добавить(Выборка.Идентификатор, Выборка.Представление);
	КонецЦикла;
	
	Элементы.ТекущаяСтрокаСостава.Видимость = (Элементы.ТекущаяСтрокаСостава.СписокВыбора.Количество()>1);
	Если НЕ Элементы.ТекущаяСтрокаСостава.Видимость Тогда
		ТекущаяСтрокаСостава = -1;
	КонецЕсли; 
	
КонецПроцедуры

#Область Итоги

&НаКлиенте
Процедура ОбновитьИтогиКлиент()
	
	СтруктураИтогов = ИтогиПоКалькуляции(ТекущаяСтрокаСостава, ТекущийВариантКП, ДанныеЗаказа, КалькуляцияНаФорме, ФактическиеДанные);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураИтогов);
	
КонецПроцедуры
 
&НаСервере
Процедура ОбновитьИтогиСервер()
	
	СтруктураИтогов = ИтогиПоКалькуляции(ТекущаяСтрокаСостава, ТекущийВариантКП, ДанныеЗаказа, КалькуляцияНаФорме, ФактическиеДанные);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураИтогов);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИтогиПоКалькуляции(ТекущаяСтрокаСостава, ТекущийВариантКП, ДанныеЗаказа, КалькуляцияНаФорме, ФактическиеДанные)
	
	СтруктураИтогов = Новый Структура;
	СтруктураИтогов.Вставить("ИтогоСебестоимость", 0);
	СтруктураИтогов.Вставить("ИтогоСтоимость", 0);
	СтруктураИтогов.Вставить("ИтогоПрибыль", 0);
	СтруктураИтогов.Вставить("ИтогоСебестоимостьФакт", 0);
	СтруктураИтогов.Вставить("ИтогоСтоимостьФакт", 0);
	СтруктураИтогов.Вставить("ИтогоПрибыльФакт", 0);
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("НомерВариантаКП", ТекущийВариантКП);
	
	Если ТекущаяСтрокаСостава<0 Тогда
		Для каждого Стр Из ДанныеЗаказа.Запасы Цикл
			Если Стр.ЭтоРазделитель ИЛИ Стр.НомерВариантаКП<>ТекущийВариантКП Тогда
				Продолжить;
			КонецЕсли;
			СтруктураИтогов.ИтогоСтоимость = СтруктураИтогов.ИтогоСтоимость + Стр.Всего;
		КонецЦикла;
		СтруктураИтогов.ИтогоСтоимость = СтруктураИтогов.ИтогоСтоимость + ДанныеЗаказа.СтоимостьДоставки;
		СтрокиВарианта = ДанныеЗаказа.Калькуляция.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрокаКалькуляции Из СтрокиВарианта Цикл
			СтруктураИтогов.ИтогоСебестоимость = СтруктураИтогов.ИтогоСебестоимость + СтрокаКалькуляции.Себестоимость;
		КонецЦикла; 
		Для каждого Стр Из КалькуляцияНаФорме Цикл
			СтруктураИтогов.ИтогоСебестоимостьФакт = СтруктураИтогов.ИтогоСебестоимостьФакт + Стр.СебестоимостьФакт;
			СтруктураИтогов.ИтогоСтоимостьФакт = СтруктураИтогов.ИтогоСтоимостьФакт + Стр.СтоимостьФакт;
			СтруктураИтогов.ИтогоПрибыльФакт = СтруктураИтогов.ИтогоПрибыльФакт + Стр.ПрибыльФакт;
		КонецЦикла; 
	Иначе
		СтрокаЗапасы = ДанныеЗаказа.Запасы.НайтиПоИдентификатору(ТекущаяСтрокаСостава);
		СтруктураИтогов.ИтогоСтоимость = СтрокаЗапасы.Всего;
		СтрокиВарианта = ДанныеЗаказа.Калькуляция.НайтиСтроки(СтруктураОтбора);
		Для каждого Стр Из СтрокиВарианта Цикл
			СтруктураИтогов.ИтогоСебестоимость = СтруктураИтогов.ИтогоСебестоимость + Стр.Себестоимость;
		КонецЦикла;
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Номенклатура", СтрокаЗапасы.Номенклатура);
		СтрокиФакт = ФактическиеДанные.НайтиСтроки(СтруктураОтбора);
		СтруктураОтбора.Вставить("НомерВариантаКП", ТекущийВариантКП);
		СтрокиБазы = ДанныеЗаказа.Запасы.НайтиСтроки(СтруктураОтбора);
		БазаКоличество = 0;
		Для каждого СтрокаБазы Из СтрокиБазы Цикл
			БазаКоличество = БазаКоличество+СтрокаБазы.Количество;
		КонецЦикла; 
		Если СтрокиФакт.Количество()>0 Тогда
			СтрокаФакт = СтрокиФакт[0];
			СтруктураИтогов.ИтогоСебестоимостьФакт = ?(БазаКоличество=0, 0, СтрокаФакт.СебестоимостьФакт / БазаКоличество * СтрокаЗапасы.Количество);
			СтруктураИтогов.ИтогоСтоимостьФакт = ?(БазаКоличество=0, 0, СтрокаФакт.СтоимостьФакт / БазаКоличество * СтрокаЗапасы.Количество);
		КонецЕсли; 
		СтруктураИтогов.ИтогоПрибыльФакт = СтруктураИтогов.ИтогоСтоимостьФакт - СтруктураИтогов.ИтогоСебестоимостьФакт;
	КонецЕсли; 
	СтруктураИтогов.ИтогоПрибыль = СтруктураИтогов.ИтогоСтоимость - СтруктураИтогов.ИтогоСебестоимость;
	
	Возврат СтруктураИтогов;
	
КонецФункции

#КонецОбласти 

#Область Скидки

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСуммуИПроцентСкидки(Запасы, СкидкаСумма, СкидкаПроцент, ТекущийВариантКП)
	
	ОбщаяСуммаРучнойСкидки	= 0;
	ОбщаяСуммаБезСкидок		= 0;
	Для каждого Стр Из Запасы Цикл
		
		Если Стр.ЭтоРазделитель ИЛИ Стр.НомерВариантаКП<>ТекущийВариантКП Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщаяСуммаБезСкидок = ОбщаяСуммаБезСкидок + Стр.Цена * Стр.Количество;
		Если Стр.ПроцентСкидкиНаценки = 0 Тогда
			Продолжить;
		ИначеЕсли Стр.Свойство("ПроцентАвтоматическойСкидки") Тогда
			ОбщаяСуммаРучнойСкидки = ОбщаяСуммаРучнойСкидки + Стр.Цена * Стр.Количество * (1 - Стр.ПроцентАвтоматическойСкидки / 100) - Стр.Сумма;
		Иначе
			ОбщаяСуммаРучнойСкидки = ОбщаяСуммаРучнойСкидки + Стр.Цена * Стр.Количество - Стр.Сумма;
		КонецЕсли; 
		
	КонецЦикла;
	
	СкидкаСумма = ОбщаяСуммаРучнойСкидки;
	СкидкаПроцент = ?(ОбщаяСуммаБезСкидок=0, 0, ОбщаяСуммаРучнойСкидки / ОбщаяСуммаБезСкидок * 100);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСкидкуЗапасов(СтрокаТабличнойЧасти)
	
	СтрокаЗапаса = СтрокаПоКлючу(ДанныеЗаказа.Запасы, СтрокаТабличнойЧасти.КлючСвязи);
	СтрокаЗапаса.ПроцентСкидкиНаценки = СтрокаТабличнойЧасти.ПроцентСкидкиНаценки;
	РассчитатьСуммуВСтрокеТабличнойЧасти(СтрокаЗапаса);
	СтрокаТабличнойЧасти.Сумма = СтрокаЗапаса.Всего;
	СтрокаТабличнойЧасти.Прибыль = СтрокаТабличнойЧасти.Сумма-СтрокаТабличнойЧасти.Себестоимость;

КонецПроцедуры

&НаКлиенте
Процедура РаспределитьСуммуНаСкидки(СуммаРаспределения)
	
	ОбщаяСумма = 0;
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаказа.Запасы Цикл
		Если СтрокаТабличнойЧасти.ЭтоРазделитель ИЛИ СтрокаТабличнойЧасти.НомерВариантаКП<>ТекущийВариантКП Тогда
			Продолжить;
		КонецЕсли;
		ОбщаяСумма = ОбщаяСумма + СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
	КонецЦикла;
	
	Если ОбщаяСумма = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПроцентСкидки = СуммаРаспределения / ОбщаяСумма * 100;
	
	ОбщаяСуммаСкидки = 0;
	ОбщаяСуммаБезСкидок = 0;
	ПоследняяСтрока = Неопределено;
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаказа.Запасы Цикл
		Если СтрокаТабличнойЧасти.ЭтоРазделитель ИЛИ СтрокаТабличнойЧасти.НомерВариантаКП<>ТекущийВариантКП Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = ПроцентСкидки;
		СтрокаТабличнойЧасти.Сумма = (СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество) * (1 - СтрокаТабличнойЧасти.ПроцентСкидкиНаценки / 100);
		
		ОбщаяСуммаСкидки = ОбщаяСуммаСкидки + (СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество - СтрокаТабличнойЧасти.Сумма);
		
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.СуммаАвтоматическойСкидки;
		
		ПоследняяСтрока = СтрокаТабличнойЧасти;
	КонецЦикла;
	
	НепогашеннаяСумма = СуммаРаспределения - ОбщаяСуммаСкидки;
	Если НепогашеннаяСумма <> 0 Тогда
		СуммаСтрокиБезСкидки = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
		ПоследняяСтрока.ПроцентСкидкиНаценки = (СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.ПроцентСкидкиНаценки/100 
			+ НепогашеннаяСумма) / СуммаСтрокиБезСкидки * 100;
			
		ПоследняяСтрока.Сумма = ПоследняяСтрока.Сумма - НепогашеннаяСумма;
	КонецЕсли;
	
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаказа.Запасы Цикл
		Если СтрокаТабличнойЧасти.ЭтоРазделитель ИЛИ СтрокаТабличнойЧасти.НомерВариантаКП<>ТекущийВариантКП Тогда
			Продолжить;
		КонецЕсли;
		СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
		СтрокаТабличнойЧасти.СуммаНДС = ?(ДанныеЗаказа.СуммаВключаетНДС, 
										  СтрокаТабличнойЧасти.Сумма - (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100),
										  СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100);
		СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(ДанныеЗаказа.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	КонецЦикла; 
	
	Если ТекущаяСтрокаСостава<0 Тогда
		ОбновитьСкидкиСуммыНаФорме();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСкидкиСуммыНаФорме()
	
	Для каждого СтрокаТабличнойЧасти Из КалькуляцияНаФорме Цикл
		Если СтрокаТабличнойЧасти.Источник<>ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Запас") Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаЗапаса = СтрокаПоКлючу(ДанныеЗаказа.Запасы, СтрокаТабличнойЧасти.КлючСвязи);
		Если СтрокаЗапаса=Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = СтрокаЗапаса.ПроцентСкидкиНаценки;
		СтрокаТабличнойЧасти.Сумма = СтрокаЗапаса.Всего;
		СтрокаТабличнойЧасти.Прибыль = СтрокаТабличнойЧасти.Сумма-СтрокаТабличнойЧасти.Себестоимость;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТабличнойЧасти)
	
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Сумма * (1 - (СтрокаТабличнойЧасти.ПроцентСкидкиНаценки+СтрокаТабличнойЧасти.ПроцентАвтоматическойСкидки) / 100);
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	СтрокаТабличнойЧасти.СуммаНДС = ?(ДанныеЗаказа.СуммаВключаетНДС, 
									  СтрокаТабличнойЧасти.Сумма - (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100),
									  СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100);
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(ДанныеЗаказа.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
КонецПроцедуры
 
#КонецОбласти 

&НаСервере
Процедура ОбновитьКалькуляциюСервер(ОбновлятьПоШаблону = Истина)
	
	Если ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры И НЕ ЗначениеЗаполнено(ВидЦен) Тогда
		Возврат;
	ИначеЕсли ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов И НЕ ЗначениеЗаполнено(ВидыЦенКонтрагентов) Тогда
		Возврат;
	КонецЕсли; 
	
	ЗапрещеноИзменениеКалькуляции = Ложь;
	Буфер = ТекущаяСтрокаСостава;
	ТекущаяСтрокаСостава = -1;
	ЗаполнитьКалькуляцию();
	ОбновитьФактическиеДанные();
	Если ОбновлятьПоШаблону Тогда
		ЗаполнитьПоШаблонуСервер();
	КонецЕсли; 
	ТекущаяСтрокаСостава = Буфер;
	Если ТекущаяСтрокаСостава<0 Тогда
		ВывестиКалькуляцию();
	Иначе
		ВывестиКалькуляцию(ТекущаяСтрокаСостава);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКалькуляцию()
	
	// Формирование служебных таблиц запасов
	ТаблицаЗапасов = ДанныеЗаказа.Запасы.Выгрузить(, "Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения, Количество").СкопироватьКолонки();
	ТаблицаЗапасов.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	Для каждого Стр Из ДанныеЗаказа.Запасы Цикл
		Если Стр.ЭтоРазделитель Тогда
			Продолжить;
		КонецЕсли;
		ИдентификаторСтроки = Стр.ПолучитьИдентификатор();
		НоваяСтрока = ТаблицаЗапасов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		НоваяСтрока.Идентификатор = ИдентификаторСтроки;
	КонецЦикла;
	
	ТаблицаМатериалов = ПустаяТаблицаМатериалов();
	ТаблицаКэшСебестоимости = ПустаяТаблицаКэшСебестоимости();
	ЗаполнитьТаблицуМатериалов(ТаблицаМатериалов);
	ОтборВидЦен = ОтборПоВидуЦен();
	РассчитатьСебестоимость(ТаблицаМатериалов, ОтборВидЦен, ДанныеЗаказа.Курс, ДанныеЗаказа.Кратность, ДанныеЗаказа.НалогообложениеНДС, ДанныеЗаказа.Дата, ЗаказСсылка);
	
	СтрокиКУдалению = Новый Массив;
	Для каждого Стр Из ДанныеЗаказа.Калькуляция Цикл
		Если Стр.РучноеИзменение Тогда
			НоваяСтрока = ТаблицаКэшСебестоимости.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		КонецЕсли; 
		Если ЗначениеЗаполнено(Стр.НоменклатураИзделие) Тогда
			СтрокиКУдалению.Добавить(Стр);
		ИначеЕсли ТипЗнч(Стр.Номенклатура)=Тип("СправочникСсылка.Номенклатура") И НЕ Стр.РучноеИзменение Тогда
			// Для добавленных в калькуляцию строк только обновляем себестоимость 
			СтруктураДанные = Новый Структура;
			СтруктураДанные.Вставить("Заказ", ЗаказСсылка);
			СтруктураДанные.Вставить("Дата", ДанныеЗаказа.Дата);
			СтруктураДанные.Вставить("НалогообложениеНДС", ДанныеЗаказа.НалогообложениеНДС);
			СтруктураДанные.Вставить("Курс", ДанныеЗаказа.Курс);
			СтруктураДанные.Вставить("Кратность", ДанныеЗаказа.Кратность);
			СтруктураДанные.Вставить("ВидыЦен", ОтборВидЦен);
			СтруктураДанные.Вставить("Номенклатура", Стр.Номенклатура);
			СтруктураДанные.Вставить("Характеристика", Стр.Характеристика);
			СтруктураДанные.Вставить("Спецификация", Стр.Спецификация);
			СтруктураДанные.Вставить("ЕдиницаИзмерения", Стр.ЕдиницаИзмерения);
			СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
			Стр.СебестоимостьЕдиницы = СтруктураДанные.СебестоимостьЕдиницы;
			Стр.Себестоимость = Стр.СебестоимостьЕдиницы*Стр.Количество;
		КонецЕсли; 
	КонецЦикла; 
	Для каждого Стр Из СтрокиКУдалению Цикл
		ДанныеЗаказа.Калькуляция.Удалить(Стр);
	КонецЦикла; 
	
	// Обновление калькуляции по запасам
	Для каждого Стр Из ТаблицаМатериалов Цикл
		СтрокаЗапаса = ДанныеЗаказа.Запасы.НайтиПоИдентификатору(Стр.Идентификатор);
		Если СтрокаЗапаса=Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		НоваяСтрока = ДанныеЗаказа.Калькуляция.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения, Количество, СебестоимостьЕдиницы, Себестоимость");
		НоваяСтрока.НомерВариантаКП = СтрокаЗапаса.НомерВариантаКП;
		НоваяСтрока.НоменклатураИзделие = СтрокаЗапаса.Номенклатура;
		НоваяСтрока.ХарактеристикаИзделие = СтрокаЗапаса.Характеристика;
		НоваяСтрока.СпецификацияИзделие = СтрокаЗапаса.Спецификация;
		НоваяСтрока.Источник = Перечисления.ИсточникиСтрокКалькуляции.Запас;
		НоваяСтрока.КлючСвязи = СтрокаЗапаса.КлючСвязи;
	КонецЦикла;
	
	// Восставновление себестоимости измененных строк из кэша
	Для каждого СтрКэш Из ТаблицаКэшСебестоимости Цикл
		СтруктураПоиска = Новый Структура("НомерВариантаКП, КлючСвязи, Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрКэш);
		Строки = ДанныеЗаказа.Калькуляция.НайтиСтроки(СтруктураПоиска);
		Для каждого Стр Из Строки Цикл
			Стр.СебестоимостьЕдиницы = СтрКэш.СебестоимостьЕдиницы;
			Если Стр.Количество=СтрКэш.Количество Тогда
				Стр.Себестоимость = СтрКэш.Себестоимость;
			Иначе
				Стр.Себестоимость = Стр.СебестоимостьЕдиницы*Стр.Количество;
			КонецЕсли;
			Стр.РучноеИзменение = Истина;
		КонецЦикла;  
	КонецЦикла;
	
	ПоказатьОшибкиРасчетаСебестоимости();
	
	Если ЗначениеЗаполнено(ДанныеЗаказа.СлужбаДоставки) Тогда
		ОбновитьСебестоимостьДоставки();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЦеныСервер()
	
	ТаблицаМатериалов = ПустаяТаблицаМатериалов();
	ОтборВидЦен = ОтборПоВидуЦен();
	
	Для каждого Стр Из ДанныеЗаказа.Калькуляция Цикл
		
		Если НЕ ТипЗнч(Стр.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			Продолжить;
		КонецЕсли;
		
		СтрМатериал = ТаблицаМатериалов.Добавить();
		СтрМатериал.НомерВариантаКП = Стр.НомерВариантаКП;
		СтрМатериал.Номенклатура = Стр.Номенклатура;
		СтрМатериал.Характеристика = Стр.Характеристика;
		СтрМатериал.Спецификация = Стр.Спецификация;
		СтрМатериал.ЕдиницаИзмерения = Стр.ЕдиницаИзмерения;
		СтрМатериал.Количество = 1;
		СтрМатериал.НоменклатураСебестоимость = Стр.Номенклатура;
		СтрМатериал.ХарактеристикаСебестоимость = Стр.Характеристика;
		СтрМатериал.СпецификацияСебестоимость = Стр.Спецификация;
		СтрМатериал.ЕдиницаИзмеренияСебестоимость = Стр.ЕдиницаИзмерения;
		СтрМатериал.КоличествоСебестоимость = 1;
		СтрМатериал.Идентификатор = Стр.ПолучитьИдентификатор();
		Пока ЕстьВложенныеСпецификации(ТаблицаМатериалов) Цикл
			Результат = РазузловатьНоменклатуру(ТаблицаМатериалов);
			Если НЕ Результат Тогда
				Возврат;
			КонецЕсли; 
		КонецЦикла;
		
	КонецЦикла; 
	
	РассчитатьСебестоимость(ТаблицаМатериалов, ОтборВидЦен, ДанныеЗаказа.Курс, ДанныеЗаказа.Кратность, ДанныеЗаказа.НалогообложениеНДС, ДанныеЗаказа.Дата, ЗаказСсылка);
	ТаблицаМатериалов.Свернуть("Идентификатор", "Себестоимость");
	
	Для каждого СтрокаМатериалов Из ТаблицаМатериалов Цикл
		Стр = ДанныеЗаказа.Калькуляция.НайтиПоИдентификатору(СтрокаМатериалов.Идентификатор);
		Стр.СебестоимостьЕдиницы = СтрокаМатериалов.Себестоимость;
		Стр.Себестоимость = Стр.СебестоимостьЕдиницы*Стр.Количество;
		Стр.РучноеИзменение = Ложь;
	КонецЦикла;
	
	ПоказатьОшибкиРасчетаСебестоимости();
	
	Если ТекущаяСтрокаСостава<0 Тогда
		ВывестиКалькуляцию();
	Иначе
		ВывестиКалькуляцию(ТекущаяСтрокаСостава);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьСпецификацию(ИдентификаторСтроки, ПоДаннымФормы = Истина)
	
	Если ПоДаннымФормы Тогда
		СтрокаТабличнойЧасти = КалькуляцияНаФорме.НайтиПоИдентификатору(ИдентификаторСтроки);
	Иначе
		СтрокаТабличнойЧасти = ДанныеЗаказа.Запасы.НайтиПоИдентификатору(ИдентификаторСтроки);
	КонецЕсли; 
	
	ТаблицаМатериалов = ПустаяТаблицаМатериалов();
	ЗаполнитьТаблицуМатериалов(ТаблицаМатериалов, СтрокаТабличнойЧасти);
	Если ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоПоследнейЦенеЗакупки Тогда
		ОтборВидЦен = Неопределено;
	ИначеЕсли ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры 
		ИЛИ ТипЗнч(ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции)=Тип("СправочникСсылка.ВидыЦен") Тогда
		ОтборВидЦен = ВидЦен;
	ИначеЕсли ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов Тогда
		ОтборВидЦен = ВидыЦенКонтрагентов;
	Иначе
		ОтборВидЦен = Неопределено;
	КонецЕсли; 
	РассчитатьСебестоимость(ТаблицаМатериалов, ОтборВидЦен, ДанныеЗаказа.Курс, ДанныеЗаказа.Кратность, ДанныеЗаказа.НалогообложениеНДС, ДанныеЗаказа.Дата, ЗаказСсылка);
	
	Если ТекущаяСтрокаСостава<0 Тогда
		
		СтрокаЗапаса = СтрокаПоКлючу(ДанныеЗаказа.Запасы, СтрокаТабличнойЧасти.КлючСвязи);
		Если СтрокаЗапаса<>Неопределено Тогда
			СтрокаЗапаса.Спецификация = СтрокаТабличнойЧасти.Спецификация;
			
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязи);
			СтрокиКУдалению = ДанныеЗаказа.Калькуляция.НайтиСтроки(СтруктураОтбора); 
			Для каждого Стр Из СтрокиКУдалению Цикл
				ДанныеЗаказа.Калькуляция.Удалить(Стр);
			КонецЦикла;
			
			Для каждого Стр Из ТаблицаМатериалов Цикл
				СтрокаЗапаса = ДанныеЗаказа.Запасы.НайтиПоИдентификатору(Стр.Идентификатор);
				Если СтрокаЗапаса=Неопределено Тогда
					Продолжить;
				КонецЕсли; 
				НоваяСтрока = ДанныеЗаказа.Калькуляция.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения, Количество, СебестоимостьЕдиницы, Себестоимость");
				НоваяСтрока.НоменклатураИзделие = СтрокаЗапаса.Номенклатура;
				НоваяСтрока.ХарактеристикаИзделие = СтрокаЗапаса.Характеристика;
				НоваяСтрока.СпецификацияИзделие = СтрокаЗапаса.Спецификация;
				НоваяСтрока.Источник = Перечисления.ИсточникиСтрокКалькуляции.Запас;
				НоваяСтрока.КлючСвязи = СтрокаЗапаса.КлючСвязи;
				НоваяСтрока.НомерВариантаКП = СтрокаЗапаса.НомерВариантаКП;
			КонецЦикла;
		КонецЕсли;
		
		Если ПоДаннымФормы Тогда
			СтрокаТабличнойЧасти.Себестоимость = ТаблицаМатериалов.Итог("Себестоимость");
			СтрокаТабличнойЧасти.СебестоимостьЕдиницы = ?(СтрокаТабличнойЧасти.Количество=0, 0, СтрокаТабличнойЧасти.Себестоимость/СтрокаТабличнойЧасти.Количество);
			СтрокаТабличнойЧасти.Прибыль = СтрокаТабличнойЧасти.Сумма-СтрокаТабличнойЧасти.Себестоимость;
		КонецЕсли; 
		ПоказатьОшибкиРасчетаСебестоимости(СтрокаТабличнойЧасти.КлючСвязи);
		
	Иначе
		
		Если ПоДаннымФормы Тогда
			СтрокаТабличнойЧасти.Себестоимость = ТаблицаМатериалов.Итог("Себестоимость");
			СтрокаТабличнойЧасти.СебестоимостьЕдиницы = ?(СтрокаТабличнойЧасти.Количество=0, 0, СтрокаТабличнойЧасти.Себестоимость/СтрокаТабличнойЧасти.Количество);
		КонецЕсли; 
		
	КонецЕсли;
 
	ОбновитьИтогиСервер();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура РассчитатьСебестоимость(ТаблицаМатериалов, ВидыЦен, Курс, Кратность, НалогообложениеНДС, Дата, Ссылка)
	
	Если ТипЗнч(ВидыЦен)=Тип("СписокЗначений") Тогда
		ПараметрВидыЦен = ВидыЦен.ВыгрузитьЗначения();
		СпособРасчета = Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов;
	ИначеЕсли ТипЗнч(ВидыЦен)=Тип("СправочникСсылка.ВидыЦен") Тогда
		ПараметрВидыЦен = Новый Массив;
		ПараметрВидыЦен.Добавить(ВидыЦен);
		СпособРасчета = Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры;
	Иначе
		ПараметрВидыЦен = Новый Массив;
		СпособРасчета = Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоПоследнейЦенеЗакупки;
	КонецЕсли; 
	
	// Расчет себестоимости и стоимости расходов
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыЦен", ПараметрВидыЦен);
	Запрос.УстановитьПараметр("СпособРасчета", СпособРасчета);
	Запрос.УстановитьПараметр("ТаблицаМатериалов", ТаблицаМатериалов);
	Запрос.УстановитьПараметр("Курс", Курс);
	Запрос.УстановитьПараметр("Кратность", Кратность);
	Запрос.УстановитьПараметр("УчитыватьНДС", (НалогообложениеНДС=Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС));
	Если Ссылка.Пустая() Тогда
		Запрос.УстановитьПараметр("Период", КонецДня(Дата));
	Иначе
		Запрос.УстановитьПараметр("Период", Дата);
	КонецЕсли;
	Запрос.УстановитьПараметр("ТаблицаПорядковОкругления", УправлениеНебольшойФирмойСервер.ТаблицаПорядковОкругления());
	Запрос.Текст = Справочники.СтавкиНДС.ПолучитьТекстЗапросаСозданияВТСтавкиНДС(Дата) +
	"ВЫБРАТЬ
	|	ТаблицаПорядковОкругления.Порядок,
	|	ТаблицаПорядковОкругления.Значение
	|ПОМЕСТИТЬ ТаблицаПорядковОкругления
	|ИЗ
	|	&ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаМатериалов.Идентификатор,
	|	ВЫРАЗИТЬ(ТаблицаМатериалов.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ТаблицаМатериалов.Характеристика,
	|	ТаблицаМатериалов.Спецификация,
	|	ТаблицаМатериалов.ЕдиницаИзмерения,
	|	ТаблицаМатериалов.Количество,
	|	ВЫРАЗИТЬ(ТаблицаМатериалов.НоменклатураСебестоимость КАК Справочник.Номенклатура) КАК НоменклатураСебестоимость,
	|	ТаблицаМатериалов.ХарактеристикаСебестоимость,
	|	ТаблицаМатериалов.ЕдиницаИзмеренияСебестоимость,
	|	ТаблицаМатериалов.КоличествоСебестоимость
	|ПОМЕСТИТЬ ТаблицаМатериалов
	|ИЗ
	|	&ТаблицаМатериалов КАК ТаблицаМатериалов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаМатериалов.Идентификатор,
	|	ТаблицаМатериалов.Номенклатура,
	|	ТаблицаМатериалов.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаМатериалов.Характеристика,
	|	ТаблицаМатериалов.Спецификация,
	|	ТаблицаМатериалов.ЕдиницаИзмерения,
	|	ТаблицаМатериалов.Количество,
	|	ТаблицаМатериалов.НоменклатураСебестоимость,
	|	ТаблицаМатериалов.НоменклатураСебестоимость.ТипНоменклатуры КАК ТипНоменклатурыСебестоимость,
	|	ТаблицаМатериалов.ХарактеристикаСебестоимость,
	|	ТаблицаМатериалов.ЕдиницаИзмеренияСебестоимость,
	|	ТаблицаМатериалов.КоличествоСебестоимость
	|ПОМЕСТИТЬ Материалы
	|ИЗ
	|	ТаблицаМатериалов КАК ТаблицаМатериалов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Материалы.Идентификатор,
	|	Материалы.Номенклатура,
	|	Материалы.ТипНоменклатуры,
	|	Материалы.Номенклатура.СпособПополнения КАК СпособПополнения,
	|	Материалы.Характеристика,
	|	Материалы.Спецификация,
	|	Материалы.ЕдиницаИзмерения,
	|	Материалы.Количество,
	|	СУММА(ЕСТЬNULL(Цены.Цена / &Курс * &Кратность, 0) * ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(Материалы.ЕдиницаИзмеренияСебестоимость) = ТИП(Справочник.ЕдиницыИзмерения)
	|				ТОГДА ВЫРАЗИТЬ(Материалы.ЕдиницаИзмеренияСебестоимость КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|			ИНАЧЕ 1
	|		КОНЕЦ * Материалы.КоличествоСебестоимость) КАК Себестоимость,
	|	Материалы.НоменклатураСебестоимость,
	|	Материалы.ХарактеристикаСебестоимость
	|ПОМЕСТИТЬ МатериалыСебестоимость
	|ИЗ
	|	Материалы КАК Материалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Закупки.Номенклатура КАК Номенклатура,
	|			Закупки.Характеристика КАК Характеристика,
	|			МИНИМУМ(ВЫРАЗИТЬ(ВЫБОР
	|						КОГДА Закупки.Количество = 0
	|							ТОГДА 0
	|						ИНАЧЕ Закупки.Сумма * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность / Закупки.Количество
	|					КОНЕЦ КАК ЧИСЛО(15, 2))) КАК Цена
	|		ИЗ
	|			(ВЫБРАТЬ
	|				Закупки.Номенклатура КАК Номенклатура,
	|				Закупки.Характеристика КАК Характеристика,
	|				МАКСИМУМ(Закупки.Период) КАК Период
	|			ИЗ
	|				РегистрНакопления.Закупки КАК Закупки
	|			ГДЕ
	|				Закупки.Период < &Период
	|				И (Закупки.Номенклатура, Закупки.Характеристика) В
	|						(ВЫБРАТЬ
	|							Материалы.НоменклатураСебестоимость,
	|							Материалы.ХарактеристикаСебестоимость
	|						ИЗ
	|							Материалы КАК Материалы
	|						ГДЕ
	|							&СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоПоследнейЦенеЗакупки)
	|							И НЕ Материалы.ТипНоменклатурыСебестоимость = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция))
	|			
	|			СГРУППИРОВАТЬ ПО
	|				Закупки.Номенклатура,
	|				Закупки.Характеристика) КАК ПоследниеЗакупки
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Закупки КАК Закупки
	|				ПО (Закупки.Номенклатура = ПоследниеЗакупки.Номенклатура)
	|					И (Закупки.Характеристика = ПоследниеЗакупки.Характеристика)
	|					И (Закупки.Период = ПоследниеЗакупки.Период),
	|			РегистрСведений.КурсыВалют.СрезПоследних(
	|					&Период,
	|					Валюта В
	|						(ВЫБРАТЬ
	|							ВалютаУчета.Значение
	|						ИЗ
	|							Константа.ВалютаУчета КАК ВалютаУчета)) КАК КурсыВалютСрезПоследних
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Закупки.Номенклатура,
	|			Закупки.Характеристика
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ЦеныНоменклатурыСрезПоследних.Номенклатура,
	|			ЦеныНоменклатурыСрезПоследних.Характеристика,
	|			(ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена * ВЫБОР
	|					КОГДА НЕ ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС
	|							И &УчитыватьНДС
	|						ТОГДА ЕСТЬNULL(ВТСтавки.СтавкаНДС.Ставка,0) / 100 + 1
	|					ИНАЧЕ 1
	|				КОНЕЦ / ВЫБОР
	|					КОГДА ТИПЗНАЧЕНИЯ(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|						ТОГДА ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|					ИНАЧЕ 1
	|				КОНЕЦ * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность / ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК ЧИСЛО(15, 0))) * ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01)
	|		ИЗ
	|			РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|					&Период,
	|					(&СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры)
	|							И ВидЦен В (&ВидыЦен)
	|							И Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция)
	|						ИЛИ Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция)
	|							И ВидЦен = ЗНАЧЕНИЕ(Справочник.ВидыЦен.Учетная))
	|						И (Номенклатура, Характеристика) В
	|							(ВЫБРАТЬ
	|								Материалы.НоменклатураСебестоимость,
	|								Материалы.ХарактеристикаСебестоимость
	|							ИЗ
	|								Материалы КАК Материалы
	|							ГДЕ
	|								(&СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры)
	|									ИЛИ Материалы.НоменклатураСебестоимость.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция)))) КАК ЦеныНоменклатурыСрезПоследних
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, ) КАК КурсыВалютСрезПоследних
	|				ПО ЦеныНоменклатурыСрезПоследних.ВидЦен.ВалютаЦены = КурсыВалютСрезПоследних.Валюта
	|				ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|				ПО ЦеныНоменклатурыСрезПоследних.ВидЦен.ПорядокОкругления = ТаблицаПорядковОкругления.Порядок
	|				ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавки
	|				ПО  ВТСтавки.ВидСтавкиНДС = ЦеныНоменклатурыСрезПоследних.Номенклатура.ВидСтавкиНДС
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ЦеныНоменклатурыКонтрагентовСрезПоследних.Номенклатура,
	|			ЦеныНоменклатурыКонтрагентовСрезПоследних.Характеристика,
	|			МИНИМУМ(ЦеныНоменклатурыКонтрагентовСрезПоследних.Цена * ВЫБОР
	|					КОГДА НЕ ЦеныНоменклатурыКонтрагентовСрезПоследних.ВидЦенКонтрагента.ЦенаВключаетНДС
	|							И &УчитыватьНДС
	|						ТОГДА  ЕСТЬNULL(ВТСтавки.СтавкаНДС.Ставка,0)/ 100 + 1
	|					ИНАЧЕ 1
	|				КОНЕЦ / ВЫБОР
	|					КОГДА ТИПЗНАЧЕНИЯ(ЦеныНоменклатурыКонтрагентовСрезПоследних.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|						ТОГДА ВЫРАЗИТЬ(ЦеныНоменклатурыКонтрагентовСрезПоследних.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|					ИНАЧЕ 1
	|				КОНЕЦ * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность)
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ЦеныНоменклатурыКонтрагентовСрезПоследних.Номенклатура КАК Номенклатура,
	|				ЦеныНоменклатурыКонтрагентовСрезПоследних.Характеристика КАК Характеристика,
	|				МАКСИМУМ(ЦеныНоменклатурыКонтрагентовСрезПоследних.Период) КАК Период
	|			ИЗ
	|				РегистрСведений.ЦеныНоменклатурыКонтрагентов.СрезПоследних(
	|						&Период,
	|						&СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов)
	|							И ВидЦенКонтрагента В (&ВидыЦен)
	|							И (Номенклатура, Характеристика) В
	|								(ВЫБРАТЬ
	|									Материалы.НоменклатураСебестоимость,
	|									Материалы.ХарактеристикаСебестоимость
	|								ИЗ
	|									Материалы КАК Материалы
	|								ГДЕ
	|									&СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов)
	|									И НЕ Материалы.ТипНоменклатурыСебестоимость = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция))) КАК ЦеныНоменклатурыКонтрагентовСрезПоследних
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ЦеныНоменклатурыКонтрагентовСрезПоследних.Номенклатура,
	|				ЦеныНоменклатурыКонтрагентовСрезПоследних.Характеристика) КАК ПоследниеЦены
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыКонтрагентов.СрезПоследних(
	|						&Период,
	|						&СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов)
	|							И ВидЦенКонтрагента В (&ВидыЦен)
	|							И (Номенклатура, Характеристика) В
	|								(ВЫБРАТЬ
	|									Материалы.НоменклатураСебестоимость,
	|									Материалы.ХарактеристикаСебестоимость
	|								ИЗ
	|									Материалы КАК Материалы
	|								ГДЕ
	|									&СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов)
	|									И НЕ Материалы.ТипНоменклатурыСебестоимость = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция))) КАК ЦеныНоменклатурыКонтрагентовСрезПоследних
	|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, ) КАК КурсыВалютСрезПоследних
	|					ПО ЦеныНоменклатурыКонтрагентовСрезПоследних.ВидЦенКонтрагента.ВалютаЦены = КурсыВалютСрезПоследних.Валюта
	|				ПО (ЦеныНоменклатурыКонтрагентовСрезПоследних.Номенклатура = ПоследниеЦены.Номенклатура)
	|					И (ЦеныНоменклатурыКонтрагентовСрезПоследних.Характеристика = ПоследниеЦены.Характеристика)
	|					И (ЦеныНоменклатурыКонтрагентовСрезПоследних.Период = ПоследниеЦены.Период)
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавки
	|			ПО ВТСтавки.ВидСтавкиНДС = ЦеныНоменклатурыКонтрагентовСрезПоследних.Номенклатура.ВидСтавкиНДС
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ЦеныНоменклатурыКонтрагентовСрезПоследних.Номенклатура,
	|			ЦеныНоменклатурыКонтрагентовСрезПоследних.Характеристика) КАК Цены
	|		ПО Материалы.НоменклатураСебестоимость = Цены.Номенклатура
	|			И Материалы.ХарактеристикаСебестоимость = Цены.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	Материалы.Идентификатор,
	|	Материалы.Номенклатура,
	|	Материалы.ТипНоменклатуры,
	|	Материалы.Номенклатура.СпособПополнения,
	|	Материалы.Характеристика,
	|	Материалы.Спецификация,
	|	Материалы.ЕдиницаИзмерения,
	|	Материалы.Количество,
	|	Материалы.НоменклатураСебестоимость,
	|	Материалы.ХарактеристикаСебестоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МатериалыСебестоимость.Идентификатор,
	|	МатериалыСебестоимость.Номенклатура КАК Номенклатура,
	|	МатериалыСебестоимость.ТипНоменклатуры,
	|	МатериалыСебестоимость.СпособПополнения,
	|	МатериалыСебестоимость.Характеристика,
	|	МатериалыСебестоимость.Спецификация,
	|	МатериалыСебестоимость.ЕдиницаИзмерения,
	|	МатериалыСебестоимость.Количество,
	|	СУММА(МатериалыСебестоимость.Себестоимость) КАК Себестоимость,
	|	ВЫБОР
	|		КОГДА МатериалыСебестоимость.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(МатериалыСебестоимость.Себестоимость) / МатериалыСебестоимость.Количество
	|	КОНЕЦ КАК СебестоимостьЕдиницы
	|ИЗ
	|	МатериалыСебестоимость КАК МатериалыСебестоимость
	|
	|СГРУППИРОВАТЬ ПО
	|	МатериалыСебестоимость.Количество,
	|	МатериалыСебестоимость.Спецификация,
	|	МатериалыСебестоимость.ТипНоменклатуры,
	|	МатериалыСебестоимость.Номенклатура,
	|	МатериалыСебестоимость.ЕдиницаИзмерения,
	|	МатериалыСебестоимость.Характеристика,
	|	МатериалыСебестоимость.СпособПополнения,
	|	МатериалыСебестоимость.Идентификатор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура";
	
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаМатериалов = Результат.Получить(Результат.Количество()-1).Выгрузить();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьОшибкиРасчетаСебестоимости(КлючСвязи = Неопределено)
	
	ТаблицаПроверки = ДанныеЗаказа.Калькуляция.Выгрузить().СкопироватьКолонки("Номенклатура, Характеристика, РучноеИзменение, СебестоимостьЕдиницы");
	Если КлючСвязи=Неопределено Тогда
		СтрокиПроверки = ДанныеЗаказа.Калькуляция.Выгрузить();
	Иначе
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("КлючСвязи", КлючСвязи);
		СтрокиПроверки = ДанныеЗаказа.Калькуляция.Выгрузить(СтруктураПоиска);
	КонецЕсли;
	Для каждого СтрокаТабличнойЧасти Из СтрокиПроверки Цикл
		Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура)<>Тип("СправочникСсылка.Номенклатура") 
			ИЛИ СтрокаТабличнойЧасти.СебестоимостьЕдиницы<>0 
			ИЛИ СтрокаТабличнойЧасти.РучноеИзменение Тогда
			Продолжить;
		КонецЕсли; 
		НоваяСтрока = ТаблицаПроверки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
	КонецЦикла; 
	
	ТаблицаПроверки.Свернуть("Номенклатура, Характеристика, РучноеИзменение, СебестоимостьЕдиницы");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаПроверки", ТаблицаПроверки);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаПроверки.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ТаблицаПроверки.Характеристика КАК Характеристика,
	|	ТаблицаПроверки.РучноеИзменение КАК РучноеИзменение,
	|	ТаблицаПроверки.СебестоимостьЕдиницы КАК СебестоимостьЕдиницы
	|ПОМЕСТИТЬ ТаблицаПроверки
	|ИЗ
	|	&ТаблицаПроверки КАК ТаблицаПроверки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПроверки.Номенклатура КАК Номенклатура,
	|	ТаблицаПроверки.Характеристика КАК Характеристика,
	|	ТаблицаПроверки.РучноеИзменение КАК РучноеИзменение,
	|	ТаблицаПроверки.СебестоимостьЕдиницы КАК СебестоимостьЕдиницы,
	|	СправочникНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ТаблицаПроверки КАК ТаблицаПроверки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаПроверки.Номенклатура = СправочникНоменклатура.Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипНоменклатуры=Перечисления.ТипыНоменклатуры.Запас Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось определить себестоимость запаса %1'");
		ИначеЕсли Выборка.ТипНоменклатуры=Перечисления.ТипыНоменклатуры.Операция Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось определить стоимость операции %1 (не установлена учетная цена)'");
		ИначеЕсли Выборка.ТипНоменклатуры=Перечисления.ТипыНоменклатуры.Работа Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось определить стоимость работы %1'");
		ИначеЕсли Выборка.ТипНоменклатуры=Перечисления.ТипыНоменклатуры.Услуга Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось определить стоимость услуги %1'");
		Иначе
			Продолжить;
		КонецЕсли; 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстСообщения,
		УправлениеНебольшойФирмойСервер.ПолучитьПредставлениеНоменклатурыДляПечати(Выборка.Номенклатура, Выборка.Характеристика));
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.КлючДанных = Выборка.Номенклатура;
		Сообщение.Сообщить(); 
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеШаблона()
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаказа.ШаблонКалькуляции) Тогда
		СоставШаблона.Очистить();
		Возврат;
	КонецЕсли; 
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ШаблонКалькуляции", ДанныеЗаказа.ШаблонКалькуляции);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	""Запас"" КАК ТипСтроки,
	|	ШаблоныКалькуляцийЗапасы.Номенклатура КАК Номенклатура,
	|	ШаблоныКалькуляцийЗапасы.Характеристика КАК Характеристика,
	|	ШаблоныКалькуляцийЗапасы.Спецификация КАК Спецификация,
	|	ШаблоныКалькуляцийЗапасы.Количество КАК Количество,
	|	ШаблоныКалькуляцийЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	НЕОПРЕДЕЛЕНО КАК СпособРасчета,
	|	НЕОПРЕДЕЛЕНО КАК Значение,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	ШаблоныКалькуляцийЗапасы.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	Справочник.ШаблоныКалькуляций.Запасы КАК ШаблоныКалькуляцийЗапасы
	|ГДЕ
	|	ШаблоныКалькуляцийЗапасы.Ссылка = &ШаблонКалькуляции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Расход"",
	|	ШаблоныКалькуляцийРасходы.Расход,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	ШаблоныКалькуляцийРасходы.СпособРасчета,
	|	ШаблоныКалькуляцийРасходы.Значение,
	|	ШаблоныКалькуляцийРасходы.Валюта,
	|	ШаблоныКалькуляцийРасходы.КлючСвязи
	|ИЗ
	|	Справочник.ШаблоныКалькуляций.Расходы КАК ШаблоныКалькуляцийРасходы
	|ГДЕ
	|	ШаблоныКалькуляцийРасходы.Ссылка = &ШаблонКалькуляции";
	СоставШаблона.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоШаблонуСервер()
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Источник", Перечисления.ИсточникиСтрокКалькуляции.Шаблон);
	Строки = ДанныеЗаказа.Калькуляция.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаТабличнойЧасти Из Строки Цикл
		ДанныеЗаказа.Калькуляция.Удалить(СтрокаТабличнойЧасти);
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаказа.ШаблонКалькуляции) Тогда
		Возврат;
	КонецЕсли;
	
	БазаРасчета = БазаРасчетаШаблонов(ДанныеЗаказа);
	ВалютаУчета = Константы.ВалютаУчета.Получить();
	ОтборВидЦен = ОтборПоВидуЦен();
	
	ПрочитатьДанныеШаблона();
	
	Для каждого СтрокаСостава Из СоставШаблона Цикл
		Для ВариантКП = ?(ДанныеЗаказа.КоличествоВариантовКП=0, 0, 1) По ДанныеЗаказа.КоличествоВариантовКП Цикл
			НоваяСтрока = ДанныеЗаказа.Калькуляция.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСостава, "Номенклатура, Характеристика, Спецификация, Количество, ЕдиницаИзмерения, КлючСвязи");
			НоваяСтрока.НомерВариантаКП = ВариантКП;
			НоваяСтрока.Источник = Перечисления.ИсточникиСтрокКалькуляции.Шаблон;
			Если СтрокаСостава.ТипСтроки="Расход" Тогда
				Если СтрокаСостава.СпособРасчета=Перечисления.СпособыРасчетаСуммыЗатрат.ФиксированнаяСумма Тогда
					ВалютаШаблона = ?(ЗначениеЗаполнено(СтрокаСостава.Валюта), СтрокаСостава.Валюта, ВалютаУчета);
					Курсы = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаШаблона, ДанныеЗаказа.Дата);
					НоваяСтрока.Себестоимость = СтрокаСостава.Значение * Курсы.Курс / Курсы.Кратность / ДанныеЗаказа.Курс * ДанныеЗаказа.Кратность;
				ИначеЕсли СтрокаСостава.СпособРасчета=Перечисления.СпособыРасчетаСуммыЗатрат.ПроцентОтСуммыПродажи Тогда
					Стоимость = БазаРасчета.Стоимость.Получить(ВариантКП);
					НоваяСтрока.Себестоимость = Стоимость * СтрокаСостава.Значение / 100;
				ИначеЕсли СтрокаСостава.СпособРасчета=Перечисления.СпособыРасчетаСуммыЗатрат.ПроцентОтПрибыли Тогда
					Прибыль = БазаРасчета.Стоимость.Получить(ВариантКП) - БазаРасчета.Себестоимость.Получить(ВариантКП);
					НоваяСтрока.Себестоимость = Прибыль * СтрокаСостава.Значение / 100;
				КонецЕсли;
			ИначеЕсли СтрокаСостава.ТипСтроки="Запас" Тогда
				СтруктураДанные = Новый Структура;
				СтруктураДанные.Вставить("Заказ", ЗаказСсылка);
				СтруктураДанные.Вставить("Дата", ДанныеЗаказа.Дата);
				СтруктураДанные.Вставить("НалогообложениеНДС", ДанныеЗаказа.НалогообложениеНДС);
				СтруктураДанные.Вставить("Курс", ДанныеЗаказа.Курс);
				СтруктураДанные.Вставить("Кратность", ДанныеЗаказа.Кратность);
				СтруктураДанные.Вставить("ВидыЦен", ОтборВидЦен);
				СтруктураДанные.Вставить("Номенклатура", НоваяСтрока.Номенклатура);
				СтруктураДанные.Вставить("Характеристика", НоваяСтрока.Характеристика);
				СтруктураДанные.Вставить("Спецификация", НоваяСтрока.Спецификация);
				СтруктураДанные.Вставить("ЕдиницаИзмерения", НоваяСтрока.ЕдиницаИзмерения);
				СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураДанные, "СебестоимостьЕдиницы");
				НоваяСтрока.Себестоимость = НоваяСтрока.СебестоимостьЕдиницы * НоваяСтрока.Количество;
			КонецЕсли;
		КонецЦикла; 
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьФормулыПоШаблону()
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаказа.ШаблонКалькуляции) Тогда
		ОбновитьИтогиКлиент();
		Возврат;
	КонецЕсли;
	
	БазаРасчета = БазаРасчетаШаблонов(ДанныеЗаказа);
	
	Для каждого Стр Из ДанныеЗаказа.Калькуляция Цикл
		Если Стр.Источник<>ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Шаблон") Тогда
			Продолжить;
		КонецЕсли;
		Если Стр.РучноеИзменение Тогда
			Продолжить;
		КонецЕсли; 
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязи", Стр.КлючСвязи);
		СтрокиСостава = СоставШаблона.НайтиСтроки(СтруктураОтбора);
		Если СтрокиСостава.Количество()=0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокаСостава = СтрокиСостава[0];
		Если СтрокаСостава.СпособРасчета=ПредопределенноеЗначение("Перечисление.СпособыРасчетаСуммыЗатрат.ПроцентОтСуммыПродажи") Тогда
			Стоимость = БазаРасчета.Стоимость.Получить(Стр.НомерВариантаКП);
			Стр.Себестоимость = Стоимость * СтрокаСостава.Значение/100;
		ИначеЕсли СтрокаСостава.СпособРасчета=ПредопределенноеЗначение("Перечисление.СпособыРасчетаСуммыЗатрат.ПроцентОтПрибыли") Тогда
			Прибыль = БазаРасчета.Стоимость.Получить(Стр.НомерВариантаКП) - БазаРасчета.Себестоимость.Получить(Стр.НомерВариантаКП);
			Стр.Себестоимость = Прибыль * СтрокаСостава.Значение/100;
		Иначе
			Продолжить;
		КонецЕсли;
		Если ТекущаяСтрокаСостава<0 Тогда
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Источник", ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Шаблон"));
			СтруктураОтбора.Вставить("КлючСвязи", СтрокаСостава.КлючСвязи);
			СтрокиНаФорме = КалькуляцияНаФорме.НайтиСтроки(СтруктураОтбора);
			Для каждого СтрокаНаФорме Из СтрокиНаФорме Цикл
				СтрокаНаФорме.Себестоимость = Стр.Себестоимость;
				СтрокаНаФорме.Прибыль = СтрокаНаФорме.Сумма-Стр.Себестоимость;
			КонецЦикла; 
		КонецЕсли; 
	КонецЦикла;
	
	Если ТекущаяСтрокаСостава<0 Тогда
		ОбновитьИтогиКлиент();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция БазаРасчетаШаблонов(ДанныеЗаказа)
	
	Результат = Новый Структура;
	
	СтоимостьВариантов = Новый Соответствие;
	СебестоимостьВариантов = Новый Соответствие;
	Для ВариантКП = ?(ДанныеЗаказа.КоличествоВариантовКП=0, 0, 1) По ДанныеЗаказа.КоличествоВариантовКП Цикл
		СтоимостьВариантов.Вставить(ВариантКП, 0);
		СебестоимостьВариантов.Вставить(ВариантКП, 0);
	КонецЦикла;
	
	Для каждого Стр Из ДанныеЗаказа.Запасы Цикл
		Если Стр.ЭтоРазделитель Тогда
			Продолжить;
		КонецЕсли;
		СтоимостьВариантов.Вставить(Стр.НомерВариантаКП, СтоимостьВариантов.Получить(Стр.НомерВариантаКП) + Стр.Всего);
	КонецЦикла;
	
	Для каждого Стр Из ДанныеЗаказа.Калькуляция Цикл
		Если Стр.Источник=ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Шаблон") Тогда
			Продолжить;
		КонецЕсли;
		СебестоимостьВариантов.Вставить(Стр.НомерВариантаКП, СебестоимостьВариантов.Получить(Стр.НомерВариантаКП) + Стр.Себестоимость);
	КонецЦикла;
	
	Результат.Вставить("Стоимость", СтоимостьВариантов);
	Результат.Вставить("Себестоимость", СебестоимостьВариантов);
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОбновитьФактическиеДанные()
	
	ФактическиеДанные.Очистить();
	Если НЕ ЗначениеЗаполнено(ЗаказСсылка) ИЛИ НЕ ПараметрыФормы.ДоступныПолныеПрава Тогда
		Возврат;
	КонецЕсли; 
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗаказПокупателя", ЗаказСсылка);
	Запрос.УстановитьПараметр("Курс", ДанныеЗаказа.Курс);
	Запрос.УстановитьПараметр("Кратность", ДанныеЗаказа.Кратность);
	Запрос.УстановитьПараметр("Период", ДанныеЗаказа.Дата);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.Номенклатура,
	|	СУММА(ВложенныйЗапрос.СтоимостьФакт * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность / &Курс * &Кратность) КАК СтоимостьФакт,
	|	СУММА(ВложенныйЗапрос.СебестоимостьФакт * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность / &Курс * &Кратность) КАК СебестоимостьФакт,
	|	СУММА(ВложенныйЗапрос.СтоимостьФакт * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность / &Курс * &Кратность) - СУММА(ВложенныйЗапрос.СебестоимостьФакт * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность / &Курс * &Кратность) КАК ПрибыльФакт
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПродажиОбороты.Номенклатура КАК Номенклатура,
	|		ПродажиОбороты.СебестоимостьОборот КАК СебестоимостьФакт,
	|		ПродажиОбороты.СуммаОборот КАК СтоимостьФакт
	|	ИЗ
	|		РегистрНакопления.Продажи.Обороты(, , Период, ЗаказПокупателя = &ЗаказПокупателя) КАК ПродажиОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ДоходыИРасходы.Аналитика ССЫЛКА Справочник.Номенклатура
	|					И ДоходыИРасходы.Аналитика <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				ТОГДА ДоходыИРасходы.Аналитика
	|			ИНАЧЕ ДоходыИРасходы.СчетУчета
	|		КОНЕЦ,
	|		ДоходыИРасходы.СуммаРасходовОборот,
	|		ДоходыИРасходы.СуммаДоходовОборот
	|	ИЗ
	|		РегистрНакопления.ДоходыИРасходы.Обороты(
	|				,
	|				,
	|				Период,
	|				ЗаказПокупателя = &ЗаказПокупателя
	|					И (НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|						ИЛИ НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж <> СчетУчета
	|							И НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж <> СчетУчета)) КАК ДоходыИРасходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ДоходыИРасходы.Аналитика ССЫЛКА Справочник.Номенклатура
	|					И ДоходыИРасходы.Аналитика <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				ТОГДА ДоходыИРасходы.Аналитика
	|			ИНАЧЕ ДоходыИРасходы.СчетУчета
	|		КОНЕЦ,
	|		-ДоходыИРасходы.СуммаРасходовОборот,
	|		-ДоходыИРасходы.СуммаДоходовОборот
	|	ИЗ
	|		РегистрНакопления.ДоходыИРасходы.Обороты(
	|				,
	|				,
	|				Месяц,
	|				ЗаказПокупателя = &ЗаказПокупателя
	|					И (НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|						ИЛИ НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж <> СчетУчета
	|							И НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж <> СчетУчета)
	|					И (СчетУчета.СпособРаспределения <> ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияРасходов.НеРаспределять)
	|						ИЛИ НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|						ИЛИ НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))) КАК ДоходыИРасходы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
	|			ПО (НАЧАЛОПЕРИОДА(ДоходыИРасходы.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ЗакрытиеМесяца.Дата, МЕСЯЦ))
	|				И (ЗакрытиеМесяца.Проведен)
	|				И (ЗакрытиеМесяца.РасчетФинансовогоРезультата)
	|	ГДЕ
	|		НЕ ЗакрытиеМесяца.Ссылка ЕСТЬ NULL
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ФинансовыйРезультат.Аналитика ССЫЛКА Справочник.Номенклатура
	|					И ФинансовыйРезультат.Аналитика <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				ТОГДА ФинансовыйРезультат.Аналитика
	|			ИНАЧЕ ФинансовыйРезультат.СчетУчета
	|		КОНЕЦ,
	|		ФинансовыйРезультат.СуммаРасходовОборот,
	|		ФинансовыйРезультат.СуммаДоходовОборот
	|	ИЗ
	|		РегистрНакопления.ФинансовыйРезультат.Обороты(
	|				,
	|				,
	|				Период,
	|				ЗаказПокупателя = &ЗаказПокупателя
	|					И СчетУчета.СпособРаспределения <> ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияРасходов.НеРаспределять)
	|					И (НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж <> СчетУчета
	|							И НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж <> СчетУчета
	|						ИЛИ НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|						ИЛИ НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))) КАК ФинансовыйРезультат
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗапасыОстаткиИОбороты.СчетУчета,
	|		ЗапасыОстаткиИОбороты.СуммаПриход - ЗапасыОстаткиИОбороты.СуммаРасход,
	|		0
	|	ИЗ
	|		РегистрНакопления.Запасы.ОстаткиИОбороты(
	|				,
	|				,
	|				Период,
	|				Движения,
	|				ЗаказПокупателя = &ЗаказПокупателя
	|					И Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|					И СчетУчета В
	|						(ВЫБРАТЬ
	|							ЗапасыОстатки.СчетУчета
	|						ИЗ
	|							РегистрНакопления.Запасы.Остатки(, ЗаказПокупателя = &ЗаказПокупателя
	|								И Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ЗапасыОстатки
	|						ГДЕ
	|							ЗапасыОстатки.КоличествоОстаток = 0
	|							И ЗапасыОстатки.СуммаОстаток <> 0)) КАК ЗапасыОстаткиИОбороты) КАК ВложенныйЗапрос,
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&Период,
	|			Валюта В
	|				(ВЫБРАТЬ
	|					ВалютаУчета.Значение
	|				ИЗ
	|					Константа.ВалютаУчета КАК ВалютаУчета)) КАК КурсыВалютСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура";
	ФактическиеДанные.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Функция СравнитьКалькуляциюИСпецификацию(Идентификатор)
	
	СтрокаЗапаса = ДанныеЗаказа.Запасы.НайтиПоИдентификатору(Идентификатор);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("КлючСвязи", СтрокаЗапаса.КлючСвязи);
	ИзмененныеСтроки = СоставыСпецификаций.НайтиСтроки(СтруктураОтбора);
	Если ИзмененныеСтроки.Количество()>0 Тогда
		// Спецификации с несохраненными изменениями пропускаем
		Возврат Истина;
	КонецЕсли; 
	ТаблицаСостава = ДанныеЗаказа.Калькуляция.Выгрузить().СкопироватьКолонки("Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения, Количество");
	ТаблицаКалькуляции = ДанныеЗаказа.Калькуляция.Выгрузить(СтруктураОтбора);
	Для каждого СтрКалькуляции Из ТаблицаКалькуляции Цикл
		Если СтрКалькуляции.Источник=Перечисления.ИсточникиСтрокКалькуляции.Шаблон Тогда
			Продолжить;
		КонецЕсли; 
		Если СтрКалькуляции.Номенклатура=СтрКалькуляции.НоменклатураИзделие И СтрКалькуляции.Характеристика=СтрКалькуляции.ХарактеристикаИзделие Тогда
			Продолжить;
		КонецЕсли; 
		НоваяСтрока = ТаблицаСостава.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрКалькуляции);
		Если СтрокаЗапаса.Количество<>0 И СтрокаЗапаса.Количество<>1 Тогда
			Для каждого Стр Из ТаблицаКалькуляции Цикл
				НоваяСтрока.Количество = НоваяСтрока.Количество;
			КонецЦикла; 
		КонецЕсли; 
	КонецЦикла; 
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаСостава", ТаблицаСостава);
	Запрос.УстановитьПараметр("Спецификация", СтрокаЗапаса.Спецификация);
	Запрос.УстановитьПараметр("КоличествоЗапасов", СтрокаЗапаса.Количество);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаСостава.Номенклатура,
	|	ТаблицаСостава.Характеристика,
	|	ТаблицаСостава.Спецификация,
	|	ТаблицаСостава.ЕдиницаИзмерения,
	|	ТаблицаСостава.Количество
	|ПОМЕСТИТЬ ТаблицаСостава
	|ИЗ
	|	&ТаблицаСостава КАК ТаблицаСостава
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Спецификация,
	|	ВложенныйЗапрос.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА СУММА(ВложенныйЗапрос.Количество) <> 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КоличествоОтличается,
	|	МАКСИМУМ(ВложенныйЗапрос.ПрисутствуетВКалькуляции) КАК ПрисутствуетВКалькуляции,
	|	МАКСИМУМ(ВложенныйЗапрос.ПрисутствуетВСпецификации) КАК ПрисутствуетВСпецификации,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаСостава.Номенклатура КАК Номенклатура,
	|		ТаблицаСостава.Характеристика КАК Характеристика,
	|		ТаблицаСостава.Спецификация КАК Спецификация,
	|		ТаблицаСостава.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ТаблицаСостава.Количество КАК Количество,
	|		ИСТИНА КАК ПрисутствуетВКалькуляции,
	|		ЛОЖЬ КАК ПрисутствуетВСпецификации
	|	ИЗ
	|		ТаблицаСостава КАК ТаблицаСостава
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СпецификацииСостав.Номенклатура,
	|		СпецификацииСостав.Характеристика,
	|		СпецификацииСостав.Спецификация,
	|		СпецификацииСостав.ЕдиницаИзмерения,
	|		-(ВЫРАЗИТЬ(СпецификацииСостав.Количество / СпецификацииСостав.КоличествоПродукции * &КоличествоЗапасов КАК ЧИСЛО(15, 3))),
	|		ЛОЖЬ,
	|		ИСТИНА
	|	ИЗ
	|		Справочник.Спецификации.Состав КАК СпецификацииСостав
	|	ГДЕ
	|		СпецификацииСостав.Ссылка = &Спецификация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СпецификацииОперации.Операция,
	|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ч),
	|		-(ВЫРАЗИТЬ(СпецификацииОперации.НормаВремени / СпецификацииОперации.КоличествоПродукции * &КоличествоЗапасов КАК ЧИСЛО(15, 3))),
	|		ЛОЖЬ,
	|		ИСТИНА
	|	ИЗ
	|		Справочник.Спецификации.Операции КАК СпецификацииОперации
	|	ГДЕ
	|		СпецификацииОперации.Ссылка = &Спецификация) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Спецификация,
	|	ВложенныйЗапрос.ЕдиницаИзмерения";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если НЕ Выборка.ПрисутствуетВКалькуляции ИЛИ НЕ Выборка.ПрисутствуетВСпецификации ИЛИ Выборка.КоличествоОтличается Тогда
			Возврат Ложь;
		КонецЕсли; 	
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВывестиКалькуляциюКлиент(Идентификатор = Неопределено)
	
	ЗапрещеноИзменениеКалькуляции = Ложь;
	Если НЕ ТолькоПросмотр И Идентификатор<>Неопределено И НЕ СравнитьКалькуляциюИСпецификацию(Идентификатор) Тогда
		Оповещение = Новый ОписаниеОповещения("ВывестиКалькуляциюКлиентЗавершение", ЭтотОбъект, Идентификатор);
		ПоказатьВопрос(
		Оповещение, 
		НСтр("ru = 'Состав спецификации был изменен. Обновить калькуляцию заказа?'"), 
		РежимДиалогаВопрос.ДаНет, 
		0, 
		КодВозвратаДиалога.Нет);
		Возврат;
	КонецЕсли; 
	ВывестиКалькуляциюКлиентЗавершение(Неопределено, Идентификатор);
	
	ВывестиКомментарий();
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиКалькуляциюКлиентЗавершение(Ответ, Идентификатор) Экспорт
	
	Если Ответ=КодВозвратаДиалога.Нет Тогда
		ЗапрещеноИзменениеКалькуляции = Истина;
	ИначеЕсли Ответ=КодВозвратаДиалога.Да Тогда
		Буфер = ТекущаяСтрокаСостава;
		ТекущаяСтрокаСостава = -1;
		ЗаменитьСпецификацию(Идентификатор, Ложь);
		ТекущаяСтрокаСостава = Буфер;
	КонецЕсли; 
	ВывестиКалькуляцию(Идентификатор);	
	
КонецПроцедуры

&НаСервере
Процедура ВывестиКалькуляцию(Идентификатор = Неопределено)
	
	УстановитьВидимостьИДоступность();
	// Переменная хранит номенклатуру, по которой уже выведены фактические данные
	ВыведенныеФактДанные = Новый Массив;
	
	
	КалькуляцияНаФорме.Очистить();
	Если Идентификатор=Неопределено Тогда
		
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("НомерВариантаКП", ТекущийВариантКП);
		
		ТаблицаСебестоимости = ДанныеЗаказа.Калькуляция.Выгрузить(СтруктураОтбора);
		Для каждого СтрокаСебестоимости Из ТаблицаСебестоимости Цикл
			Если ЗначениеЗаполнено(СтрокаСебестоимости.НоменклатураИзделие) И СтрокаСебестоимости.НоменклатураИзделие<>СтрокаСебестоимости.Номенклатура Тогда
				СтрокаСебестоимости.РучноеИзменение = Ложь;
			КонецЕсли; 
		КонецЦикла; 
		ТаблицаСебестоимости.Свернуть("НоменклатураИзделие, ХарактеристикаИзделие, СпецификацияИзделие, КлючСвязи, Источник, РучноеИзменение", "Себестоимость");
		
		СтруктураОтбораЗапасы = Новый Структура;
		СтруктураОтбораЗапасы.Вставить("НомерВариантаКП", ТекущийВариантКП);
		СтруктураОтбораЗапасы.Вставить("ЭтоРазделитель", Ложь);
		
		ТаблицаЗапасов = ДанныеЗаказа.Запасы.Выгрузить(СтруктураОтбораЗапасы);
		ТаблицаЗапасов.Колонки.Добавить("Источник", Новый ОписаниеТипов("ПеречислениеСсылка.ИсточникиСтрокКалькуляции"));
		ТаблицаЗапасов.ЗаполнитьЗначения(Перечисления.ИсточникиСтрокКалькуляции.Запас, "Источник");
		// Доставка
		Если ЗначениеЗаполнено(ДанныеЗаказа.СлужбаДоставки) И ЗначениеЗаполнено(ДанныеЗаказа.СтоимостьДоставки) Тогда
			СтрокаТабличнойЧасти = ТаблицаЗапасов.Добавить();
			СтрокаТабличнойЧасти.Номенклатура = ДанныеЗаказа.НоменклатураДоставки;
			СтрокаТабличнойЧасти.ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаказа.НоменклатураДоставки, "ЕдиницаИзмерения");
			СтрокаТабличнойЧасти.Количество = 1;
			СтрокаТабличнойЧасти.Источник = Перечисления.ИсточникиСтрокКалькуляции.Доставка;
			СтрокаТабличнойЧасти.СтавкаНДС = ДанныеЗаказа.СтавкаНДСДоставки;
			СтрокаТабличнойЧасти.Цена = ДанныеЗаказа.СтоимостьДоставки;
			СтрокаТабличнойЧасти.Всего = ДанныеЗаказа.СтоимостьДоставки;
		КонецЕсли; 
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТаблицаСебестоимости", ТаблицаСебестоимости);
		Запрос.УстановитьПараметр("ТаблицаЗапасов", ТаблицаЗапасов);
		Запрос.УстановитьПараметр("ФактическиеДанные", ФактическиеДанные.Выгрузить());
		Запрос.УстановитьПараметр("ЦенаБезНДС", ДанныеЗаказа.НалогообложениеНДС=Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС И НЕ ДанныеЗаказа.СуммаВключаетНДС);
		НоменклатураКалькуляции = Новый Массив;
		Для каждого Стр Из ДанныеЗаказа.Калькуляция Цикл
			Если Стр.НомерВариантаКП<>ТекущийВариантКП Тогда
				Продолжить;
			КонецЕсли; 
			Если Стр.Источник=Перечисления.ИсточникиСтрокКалькуляции.Запас Тогда
				Продолжить;
			КонецЕсли; 
			НоменклатураКалькуляции.Добавить(Стр.Номенклатура);
		КонецЦикла; 
		Запрос.УстановитьПараметр("НоменклатураКалькуляции", НоменклатураКалькуляции);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаСебестоимости.КлючСвязи КАК КлючСвязи,
		|	ТаблицаСебестоимости.РучноеИзменение КАК РучноеИзменение,
		|	ТаблицаСебестоимости.Источник КАК Источник,
		|	ТаблицаСебестоимости.Себестоимость КАК Себестоимость
		|ПОМЕСТИТЬ ТаблицаСебестоимости
		|ИЗ
		|	&ТаблицаСебестоимости КАК ТаблицаСебестоимости
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТаблицаЗапасов.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
		|	ВЫРАЗИТЬ(ТаблицаЗапасов.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
		|	ВЫРАЗИТЬ(ТаблицаЗапасов.Партия КАК Справочник.ПартииНоменклатуры) КАК Партия,
		|	ВЫРАЗИТЬ(ТаблицаЗапасов.Спецификация КАК Справочник.Спецификации) КАК Спецификация,
		|	ТаблицаЗапасов.Источник КАК Источник,
		|	ТаблицаЗапасов.КлючСвязи КАК КлючСвязи,
		|	ТаблицаЗапасов.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаЗапасов.Количество КАК Количество,
		|	ТаблицаЗапасов.Цена * ВЫБОР
		|		КОГДА &ЦенаБезНДС
		|			ТОГДА (100 + ВЫРАЗИТЬ(ТаблицаЗапасов.СтавкаНДС КАК Справочник.СтавкиНДС).Ставка) / 100
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Цена,
		|	ТаблицаЗапасов.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
		|	ТаблицаЗапасов.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
		|	ТаблицаЗапасов.Всего КАК Сумма
		|ПОМЕСТИТЬ ТаблицаЗапасов
		|ИЗ
		|	&ТаблицаЗапасов КАК ТаблицаЗапасов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФактическиеДанные.Номенклатура КАК Номенклатура,
		|	ФактическиеДанные.СебестоимостьФакт КАК СебестоимостьФакт,
		|	ФактическиеДанные.СтоимостьФакт КАК СтоимостьФакт,
		|	ФактическиеДанные.ПрибыльФакт КАК ПрибыльФакт
		|ПОМЕСТИТЬ ФактическиеДанные
		|ИЗ
		|	&ФактическиеДанные КАК ФактическиеДанные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЗапасов.Номенклатура КАК Номенклатура,
		|	СУММА(ТаблицаЗапасов.Количество) КАК Количество
		|ПОМЕСТИТЬ БазаРаспределения
		|ИЗ
		|	ТаблицаЗапасов КАК ТаблицаЗапасов
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЗапасов.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЗапасов.Номенклатура КАК Номенклатура,
		|	ТаблицаЗапасов.Характеристика КАК Характеристика,
		|	ТаблицаЗапасов.Партия КАК Партия,
		|	ТаблицаЗапасов.Спецификация КАК Спецификация,
		|	ТаблицаЗапасов.Источник КАК Источник,
		|	ТаблицаЗапасов.КлючСвязи КАК КлючСвязи,
		|	ТаблицаЗапасов.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаЗапасов.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияБазовая,
		|	ТаблицаЗапасов.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ТаблицаЗапасов.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
		|			ТОГДА ТаблицаЗапасов.Количество * ВЫРАЗИТЬ(ТаблицаЗапасов.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
		|		ИНАЧЕ ТаблицаЗапасов.Количество
		|	КОНЕЦ КАК КоличествоБазовое,
		|	ВЫБОР
		|		КОГДА ТаблицаЗапасов.ПроцентСкидкиНаценки = 100
		|			ТОГДА ТаблицаЗапасов.Цена * ТаблицаЗапасов.Количество * (100 - ТаблицаЗапасов.ПроцентАвтоматическойСкидки) / 100
		|		ИНАЧЕ ТаблицаЗапасов.Сумма / (1 - ТаблицаЗапасов.ПроцентСкидкиНаценки / 100)
		|	КОНЕЦ КАК СуммаБезСкидки,
		|	ТаблицаЗапасов.Сумма КАК Сумма,
		|	ТаблицаЗапасов.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
		|	ТаблицаЗапасов.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
		|	ВЫБОР
		|		КОГДА ТаблицаЗапасов.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
		|		КОГДА ТаблицаЗапасов.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
		|		ИНАЧЕ ТаблицаЗапасов.Номенклатура.СпособПополнения
		|	КОНЕЦ КАК СпособПополнения,
		|	ТаблицаЗапасов.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ТаблицаЗапасов.Номенклатура.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики,
		|	ТаблицаЗапасов.Номенклатура.ИспользоватьПартии КАК ИспользоватьПартии
		|ПОМЕСТИТЬ ЗапасыДляРасчета
		|ИЗ
		|	ТаблицаЗапасов КАК ТаблицаЗапасов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗапасыДляРасчета.Номенклатура КАК Номенклатура,
		|	ЗапасыДляРасчета.Характеристика КАК Характеристика,
		|	ЗапасыДляРасчета.Партия КАК Партия,
		|	ЗапасыДляРасчета.Спецификация КАК Спецификация,
		|	ЗапасыДляРасчета.КлючСвязи КАК КлючСвязи,
		|	ЗапасыДляРасчета.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗапасыДляРасчета.Количество КАК Количество,
		|	ЗапасыДляРасчета.Сумма КАК Сумма,
		|	ЗапасыДляРасчета.СуммаБезСкидки КАК СуммаБезСкидки,
		|	ЗапасыДляРасчета.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
		|	ЗапасыДляРасчета.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
		|	ЗапасыДляРасчета.СпособПополнения КАК СпособПополнения,
		|	ЗапасыДляРасчета.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ЗапасыДляРасчета.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики,
		|	ЗапасыДляРасчета.ИспользоватьПартии КАК ИспользоватьПартии,
		|	ЕСТЬNULL(ТаблицаСебестоимости.Себестоимость, 0) КАК Себестоимость,
		|	ЕСТЬNULL(ТаблицаСебестоимости.РучноеИзменение, ЛОЖЬ) КАК РучноеИзменение,
		|	ВЫБОР
		|		КОГДА ЗапасыДляРасчета.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ТаблицаСебестоимости.Себестоимость, 0) / ЗапасыДляРасчета.Количество
		|	КОНЕЦ КАК СебестоимостьЕдиницы,
		|	ЗапасыДляРасчета.Сумма - ЕСТЬNULL(ТаблицаСебестоимости.Себестоимость, 0) КАК Прибыль,
		|	ЗапасыДляРасчета.Источник КАК Источник,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА БазаРаспределения.Количество = 0
		|				ТОГДА 0
		|			ИНАЧЕ ФактическиеДанные.СебестоимостьФакт * ЗапасыДляРасчета.Количество / БазаРаспределения.Количество
		|		КОНЕЦ, 0) КАК СебестоимостьФакт,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА БазаРаспределения.Количество = 0
		|				ТОГДА 0
		|			ИНАЧЕ ФактическиеДанные.СтоимостьФакт * ЗапасыДляРасчета.Количество / БазаРаспределения.Количество
		|		КОНЕЦ, 0) КАК СтоимостьФакт,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА БазаРаспределения.Количество = 0
		|				ТОГДА 0
		|			ИНАЧЕ ФактическиеДанные.ПрибыльФакт * ЗапасыДляРасчета.Количество / БазаРаспределения.Количество
		|		КОНЕЦ, 0) КАК ПрибыльФакт,
		|	ЛОЖЬ КАК НеСохранять
		|ИЗ
		|	ЗапасыДляРасчета КАК ЗапасыДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСебестоимости КАК ТаблицаСебестоимости
		|		ПО ЗапасыДляРасчета.КлючСвязи = ТаблицаСебестоимости.КлючСвязи
		|			И (ТаблицаСебестоимости.Источник = ЗНАЧЕНИЕ(Перечисление.ИсточникиСтрокКалькуляции.Запас))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ФактическиеДанные КАК ФактическиеДанные
		|		ПО ЗапасыДляРасчета.Номенклатура = ФактическиеДанные.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения КАК БазаРаспределения
		|		ПО ЗапасыДляРасчета.Номенклатура = БазаРаспределения.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ФактическиеДанные.Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.Номенклатура ССЫЛКА Справочник.Номенклатура
		|			ТОГДА ВЫРАЗИТЬ(ФактическиеДанные.Номенклатура КАК Справочник.Номенклатура).СпособПополнения
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.Номенклатура ССЫЛКА Справочник.Номенклатура
		|			ТОГДА ВЫРАЗИТЬ(ФактическиеДанные.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.Номенклатура ССЫЛКА Справочник.Номенклатура
		|			ТОГДА ВЫРАЗИТЬ(ФактическиеДанные.Номенклатура КАК Справочник.Номенклатура).ИспользоватьХарактеристики
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.Номенклатура ССЫЛКА Справочник.Номенклатура
		|			ТОГДА ВЫРАЗИТЬ(ФактическиеДанные.Номенклатура КАК Справочник.Номенклатура).ИспользоватьПартии
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	0,
		|	ЛОЖЬ,
		|	0,
		|	0,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиСтрокКалькуляции.Прочее),
		|	ФактическиеДанные.СебестоимостьФакт,
		|	ФактическиеДанные.СтоимостьФакт,
		|	ФактическиеДанные.ПрибыльФакт,
		|	ИСТИНА
		|ИЗ
		|	ФактическиеДанные КАК ФактическиеДанные
		|ГДЕ
		|	НЕ ФактическиеДанные.Номенклатура В
		|				(ВЫБРАТЬ
		|					БазаРаспределения.Номенклатура
		|				ИЗ
		|					БазаРаспределения КАК БазаРаспределения)
		|	И НЕ ФактическиеДанные.Номенклатура В (&НоменклатураКалькуляции)";
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.НеСохранять И Выборка.СтоимостьФакт=0 И НЕ ОтображатьСебестоимость Тогда
				Продолжить;
			КонецЕсли; 
			НоваяСтрока = КалькуляцияНаФорме.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.СебестоимостьЕдиницыФакт = ?(НоваяСтрока.Количество<>0, НоваяСтрока.СебестоимостьФакт / НоваяСтрока.Количество, 0);
			НоваяСтрока.ВложенныйСостав = Выборка.Источник=Перечисления.ИсточникиСтрокКалькуляции.Запас 
			И (Выборка.СпособПополнения=Перечисления.СпособыПополненияЗапасов.Производство ИЛИ ЗначениеЗаполнено(Выборка.Спецификация));
			ВыведенныеФактДанные.Добавить(Выборка.Номенклатура);
		КонецЦикла; 
		Строки = Новый Массив;
		Для каждого Стр Из ДанныеЗаказа.Калькуляция Цикл
			Если Стр.НомерВариантаКП<>ТекущийВариантКП Тогда
				Продолжить;
			КонецЕсли; 
			Если Стр.Источник=Перечисления.ИсточникиСтрокКалькуляции.Запас Тогда
				Продолжить;
			КонецЕсли;
			Если Стр.Источник=Перечисления.ИсточникиСтрокКалькуляции.Доставка Тогда
				// Себестоимость доставки следует объединить со строкой дохода
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("Источник", Стр.Источник);
				СтрокиНаФорме = КалькуляцияНаФорме.НайтиСтроки(СтруктураОтбора);
				Если СтрокиНаФорме.Количество()>0 Тогда
					СтрокаНаФорме = СтрокиНаФорме[0];
					СтрокаНаФорме.Себестоимость = Стр.Себестоимость;
					СтрокаНаФорме.Прибыль = СтрокаНаФорме.Сумма-СтрокаНаФорме.Себестоимость;
					Продолжить;
				КонецЕсли; 
			КонецЕсли; 
			Строки.Добавить(Стр);
		КонецЦикла; 
	Иначе
		СтрЗапасы = ДанныеЗаказа.Запасы.НайтиПоИдентификатору(Идентификатор);
		Если СтрЗапасы=Неопределено Тогда
			Возврат;
		КонецЕсли; 
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязи", СтрЗапасы.КлючСвязи);
		СтруктураОтбора.Вставить("Источник", Перечисления.ИсточникиСтрокКалькуляции.Запас);
		Строки = ДанныеЗаказа.Калькуляция.НайтиСтроки(СтруктураОтбора);
	КонецЕсли; 
	Для каждого Стр Из Строки Цикл
		Если Стр.Номенклатура=Стр.НоменклатураИзделие И Стр.Характеристика=Стр.ХарактеристикаИзделие Тогда
			Продолжить;
		КонецЕсли; 
		НоваяСтрока = КалькуляцияНаФорме.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "Номенклатура, Характеристика, Спецификация, Количество, ЕдиницаИзмерения, СебестоимостьЕдиницы, Себестоимость, Источник, КлючСвязи, РучноеИзменение");
		Если Идентификатор<>Неопределено Тогда
			НоваяСтрока.Источник = Перечисления.ИсточникиСтрокКалькуляции.Прочее;
		КонецЕсли; 
		НоваяСтрока.Прибыль = -Стр.Себестоимость;
		Если Идентификатор=Неопределено И ВыведенныеФактДанные.Найти(Стр.Номенклатура)=Неопределено Тогда
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Номенклатура", Стр.Номенклатура);
			СтрокиФакт = ФактическиеДанные.НайтиСтроки(СтруктураОтбора);
			СтруктураОтбора.Вставить("НомерВариантаКП", ТекущийВариантКП);
			СтрокиЗапасы = ДанныеЗаказа.Запасы.НайтиСтроки(СтруктураОтбора);
			Если СтрокиФакт.Количество()>0 И СтрокиЗапасы.Количество()=0 Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокиФакт[0], "СебестоимостьФакт, СтоимостьФакт, ПрибыльФакт");
				НоваяСтрока.СебестоимостьЕдиницыФакт = ?(НоваяСтрока.Количество<>0, НоваяСтрока.СебестоимостьФакт / НоваяСтрока.Количество, 0);
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
	ОбновитьНомераСтрок(КалькуляцияНаФорме);
	
	ОбновитьДополнительныеРеквизиты();
	
	ОбновитьИтогиСервер();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьНомераСтрок(Таблица)
	
	НомерСтроки = 0;
	Для каждого Стр Из Таблица Цикл
		НомерСтроки = НомерСтроки+1;
		Стр.НомерСтроки = НомерСтроки;
	КонецЦикла; 	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДополнительныеРеквизиты()
	
	ТаблицаНоменклатуры = Новый ТаблицаЗначений;
	ТаблицаНоменклатуры.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	ТаблицаНоменклатуры.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Для каждого СтрокаТабличнойЧасти Из КалькуляцияНаФорме Цикл
		Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура)=Тип("СправочникСсылка.Номенклатура") 
			И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ТипНоменклатуры) Тогда
			НоваяСтрока = ТаблицаНоменклатуры.Добавить();
			НоваяСтрока.Номенклатура = СтрокаТабличнойЧасти.Номенклатура;
			НоваяСтрока.Идентификатор = СтрокаТабличнойЧасти.ПолучитьИдентификатор();
		КонецЕсли; 
	КонецЦикла;
	
	Если ТаблицаНоменклатуры.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Идентификатор КАК Идентификатор,
	|	ВЫРАЗИТЬ(ТаблицаНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Идентификатор КАК Идентификатор,
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТаблицаНоменклатуры.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
	|		ИНАЧЕ ТаблицаНоменклатуры.Номенклатура.СпособПополнения
	|	КОНЕЦ КАК СпособПополнения,
	|	ТаблицаНоменклатуры.Номенклатура.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики,
	|	ТаблицаНоменклатуры.Номенклатура.ИспользоватьПартии КАК ИспользоватьПартии
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТабличнойЧасти = КалькуляцияНаФорме.НайтиПоИдентификатору(Выборка.Идентификатор);
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка, "ТипНоменклатуры, СпособПополнения, ИспользоватьХарактеристики, ИспользоватьПартии");
	КонецЦикла;
	
	Для каждого СтрокаТабличнойЧасти Из КалькуляцияНаФорме Цикл
		Если СтрокаТабличнойЧасти.Источник<>Перечисления.ИсточникиСтрокКалькуляции.Шаблон Тогда
			Продолжить;
		КонецЕсли;
		СтрокаШаблона = СтрокаПоКлючу(СоставШаблона, СтрокаТабличнойЧасти.КлючСвязи);
		Если СтрокаШаблона=Неопределено ИЛИ СтрокаШаблона.ТипСтроки<>"Расход" Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаТабличнойЧасти.СпособРасчета = СтрокаШаблона.СпособРасчета;
	КонецЦикла; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПустаяТаблицаМатериалов()
	
	ТаблицаМатериалов = Новый ТаблицаЗначений;
	ТаблицаМатериалов.Колонки.Добавить("НомерВариантаКП", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2, 0)));
	ТаблицаМатериалов.Колонки.Добавить("КлючСвязи", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0)));
	ТаблицаМатериалов.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	ТаблицаМатериалов.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаМатериалов.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаМатериалов.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.Спецификации"));
	ТаблицаМатериалов.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения,СправочникСсылка.КлассификаторЕдиницИзмерения"));
	ТаблицаМатериалов.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	ТаблицаМатериалов.Колонки.Добавить("НоменклатураСебестоимость", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаМатериалов.Колонки.Добавить("ХарактеристикаСебестоимость", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаМатериалов.Колонки.Добавить("СпецификацияСебестоимость", Новый ОписаниеТипов("СправочникСсылка.Спецификации"));
	ТаблицаМатериалов.Колонки.Добавить("ЕдиницаИзмеренияСебестоимость", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения,СправочникСсылка.КлассификаторЕдиницИзмерения"));
	ТаблицаМатериалов.Колонки.Добавить("КоличествоСебестоимость", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	ТаблицаМатериалов.Колонки.Добавить("ПроверкаЗацикливания", Новый ОписаниеТипов("Массив"));
	Возврат ТаблицаМатериалов;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПустаяТаблицаКэшСебестоимости()
	
	ТаблицаМатериалов = Новый ТаблицаЗначений;
	ТаблицаМатериалов.Колонки.Добавить("НомерВариантаКП", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2, 0)));
	ТаблицаМатериалов.Колонки.Добавить("КлючСвязи", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0)));
	ТаблицаМатериалов.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаМатериалов.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаМатериалов.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.Спецификации"));
	ТаблицаМатериалов.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения,СправочникСсылка.КлассификаторЕдиницИзмерения"));
	ТаблицаМатериалов.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	ТаблицаМатериалов.Колонки.Добавить("СебестоимостьЕдиницы", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	ТаблицаМатериалов.Колонки.Добавить("Себестоимость", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Возврат ТаблицаМатериалов;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуМатериалов(ТаблицаМатериалов, СтрокаКалькуляции = Неопределено)
	
	ЗаменаСпецификации = (СтрокаКалькуляции<>Неопределено);
	
	Если НЕ ЗаменаСпецификации Тогда
		Строки = ДанныеЗаказа.Запасы;
	Иначе
		Строки = Новый Массив;
		Строки.Добавить(СтрокаКалькуляции);
	КонецЕсли;
	
	Для каждого Стр Из Строки Цикл
		Если НЕ ЗаменаСпецификации И Стр.ЭтоРазделитель Тогда
			Продолжить;
		КонецЕсли;
		СтрокаЗапасов = СтрокаПоКлючу(ДанныеЗаказа.Запасы, Стр.КлючСвязи);
		НоваяСтрока = ТаблицаМатериалов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "КлючСвязи, Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения");
		НоваяСтрока.Количество = Стр.Количество;
		Если СтрокаЗапасов<>Неопределено Тогда
			НоваяСтрока.Идентификатор = СтрокаЗапасов.ПолучитьИдентификатор();
		КонецЕсли;
		Если ЗаменаСпецификации Тогда
			НоваяСтрока.НомерВариантаКП = ТекущийВариантКП;
		Иначе
			НоваяСтрока.НомерВариантаКП = НоваяСтрока.НомерВариантаКП;
		КонецЕсли; 
		Если ЗначениеЗаполнено(Стр.Спецификация) Тогда
			НоваяСтрока.ПроверкаЗацикливания.Добавить(Стр.Спецификация);
		КонецЕсли; 
		НоваяСтрока.НоменклатураСебестоимость = НоваяСтрока.Номенклатура;
		НоваяСтрока.ХарактеристикаСебестоимость = НоваяСтрока.Характеристика;
		НоваяСтрока.СпецификацияСебестоимость = НоваяСтрока.Спецификация;
		НоваяСтрока.ЕдиницаИзмеренияСебестоимость = НоваяСтрока.ЕдиницаИзмерения;
		НоваяСтрока.КоличествоСебестоимость = Стр.Количество;
	КонецЦикла; 
	
	Уровень = ?(ТекущаяСтрокаСостава<0, 0, 1);
	Пока ЕстьВложенныеСпецификации(ТаблицаМатериалов) Цикл
		Уровень = Уровень+1;
		Результат = РазузловатьНоменклатуру(ТаблицаМатериалов, СоставыСпецификаций, Уровень);
		Если НЕ Результат Тогда
			// Ошибка разузлования
			ТаблицаМатериалов.Очистить();
			Возврат;
		КонецЕсли; 
	КонецЦикла; 
	
	ТаблицаМатериалов.Свернуть("Идентификатор, Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения, НоменклатураСебестоимость, ХарактеристикаСебестоимость, ЕдиницаИзмеренияСебестоимость, Количество", "КоличествоСебестоимость");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазузловатьНоменклатуру(ТаблицаМатериалов, СоставыСпецификаций = Неопределено, Уровень = 0)
	
	СтрокиСпецификаций = Новый Массив;
	Для каждого Стр Из ТаблицаМатериалов Цикл
		Если ЗначениеЗаполнено(Стр.СпецификацияСебестоимость) Тогда
			СтрокиСпецификаций.Добавить(Стр);
		КонецЕсли; 
	КонецЦикла;
	ТаблицаСостава = ТаблицаСоставаСпецификаций(СтрокиСпецификаций, СоставыСпецификаций, Уровень);
	
	Для каждого СтрМатериала Из СтрокиСпецификаций Цикл
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("СпецификацияУзел", СтрМатериала.СпецификацияСебестоимость);
		СтрокиСостава = ТаблицаСостава.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрСостава Из СтрокиСостава Цикл
			Если ЗначениеЗаполнено(СтрСостава.Спецификация) И СтрМатериала.ПроверкаЗацикливания.Найти(СтрСостава.Спецификация)<>Неопределено Тогда
				Сообщить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Зацикливание сецификации %1 при изготовлении изделия %2'"),
				СтрСостава.Спецификация,
				СтрМатериала.Номенклатура));
				Возврат Ложь;
			КонецЕсли; 
			НоваяСтрока = ТаблицаМатериалов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрМатериала, "Идентификатор, ПроверкаЗацикливания");
			Если Уровень=1 Тогда
				// Храним первый уровень вложенности спецификаций 
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрСостава, "Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения");
				НоваяСтрока.Количество = СтрСостава.Количество*СтрМатериала.КоличествоСебестоимость
				*?(ТипЗнч(СтрМатериала.ЕдиницаИзмерения)=Тип("СправочникСсылка.ЕдиницыИзмерения"), 
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрМатериала.ЕдиницаИзмерения, "Коэффициент"), 
				1);
			Иначе
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрМатериала, "Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения, Количество");
			КонецЕсли; 
			НоваяСтрока.ПроверкаЗацикливания.Добавить(СтрМатериала.СпецификацияСебестоимость);
			НоваяСтрока.НоменклатураСебестоимость = СтрСостава.Номенклатура;
			НоваяСтрока.ХарактеристикаСебестоимость = СтрСостава.Характеристика;
			НоваяСтрока.СпецификацияСебестоимость = СтрСостава.Спецификация;
			НоваяСтрока.ЕдиницаИзмеренияСебестоимость = СтрСостава.ЕдиницаИзмерения;
			НоваяСтрока.КоличествоСебестоимость = СтрСостава.Количество*СтрМатериала.КоличествоСебестоимость
			*?(ТипЗнч(СтрМатериала.ЕдиницаИзмерения)=Тип("СправочникСсылка.ЕдиницыИзмерения"), 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрМатериала.ЕдиницаИзмерения, "Коэффициент"), 
			1);
		КонецЦикла;
		ТаблицаМатериалов.Удалить(СтрМатериала);
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТаблицаСоставаСпецификаций(СтрокиСпецификаций, СоставыСпецификаций, Уровень)
	
	ИзмененныеСтроки = Новый Массив;
	Спецификации = Новый Массив;
	Для каждого Стр Из СтрокиСпецификаций Цикл
		Если Уровень=1 И СоставыСпецификаций<>Неопределено Тогда
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("КлючСвязи", Стр.КлючСвязи);
			Если СоставыСпецификаций.НайтиСтроки(СтруктураОтбора).Количество()>0 Тогда
				ИзмененныеСтроки.Добавить(Стр);
				Продолжить;
			КонецЕсли; 
		КонецЕсли; 
		Спецификации.Добавить(Стр.СпецификацияСебестоимость);
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Спецификации", Спецификации);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпецификацииСостав.Ссылка КАК СпецификацияУзел,
	|	СпецификацииСостав.ТипСтрокиСостава,
	|	СпецификацииСостав.Номенклатура,
	|	СпецификацииСостав.Характеристика,
	|	СпецификацииСостав.ЕдиницаИзмерения,
	|	СпецификацииСостав.Спецификация,
	|	СпецификацииСостав.Количество / СпецификацииСостав.КоличествоПродукции КАК Количество
	|ИЗ
	|	Справочник.Спецификации.Состав КАК СпецификацииСостав
	|ГДЕ
	|	СпецификацииСостав.Ссылка В(&Спецификации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпецификацииОперации.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.ПустаяСсылка),
	|	СпецификацииОперации.Операция,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ч),
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
	|	СпецификацииОперации.НормаВремени / СпецификацииОперации.КоличествоПродукции
	|ИЗ
	|	Справочник.Спецификации.Операции КАК СпецификацииОперации
	|ГДЕ
	|	СпецификацииОперации.Ссылка В(&Спецификации)";
	ТаблицаСостава = Запрос.Выполнить().Выгрузить();
	ТаблицаСостава.Индексы.Добавить("СпецификацияУзел");
	
	Если Уровень=1 И СоставыСпецификаций<>Неопределено Тогда
		Для каждого Стр Из ИзмененныеСтроки Цикл
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("КлючСвязи", Стр.КлючСвязи);
			СтрокиСостава = СоставыСпецификаций.НайтиСтроки(СтруктураОтбора);
			Для каждого СтрСостава Из СтрокиСостава Цикл
				НоваяСтрока = ТаблицаСостава.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрСостава);
				НоваяСтрока.СпецификацияУзел = Стр.Спецификация;
				НоваяСтрока.Количество = СтрСостава.Количество/?(СтрСостава.КоличествоПродукции=0, 1, СтрСостава.КоличествоПродукции);
			КонецЦикла; 
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат ТаблицаСостава;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьВложенныеСпецификации(ТаблицаМатериалов)
	
	Для каждого Стр Из ТаблицаМатериалов Цикл
		Если ЗначениеЗаполнено(Стр.СпецификацияСебестоимость) Тогда
			Возврат Истина;
		КонецЕсли; 
	КонецЦикла; 
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Функция СтруктураДанныхШапки()
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Заказ", ЗаказСсылка);
	СтруктураДанные.Вставить("Дата", ДанныеЗаказа.Дата);
	СтруктураДанные.Вставить("НалогообложениеНДС", ДанныеЗаказа.НалогообложениеНДС);
	СтруктураДанные.Вставить("Курс", ДанныеЗаказа.Курс);
	СтруктураДанные.Вставить("Кратность", ДанныеЗаказа.Кратность);
	Если ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=ПредопределенноеЗначение("Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоПоследнейЦенеЗакупки") Тогда
		ОтборВидЦен = Неопределено;
	ИначеЕсли ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=ПредопределенноеЗначение("Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры")
		ИЛИ ТипЗнч(ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции)=Тип("СправочникСсылка.ВидыЦен") Тогда
		ОтборВидЦен = ВидЦен;
	ИначеЕсли ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=ПредопределенноеЗначение("Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов") Тогда
		ОтборВидЦен = ВидыЦенКонтрагентов;
	Иначе
		ОтборВидЦен = Неопределено;
	КонецЕсли;
	СтруктураДанные.Вставить("ВидыЦен", ОтборВидЦен);
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные)
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктураДанные.Номенклатура, "ЕдиницаИзмерения, СпособПополнения, ТипНоменклатуры, ИспользоватьХарактеристики, ИспользоватьПартии");
	
	СтруктураДанные.Вставить("СпособПополнения", ?(ЗначенияРеквизитов.ТипНоменклатуры=Перечисления.ТипыНоменклатуры.Работа, Перечисления.СпособыПополненияЗапасов.Производство, ЗначенияРеквизитов.СпособПополнения));
	СтруктураДанные.Вставить("ТипНоменклатуры", ЗначенияРеквизитов.ТипНоменклатуры);
	СтруктураДанные.Вставить("ИспользоватьХарактеристики", ЗначенияРеквизитов.ИспользоватьХарактеристики);
	СтруктураДанные.Вставить("ИспользоватьПартии", ЗначенияРеквизитов.ИспользоватьПартии);
	Если НЕ СтруктураДанные.Свойство("ЕдиницаИзмерения") ИЛИ НЕ ЗначениеЗаполнено(СтруктураДанные.ЕдиницаИзмерения) Тогда
		СтруктураДанные.Вставить("ЕдиницаИзмерения", ЗначенияРеквизитов.ЕдиницаИзмерения);
	КонецЕсли;
	Если НЕ СтруктураДанные.Свойство("Спецификация") Тогда
		Если СтруктураДанные.Свойство("Характеристика") Тогда
			СтруктураДанные.Вставить("Спецификация", УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
		Иначе
			СтруктураДанные.Вставить("Спецификация", УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтруктураДанные.Номенклатура));
		КонецЕсли;
	КонецЕсли; 
	
	ТаблицаМатериалов = ПустаяТаблицаМатериалов();
	СтрМатериал = ТаблицаМатериалов.Добавить();
	СтрМатериал.Номенклатура = СтруктураДанные.Номенклатура;
	СтрМатериал.Характеристика = СтруктураДанные.Характеристика;
	СтрМатериал.Спецификация = СтруктураДанные.Спецификация;
	СтрМатериал.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	СтрМатериал.Количество = 1;
	СтрМатериал.НоменклатураСебестоимость = СтруктураДанные.Номенклатура;
	СтрМатериал.ХарактеристикаСебестоимость = СтруктураДанные.Характеристика;
	СтрМатериал.СпецификацияСебестоимость = СтруктураДанные.Спецификация;
	СтрМатериал.ЕдиницаИзмеренияСебестоимость = СтруктураДанные.ЕдиницаИзмерения;
	СтрМатериал.КоличествоСебестоимость = 1;
	Пока ЕстьВложенныеСпецификации(ТаблицаМатериалов) Цикл
		Результат = РазузловатьНоменклатуру(ТаблицаМатериалов);
		Если НЕ Результат Тогда
			// Ошибка разузлования
			СтруктураДанные.Вставить("СебестоимостьЕдиницы", 0);
			Возврат СтруктураДанные;
		КонецЕсли; 
	КонецЦикла;
	
	РассчитатьСебестоимость(ТаблицаМатериалов, СтруктураДанные.ВидыЦен, СтруктураДанные.Курс, СтруктураДанные.Кратность, СтруктураДанные.НалогообложениеНДС, СтруктураДанные.Дата, СтруктураДанные.Заказ);
	
	СтруктураДанные.Вставить("СебестоимостьЕдиницы", ТаблицаМатериалов.Итог("Себестоимость"));
 
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервере
Функция ОтборПоВидуЦен()
	
	Если ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=ПредопределенноеЗначение("Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоПоследнейЦенеЗакупки") Тогда
		Возврат Неопределено;
	ИначеЕсли ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=ПредопределенноеЗначение("Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры")
		ИЛИ ТипЗнч(ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции)=Тип("СправочникСсылка.ВидыЦен") Тогда
		Возврат ВидЦен;
	ИначеЕсли ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=ПредопределенноеЗначение("Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов") Тогда
		Возврат ВидыЦенКонтрагентов;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПересчитатьСебестоимость(СтрокаТабличнойЧасти)
	
	СтрокаТабличнойЧасти.Себестоимость = СтрокаТабличнойЧасти.СебестоимостьЕдиницы*СтрокаТабличнойЧасти.Количество;
	СтрокаТабличнойЧасти.Прибыль = СтрокаТабличнойЧасти.Сумма-СтрокаТабличнойЧасти.Себестоимость;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСебестоимостьСервер()
	
	УстановитьВидимостьИДоступность();
	
	ХранилищеСистемныхНастроек.Сохранить("ЗаказПокупателя", "ОтображатьСебестоимость", ОтображатьСебестоимость);
	
КонецПроцедуры

&НаСервере
Процедура ВнестиИзмененияВКалькуляцию()
	
	Если ТекущаяСтрокаСостава>=0 Тогда
		// Режим: изменение спецификации изделия
		
		СтрЗапасов = ДанныеЗаказа.Запасы.НайтиПоИдентификатору(ТекущаяСтрокаСостава);
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязи", СтрЗапасов.КлючСвязи);
		Строки = СоставыСпецификаций.НайтиСтроки(СтруктураОтбора);
		Для каждого Стр Из Строки Цикл
			СоставыСпецификаций.Удалить(Стр);
		КонецЦикла;
		
		Для каждого Стр Из КалькуляцияНаФорме Цикл
			НоваяСтрока = СоставыСпецификаций.Добавить();
			НоваяСтрока.КлючСвязи = СтрЗапасов.КлючСвязи;
			НоваяСтрока.НоменклатураИзделие = СтрЗапасов.Номенклатура;
			НоваяСтрока.ХарактеристикаИзделие = СтрЗапасов.Характеристика;
			НоваяСтрока.СпецификацияИзделие = СтрЗапасов.Спецификация;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения, Количество");
			НоваяСтрока.КоличествоПродукции = ?(СтрЗапасов.Количество=0, 1, СтрЗапасов.Количество);
			Если Стр.ТипНоменклатуры=ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Запас") 
				И Стр.СпособПополнения=ПредопределенноеЗначение("Перечисление.СпособыПополненияЗапасов.Закупка") Тогда
				НоваяСтрока.ТипСтрокиСостава = ПредопределенноеЗначение("Перечисление.ТипыСтрокСоставаСпецификации.Материал");
			ИначеЕсли Стр.ТипНоменклатуры=ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Запас") Тогда
				НоваяСтрока.ТипСтрокиСостава = ПредопределенноеЗначение("Перечисление.ТипыСтрокСоставаСпецификации.Сборка");
			ИначеЕсли Стр.ТипНоменклатуры=ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Операция") Тогда
				НоваяСтрока.ТипСтрокиСостава = ПредопределенноеЗначение("Перечисление.ТипыСтрокСоставаСпецификации.ПустаяСсылка");
			Иначе
				НоваяСтрока.ТипСтрокиСостава = ПредопределенноеЗначение("Перечисление.ТипыСтрокСоставаСпецификации.Расход");
			КонецЕсли; 
		КонецЦикла;
		
		// Обновление калькуляции
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязи", СтрЗапасов.КлючСвязи);
		СтруктураОтбора.Вставить("Источник", Перечисления.ИсточникиСтрокКалькуляции.Запас);
		Строки = ДанныеЗаказа.Калькуляция.НайтиСтроки(СтруктураОтбора);
		Для каждого Стр Из Строки Цикл
			ДанныеЗаказа.Калькуляция.Удалить(Стр);
		КонецЦикла;
		
		Для каждого Стр Из КалькуляцияНаФорме Цикл
			НоваяСтрока = ДанныеЗаказа.Калькуляция.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения, Количество, Себестоимость, СебестоимостьЕдиницы, РучноеИзменение");
			НоваяСтрока.НомерВариантаКП = ТекущийВариантКП;
			НоваяСтрока.НоменклатураИзделие = СтрЗапасов.Номенклатура;
			НоваяСтрока.ХарактеристикаИзделие = СтрЗапасов.Характеристика;
			НоваяСтрока.СпецификацияИзделие = СтрЗапасов.Спецификация;
			НоваяСтрока.Источник = Перечисления.ИсточникиСтрокКалькуляции.Запас;
			НоваяСтрока.КлючСвязи = СтрЗапасов.КлючСвязи;
		КонецЦикла;
		
	Иначе
		
		СтрокиКУдалению = Новый Массив;
		Для каждого Стр Из ДанныеЗаказа.Калькуляция Цикл
			Если Стр.НомерВариантаКП<>ТекущийВариантКП Тогда
				Продолжить;
			КонецЕсли; 
			Если Стр.Номенклатура<>Стр.НоменклатураИзделие И ЗначениеЗаполнено(Стр.НоменклатураИзделие) Тогда
				Продолжить;
			КонецЕсли;
			СтрокиКУдалению.Добавить(Стр);
		КонецЦикла;
		Для каждого Стр Из СтрокиКУдалению Цикл
			ДанныеЗаказа.Калькуляция.Удалить(Стр);
		КонецЦикла; 
		
		Для каждого Стр Из КалькуляцияНаФорме Цикл
			Если Стр.ВложенныйСостав Тогда
				Продолжить;
			КонецЕсли;
			Если Стр.НеСохранять Тогда
				// Строка фактических данных
				Продолжить;
			КонецЕсли; 
			НоваяСтрока = ДанныеЗаказа.Калькуляция.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "Номенклатура, Характеристика, Спецификация, ЕдиницаИзмерения, Количество, Себестоимость, СебестоимостьЕдиницы, Источник, КлючСвязи, РучноеИзменение");
			НоваяСтрока.НомерВариантаКП = ТекущийВариантКП;
			Если Стр.Источник=Перечисления.ИсточникиСтрокКалькуляции.Запас Тогда
				СтрЗапасов = СтрокаПоКлючу(ДанныеЗаказа.Запасы, Стр.КлючСвязи);
				Если СтрЗапасов<>Неопределено Тогда
					НоваяСтрока.НоменклатураИзделие = СтрЗапасов.Номенклатура;
					НоваяСтрока.ХарактеристикаИзделие = СтрЗапасов.Характеристика;
					НоваяСтрока.СпецификацияИзделие = СтрЗапасов.Спецификация;
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла;
		
	КонецЕсли;
	
	ОбновитьИтогиСервер();	
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьДанныеКалькуляцииВХранилище()
	
	ОбновитьСпецификации();
	
	СтруктураДанных = Новый Структура;
	ТаблицаЗапасов = ДанныеЗаказа.Запасы.Выгрузить();
	СтруктураДанных.Вставить("Запасы", ТаблицаЗапасов);
	ТаблицаКалькуляции = ДанныеЗаказа.Калькуляция.Выгрузить();
	СтруктураДанных.Вставить("Калькуляция", ТаблицаКалькуляции);
	Если ТипЗнч(ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции)=Тип("СправочникСсылка.ВидыЦен") Тогда
		СтруктураДанных.Вставить("СпособРасчетаСебестоимостиКалькуляции", Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры);
	Иначе
		СтруктураДанных.Вставить("СпособРасчетаСебестоимостиКалькуляции", ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции);
	КонецЕсли; 
	Если ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамКонтрагентов Тогда
		СтруктураДанных.Вставить("ВидыЦен", ВидыЦенКонтрагентов.ВыгрузитьЗначения());
	ИначеЕсли ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=Перечисления.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры
		ИЛИ ТипЗнч(ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции)=Тип("СправочникСсылка.ВидыЦен") Тогда
		МассивЦен = Новый Массив;
		МассивЦен.Добавить(ВидЦен);
		СтруктураДанных.Вставить("ВидыЦен", МассивЦен);
	Иначе
		СтруктураДанных.Вставить("ВидыЦен", Новый Массив);
	КонецЕсли; 
	СтруктураДанных.Вставить("ШаблонКалькуляции", ДанныеЗаказа.ШаблонКалькуляции);
	СтруктураДанных.Вставить("КомментарийКалькуляции", ДанныеЗаказа.КомментарийКалькуляции);
	АдресДанных = ПоместитьВоВременноеХранилище(СтруктураДанных, АдресДанных);
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСпецификации()
	
	ТаблицаИдентификаторов = СоставыСпецификаций.Выгрузить(, "КлючСвязи, НоменклатураИзделие, ХарактеристикаИзделие, СпецификацияИзделие");
	ТаблицаИдентификаторов.Свернуть("КлючСвязи, НоменклатураИзделие, ХарактеристикаИзделие, СпецификацияИзделие");
	Для каждого СтрИдентификатор Из ТаблицаИдентификаторов Цикл
		СтрЗапас = СтрокаПоКлючу(ДанныеЗаказа.Запасы, СтрИдентификатор.КлючСвязи);
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязи", СтрИдентификатор.КлючСвязи);
		Строки = СоставыСпецификаций.НайтиСтроки(СтруктураОтбора);
		Если СтрЗапас=Неопределено
			ИЛИ НЕ ЗначениеЗаполнено(СтрЗапас.Номенклатура)
			ИЛИ СтрЗапас.Номенклатура<>СтрИдентификатор.НоменклатураИзделие
			ИЛИ СтрЗапас.Характеристика<>СтрИдентификатор.ХарактеристикаИзделие
			ИЛИ СтрЗапас.Спецификация<>СтрИдентификатор.СпецификацияИзделие Тогда
			// Устаревшая информация
			Для каждого Стр Из Строки Цикл
				СоставыСпецификаций.Удалить(Стр);
			КонецЦикла; 
			Продолжить;
		КонецЕсли; 
		ЗаказСпецификации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрЗапас.Спецификация, "ЗаказПокупателя");
		Если НЕ ЗначениеЗаполнено(СтрЗапас.Спецификация) 
			ИЛИ ЗаказСпецификации<>ЗаказСсылка Тогда
			Спецификация = Справочники.Спецификации.СоздатьЭлемент();
			Спецификация.Владелец = СтрЗапас.Номенклатура;
			Спецификация.ХарактеристикаПродукции = СтрЗапас.Характеристика;
			Спецификация.ЗаказПокупателя = ЗаказСсылка;
			Спецификация.Наименование = СокрЛП(СтрЗапас.Номенклатура)+
			" ("+ПредставлениеЗаказа(ДанныеЗаказа.Номер, ДанныеЗаказа.Дата)+")";
		Иначе
			Спецификация = СтрЗапас.Спецификация.ПолучитьОбъект();
			Спецификация.Состав.Очистить();
			Спецификация.Операции.Очистить();
		КонецЕсли; 
		Для каждого Стр Из Строки Цикл
			Если ЗначениеЗаполнено(Стр.ТипСтрокиСостава) Тогда
				НоваяСтрока = Спецификация.Состав.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
			Иначе
				НоваяСтрока = Спецификация.Операции.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "КоличествоПродукции");
				НоваяСтрока.Операция = Стр.Номенклатура;
				НоваяСтрока.НормаВремени = Стр.Количество;
			КонецЕсли; 
		КонецЦикла;
		Спецификация.Записать();
		СтрЗапас.Спецификация = Спецификация.Ссылка;
		Для каждого Стр Из Строки Цикл
			СоставыСпецификаций.Удалить(Стр);
		КонецЦикла; 
	КонецЦикла;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Изменен", Истина);
	СтрокиКомментариев = КомментарииСпецификаций.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрКомментарий Из СтрокиКомментариев Цикл
		СтрЗапас = СтрокаПоКлючу(ДанныеЗаказа.Запасы, СтрКомментарий.КлючСвязи);
		Если СтрЗапас=Неопределено 
			ИЛИ НЕ ЗначениеЗаполнено(СтрЗапас.Номенклатура) 
			ИЛИ НЕ ЗначениеЗаполнено(СтрЗапас.Спецификация) Тогда
			КомментарииСпецификаций.Удалить(СтрКомментарий);
			Продолжить;
		КонецЕсли;
		Спецификация = СтрЗапас.Спецификация.ПолучитьОбъект();
		Спецификация.Комментарий = СтрКомментарий.Комментарий;
		Спецификация.Записать();
		СтрКомментарий.Изменен = Ложь;
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеЗаказа(Номер, Дата)
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1 %2 от %3'"),
		НСтр("ru = 'Заказ покупателя'"),
		?(ЗначениеЗаполнено(Номер), ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Номер, Истина, Истина), ""),
		Формат(Дата, "ДЛФ=D"));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаПоКлючу(Таблица, КлючСвязи)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("КлючСвязи", КлючСвязи);
	Строки = Таблица.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат Строки[0];
	КонецЕсли; 
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаполненРеквизитТЧ(Таблица, ИмяРеквизита)
	
	Для каждого Стр Из Таблица Цикл
		Если Стр.ЭтоРазделитель Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(Стр.Партия) Тогда
			Возврат Истина;
		КонецЕсли; 
	КонецЦикла;
	Возврат Ложь;
	
КонецФункции 

&НаСервере
Процедура ОбновитьСебестоимостьДоставки()
	
	Если ЗначениеЗаполнено(ДанныеЗаказа.СлужбаДоставки) Тогда
		
		СтруктураПараметров = Новый Структура;
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СлужбаДоставки", ДанныеЗаказа.СлужбаДоставки);
		Запрос.УстановитьПараметр("ТаблицаЗначений", ДанныеЗаказа.ПараметрыДоставки.Выгрузить());
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Значения.Параметр,
		|	Значения.Значение
		|ПОМЕСТИТЬ Значения
		|ИЗ
		|	&ТаблицаЗначений КАК Значения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СлужбыДоставкиПараметрыРасчетов.Параметр,
		|	СлужбыДоставкиПараметрыРасчетов.Параметр.Идентификатор КАК Идентификатор,
		|	СлужбыДоставкиПараметрыРасчетов.Параметр.Наименование КАК Наименование,
		|	ЕСТЬNULL(Значения.Значение, 0) КАК Значение
		|ИЗ
		|	Справочник.СлужбыДоставки.ПараметрыРасчетов КАК СлужбыДоставкиПараметрыРасчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Значения КАК Значения
		|		ПО СлужбыДоставкиПараметрыРасчетов.Параметр = Значения.Параметр
		|ГДЕ
		|	СлужбыДоставкиПараметрыРасчетов.Ссылка = &СлужбаДоставки
		|	И СлужбыДоставкиПараметрыРасчетов.Параметр.ЗадаватьЗначениеПриРасчете";
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СтруктураПараметров.Вставить(Выборка.Идентификатор, Выборка.Значение);
		КонецЦикла; 
		СебестоимостьДоставки = ДоставкаСервер.СебестоимостьДоставки(ДанныеЗаказа, СтруктураПараметров);
		
	Иначе
		
		СебестоимостьДоставки = 0;
		
	КонецЕсли; 
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Источник", Перечисления.ИсточникиСтрокКалькуляции.Доставка);
	Строки = ДанныеЗаказа.Калькуляция.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаТабличнойЧасти Из Строки Цикл
		ДанныеЗаказа.Калькуляция.Удалить(СтрокаТабличнойЧасти);
	КонецЦикла;
	
	Если СебестоимостьДоставки<>0 Тогда
		Для ВариантКП = ?(ДанныеЗаказа.КоличествоВариантовКП=0, 0, 1) По ДанныеЗаказа.КоличествоВариантовКП Цикл
			СтрокаТабличнойЧасти = ДанныеЗаказа.Калькуляция.Добавить();
			СтрокаТабличнойЧасти.НомерВариантаКП = ВариантКП;
			СтрокаТабличнойЧасти.Номенклатура = ДанныеЗаказа.НоменклатураДоставки;
			СтрокаТабличнойЧасти.ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаказа.НоменклатураДоставки, "ЕдиницаИзмерения");
			СтрокаТабличнойЧасти.Количество = 1;
			СтрокаТабличнойЧасти.Источник = Перечисления.ИсточникиСтрокКалькуляции.Доставка;
			СтрокаТабличнойЧасти.Себестоимость = СебестоимостьДоставки;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

#Область Комментарии

&НаКлиенте
Процедура КомментарийЗавершениеВвода(ТекстКомментария, ДополнительныеПараметры) Экспорт
	
	Если ТекстКомментария=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	Комментарий = ТекстКомментария;
	ПриИзмененииКомментария();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКомментария()
	
	Если ТекущаяСтрокаСостава=-1 Тогда
		ДанныеЗаказа.КомментарийКалькуляции = Комментарий;
	Иначе
		СтрЗапасы = ДанныеЗаказа.Запасы.НайтиПоИдентификатору(ТекущаяСтрокаСостава);
		Если СтрЗапасы=Неопределено Тогда
			Возврат;
		КонецЕсли;
		СтрокаКомментарий = СтрокаПоКлючу(КомментарииСпецификаций, СтрЗапасы.КлючСвязи);
		Если СтрокаКомментарий=Неопределено Тогда
			СтрокаКомментарий = КомментарииСпецификаций.Добавить();
			СтрокаКомментарий.КлючСвязи = ТекущаяСтрокаСостава;
		КонецЕсли; 
		СтрокаКомментарий.Комментарий = Комментарий;
		СтрокаКомментарий.Изменен = Истина;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКомментарииСпецификаций()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИзмененныеКомментарии", ИзмененныеКомментарии());
	Запрос.УстановитьПараметр("Запасы", ДанныеЗаказа.Запасы.Выгрузить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИзмененныеКомментарии.КлючСвязи КАК КлючСвязи,
	|	ИзмененныеКомментарии.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ИзмененныеКомментарии
	|ИЗ
	|	&ИзмененныеКомментарии КАК ИзмененныеКомментарии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Запасы.Спецификация КАК Справочник.Спецификации) КАК Спецификация,
	|	Запасы.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ Запасы
	|ИЗ
	|	&Запасы КАК Запасы
	|ГДЕ
	|	Запасы.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запасы.КлючСвязи КАК КлючСвязи,
	|	ЕСТЬNULL(ИзмененныеКомментарии.Комментарий, Запасы.Спецификация.Комментарий) КАК Комментарий,
	|	ВЫБОР
	|		КОГДА ИзмененныеКомментарии.КлючСвязи ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Изменен
	|ИЗ
	|	Запасы КАК Запасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИзмененныеКомментарии КАК ИзмененныеКомментарии
	|		ПО Запасы.КлючСвязи = ИзмененныеКомментарии.КлючСвязи";
	
	КомментарииСпецификаций.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Функция ИзмененныеКомментарии()
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Изменен", Истина);
	Возврат КомментарииСпецификаций.Выгрузить(СтруктураОтбора);
	
КонецФункции
 
&НаКлиенте
Процедура ВывестиКомментарий()
	
	Комментарий = "";
	Если ТекущаяСтрокаСостава=-1 Тогда
		Комментарий = ДанныеЗаказа.КомментарийКалькуляции;
		Элементы.Комментарий.ПодсказкаВвода = НСтр("ru = 'Комментарий калькуляции'");
	Иначе
		СтрЗапасы = ДанныеЗаказа.Запасы.НайтиПоИдентификатору(ТекущаяСтрокаСостава);
		Если СтрЗапасы=Неопределено Тогда
			Возврат;
		КонецЕсли;
		СтрокаКомментарий = СтрокаПоКлючу(КомментарииСпецификаций, СтрЗапасы.КлючСвязи);
		Если СтрокаКомментарий=Неопределено Тогда
			Комментарий = КомментарийСпецификации(СтрЗапасы.Спецификация);
			Если ЗначениеЗаполнено(СтрЗапасы.Спецификация) Тогда
				СтрокаКомментарий = КомментарииСпецификаций.Добавить();
				СтрокаКомментарий.КлючСвязи = СтрЗапасы.КлючСвязи;
				СтрокаКомментарий.Комментарий = Комментарий;
			КонецЕсли; 
		Иначе
			Комментарий = СтрокаКомментарий.Комментарий;
		КонецЕсли; 
		Элементы.Комментарий.ПодсказкаВвода = НСтр("ru = 'Комментарий спецификации'");
	КонецЕсли; 		
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КомментарийСпецификации(Спецификация)
	
	Если НЕ ЗначениеЗаполнено(Спецификация) ИЛИ ТипЗнч(Спецификация)<>Тип("СправочникСсылка.Спецификации") Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Спецификация, "Комментарий");
	
КонецФункции
 
#КонецОбласти 

#КонецОбласти 

#Область РаботаСПодбором

&НаКлиенте
Процедура Подбор(Команда)
	
	ИмяТабличнойЧасти = "Калькуляция";
	МаркерПодбора = "Калькуляция";
	
	Если НЕ ПустаяСтрока(ПараметрыОткрытияПодбора[ИмяТабличнойЧасти]) Тогда
		
		ПодборНоменклатурыВДокументахКлиент.ОткрытьПодбор(ЭтотОбъект, ИмяТабличнойЧасти, ПараметрыОткрытияПодбора[ИмяТабличнойЧасти]);
		Возврат;
		
	КонецЕсли;
	
	ПараметрыПодбора = Новый Структура;
	
	ПараметрыПодбора.Вставить("Период", 				ДанныеЗаказа.Дата);
	ПараметрыПодбора.Вставить("ИспользуютсяСпецификации", Истина);
	ПараметрыПодбора.Вставить("ИспользуютсяПартии", 	Ложь);
	ПараметрыПодбора.Вставить("ЗаполнятьРезерв", 		Ложь);
	ПараметрыПодбора.Вставить("ДоступноРедактированиеСтруктурнойЕдиницы", Истина);
	ПараметрыПодбора.Вставить("ОрганизацияДокумента", 	ДанныеЗаказа.Организация);
	ПараметрыПодбора.Вставить("Организация", 	ДанныеЗаказа.Организация);
	ПараметрыПодбора.Вставить("ЭтоНабор", 	Ложь);
	
	Если ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции=ПредопределенноеЗначение("Перечисление.СпособыРасчетаСебестоимостиКалькуляции.ПоЦенамНоменклатуры")
		ИЛИ ТипЗнч(ДанныеЗаказа.СпособРасчетаСебестоимостиКалькуляции)=Тип("СправочникСсылка.ВидыЦен") Тогда
		ПараметрыПодбора.Вставить("ВидЦен", 				ВидЦен);
		ПараметрыПодбора.Вставить("Валюта", 				ДанныеЗаказа.ВалютаДокумента);
		ПараметрыПодбора.Вставить("СуммаВключаетНДС", 		ДанныеЗаказа.СуммаВключаетНДС);
		ПараметрыПодбора.Вставить("НалогообложениеНДС",		ДанныеЗаказа.НалогообложениеНДС);
		ПараметрыПодбора.Вставить("ДоступноИзменениеЦены",	Ложь);
	КонецЕсли; 
	
	ТипНоменклатуры = Новый СписокЗначений;
	Для каждого ЭлементМассива Из Элементы.КалькуляцияНаФормеНоменклатура.ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
				Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
					ТипНоменклатуры.Добавить(ЭлементФиксМассива);
				КонецЦикла; 
			Иначе
				ТипНоменклатуры.Добавить(ЭлементМассива.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ПараметрыПодбора.Вставить("ТипНоменклатуры", ТипНоменклатуры);
	ПараметрыПодбора.Вставить("ЭтоНабор", Ложь);
	
	ПараметрыПодбора.Вставить("УникальныйИдентификаторФормыВладельца", УникальныйИдентификатор);
	
	ОткрытьФорму("ОбщаяФорма.ФормаПодбора", ПараметрыПодбора, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьОшибкуЧтенияДанныхИзХранилища()
	
	ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации("Ошибка", , ТекстОшибкиЖурналаРегистрации);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, ЕстьХарактеристики, ЕстьПартии)
	
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	
	Если НЕ (ТипЗнч(ТаблицаДляЗагрузки) = Тип("ТаблицаЗначений")
		ИЛИ ТипЗнч(ТаблицаДляЗагрузки) = Тип("Массив")) Тогда
		
		ТекстОшибкиЖурналаРегистрации = "Несоответствие типа переданного в документ из подбора [" + ТипЗнч(ТаблицаДляЗагрузки) + "].
				|Адрес запасов в хранилище: " + СокрЛП(АдресЗапасовВХранилище) + "
				|Имя табличной части: " + СокрЛП(ИмяТабличнойЧасти);
		
		Возврат;
		
	Иначе
		
		ТекстОшибкиЖурналаРегистрации = "";
		
	КонецЕсли;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Заказ", ЗаказСсылка);
	СтруктураДанные.Вставить("Дата", ДанныеЗаказа.Дата);
	СтруктураДанные.Вставить("НалогообложениеНДС", ДанныеЗаказа.НалогообложениеНДС);
	СтруктураДанные.Вставить("Курс", ДанныеЗаказа.Курс);
	СтруктураДанные.Вставить("Кратность", ДанныеЗаказа.Кратность);
	СтруктураДанные.Вставить("ВидыЦен", ОтборПоВидуЦен());
	
	Для каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		
		НоваяСтрока = КалькуляцияНаФорме.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗагрузки);
		
		НоваяСтрока.Источник = Перечисления.ИсточникиСтрокКалькуляции.Прочее;
		
		СтруктураДанные.Вставить("Номенклатура", НоваяСтрока.Номенклатура);
		СтруктураДанные.Вставить("Характеристика", НоваяСтрока.Характеристика);
		СтруктураДанные.Вставить("Спецификация", НоваяСтрока.Спецификация);
		СтруктураДанные.Вставить("ЕдиницаИзмерения", НоваяСтрока.ЕдиницаИзмерения);
		
		СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураДанные, "СебестоимостьЕдиницы, СпособПополнения, ТипНоменклатуры, ИспользоватьХарактеристики, ИспользоватьПартии");
		НоваяСтрока.Себестоимость = НоваяСтрока.СебестоимостьЕдиницы*НоваяСтрока.Количество;
		НоваяСтрока.Прибыль = НоваяСтрока.Сумма-НоваяСтрока.Себестоимость;
	
	КонецЦикла;
	
	ВнестиИзмененияВКалькуляцию();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
&НаСервере
Процедура ЗагрузитьИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Заказ", ЗаказСсылка);
	СтруктураДанные.Вставить("Дата", ДанныеЗаказа.Дата);
	СтруктураДанные.Вставить("НалогообложениеНДС", ДанныеЗаказа.НалогообложениеНДС);
	СтруктураДанные.Вставить("Курс", ДанныеЗаказа.Курс);
	СтруктураДанные.Вставить("Кратность", ДанныеЗаказа.Кратность);
	СтруктураДанные.Вставить("ВидыЦен", ОтборПоВидуЦен());
	СтрокаЗапаса = ?(ТекущаяСтрокаСостава<0, Неопределено, ДанныеЗаказа.Запасы.НайтиПоИдентификатору(ТекущаяСтрокаСостава));
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	ТоварыДобавлены = Ложь;
	
	Для каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл 
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ДанныеЗаказа.Калькуляция.Добавить();
		НоваяСтрока.НомерВариантаКП = ТекущийВариантКП;
		НоваяСтрока.Номенклатура = СтрокаТаблицы.Номенклатура;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
			
			НоваяСтрока.Характеристика = СтрокаТаблицы.Характеристика;
			
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПодсистемуРаботы")
			ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПодсистемуПроизводство") Тогда
			
			НоваяСтрока.Спецификация = СтрокаТаблицы.Спецификация;
			
		КонецЕсли;
				
		Если ПолучитьФункциональнуюОпцию("УчетВРазличныхЕдиницахИзмерения") Тогда
			
			НоваяСтрока.ЕдиницаИзмерения = СтрокаТаблицы.ЕдиницаИзмерения;
			
		ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
			
			НоваяСтрока.ЕдиницаИзмерения = СтрокаТаблицы.Номенклатура.ЕдиницаИзмерения;
			
		КонецЕсли;
		
		НоваяСтрока.Количество = СтрокаТаблицы.Количество;
		НоваяСтрока.Себестоимость = СтрокаТаблицы.Себестоимость;
		НоваяСтрока.СебестоимостьЕдиницы = ?(НоваяСтрока.Количество<>0, НоваяСтрока.Себестоимость/НоваяСтрока.Количество, 0);
		
		Если НЕ СтрокаЗапаса=Неопределено Тогда
			НоваяСтрока.НоменклатураИзделие = СтрокаЗапаса.Номенклатура;
			НоваяСтрока.ХарактеристикаИзделие = СтрокаЗапаса.Характеристика;
			НоваяСтрока.СпецификацияИзделие = СтрокаЗапаса.Спецификация;
			НоваяСтрока.КлючСвязи = СтрокаЗапаса.КлючСвязи;
			НоваяСтрока.Источник = Перечисления.ИсточникиСтрокКалькуляции.Запас;
		Иначе
			НоваяСтрока.Источник = Перечисления.ИсточникиСтрокКалькуляции.Прочее;
		КонецЕсли;
				
		Если НЕ СтрокаТаблицы.ЭтоРасход И НоваяСтрока.Себестоимость=0 Тогда
			СтруктураДанные.Вставить("Номенклатура", НоваяСтрока.Номенклатура);
			СтруктураДанные.Вставить("Характеристика", НоваяСтрока.Характеристика);
			СтруктураДанные.Вставить("Спецификация", НоваяСтрока.Спецификация);
			СтруктураДанные.Вставить("ЕдиницаИзмерения", НоваяСтрока.ЕдиницаИзмерения);
			СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
			НоваяСтрока.СебестоимостьЕдиницы = СтруктураДанные.СебестоимостьЕдиницы;
			НоваяСтрока.Себестоимость = НоваяСтрока.СебестоимостьЕдиницы*НоваяСтрока.Количество;
		КонецЕсли;
		
		ТоварыДобавлены = Истина;
		
	КонецЦикла;
	
	Если ТоварыДобавлены Тогда
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
	Если ТекущаяСтрокаСостава<0 Тогда
		ВывестиКалькуляцию();
	Иначе
		ВывестиКалькуляцию(ТекущаяСтрокаСостава);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(АдресЗагруженныхДанных) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаНаСервере(АдресЗагруженныхДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаИзФайлаЗапасы(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата", ЭтотОбъект, НастройкиЗагрузкиДанных);
	
	НастройкиЗагрузкиДанных.Вставить("ПолноеИмяТабличнойЧасти", "ЗаказПокупателя.Калькуляция");
	НастройкиЗагрузкиДанных.Вставить("ИмяМакетаСШаблоном", "ЗагрузкаИзФайлаКалькуляция");
	ЗагрузкаКалькуляции = (ТекущаяСтрокаСостава<0);
	Если ЗагрузкаКалькуляции Тогда
		НастройкиЗагрузкиДанных.Вставить("Заголовок", НСтр("ru = 'Загрузка калькуляции из файла'"));
	Иначе
		НастройкиЗагрузкиДанных.Вставить("Заголовок", НСтр("ru = 'Загрузка спецификации из файла'"));
	КонецЕсли; 
	НастройкиЗагрузкиДанных.Вставить("ЗагрузкаКалькуляции", ЗагрузкаКалькуляции);
	НастройкиЗагрузкиДанных.Вставить("ОтображатьСебестоимость", ОтображатьСебестоимость);
	
	ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных, ОписаниеОповещения, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата(РезультатЗагрузки, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗагрузки) = Тип("Структура") Тогда
		
		Если РезультатЗагрузки.ОписаниеДействия = "ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника" Тогда
		
			ЗагрузкаДанныхИзВнешнегоИсточника.ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных.ИмяФормыЗагрузкиДанныхИзВнешнихИсточников);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата", ЭтотОбъект, НастройкиЗагрузкиДанных);
			ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных, ОписаниеОповещения, ЭтотОбъект);
			
		ИначеЕсли РезультатЗагрузки.ОписаниеДействия = "ОбработатьПодготовленныеДанные" Тогда
			
			ОбработатьПодготовленныеДанные(РезультатЗагрузки);
			
		КонецЕсли;
		
	Иначе
		
		ЗагрузитьИзФайлаЗавершение(РезультатЗагрузки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПодготовленныеДанные(РезультатЗагрузки)
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Заказ", ЗаказСсылка);
	СтруктураДанные.Вставить("Дата", ДанныеЗаказа.Дата);
	СтруктураДанные.Вставить("НалогообложениеНДС", ДанныеЗаказа.НалогообложениеНДС);
	СтруктураДанные.Вставить("Курс", ДанныеЗаказа.Курс);
	СтруктураДанные.Вставить("Кратность", ДанныеЗаказа.Кратность);
	СтруктураДанные.Вставить("ВидыЦен", ОтборПоВидуЦен());
	СтрокаЗапаса = ?(ТекущаяСтрокаСостава<0, Неопределено, ДанныеЗаказа.Запасы.НайтиПоИдентификатору(ТекущаяСтрокаСостава));
	
	Попытка
		
		ПоказатьСодержание = Ложь;
		
		ТаблицаСопоставленияДанных = РезультатЗагрузки.ТаблицаСопоставленияДанных;
		Для каждого СтрокаТаблицы Из ТаблицаСопоставленияДанных Цикл
			
			ЗагрузкаВПриложениеВозможна = СтрокаТаблицы[ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна()];
			Если ЗагрузкаВПриложениеВозможна Тогда
				
				НоваяСтрока = ДанныеЗаказа.Калькуляция.Добавить();
				НоваяСтрока.НомерВариантаКП = ТекущийВариантКП;
				
				ИменаСвойств = "Номенклатура, Количество";
				
				Если ТекущаяСтрокаСостава<0 Тогда
					
					ИменаСвойств = ИменаСвойств + ", Себестоимость";
					
				КонецЕсли; 
				
				Если ПолучитьФункциональнуюОпцию("УчетВРазличныхЕдиницахИзмерения") Тогда
					
					ИменаСвойств = ИменаСвойств + ", ЕдиницаИзмерения";
					
				КонецЕсли;
				
				Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
					
					ИменаСвойств = ИменаСвойств + ", Характеристика";
					
				КонецЕсли;
				
				Если ПолучитьФункциональнуюОпцию("ИспользоватьПодсистемуРаботы")
					ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПодсистемуПроизводство") Тогда
					
					ИменаСвойств = ИменаСвойств + ", Спецификация";
					
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ИменаСвойств);
				
				Если НЕ ПолучитьФункциональнуюОпцию("УчетВРазличныхЕдиницахИзмерения") 
					И ТипЗнч(НоваяСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
					
					НоваяСтрока.ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Номенклатура, "ЕдиницаИзмерения");
					
				КонецЕсли; 
				
				Если НЕ СтрокаЗапаса=Неопределено Тогда
					НоваяСтрока.НоменклатураИзделие = СтрокаЗапаса.Номенклатура;
					НоваяСтрока.ХарактеристикаИзделие = СтрокаЗапаса.Характеристика;
					НоваяСтрока.СпецификацияИзделие = СтрокаЗапаса.Спецификация;
					НоваяСтрока.КлючСвязи = СтрокаЗапаса.КлючСвязи;
					НоваяСтрока.Источник = Перечисления.ИсточникиСтрокКалькуляции.Запас;
				Иначе
					НоваяСтрока.Источник = Перечисления.ИсточникиСтрокКалькуляции.Прочее;
				КонецЕсли; 
				
				Если НоваяСтрока.Себестоимость=0 И (ТекущаяСтрокаСостава>=0 ИЛИ НЕ СтрокаТаблицы.ЭтоРасход) Тогда
					СтруктураДанные.Вставить("Номенклатура", НоваяСтрока.Номенклатура);
					СтруктураДанные.Вставить("Характеристика", НоваяСтрока.Характеристика);
					СтруктураДанные.Вставить("Спецификация", НоваяСтрока.Спецификация);
					СтруктураДанные.Вставить("ЕдиницаИзмерения", НоваяСтрока.ЕдиницаИзмерения);
					СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
					НоваяСтрока.СебестоимостьЕдиницы = СтруктураДанные.СебестоимостьЕдиницы;
					НоваяСтрока.Себестоимость = НоваяСтрока.СебестоимостьЕдиницы*НоваяСтрока.Количество;
				Иначе
					НоваяСтрока.СебестоимостьЕдиницы = ?(НоваяСтрока.Количество<>0, НоваяСтрока.Себестоимость/НоваяСтрока.Количество, 0);
				КонецЕсли; 
				
			КонецЕсли;
			
		КонецЦикла;
		
		Модифицированность = Истина;
		
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		ЗаписьЖурналаРегистрации(Нстр("ru='Загрузка данных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.ЗаказПокупателя, , ТекстОшибки);
		
	КонецПопытки;
	
	Если ТекущаяСтрокаСостава<0 Тогда
		ВывестиКалькуляцию();
	Иначе
		ВывестиКалькуляцию(ТекущаяСтрокаСостава);
	КонецЕсли; 
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#КонецОбласти 