
#Область ОписаниеПеременных

&НаКлиенте
Перем ПриИзмененииСтарт;

&НаКлиенте
Перем СтароеКоличествоПродукции;

&НаКлиенте
Перем ПриИзмененииФиниш;

&НаКлиенте
Перем ДанныеВыбораСостояния;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеФормы();
	Если Объект.Ссылка.Пустая() Тогда
		ЗаполнитьПараметрыФормы();
	КонецЕсли; 
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;
	ЭтотОбъект.ОснованиеСоздания = Параметры.Основание;
	ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Истина;
	
	Компания = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация);
	ОбновитьСпособРаспределенияПоУмолчанию();
	
	//Установить надпись основание
	Если ПараметрыФормы.РезервированиеЗапасов Тогда
		Элементы.ДокументОснованиеНадпись.Видимость = Ложь;
	Иначе
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Объект.ДокументОснование);
	КонецЕсли;
	
	УстановитьРежимИСписокВыбора();
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОповеститьРабочийКалендарь = Ложь;
	Иначе
		ОповеститьРабочийКалендарь = Истина;
	КонецЕсли; 
	ДокументМодифицирован = Ложь;
	
	// КопированиеСтрокТабличныхЧастей
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСевере(Элементы, "Продукция");
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = УправлениеСвойствамиПереопределяемый.ЗаполнитьДополнительныеПараметры(Объект, "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПодключаемоеОборудование
	ИспользоватьПодключаемоеОборудование = УправлениеНебольшойФирмойПовтИсп.ИспользоватьПодключаемоеОборудование();
	СписокЭлектронныхВесов = МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам("ЭлектронныеВесы", , МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента());
	Если СписокЭлектронныхВесов.Количество() = 0 Тогда
		// Нет подключенных весов.
		Элементы.ЗапасыПолучитьВес.Видимость = Ложь;
	КонецЕсли;
	Элементы.ЗапасыЗагрузитьДанныеИзТСД.Видимость = ИспользоватьПодключаемоеОборудование;
	Элементы.ПродукцияЗагрузитьДанныеИзТСД.Видимость = ИспользоватьПодключаемоеОборудование;
	// Конец ПодключаемоеОборудование
	
	// Характеристики
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить("Продукция");
	СоответствиеТЧ.Вставить("Запасы");
	НоменклатураВДокументахСервер.ОбновитьУсловноеОформлениеТабличнойЧастиДляХарактеристик(ЭтаФорма, СоответствиеТЧ);
	
	ОбновитьУсловноеОформлениеТабличнойЧастиДляХарактеристик();
	Если Параметры.Ключ.Пустая()
		Тогда
		ТипСтруктурнойЕдиницы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы");
		НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект, Истина);
		ЗаполнитьПризнакиИспользованияЭтапов();
		ОбновитьКэшиДанныхСервер();
		ЗаполнитьСлужебныеДанныеПослеЧтенияОбъекта(Объект);
	КонецЕсли;
	
	ТекущаяПродукция = -1;
	УстановитьКартинкиЗакладок(ЭтотОбъект);
	
	РучноеРаспределение = Объект.РучноеРаспределение;
	Если Объект.РучноеРаспределение Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли; 
	
	УправлениеФормой(ЭтотОбъект);
	
	// Установка видимости реквизитов от пользовательских настроек
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	
	// МобильныйКлиент
	УстановитьВидимостьЭлементовДляМобильногоКлиента();
	// Конец МобильныйКлиент
	
	ОткрытИзПланировщика = Параметры.Свойство("ВыбранныеРесурсы");
	
	Если ОткрытИзПланировщика и Не ЭтаФорма.ТолькоПросмотр Тогда
		ЗаполнитьРесурсыИзПланировщика(Параметры.ВыбранныеРесурсы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриИзмененииСтарт = Объект.Старт;
	ПриИзмененииФиниш = Объект.Финиш;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода");
	// Конец ПодключаемоеОборудование
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбновитьСпискиВыбораПродукции();
	ОбновитьПодсказкуРаспределение();
	
	//Ресурсы
	Если ОткрытИзПланировщика и ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не Элементы.Найти("ТЧРесурсыПредприятия") = Неопределено Тогда
			ЭтаФорма.ТекущийЭлемент = Элементы.ТЧРесурсыПредприятия;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЗначениеСвойстваЭлементаФормы(Элементы, "Страницы", "ТекущаяСтраница") = Неопределено Тогда
		Если Элементы.Страницы.ТекущаяСтраница.Имя = "ТЧРесурсыПредприятия" Тогда
			УстановитьДоступностьПовторов(Истина);
			ЗаполнитьДанныеТаблицыРесурсовНаФорме();
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьОтметкуСтруктурнойЕдиницыРезерва();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ОповеститьРабочийКалендарь Тогда
		Оповестить("ИзмененЗаказНаПроизводство", Объект.Ответственный);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование"
		И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			//Преобразуем предварительно к ожидаемому формату
			Данные = Новый Массив();
			Если Параметр[1] = Неопределено Тогда
				Данные.Добавить(Новый Структура("Штрихкод, Количество, ДоляСтоимости", Параметр[0], 1, 1)); // Достаем штрихкод из основных данных
			Иначе
				Данные.Добавить(Новый Структура("Штрихкод, Количество, ДоляСтоимости", Параметр[1][1], 1, 1)); // Достаем штрихкод из дополнительных данных
			КонецЕсли;
			
			ПолученыШтрихкоды(Данные);
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	Если ИмяСобытия = "ПодборПроизведен" 
		И ЗначениеЗаполнено(Параметр) 
		//Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор
		Тогда
		
		АдресЗапасовВХранилище	= Параметр;
		ТекущаяСтраницыПродукция= (Элементы.Страницы.ТекущаяСтраница = Элементы.ТЧПродукция);
		ИмяТабличнойЧасти		= ?(ТекущаяСтраницыПродукция, "Продукция", "Запасы");
		
		ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, Истина, Ложь);
		
		
		Если ИмяТабличнойЧасти="Продукция" Тогда
			УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
			ОбновитьСпискиВыбораПродукции();
		ИначеЕсли ИмяТабличнойЧасти="Запасы" Тогда
			ВывестиОтметкиКонтроля(ЭтотОбъект);
		КонецЕсли; 
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_СостоянияЗаказовНаПроизводство" Тогда
		ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Истина;
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменениеСостояния_ЗаказНаПроизводство" И Параметр <> Неопределено Тогда
		
		ЗаказИзменен = Неопределено;
		
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			ЗаказИзменен = Параметр.Найти(Объект.Ссылка);
		ИначеЕсли ТипЗнч(Параметр) = Тип("ДокументСсылка.ЗаказНаПроизводство") Тогда
			ЗаказИзменен = Параметр = Объект.Ссылка;
		КонецЕсли;
		
		Если ЗаказИзменен = Неопределено ИЛИ (ТипЗнч(ЗаказИзменен) = Тип("Булево") И ЗаказИзменен = Ложь) Тогда
			Возврат;
		КонецЕсли;
		
		ЭтотОбъект.Прочитать();
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
	// КопированиеСтрокТабличныхЧастей
	Если ИмяСобытия = "БуферОбменаТабличнаяЧастьКопированиеСтрок" Тогда
		КопированиеТабличнойЧастиКлиент.ОбработкаОповещения(Элементы, "Продукция");
	КонецЕсли;
	
	// Обсуждения
	ОбсужденияКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтотОбъект, Объект.Ссылка);
	// Конец Обсуждения
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьПараметрыФормы();	
	
	Элементы.ВариантЗавершенияРасширеннаяПодсказка.Заголовок = СтрШаблон(НСтр("ru='Предыдущее состояние: %1'"),
		СостоянияЗаказов.ПолучитьСостояниеЗаказаПередЗавершением(
			ТекущийОбъект.Ссылка,
			Справочники.СостоянияЗаказовНаПроизводство.Завершен)
	);
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ТипСтруктурнойЕдиницы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы");
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	ОбновитьКэшиДанныхСервер();
	ЗаполнитьСлужебныеДанныеПослеЧтенияОбъекта(Объект);
	
	ПланированиеРесурсовУНФ.ПерезаполнитьСлужебныеРеквизитыТаблицыРесурсов(Объект.РесурсыПредприятия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "Проведение"+ РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	КонецЕсли;
	// СтандартныеПодсистемы.ОценкаПроизводительности
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Модифицированность Тогда
		ДокументМодифицирован = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// Обсуждения
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность",Модифицированность);
	// Конец Обсуждения
	
	//Характеристики и Партии таб. часть "РаспределениеЗапасов"
	 ПроверитьЗаполнениеХарактеристикРаспределениеЗапасов(Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Элементы.ВариантЗавершенияРасширеннаяПодсказка.Заголовок = СтрШаблон(НСтр("ru='Предыдущее состояние: %1'"),
		СостоянияЗаказов.ПолучитьСостояниеЗаказаПередЗавершением(
			ТекущийОбъект.Ссылка,
			Справочники.СостоянияЗаказовНаПроизводство.Завершен)
	);
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	//Обсуждения
	ОбсужденияСервер.ПослеЗаписиНаСервере(ТекущийОбъект);
	// Конец Обсуждения
	
	ПланированиеРесурсовУНФ.ПерезаполнитьСлужебныеРеквизитыТаблицыРесурсов(Объект.РесурсыПредприятия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ЗаполненЗаказ = Ложь;
	Для каждого СтрокаТЧ Из Объект.Продукция Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.ЗаказПокупателя) Тогда
			ЗаполненЗаказ = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ЗаполненЗаказ Тогда
		Оповестить("Запись_ЗаказНаПроизводство", Объект.Ссылка);
	КонецЕсли;
	
	Если ДокументМодифицирован Тогда
		ОповеститьРабочийКалендарь = Истина;
		ДокументМодифицирован = Ложь;
	КонецЕсли;
	
	Если ОткрытИзПланировщика Тогда Оповестить("ОбновитьПланировщик") КонецЕсли;
	
	ЗаполнитьСлужебныеДанныеПослеЧтенияОбъекта(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	// Изменен состав бригады в подчиненной форме
	Если ТипЗнч(ВыбранноеЗначение)=Тип("Структура") И ВыбранноеЗначение.Свойство("Событие") И ВыбранноеЗначение.Событие="ИзмененСоставБригады" Тогда
		СтрокаТабличнойЧасти = Объект.Операции.НайтиПоИдентификатору(ВыбранноеЗначение.Идентификатор);
		Если СтрокаТабличнойЧасти.КлючСвязи=0 Тогда
			УправлениеНебольшойФирмойКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект, СтрокаТабличнойЧасти);
		КонецЕсли;
		УправлениеНебольшойФирмойКлиентСервер.УдалитьСтрокиПоКлючуСвязи(Объект.СоставБригады, СтрокаТабличнойЧасти);
		Для каждого ОписаниеСтроки Из ВыбранноеЗначение.СоставБригады Цикл
			НоваяСтрока = Объект.СоставБригады.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеСтроки);
			НоваяСтрока.КлючСвязи = СтрокаТабличнойЧасти.КлючСвязи;
		КонецЦикла; 
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДатаПриИзменении()

&НаКлиенте
Процедура СостояниеЗаказаПриИзменении(Элемент)
	
	Если Объект.СостояниеЗаказа = ПредопределенноеЗначение("Справочник.СостоянияЗаказовНаПроизводство.Завершен") Тогда
		Объект.ВариантЗавершения = ПредопределенноеЗначение("Перечисление.ВариантыЗавершенияЗаказа.Успешно");
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЗавершениеЗаказа;
		
		#Если ВебКлиент Тогда
			// В веб-клиенте установка текущей страницы должна происходить после включения видимости у страницы
			ПодключитьОбработчикОжидания("УстановитьТекущейСтраницейЗавершениеЗаказа", 0.1, Истина);
		#КонецЕсли
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЗаказаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния Тогда
		ДанныеВыбораСостояния = ПолучитьДанныеВыбора(Тип("СправочникСсылка.СостоянияЗаказовНаПроизводство"), ПараметрыПолученияДанных);
		ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Ложь;
	КонецЕсли;
	
	ДанныеВыбора = ДанныеВыбораСостояния;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	// Обработка события изменения организации.
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация);
	Компания = СтруктураДанные.Компания;
	
КонецПроцедуры // ОрганизацияПриИзменении()

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказПокупателяПриИзменении(Элемент)
	
	ЗаполнитьРеквизитыТЧПоШапке(Объект, "ЗаказПокупателя");
	
	УправлениеФормой(ЭтотОбъект);
	
	ПриИзмененииВидаОперацииЗаказа();
	
КонецПроцедуры // ЗаказПокупателяПриИзменении()

&НаКлиенте
Процедура ВидОперацииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка") Тогда
		
		ТипНоменклатурыЗапас = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Запас");
		Для каждого СтрокаПродукция Из Объект.Продукция Цикл
			
			Если ЗначениеЗаполнено(СтрокаПродукция.Номенклатура)
				И СтрокаПродукция.ТипНоменклатуры <> ТипНоменклатурыЗапас Тогда
				
				ТекстСообщения = НСтр("ru = 'Операция разборки не выполняется для работ и услуг!
								|В строке №%Номер% табличной части ""Продукция"" номенклатура ""%НоменклатураПредставление%"" является работой (услугой)'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", СтрокаПродукция.НомерСтроки);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НоменклатураПредставление%", Строка(СтрокаПродукция.Номенклатура));
				
				УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(Объект, ТекстСообщения);
				СтандартнаяОбработка = Ложь;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ВидОперацииОбработкаВыбора()

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
	ПриИзмененииВидаОперацииЗаказа();
	
КонецПроцедуры // ВидОперацииПриИзменении()

&НаКлиенте
Процедура СтартПриИзменении(Элемент)
	
	Если Объект.Старт > Объект.Финиш И ЗначениеЗаполнено(Объект.Финиш) Тогда
		Объект.Старт = ПриИзмененииСтарт;
		Сообщить(НСтр("ru='Дата старта не может быть больше даты финиша.'"));
	Иначе
		ПриИзмененииСтарт = Объект.Старт;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФинишПриИзменении(Элемент)
	
	Если Час(Объект.Финиш) = 0 И Минута(Объект.Финиш) = 0 Тогда
		Объект.Финиш = КонецДня(Объект.Финиш);
	КонецЕсли;
	
	Если Объект.Финиш < Объект.Старт Тогда
		Объект.Финиш = ПриИзмененииФиниш;
		Сообщить(НСтр("ru='Дата финиша не может быть меньше даты старта.'"));
	Иначе
		ПриИзмененииФиниш = Объект.Финиш;
	КонецЕсли;
	
КонецПроцедуры // ФинишПриИзменении()

&НаКлиенте
Процедура СтруктурнаяЕдиницаПриИзменении(Элемент)
	
	ДанныеСтруктурнаяЕдиница = ПолучитьДанныеСтруктурнаяЕдиницаПриИзменении(Объект.СтруктурнаяЕдиница);
	
	Если ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница)
		И ЗначениеЗаполнено(ДанныеСтруктурнаяЕдиница.ИсточникПеремещения) Тогда
		
		Объект.СтруктурнаяЕдиницаРезерв = ДанныеСтруктурнаяЕдиница.ИсточникПеремещения;
		ОбновитьСкладВТЧ();
		
	КонецЕсли;
	
	ТипСтруктурнойЕдиницы = ДанныеСтруктурнаяЕдиница.ТипСтруктурнойЕдиницы;
	
	Для каждого СтрокаПродукция Из Объект.Продукция Цикл
		Если ТипСтруктурнойЕдиницы=ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение")
			И ПараметрыФормы.ИспользоватьЭтапыПроизводства
			И СтрокаПродукция.ИспользоватьЭтапыПроизводства Тогда
			СтрокаПродукция.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
		Иначе
			СтрокаПродукция.ПодразделениеЗавершающегоЭтапа = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры // СтруктурнаяЕдиницаПриИзменении()

&НаКлиенте
Процедура СтруктурнаяЕдиницаОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элементы.СтруктурнаяЕдиница.РежимВыбораИзСписка
		И НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // СтруктурнаяЕдиницаОткрытие()

&НаКлиенте
Процедура СтруктурнаяЕдиницаРезервПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаРезерв)
		И Не ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
		
		ДанныеСтруктурнаяЕдиница = ПолучитьДанныеСтруктурнаяЕдиницаРезервПриИзменении(Объект.СтруктурнаяЕдиницаРезерв);
		Объект.СтруктурнаяЕдиница = ДанныеСтруктурнаяЕдиница;
		
	КонецЕсли;
	
	ОбновитьСкладВТЧ();
	
КонецПроцедуры // СтруктурнаяЕдиницаРезервПриИзменении()

&НаКлиенте
Процедура ПродукцияСтруктурнаяЕдиницаРезервОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элементы.ПродукцияСтруктурнаяЕдиницаРезерв.РежимВыбораИзСписка
		И НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаРезерв) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // ПродукцияСтруктурнаяЕдиницаРезервОткрытие()

&НаКлиенте
Процедура ЗапасыСтруктурнаяЕдиницаРезервОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элементы.ЗапасыСтруктурнаяЕдиницаРезерв.РежимВыбораИзСписка
		И НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаРезерв) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // ЗапасыСтруктурнаяЕдиницаРезервОткрытие()

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
		
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "удалить" Тогда
		Объект.ДокументОснование = Неопределено;
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Неопределено);
		Модифицированность = Истина;
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "заполнить" Тогда
		ЗаполнитьПоОснованиюНачало();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "выбрать" Тогда
		//Выбрать основание
		СписокОснований = Новый СписокЗначений;
		СписокОснований.Добавить("Документ.ЗаказПокупателя.ФормаВыбора", "Заказ покупателя");
		СписокОснований.Добавить("Документ.ЗаказНаПроизводство.ФормаВыбора", "Заказ на производство");
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборТипаОснованияЗавершение", ЭтотОбъект);
		ПоказатьВыборИзМеню(ОписаниеОповещения, СписокОснований, Элементы.ДокументОснованиеНадпись);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "открыть" Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) тогда
			возврат;
		КонецЕсли;
		
		РаботаСФормойДокументаКлиент.ОткрытьФормуДокументаПоТипу(Объект.ДокументОснование);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииВидаОперацииЗаказа()
	
	ПриИзмененииВидаОперацииЗаказаСервер();
	
	ЭтоРазборка = (Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка"));
	
	Если ЭтоРазборка Тогда
		
		Для каждого СтрокаПродукция Из Объект.Продукция Цикл
			СтрокаПродукция.Резерв = 0;
		КонецЦикла;
		
	Иначе
		
		Для каждого СтрокаЗапасы Из Объект.Запасы Цикл
			СтрокаЗапасы.Резерв = 0;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЭтоРазборка Тогда
		Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
			СтрокаТабличнойЧасти.Этап = ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ПустаяСсылка");
		КонецЦикла;
		Для каждого СтрокаТабличнойЧасти Из Объект.РаспределениеЗапасов Цикл
			СтрокаТабличнойЧасти.Этап = ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ПустаяСсылка");
		КонецЦикла;
		Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
			СтрокаТабличнойЧасти.Этап = ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ПустаяСсылка");
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ЗаказПокупателя) И Объект.ПоложениеЗаказаПокупателя<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		Объект.ДокументОснование = Объект.ЗаказПокупателя;
	КонецЕсли;
	
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	
	ОбновитьСкладВТЧ();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииВидаОперацииЗаказаСервер()
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка") Тогда
		ИмяТЧ = "Запасы";
	Иначе
		ИмяТЧ = "Продукция";
	КонецЕсли;
	Для каждого СтрокаПродукция Из Объект[ИмяТЧ] Цикл
		Если ЗначениеЗаполнено(СтрокаПродукция.Партия)
			И СтрокаПродукция.Партия.Статус = ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье") Тогда
			СтрокаПродукция.Партия = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РучноеРаспределениеПриИзменении(Элемент)
	
	Объект.РучноеРаспределение = РучноеРаспределение;
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтотОбъект);
	Если Объект.РучноеРаспределение Тогда
		РаспределитьФрагмент();
		ОбновитьСпискиВыбораПродукции();
	Иначе
		Объект.РаспределениеЗапасов.Очистить();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктурнаяЕдиницаОперацийПриИзменении(Элемент)
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиница;
	КонецЦикла; 	
	Для каждого СтрокаТабличнойЧасти Из Объект.СоставБригады Цикл
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиница;
	КонецЦикла; 	
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	Объект.СоставБригады.Очистить();
	
	Если ЗначениеЗаполнено(Объект.Исполнитель) И ТипЗнч(Объект.Исполнитель)=Тип("СправочникСсылка.Бригады") Тогда
		ЗаполнитьСоставБригадыНаСервере(Объект.Исполнитель);
	ИначеЕсли ЗначениеЗаполнено(Объект.Исполнитель) И ТипЗнч(Объект.Исполнитель)=Тип("СправочникСсылка.Сотрудники") Тогда
		ОбновитьКэшиДанных();
		Подразделение = КэшПодразделения.Получить(Объект.Исполнитель);
		Если НЕ ПараметрыФормы.УчетПоНесколькимПодразделениям Тогда
			Объект.СтруктурнаяЕдиницаОпераций = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ОсновноеПодразделение");
		ИначеЕсли Объект.ПоложениеСтруктурнойЕдиницыОпераций=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") Тогда
			Объект.СтруктурнаяЕдиницаОпераций = Подразделение;
		ИначеЕсли Объект.ПоложениеСтруктурнойЕдиницыОпераций=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
			Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Подразделение;
			КонецЦикла; 
			Для каждого СтрокаТабличнойЧасти Из Объект.СоставБригады Цикл
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Подразделение;
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница.Имя = "ТЧРесурсыПредприятия" Тогда
		УстановитьДоступностьПовторов(Истина);
		ЗаполнитьДанныеТаблицыРесурсовНаФорме();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПродукция

&НаКлиенте
Процедура ПродукцияНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	
	СтатусПартии = Новый СписокЗначений;
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
	
	СтруктураДанные.Вставить("СтатусПартии", СтатусПартии);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	СтрокаТабличнойЧасти.Количество = 1;
	СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
	СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства = СтруктураДанные.ИспользоватьЭтапыПроизводства;
	Если СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства
		И ПараметрыФормы.ИспользоватьЭтапыПроизводства
		И ТипСтруктурнойЕдиницы=ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") Тогда
		СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
	КонецЕсли; 
	
	СтрокаТабличнойЧасти.ТипНоменклатуры = СтруктураДанные.ТипНоменклатуры;
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка") Тогда
		Если НЕ СкладВШапке Тогда
			ЗаполнениеОбъектовУНФКлиент.ЗаполнитьСкладВСтрокеТЧЗапасы(СтрокаТабличнойЧасти, Объект, СтруктураДанные);
		Иначе
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаРезерв;
		КонецЕсли; 
	КонецЕсли; 

	СтрокаТабличнойЧасти.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
	СтрокаТабличнойЧасти.ЗаполнениеХарактеристикиПроверено = Истина;
	
	Если СтруктураДанные.ИспользоватьХарактеристики
		Тогда
		СтрокаТабличнойЧасти.Характеристика = СтруктураДанные.Характеристика;
	КонецЕсли;
	
	//Партии
	СтрокаТабличнойЧасти.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
	
	Если СтруктураДанные.ИспользоватьПартии
		Тогда
		СтрокаТабличнойЧасти.Партия = СтруктураДанные.Партия;
	КонецЕсли;
	// Конец Партии
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) И Объект.Запасы.Количество()>0 Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	ОбновитьСпискиВыбораПродукции();
	ЗаполнитьСлужебныеДанныеОперации(Объект);
	УправлениеВидимостьюЭтапов(ЭтотОбъект);

КонецПроцедуры // ПродукцияНоменклатураПриИзменении()

&НаКлиенте
Процедура ПродукцияХарактеристикаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	
	СтруктураДанные = ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
	СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства = СтруктураДанные.ИспользоватьЭтапыПроизводства;
	Если СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства
		И ПараметрыФормы.ИспользоватьЭтапыПроизводства
		И ТипСтруктурнойЕдиницы=ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") Тогда
		СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) И Объект.Запасы.Количество()>0 Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	ОбновитьСпискиВыбораПродукции();
	ЗаполнитьСлужебныеДанныеОперации(Объект);
	УправлениеВидимостьюЭтапов(ЭтотОбъект);
	
КонецПроцедуры // ПродукцияХарактеристикаПриИзменении()

&НаКлиенте
Процедура ПродукцияЗаказПокупателяПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) Тогда
		СтрокаТабличнойЧасти.Резерв = 0;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) И Объект.Запасы.Количество()>0 Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	ОбновитьСпискиВыбораПродукции();
	ЗаполнитьСлужебныеДанныеОперации(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	
	Если НоваяСтрока И Копирование Тогда
		ТекущаяСтрока.КлючСвязи = 0;
	КонецЕсли;
		
	ИмяТабличнойЧасти = "Продукция";
	
	Если НоваяСтрока ИЛИ ТекущаяСтрока.КлючСвязи=0 Тогда
		
		УправлениеНебольшойФирмойКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект);
		
	КонецЕсли;
	
	Если НоваяСтрока И НЕ Копирование Тогда
		ЗаполнитьДанныеШапки(ТекущаяСтрока, "Продукция"); 
	КонецЕсли; 
	
	СтароеКоличествоПродукции = ТекущаяСтрока.Количество;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияКоличествоПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Элементы.Продукция.ТекущиеДанные.Спецификация) Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли;
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда Возврат КонецЕсли;
	НоменклатураВДокументахКлиент.КонтрольЗначенияРезерваВТекущейСтроке(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияЕдиницаИзмеренияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Элементы.Продукция.ТекущиеДанные.Спецификация) Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияСпецификацияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Спецификация", СтрокаТабличнойЧасти.Спецификация);
	СтруктураДанные = ПолучитьДанныеСпецификацияПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства = СтруктураДанные.ИспользоватьЭтапыПроизводства;
	Если СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства
		И ПараметрыФормы.ИспользоватьЭтапыПроизводства
		И ТипСтруктурнойЕдиницы=ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") Тогда
		СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) И Объект.Запасы.Количество()>0 Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	ОбновитьСпискиВыбораПродукции();
	ЗаполнитьСлужебныеДанныеОперации(Объект);
	УправлениеВидимостьюЭтапов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПриИзменении(Элемент)
	
	ОбновитьОтметкуСтруктурнойЕдиницыРезерва(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПередУдалением(Элемент, Отказ)
	
	ЕстьСпецификации = Ложь;
	Для каждого ВыделеннаяСтрока Из Элемент.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элемент.ДанныеСтроки(ВыделеннаяСтрока);
		Если ЗначениеЗаполнено(ДанныеСтроки.Спецификация) Тогда
			ЕстьСпецификации = Истина;
		КонецЕсли;
		Если Объект.РучноеРаспределение Тогда
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("КлючСвязиПродукция", ДанныеСтроки.КлючСвязи);
			Строки = Объект.РаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
			Если Строки.Количество()>0 Тогда
				ЗапасыНеРаспределены = Истина;
			КонецЕсли;
			Для каждого СтрокаРаспределения Из Строки Цикл
				СтрокаРаспределения.КлючСвязиПродукция = 0;
			КонецЦикла; 
		КонецЕсли; 
	КонецЦикла;
	
	Если Объект.Запасы.Количество()>0 И ЕстьСпецификации Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПослеУдаления(Элемент)
	
	ОбновитьСпискиВыбораПродукции();
	ЗаполнитьСлужебныеДанныеОперации(Объект);
	
	ОбновитьОтметкуСтруктурнойЕдиницыРезерва(); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗапасы

&НаКлиенте
Процедура ЗапасыНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	
	СтатусПартии = Новый СписокЗначений;
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
	
	СтруктураДанные.Вставить("СтатусПартии", СтатусПартии);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	СтрокаТабличнойЧасти.Количество = 1;
	СтрокаТабличнойЧасти.ДоляСтоимости = 1;
	СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
	
	СтрокаТабличнойЧасти.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
	СтрокаТабличнойЧасти.ЗаполнениеХарактеристикиПроверено = Истина;
	
	Если СтруктураДанные.ИспользоватьХарактеристики
		Тогда
		СтрокаТабличнойЧасти.Характеристика = СтруктураДанные.Характеристика;
	КонецЕсли;
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Сборка") Тогда
		Если НЕ СкладВШапке Тогда
			ЗаполнениеОбъектовУНФКлиент.ЗаполнитьСкладВСтрокеТЧЗапасы(СтрокаТабличнойЧасти, Объект, СтруктураДанные);
		Иначе
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаРезерв;
		КонецЕсли;
	КонецЕсли;
	
	//Партии
	СтрокаТабличнойЧасти.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
	
	Если СтруктураДанные.ИспользоватьПартии
		Тогда
		СтрокаТабличнойЧасти.Партия = СтруктураДанные.Партия;
	КонецЕсли;
	// Конец Партии
	
КонецПроцедуры // ЗапасыНоменклатураПриИзменении()

&НаКлиенте
Процедура ЗапасыХарактеристикаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	
	СтруктураДанные = ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
	
КонецПроцедуры // ЗапасыХарактеристикаПриИзменении()

&НаКлиенте
Процедура ЗапасыЗаказПокупателяПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) Тогда
		СтрокаТабличнойЧасти.Резерв = 0;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	
	Если НоваяСтрока И Копирование Тогда
		ТекущаяСтрока.КлючСвязи = 0;
	КонецЕсли;
		
	ИмяТабличнойЧасти = "Запасы";
	
	Если НоваяСтрока ИЛИ ТекущаяСтрока.КлючСвязи=0 Тогда
		УправлениеНебольшойФирмойКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект);
	КонецЕсли; 
	
	Если НоваяСтрока И НЕ Копирование Тогда
		СтарыеДанныеСтроки = Неопределено;
		ЗаполнитьДанныеШапки(ТекущаяСтрока);
	ИначеЕсли НЕ НоваяСтрока Тогда 
		СтроктураПолей = Новый Структура(ИменаКолонокЗапасов(Объект));
		ЗаполнитьЗначенияСвойств(СтроктураПолей, ТекущаяСтрока);
		СтарыеДанныеСтроки = Новый ФиксированнаяСтруктура(СтроктураПолей);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриИзменении(Элемент)
	
	ОбновитьОтметкуСтруктурнойЕдиницыРезерва(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПослеУдаления(Элемент)
	
	Если НЕ Объект.РучноеРаспределение Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект);
	КонецЕсли;
	Если ТипЗнч(СтарыеДанныеСтроки)=Тип("ФиксированныйМассив") Тогда
		Значения = Новый Массив(СтарыеДанныеСтроки);
		Для каждого Значение Из Значения Цикл
			СтарыеДанныеСтроки = Значение;
			ОбновитьКэшКонтроляПриИзмененииДанныхСтроки();
		КонецЦикла; 
	Иначе
		ОбновитьКэшКонтроляПриИзмененииДанныхСтроки();
	КонецЕсли; 
	
	ОбновитьОтметкуСтруктурнойЕдиницыРезерва(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьКэшКонтроляПриИзмененииДанныхСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры
 
&НаКлиенте
Процедура ЗапасыПередУдалением(Элемент, Отказ)
	
	Если Отказ ИЛИ НЕ Объект.РучноеРаспределение Тогда
		СтарыеДанныеСтроки = Неопределено;
	ИначеЕсли Элементы.Запасы.ВыделенныеСтроки.Количество()>1 Тогда
		СохраняемыеДанные = Новый Массив;
		Для каждого ВыделеннаяСтрока Из Элементы.Запасы.ВыделенныеСтроки Цикл
			ТекущиеДанныеСтроки = Элементы.Запасы.ДанныеСтроки(ВыделеннаяСтрока);
			СтруктураПолей = Новый Структура(ИменаКолонокЗапасов(Объект));
			ЗаполнитьЗначенияСвойств(СтруктураПолей, ТекущиеДанныеСтроки);
			СохраняемыеДанные.Добавить(Новый ФиксированнаяСтруктура(СтруктураПолей));
		КонецЦикла;
		СтарыеДанныеСтроки = Новый ФиксированныйМассив(СохраняемыеДанные);
	ИначеЕсли Элементы.Запасы.ВыделенныеСтроки.Количество()=1 Тогда
		СтруктураПолей = Новый Структура(ИменаКолонокЗапасов(Объект));
		ЗаполнитьЗначенияСвойств(СтруктураПолей, Элемент.ТекущиеДанные);
		СтарыеДанныеСтроки = Новый ФиксированнаяСтруктура(СтруктураПолей);
	Иначе
		СтарыеДанныеСтроки = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПредупреждениеЗапасыЗакрытьНажатие(Элемент)
	
	УстановитьКартинкиЗакладок(ЭтотОбъект, Истина);		
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыЭтапНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыЭтапАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивСпецификаций = Новый Массив;
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		МассивСпецификаций.Добавить(СтрокаТабличнойЧасти.Спецификация);
	КонецЦикла; 
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(ЭтапыПроизводства(МассивСпецификаций));
	ОбновитьПредставлениеПустогоЭтапа(ДанныеВыбора); 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКоличествоПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда Возврат КонецЕсли;
	НоменклатураВДокументахКлиент.КонтрольЗначенияРезерваВТекущейСтроке(СтрокаТабличнойЧасти);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаспределениеЗапасов

&НаКлиенте
Процедура РаспределениеЗапасовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущаяСтрока = Элемент.ТекущиеДанные;
		ТекущаяСтрока.НомерСтроки = РаспределениеЗапасов.Количество();
		СтарыеДанныеСтроки = Неопределено;
		Если ЗначениеЗаполнено(ТекущаяПродукция) Тогда
			ТекущаяСтрока.КлючСвязиПродукция = ТекущаяПродукция;
			СтрокаПродукции = СтрокаПоКлючу(Объект.Продукция, ТекущаяПродукция);
			ЗаполнитьДанныеПродукцииВСтрокеРаспределения(ТекущаяСтрока, СтрокаПродукции);
		КонецЕсли; 
		ЗаполнитьДанныеШапки(Элемент.ТекущиеДанные);
	Иначе
		СтроктураПолей = Новый Структура(ИменаКолонокЗапасов(Объект));
		ЗаполнитьЗначенияСвойств(СтроктураПолей, Элемент.ТекущиеДанные);
		СтарыеДанныеСтроки = Новый ФиксированнаяСтруктура(СтроктураПолей);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьДанныеТЧРаспределениеЗапасов(ЭтотОбъект);
	ОбновитьКэшКонтроляПриИзмененииДанныхСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	ДанныеПеретаскивания = Новый Структура;
	ДанныеПеретаскивания.Вставить("Событие", "ПерераспределениеМатериалов");
	ДанныеПеретаскивания.Вставить("Строка", Элемент.ТекущаяСтрока);
	
	ПараметрыПеретаскивания.Значение = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПеретаскивания);
	
КонецПроцедуры                                                                                                                           

&НаКлиенте
Процедура РаспределениеЗапасовПередУдалением(Элемент, Отказ)
	
	Если Отказ Тогда
		СтарыеДанныеСтроки = Неопределено;
	Иначе
		СтроктураПолей = Новый Структура(ИменаКолонокЗапасов(Объект));
		ЗаполнитьЗначенияСвойств(СтроктураПолей, Элемент.ТекущиеДанные);
		СтарыеДанныеСтроки = Новый ФиксированнаяСтруктура(СтроктураПолей);
	КонецЕсли; 	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПродукцииПриАктивизацииСтроки(Элемент)
	
	ЭлементСписка = Элементы.СписокПродукции.ТекущиеДанные;
	Если ЭлементСписка=Неопределено ИЛИ ЭлементСписка.Значение=ТекущаяПродукция Тогда
		Возврат;
	КонецЕсли; 
	
	ОбновитьРаспределениеЗапасовНаФорме();
	
	// МобильныйКлиент
	Если ЭтоМобильныйКлиент Тогда
		Если ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
			Элементы.ПраваяПанель.Заголовок = НСтр("ru = 'Отборы (установлены)'");
		Иначе
			Элементы.ПраваяПанель.Заголовок = НСтр("ru = 'Отборы'");
		КонецЕсли;
	КонецЕсли; 
	// Конец МобильныйКлиент	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПродукцииПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли; 
	
	Если Строка=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение)<>Тип("Массив") ИЛИ ПараметрыПеретаскивания.Значение.Количество()<>1 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСобытия = ПараметрыПеретаскивания.Значение[0];
	
	Если ТипЗнч(ОписаниеСобытия)<>Тип("Структура") ИЛИ НЕ ОписаниеСобытия.Свойство("Событие") ИЛИ ОписаниеСобытия.Событие<>"ПерераспределениеМатериалов" Тогда
		Возврат;
	КонецЕсли; 
	
	ЭлементСписка = СписокПродукции.НайтиПоИдентификатору(Строка);
	СтрокаРаспределения = РаспределениеЗапасов.НайтиПоИдентификатору(ОписаниеСобытия.Строка);
	Если ЭлементСписка=Неопределено ИЛИ ЭлементСписка.Значение=0 ИЛИ СтрокаРаспределения.КлючСвязиПродукция=ЭлементСписка.Значение Тогда
		Возврат;
	КонецЕсли; 
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПродукцииПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли; 
	
	ЭлементСписка = СписокПродукции.НайтиПоИдентификатору(Строка);
	Если ЭлементСписка=Неопределено ИЛИ ЭлементСписка.Значение=0 Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение)<>Тип("Массив") ИЛИ ПараметрыПеретаскивания.Значение.Количество()<>1 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСобытия = ПараметрыПеретаскивания.Значение[0];
	
	СтрокаРаспределения = РаспределениеЗапасов.НайтиПоИдентификатору(ОписаниеСобытия.Строка);
	Если СтрокаРаспределения=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПродукции = СтрокаПоКлючу(Объект.Продукция, ЭлементСписка.Значение);
	Если СтрокаПродукции=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СтрокаПродукции", СтрокаПродукции);
	СтруктураПараметров.Вставить("СтрокаРаспределения", СтрокаРаспределения);
	Оповещение = Новый ОписаниеОповещения("СписокПродукцииПеретаскиваниеЗавершение", ЭтотОбъект, СтруктураПараметров);
	ПоказатьВводЧисла(Оповещение, СтрокаРаспределения.Количество, НСтр("ru = 'Количество'"), 15, 3);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПродукцииПеретаскиваниеЗавершение(Количество, СтруктураПараметров) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Количество) ИЛИ ТипЗнч(Количество)<>Тип("Число") Тогда
		Возврат;
	КонецЕсли; 	
	
	СтрокаПродукции = СтруктураПараметров.СтрокаПродукции;
	СтрокаРаспределения = СтруктураПараметров.СтрокаРаспределения;
	
	Если СтрокаПродукции.ЗаказПокупателя<>СтрокаРаспределения.ЗаказПокупателя Тогда
		// Предварительно требуется замена идентификатора запасов
		СтрокаРаспределения.ЗаказПокупателя = СтрокаПродукции.ЗаказПокупателя;
	КонецЕсли; 
	
	Если Количество>СтрокаРаспределения.Количество Тогда
		Количество = СтрокаРаспределения.Количество;
	КонецЕсли;
	
	Если ТекущаяПродукция=0 Тогда
		// Отображается полный список распределенных запасов
		НоваяСтрока = НайтиСуществующуюСтрокуРаспределения(РаспределениеЗапасов, СтрокаПродукции.КлючСвязи, СтрокаРаспределения);
		Если Количество=СтрокаРаспределения.Количество Тогда
			// Переносятся все материалы из строки распределения
			Если НоваяСтрока=Неопределено Тогда
				ЗаполнитьДанныеПродукцииВСтрокеРаспределения(СтрокаРаспределения, СтрокаПродукции);
			Иначе
				НоваяСтрока.Количество = НоваяСтрока.Количество+Количество;
				РаспределениеЗапасов.Удалить(СтрокаРаспределения);
			КонецЕсли; 
		Иначе
			// Переносится часть материалов
			Если НоваяСтрока=Неопределено Тогда
				НоваяСтрока = РаспределениеЗапасов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
				НоваяСтрока.Количество = Количество;
				ЗаполнитьДанныеПродукцииВСтрокеРаспределения(НоваяСтрока, СтрокаПродукции);
			Иначе
				НоваяСтрока.Количество = НоваяСтрока.Количество+Количество;
			КонецЕсли; 
			СтрокаРаспределения.Количество = СтрокаРаспределения.Количество-Количество;
		КонецЕсли;
		ОбновитьДанныеТЧРаспределениеЗапасов(ЭтотОбъект);
	Иначе
		// Отображается список распределенных запасов с отбором по изделию
		СтараяСтрока = НайтиСуществующуюСтрокуРаспределения(Объект.РаспределениеЗапасов, СтрокаРаспределения.КлючСвязиПродукция, СтрокаРаспределения);
		НоваяСтрока = НайтиСуществующуюСтрокуРаспределения(Объект.РаспределениеЗапасов, СтрокаПродукции.КлючСвязи, СтараяСтрока);
		Если Количество=СтрокаРаспределения.Количество Тогда
			// Переносятся все материалы из строки распределения
			Если НоваяСтрока=Неопределено Тогда
				СтараяСтрока.КлючСвязиПродукция = СтрокаПродукции.КлючСвязи;
			Иначе
				НоваяСтрока.Количество = НоваяСтрока.Количество+Количество;
				Объект.РаспределениеЗапасов.Удалить(СтараяСтрока);
			КонецЕсли; 
			РаспределениеЗапасов.Удалить(СтрокаРаспределения);
		Иначе
			// Переносится часть материалов
			Если НоваяСтрока=Неопределено Тогда
				НоваяСтрока = Объект.РаспределениеЗапасов.Добавить();
				ЗаполнитьДанныеЗапасовВСтрокеРаспределения(Объект, НоваяСтрока, СтрокаРаспределения);
				НоваяСтрока.КлючСвязиПродукция = СтрокаПродукции.КлючСвязи;
				НоваяСтрока.Количество = Количество;
			Иначе
				НоваяСтрока.Количество = НоваяСтрока.Количество+Количество;
			КонецЕсли;
			СтараяСтрока.Количество = СтараяСтрока.Количество-Количество;
			СтрокаРаспределения.Количество = СтрокаРаспределения.Количество-Количество;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовНоменклатураПродукцияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение)<>Тип("Число") Тогда
		Возврат;
	КонецЕсли; 	
	
	СтрокаПродукция = СтрокаПоКлючу(Объект.Продукция, ВыбранноеЗначение);
	Если СтрокаПродукция=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ТекущаяСтрока = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	
	ЗаполнитьДанныеПродукцииВСтрокеРаспределения(ТекущаяСтрока, СтрокаПродукция);
	
	Если Элементы.Найти("РаспределениеЗапасовНоменклатура")<>Неопределено Тогда
		Элементы.РаспределениеЗапасов.ТекущийЭлемент = Элементы.РаспределениеЗапасовНоменклатура;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовНоменклатураПриИзменении(Элемент)
	
	ТекСтр = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", ТекСтр.Номенклатура);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	ТекСтр.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	ТекСтр.Количество = 1;
	ТекСтр.Спецификация = СтруктураДанные.Спецификация;
	
	//Характеристики
	ТекСтр.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
	ТекСтр.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
	ТекСтр.ЗаполнениеХарактеристикиПроверено = Истина;
	
	Если СтруктураДанные.ИспользоватьХарактеристики
		Тогда
		ТекСтр.Характеристика = СтруктураДанные.Характеристика;
	КонецЕсли;
	//Конец Характеристики
	
	//Партии
	ТекСтр.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
	ТекСтр.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
	
	Если СтруктураДанные.ИспользоватьПартии
		Тогда
		ТекСтр.Партия = СтруктураДанные.Партия;
	КонецЕсли;
	// Конец Партии
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Сборка") Тогда
		Если НЕ СкладВШапке Тогда
			ЗаполнениеОбъектовУНФКлиент.ЗаполнитьСкладВСтрокеТЧЗапасы(ТекСтр, Объект, СтруктураДанные);
		Иначе
			ТекСтр.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаРезерв;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовХарактеристикаПриИзменении(Элемент)
	
	ТекСтр = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", ТекСтр.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", ТекСтр.Характеристика);
	
	СтруктураДанные = ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные);
	
	ТекСтр.Спецификация = СтруктураДанные.Спецификация;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьПанель(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость);

КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовПриАктивизацииСтроки(Элемент)
	
	ОбновитьТекстПодсказкиРаспределения();	
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовПослеУдаления(Элемент)
	
	ОбновитьДанныеТЧРаспределениеЗапасов(ЭтотОбъект);
	ОбновитьКэшКонтроляПриИзмененииДанныхСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура РучноеРаспределениеРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РазделПоУмолчанию", "Производство");
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриИзмененииСпособаРаспределенияПоУмолчанию", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.НастройкаПрограммы.Форма",
			ПараметрыОткрытияФормы,
			ЭтотОбъект,
			Ложь,,,
			ОписаниеОповещения
	);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовНоменклатураПродукцияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовЭтапНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовЭтапАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаТабличнойЧасти = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СпецификацияПродукция) Тогда
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.ЗагрузитьЗначения(ЭтапыПроизводства(СтрокаТабличнойЧасти.СпецификацияПродукция));
		ОбновитьПредставлениеПустогоЭтапа(ДанныеВыбора); 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРесурсыПредприятия

&НаКлиенте
Процедура РесурсыПредприятияРесурсПредприятияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РесурсыПредприятия.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Ресурс", ТекущиеДанные.РесурсПредприятия);
	
	ТекущиеДанные.Мощность = 1;
	
	ПланированиеРесурсовУНФКлиент.ОчиститьДанныеСтроки(ТекущиеДанные);
	ЗаполнитьДанныеТаблицыРесурсовНаФорме();
	
КонецПроцедуры

&НаКлиенте
Процедура РесурсыПредприятияСтартПриИзменении(Элемент)
	ПриИзмененииПериода(Истина)
КонецПроцедуры

&НаКлиенте
Процедура РесурсыПредприятияСтартВремяПриИзменении(Элемент)
	ПриИзмененииПериода(Истина)
КонецПроцедуры

&НаКлиенте
Процедура РесурсыПредприятияДниПриИзменении(Элемент)
	ЗадатьОкончаниеПериода();
	УстановитьДоступностьПовторов();
КонецПроцедуры

&НаКлиенте
Процедура РесурсыПредприятияВремяПриИзменении(Элемент)
	ЗадатьОкончаниеПериода();
	УстановитьДоступностьПовторов();
КонецПроцедуры

&НаКлиенте
Процедура РесурсыПредприятияФинишПриИзменении(Элемент)
	ПриИзмененииПериода()
КонецПроцедуры

&НаКлиенте
Процедура РесурсыПредприятияФинишВремяПриИзменении(Элемент)
	ПриИзмененииПериода()
КонецПроцедуры

&НаКлиенте
Процедура РесурсыПредприятияРасписаниеПредставлениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.РесурсыПредприятия.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	ДатаСтроки = День(ТекущиеДанные.Старт);
	
	ПараметрыОповещения = Новый Структура;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОкончанияРедактированияРасписания", ЭтотОбъект, ПараметрыОповещения);
	
	СтруктураПовторов = Новый Структура("ИнтервалПовторения, Пн, Вт, Ср, Чт, Пт, Сб, Вс, ПоследнийДеньМесяца, ДатаПовторения, ДеньНеделиМесяца, ДатаСтроки, ТекДеньНедели, НомерНеделиМесяца, ПериодСтроки, НомерМесяца"
										,ТекущиеДанные.ИнтервалПовторения, ТекущиеДанные.Пн, ТекущиеДанные.Вт, ТекущиеДанные.Ср
										,ТекущиеДанные.Чт,ТекущиеДанные.Пт, ТекущиеДанные.Сб, ТекущиеДанные.Вс, ТекущиеДанные.ПоследнийДеньМесяца
										,ТекущиеДанные.ДатаПовторения, ТекущиеДанные.ДеньНеделиМесяца, ДатаСтроки, ДеньНедели(ТекущиеДанные.Старт), ТекущиеДанные.НомерНеделиМесяца, ТекущиеДанные.Старт, ТекущиеДанные.НомерМесяца);
	
	ПараметрыОткрытия = Новый Структура("Повторяемость, СтруктураПовторов", ТекущиеДанные.ВидПовтора, СтруктураПовторов);
	
	ОткрытьФорму("Обработка.ПланировщикРесурсов.Форма.ФормаРедактированияРасписания",ПараметрыОткрытия, ЭтаФорма,,,,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Контроль(Команда)
	КонтрольНаСервере();
КонецПроцедуры

&НаСервере
Процедура КонтрольНаСервере()
	ПланированиеРесурсовУНФ.КонтрольПараметровЗагрузкиРесурсов(Истина,, Объект.РесурсыПредприятия, ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура КонтрольВсеНаСервере()
	ПланированиеРесурсовУНФ.КонтрольПараметровЗагрузкиРесурсов(Истина, Истина, Объект.РесурсыПредприятия, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КонтрольВсе(Команда)
	КонтрольВсеНаСервере();
КонецПроцедуры

&НаСервере
Процедура КонтрольГраницНаСервере()
	ПланированиеРесурсовУНФ.КонтрольПараметровЗагрузкиРесурсов(,Истина, Объект.РесурсыПредприятия, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КонтрольГраниц(Команда)
	КонтрольГраницНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПодборРесурсы(Команда)
	
	ПараметрыОткрытия = Новый Структура("ЭтоПодбор, РесурсыПредприятия, ГраницыПланирования, НомерПодсистемы", Истина, Объект.РесурсыПредприятия,,2);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодбораИзПланировщикаЗавершение", ЭтотОбъект, ПараметрыОткрытия);
	
	ОткрытьФорму("Обработка.ПланировщикРесурсов.Форма.ФормаПланировщика", ПараметрыОткрытия,,,,,Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодбораИзПланировщикаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = Неопределено Тогда
		
		Объект.РесурсыПредприятия.Очистить();
		
		ВыбранныеРесурсы = Результат;
		
		Для Каждого СтрокаРесурсов из ВыбранныеРесурсы Цикл
			
			НоваяСтрока = Объект.РесурсыПредприятия.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРесурсов);
			
			НоваяСтрока.РесурсПредприятия = СтрокаРесурсов.Ресурс;
			НоваяСтрока.Старт = СтрокаРесурсов.НачалоПериода;
			НоваяСтрока.Финиш = СтрокаРесурсов.ОкончаниеПериода;
			НоваяСтрока.Мощность = СтрокаРесурсов.Загрузка;
			НоваяСтрока.Длительность = Дата(1,1,1)+(НоваяСтрока.Финиш - НоваяСтрока.Старт); 
			
		КонецЦикла;
		
		ЗаполнитьДанныеТаблицыРесурсовНаФорме();
		
		УстановитьДоступностьПовторов(, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	Ответ = Неопределено;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоОснованиюЗавершение", ЭтотОбъект), НСтр("ru = 'Документ будет полностью перезаполнен по ""Основанию""! Продолжить выполнение операции?'"), РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованиюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьПоДокументу();
		
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка") Тогда
			
			Если НЕ ЗначениеЗаполнено(Объект.ЗаказПокупателя) 
				И Объект.ПоложениеЗаказаПокупателя<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
				
				Для Каждого СтрокаЗапасы Из Объект.Продукция Цикл
					СтрокаЗапасы.Резерв = 0;
				КонецЦикла;
				Элементы.ПродукцияРезерв.Видимость = Ложь;
				
			Иначе
				
				Если Элементы.ПродукцияРезерв.Видимость = Ложь Тогда
					Элементы.ПродукцияРезерв.Видимость = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если НЕ ЗначениеЗаполнено(Объект.ЗаказПокупателя) 
				И Объект.ПоложениеЗаказаПокупателя<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
				
				Для Каждого СтрокаЗапасы Из Объект.Запасы Цикл
					СтрокаЗапасы.Резерв = 0;
				КонецЦикла;
				
				Элементы.ЗапасыРезерв.Видимость = Ложь;
				Элементы.ЗапасыИзменитьРезерв.Видимость = Ложь;
				
			Иначе
				
				Если НЕ Элементы.ЗапасыРезерв.Видимость Тогда
					Элементы.ЗапасыРезерв.Видимость = Истина;
					Элементы.ЗапасыИзменитьРезерв.Видимость = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ОбновитьСпискиВыбораПродукции();
		ЗапасыНеЗаполнены = Ложь;
		ВывестиОтметкиКонтроля(ЭтотОбъект);
		
		ОбновитьОтметкуСтруктурнойЕдиницыРезерва();
		
		Если Объект.РесурсыПредприятия.Количество() Тогда
			УстановитьДоступностьПовторов();
			ЗаполнитьДанныеТаблицыРесурсовНаФорме();
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры  // ЗаполнитьВыполнить()

&НаКлиенте
Процедура ЗаполнитьПоЗаказуПокупателя(Команда)
	
	Ответ = Неопределено;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоЗаказуПокупателяЗавершение", ЭтотОбъект), НСтр("ru = 'Документ будет полностью перезаполнен по ""Заказу покупателя""! Продолжить выполнение операции?'"), РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоЗаказуПокупателяЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьПоДокументу("ЗаказПокупателя");
		
		ОбновитьСпискиВыбораПродукции();
		ЗапасыНеЗаполнены = Ложь;
		ВывестиОтметкиКонтроля(ЭтотОбъект);
		
		ОбновитьОтметкуСтруктурнойЕдиницыРезерва();
		
		Если Объект.РесурсыПредприятия.Количество() Тогда
			УстановитьДоступностьПовторов();
			ЗаполнитьДанныеТаблицыРесурсовНаФорме();
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ЗаполнитьПоЗаказуПокупателя()

&НаКлиенте
Процедура ИзменитьРезервЗаполнитьПоОстаткам(Команда)
	
	Если Объект.Запасы.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Табличная часть ""Запасы"" не заполнена!'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьКолонкуРезервПоОстаткамНаСервере();
	
	ОбновитьОтметкуСтруктурнойЕдиницыРезерва(); 
	
КонецПроцедуры // ИзменитьРезервЗаполнитьПоОстаткам()

&НаКлиенте
Процедура ИзменитьРезервОчиститьРезерв(Команда)
	
	Если Объект.Запасы.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Табличная часть ""Запасы"" не заполнена!'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
		СтрокаТабличнойЧасти.Резерв = 0;
	КонецЦикла;
	
	ОбновитьОтметкуСтруктурнойЕдиницыРезерва(); 
	
КонецПроцедуры // ИзменитьРезервЗаполнитьПоОстаткам()

&НаКлиенте
Процедура ШапкаТабличнаяЧасть(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ПоложениеСклада) Тогда
		Объект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке");
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(Объект.ПоложениеЗаказаПокупателя) Тогда
		Объект.ПоложениеЗаказаПокупателя = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ПоложениеИсполнителя) Тогда
		Объект.ПоложениеИсполнителя = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ПоложениеСтруктурнойЕдиницыОпераций) Тогда
		Объект.ПоложениеСтруктурнойЕдиницыОпераций = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке");
	КонецЕсли;
	
	ПараметрыДиалога = Новый Структура;
	ПараметрыДиалога.Вставить("ПоложениеСкладаВДокументахПроизводства", Объект.ПоложениеСклада);
	Если ПараметрыФормы.РезервированиеЗапасов Тогда
		ПараметрыДиалога.Вставить("ПоложениеЗаказаПокупателяВДокументахПроизводства", Объект.ПоложениеЗаказаПокупателя);
	КонецЕсли; 
	ПараметрыДиалога.Вставить("ПоложениеИсполнителяВСдельномНаряде", Объект.ПоложениеИсполнителя);
	Если ПараметрыФормы.УчетПоНесколькимПодразделениям Тогда
		ПараметрыДиалога.Вставить("ПоложениеСтруктурнойЕдиницыВСдельномНаряде", Объект.ПоложениеСтруктурнойЕдиницыОпераций);
	КонецЕсли; 
	ПараметрыДиалога.Вставить("БылиВнесеныИзменения", Ложь);
	
	ОткрытьФорму(
	"ОбщаяФорма.ШапкаТабличнаяЧасть",
	ПараметрыДиалога,,,,,
	Новый ОписаниеОповещения("ШапкаТабличнаяЧастьЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ШапкаТабличнаяЧастьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.БылиВнесеныИзменения Тогда
		ШапкаТабличнаяЧастьЗавершениеНаСервере(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ШапкаТабличнаяЧастьЗавершениеНаСервере(Знач Результат)
	
	Если Результат.Свойство("ПоложениеСкладаВДокументахПроизводства") 
		И Объект.ПоложениеСклада <> Результат.ПоложениеСкладаВДокументахПроизводства Тогда
		Объект.ПоложениеСклада = Результат.ПоложениеСкладаВДокументахПроизводства;
		Если Объект.ВидОперации=Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
			ИмяТЧ = "Продукция";
		Иначе
			ИмяТЧ = "Запасы";
		КонецЕсли; 
		Для каждого СтрокаТабличнойЧасти Из Объект[ИмяТЧ] Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаРезерв;
		КонецЦикла; 
	КонецЕсли; 
	
	Если Результат.Свойство("ПоложениеЗаказаПокупателяВДокументахПроизводства") 
		И Объект.ПоложениеЗаказаПокупателя <> Результат.ПоложениеЗаказаПокупателяВДокументахПроизводства Тогда
		Объект.ПоложениеЗаказаПокупателя = Результат.ПоложениеЗаказаПокупателяВДокументахПроизводства;
		Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
		КонецЦикла; 
		Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
		КонецЦикла; 
	КонецЕсли; 
	
	Если Результат.Свойство("ПоложениеИсполнителяВСдельномНаряде") 
		И Объект.ПоложениеИсполнителя<>Результат.ПоложениеИсполнителяВСдельномНаряде Тогда
		Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
			СтрокаТабличнойЧасти.Исполнитель = Объект.Исполнитель;
		КонецЦикла;
		Если Результат.ПоложениеИсполнителяВСдельномНаряде=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			Если ТипЗнч(Объект.Исполнитель)=Тип("СправочникСсылка.Бригады") Тогда
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("КлючСвязи", 0);
				СтрокиСостава = Объект.СоставБригады.НайтиСтроки(СтруктураОтбора);
				Для каждого СтрокаОперации Из Объект.Операции Цикл
					Если СтрокаОперации.КлючСвязи=0 Тогда
						ИмяТабличнойЧасти = "Операции";
						СтрокаОперации.КлючСвязи = УправлениеНебольшойФирмойСервер.СоздатьНовыйКлючСвязи(ЭтотОбъект);
					КонецЕсли; 
					Для каждого СтрокаСостава Из СтрокиСостава Цикл
						НоваяСтрока = Объект.СоставБригады.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСостава);
						НоваяСтрока.КлючСвязи = СтрокаОперации.КлючСвязи;
					КонецЦикла; 
				КонецЦикла; 
				Для каждого СтрокаСостава Из СтрокиСостава Цикл
					Объект.СоставБригады.Удалить(СтрокаСостава);
				КонецЦикла; 
			КонецЕсли; 
			Объект.Исполнитель = Неопределено;
		Иначе
			Объект.СоставБригады.Очистить();
		КонецЕсли; 
		Объект.ПоложениеИсполнителя = Результат.ПоложениеИсполнителяВСдельномНаряде;
	КонецЕсли; 
	
	Если Результат.Свойство("ПоложениеСтруктурнойЕдиницыВСдельномНаряде") 
		И Объект.ПоложениеСтруктурнойЕдиницыОпераций<>Результат.ПоложениеСтруктурнойЕдиницыВСдельномНаряде Тогда
		Если Результат.ПоложениеСтруктурнойЕдиницыВСдельномНаряде=Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
			Объект.СтруктурнаяЕдиницаОпераций = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОсновноеПодразделение");
			Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаОпераций;
			КонецЦикла; 
			Для каждого СтрокаТабличнойЧасти Из Объект.СоставБригады Цикл
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаОпераций;
			КонецЦикла; 
		Иначе
			Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаОпераций;
			КонецЦикла; 
			Для каждого СтрокаТабличнойЧасти Из Объект.СоставБригады Цикл
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаОпераций;
			КонецЦикла; 
			Объект.СтруктурнаяЕдиницаОпераций = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
		КонецЕсли; 
		Объект.ПоложениеСтруктурнойЕдиницыОпераций = Результат.ПоложениеСтруктурнойЕдиницыВСдельномНаряде;
	КонецЕсли; 
	
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	
	ЗаполнитьСлужебныеДанныеПослеЧтенияОбъекта(Объект);
	Если Объект.РучноеРаспределение Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
		ВывестиОтметкиКонтроля(ЭтотОбъект);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Распределить(Команда)
	
	Если Объект.РаспределениеЗапасов.Количество() <> 0 Тогда
		
		Ответ = Неопределено;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("РаспределитьЗавершение", ЭтотОбъект), НСтр("ru = 'Табличная часть ""Распределение"" будет перезаполнена! Продолжить выполнение операции?'"), 
							РежимДиалогаВопрос.ДаНет, 0);
        Возврат;
		
	КонецЕсли;
	
	РаспределитьФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    
    РаспределитьФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура РаспределитьФрагмент()
	
	РаспределитьСервер();
	ОбновитьРаспределениеЗапасовНаФорме();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСпецификации(Команда)
	
	Если Объект.Запасы.Количество() <> 0 Тогда
		
		Ответ = Неопределено;

		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоСпецификацииЗавершение", ЭтотОбъект), НСтр("ru = 'Табличная часть ""Материалы"" будет перезаполнена! Продолжить выполнение операции?'"), 
							РежимДиалогаВопрос.ДаНет, 0);
        Возврат;
		
	КонецЕсли;
	
	ЗаполнитьПоСпецификацииФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСпецификацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    ЗаполнитьПоСпецификацииФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСпецификацииФрагмент()
    
    ЗаполнитьПоСпецификацииНаСервере();
	
	ОбновитьОтметкуСтруктурнойЕдиницыРезерва(); 
		
КонецПроцедуры // ЗаполнитьПоСпецификации()

&НаКлиенте
Процедура ЗаполнитьПоРаспределению(Команда)
	
	Если Объект.Запасы.Количество() <> 0 Тогда
		
		Ответ = Неопределено;

		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоРаспределениюЗавершение", ЭтотОбъект), НСтр("ru = 'Табличная часть ""Материалы"" будет перезаполнена! Продолжить выполнение операции?'"), 
							РежимДиалогаВопрос.ДаНет, 0);
        Возврат;
		
	КонецЕсли;
	
	ЗаполнитьПоРаспределениюФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРаспределениюЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    ЗаполнитьПоРаспределениюФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРаспределениюФрагмент()
    
    ЗаполнитьПоРаспределениюСервер();
	
	ОбновитьОтметкуСтруктурнойЕдиницыРезерва(); 
		
КонецПроцедуры // ЗаполнитьПоРаспределению()

&НаКлиенте
Процедура ДобавитьИзЗаказов(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьИзЗаказовЗавершение", ЭтотОбъект);
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("РежимВыбора", Истина);
	СтруктураОткрытия.Вставить("МножественныйВыбор", Истина);
	СтруктураОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	ОткрытьФорму("Документ.ЗаказПокупателя.ФормаВыбора", СтруктураОткрытия, ЭтотОбъект, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзЗаказовЗавершение(Заказы, ДополнительныеДанные) Экспорт
	
	Если Заказы=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТабличнойЧасти = "Продукция";
	
	КоличествоСтрок = Объект.Продукция.Количество();
	
	ДобавитьПродукциюИзЗаказов(Заказы);
	
	ЕстьСпецификации = Ложь;
	Для ии = КоличествоСтрок + 1 По Объект.Продукция.Количество() Цикл
		СтрокаТабличнойЧасти = Объект.Продукция[ии - 1];
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
			ЕстьСпецификации = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;  
	
	Если ЕстьСпецификации Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	ОбновитьСпискиВыбораПродукции();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОперации(Команда)
	
	Если Объект.Операции.Количество() <> 0 Тогда
		
		Ответ = Неопределено;

		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьОперацииЗавершение", ЭтотОбъект), НСтр("ru = 'Табличная часть ""Операции"" будет перезаполнена! Продолжить выполнение операции?'"), 
							РежимДиалогаВопрос.ДаНет, 0);
        Возврат;
		
	КонецЕсли;
	
	ЗаполнитьОперацииФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОперацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    ЗаполнитьОперацииФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОперацииФрагмент()
    
    ЗаполнитьОперацииНаСервере();
	
КонецПроцедуры // ЗаполнитьПоСпецификации()

&НаКлиенте
// Процедура - обработчик команды ЗаполнитьСоставБригады.
//
Процедура ЗаполнитьСоставБригады(Команда)
	
	ЗаполнитьСоставБригадыНаСервере(Объект.Исполнитель);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоответствиеДанныхРесурсов()
	
	СвернутаяТаблицаРесурсов = Объект.РесурсыПредприятия.Выгрузить(,"РесурсПредприятия");
	СвернутаяТаблицаРесурсов.Свернуть("РесурсПредприятия");
	
	СоответствиеВозврата = Новый Соответствие();
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	
	ТаблицаДанных.Колонки.Добавить("РесурсПредприятия");
	ТаблицаДанных.Колонки.Добавить("КонтролироватьШаг");
	ТаблицаДанных.Колонки.Добавить("КратностьПланирования");
	
	Для каждого СтрокаТаблицы из СвернутаяТаблицаРесурсов Цикл
		
		РесурсПредприятия = СтрокаТаблицы.РесурсПредприятия;
		
		СтруктураДанных = Новый Структура("КонтролироватьШаг,КратностьПланирования"
											,РесурсПредприятия.КонтролироватьШагИнтервалаВДокументах, РесурсПредприятия.КратностьПланирования);
		
		СоответствиеВозврата.Вставить(РесурсПредприятия, СтруктураДанных);
		
	КонецЦикла;
	
	Возврат СоответствиеВозврата;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДоступностьПовторов(ОткрытиеФормы = Ложь, БылПодбор = Ложь)
	
	Если ОткрытиеФормы или БылПодбор Тогда
		
		Для Каждого СтрокаРесурсыПредприятия из Объект.РесурсыПредприятия Цикл
			
			СтрокаРесурсыПредприятия.РасписаниеПредставление = ?(СтрокаРесурсыПредприятия.РасписаниеПредставление = "", "Не повторять", СтрокаРесурсыПредприятия.РасписаниеПредставление);
			
			Если ЗначениеЗаполнено(СтрокаРесурсыПредприятия.Старт) и ЗначениеЗаполнено(СтрокаРесурсыПредприятия.Финиш) Тогда
				
				СтрокаРесурсыПредприятия.ПериодРазличный = ?(Не НачалоДня(СтрокаРесурсыПредприятия.Старт) = НачалоДня(СтрокаРесурсыПредприятия.Финиш), Истина, Ложь);
				
				Если НачалоДня(СтрокаРесурсыПредприятия.Старт) = НачалоДня(СтрокаРесурсыПредприятия.Финиш) Тогда
					СтрокаРесурсыПредприятия.ПовторыДоступны = Истина;
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.РесурсыПредприятия.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	ТекущиеДанные.ПовторыДоступны = Ложь;
	ТекущиеДанные.ПериодРазличный = Ложь;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Старт) и ЗначениеЗаполнено(ТекущиеДанные.Финиш) Тогда
		
		ТекущиеДанные.ПериодРазличный = ?(Не НачалоДня(ТекущиеДанные.Старт) = НачалоДня(ТекущиеДанные.Финиш), Истина, Ложь);
		
		Если НачалоДня(ТекущиеДанные.Старт) = НачалоДня(ТекущиеДанные.Финиш) Тогда
			ТекущиеДанные.ПовторыДоступны = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПериода(ЭтоДатаНачала = Ложь)
	
	ТекущиеДанные = Элементы.РесурсыПредприятия.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	ОстатокСекундДоКонцаДня = КонецДня(ТекущиеДанные.Финиш) - ТекущиеДанные.Финиш;
	
	Если ОстатокСекундДоКонцаДня = 59 Тогда ТекущиеДанные.Финиш = КонецДня(ТекущиеДанные.Финиш) КонецЕсли;
	Если ТекущиеДанные.Финиш = НачалоДня(ТекущиеДанные.Финиш) Тогда ТекущиеДанные.Финиш = ТекущиеДанные.Финиш-1 КонецЕсли; 
	
	ТекущиеДанные.Старт = ?(Минута(ТекущиеДанные.Старт)%5 = 0, ТекущиеДанные.Старт, ТекущиеДанные.Старт - (Минута(ТекущиеДанные.Старт)%5*60));
	
	ОстатокОтДеления = Минута(ТекущиеДанные.Финиш)%5;
	
	Если Не (ОстатокОтДеления = 0 или ТекущиеДанные.Финиш = КонецДня(ТекущиеДанные.Финиш)) Тогда
		
		Если ОстатокОтДеления < 3 Тогда
			ТекущиеДанные.Финиш = ТекущиеДанные.Финиш - (ОстатокОтДеления*60);
		ИначеЕсли (КонецДня(ТекущиеДанные.Финиш) - ТекущиеДанные.Финиш)<300 Тогда
			ТекущиеДанные.Финиш = КонецДня(ТекущиеДанные.Финиш);
		Иначе
			ТекущиеДанные.Финиш = ТекущиеДанные.Финиш + (300 - (ОстатокОтДеления*60));
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТекущиеДанные.Старт > ТекущиеДанные.Финиш Тогда 
		Если ЭтоДатаНачала Тогда 
			ТекущиеДанные.Финиш = ТекущиеДанные.Старт+ТекущиеДанные.КратностьПланирования*60;
		Иначе
			ТекущиеДанные.Финиш = ТекущиеДанные.Старт;
		КонецЕсли;
		
	КонецЕсли;
	
	ТекущиеДанные.Старт = ?(Секунда(ТекущиеДанные.Старт) = 0, ТекущиеДанные.Старт, ТекущиеДанные.Старт - Секунда(ТекущиеДанные.Старт));
	
	Если Не (Секунда(ТекущиеДанные.Финиш) = 0 или ТекущиеДанные.Финиш = КонецДня(ТекущиеДанные.Финиш)) Тогда  
		ТекущиеДанные.Финиш = ТекущиеДанные.Финиш - Секунда(ТекущиеДанные.Финиш)
	КонецЕсли;
	
	ПланированиеРесурсовУНФКлиент.ПроконтролироватьШагПланирования(ТекущиеДанные,ЭтоДатаНачала,Истина);
	
	УстановитьДоступностьПовторов();
	
	Если ТекущиеДанные.ПовторыДоступны Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ДатаПовторения) Тогда
			ТекущиеДанные.ДатаПовторения = День(ТекущиеДанные.Старт);
		КонецЕсли;
		
		Если ТекущиеДанные.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.Ежемесячно") Тогда
			
			Если ЗначениеЗаполнено(ТекущиеДанные.ДеньНеделиМесяца) Тогда
				
				Если КонецДня(ТекущиеДанные.Старт) = КонецМесяца(ТекущиеДанные.Старт) Тогда
					
					ТекущиеДанные.ДеньНеделиМесяца = 0;
					ТекущиеДанные.НомерНеделиМесяца = 0;
					ТекущиеДанные.ПоследнийДеньМесяца = Истина
					
				Иначе
					
					ТекущиеДанные.ДеньНеделиМесяца = ДеньНедели(ТекущиеДанные.Старт);
					
					ТекНомерНедели = НеделяГода(ТекущиеДанные.Старт)-НеделяГода(НачалоМесяца(ТекущиеДанные.Старт))+1;
					
					Если ЗначениеЗаполнено(ТекущиеДанные.НомерНеделиМесяца) и Не ТекущиеДанные.НомерНеделиМесяца = ТекНомерНедели  Тогда
						ТекущиеДанные.НомерНеделиМесяца = ТекНомерНедели;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ТекущиеДанные.ПоследнийДеньМесяца и Не КонецДня(ТекущиеДанные.Старт) = КонецМесяца(ТекущиеДанные.Старт) Тогда
				
				ТекущиеДанные.ПоследнийДеньМесяца = Ложь;
				ТекущиеДанные.ДеньНеделиМесяца = ДеньНедели(ТекущиеДанные.Старт);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТекущиеДанные.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.Ежегодно")
										и ЗначениеЗаполнено(ТекущиеДанные.НомерМесяца) Тогда
			
			ТекущиеДанные.НомерМесяца = Месяц(ТекущиеДанные.Старт);
			
		КонецЕсли;
		
	Иначе
		
		ТекущиеДанные.НомерНеделиМесяца = 0;
		ТекущиеДанные.НомерМесяца = 0;
		ТекущиеДанные.ДатаПовторения = 0;
		ТекущиеДанные.ДеньНеделиМесяца = 0;
		ТекущиеДанные.ПоследнийДеньМесяца = Ложь;
		
		ТекущиеДанные.ВидЗавершения = Неопределено;
		ТекущиеДанные.ЗавершатьПосле = Неопределено;
		
		ТекущиеДанные.ИнтервалПовторения = 0;
		ТекущиеДанные.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.НеПовторять");
		ТекущиеДанные.РасшифровкаСчетчика = "";
		
		ТекущиеДанные.Пн = Ложь;
		ТекущиеДанные.Вт = Ложь;
		ТекущиеДанные.Ср = Ложь;
		ТекущиеДанные.Чт = Ложь;
		ТекущиеДанные.Пт = Ложь;
		ТекущиеДанные.Сб = Ложь;
		ТекущиеДанные.Вс = Ложь;
		
	КонецЕсли;
	
	ТекущиеДанные.Длительность = Дата(1,1,1)+(ТекущиеДанные.Финиш - ТекущиеДанные.Старт);
	
	ЗаполнитьДанныеТаблицыРесурсовНаФорме();
	
	Если ЭтоДатаНачала Тогда
		Если ТипЗнч(ТекущиеДанные.ЗавершатьПосле) = Тип("Дата")
			и ЗначениеЗаполнено(ТекущиеДанные.ЗавершатьПосле)
			и ЗначениеЗаполнено(ТекущиеДанные.Старт)
			и ТекущиеДанные.ЗавершатьПосле<НачалоДня(ТекущиеДанные.Старт)
			Тогда
			ТекущиеДанные.ЗавершатьПосле=НачалоДня(ТекущиеДанные.Старт)
		КонецЕсли;
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеТаблицыРесурсовНаФорме()
	
	ДанныеРесурсов = СоответствиеДанныхРесурсов();
	
	Элементы.РесурсыПредприятияПодборРесурсы.Доступность = Не ЭтаФорма.ТолькоПросмотр;
	Элементы.РесурсыПредприятияГруппаПроверить.Доступность = Не ЭтаФорма.ТолькоПросмотр;
	
	Для Каждого СтрокаРесуров из Объект.РесурсыПредприятия Цикл
		
		ЕстьРасшифровкаСчетчика = ?(ТипЗнч(СтрокаРесуров.ЗавершатьПосле) = Тип("Число"), Истина, Ложь);
		
		ДанныеРесурса = ДанныеРесурсов.Получить(СтрокаРесуров.РесурсПредприятия);
		
		Если Не ДанныеРесурса = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтрокаРесуров, ДанныеРесурса)
		КонецЕсли;
		
		ВыбранныеДниНедели = ПланированиеРесурсовУНФКлиент.ПредставлениеДнейНедели(СтрокаРесуров);
		
		ДополнениеПоМесяцуГоду = "";
		
		Старт = СтрокаРесуров.Старт;
		ДеньНеделиМесяца = СтрокаРесуров.ДеньНеделиМесяца;
		ДатаПовторения = СтрокаРесуров.ДатаПовторения;
		
		Если СтрокаРесуров.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.Ежемесячно")
										или СтрокаРесуров.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.Ежегодно") Тогда
			
			Если ЗначениеЗаполнено(ДатаПовторения) Тогда
				
				ДополнениеПоМесяцуГоду = ?(ЗначениеЗаполнено(ДатаПовторения), ", каждое " 
										+ Строка(ДатаПовторения) + "-ое "+ПланированиеРесурсовУНФКлиент.ПолучитьМесяцПоНомеру(СтрокаРесуров.НомерМесяца)+".","");
			ИначеЕсли ЗначениеЗаполнено(ДеньНеделиМесяца) Тогда
				
				Если ПланированиеРесурсовУНФКлиент.ЭтоПоследняяНеделяМесяца(Старт) Тогда
					ДополнениеПоМесяцуГоду = ", в послед. " + ПланированиеРесурсовУНФКлиент.СоответствиеНомераДнюНедели(ДеньНеделиМесяца)+ " месяца.";
				Иначе
				НомерНеделиМесяца = НеделяГода(Старт)-НеделяГода(НачалоМесяца(Старт))+1;
				ДополнениеПоМесяцуГоду = " "+ПланированиеРесурсовУНФКлиент.СоответствиеНомераДнюНедели(ДеньНеделиМесяца) + " кажд. " +Строка(НомерНеделиМесяца)+ "-й" + " недели";
				КонецЕсли;
				
			ИначеЕсли СтрокаРесуров.ПоследнийДеньМесяца = Истина Тогда
				ДополнениеПоМесяцуГоду = ", последний день месяца.";
			КонецЕсли;
			
		КонецЕсли;
		
		Междометие = ?(СтрокаРесуров.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.Еженедельно"),"каждую", "каждый");
		
		Окончание = "";
		
		Если СтрокаРесуров.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.Еженедельно") Тогда
			Окончание = "неделю"
		ИначеЕсли СтрокаРесуров.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.Ежедневно") Тогда
			Окончание = "день"
		ИначеЕсли СтрокаРесуров.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.Ежемесячно") Тогда
			Окончание = "месяц"
		ИначеЕсли СтрокаРесуров.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.Ежегодно") Тогда
			Окончание = "год"
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаРесуров.ВидПовтора) 
			и Не СтрокаРесуров.ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.НеПовторять") Тогда
			
			РасписаниеПредставление = Строка(СтрокаРесуров.ВидПовтора)+", "+Междометие+" "+Строка(СтрокаРесуров.ИнтервалПовторения)+
			" "+ Окончание+ВыбранныеДниНедели+ДополнениеПоМесяцуГоду;
		Иначе
			РасписаниеПредставление = "Не повторять"
		КонецЕсли;
		
		СтрокаРесуров.РасписаниеПредставление = РасписаниеПредставление;
		
		Если СтрокаРесуров.ВидЗавершения = ПредопределенноеЗначение("Перечисление.ВидыЗавершенияПовторовРасписания.ПоСчетчику")
			и ЗначениеЗаполнено(СтрокаРесуров.ЗавершатьПосле) Тогда 
			
			ФорматнаяСтрока = "Л = ru_RU";
			
			РасшифровкаСчетчика = ПланированиеРесурсовУНФКлиент.ПредметИсчисления(
			СтрокаРесуров.ЗавершатьПосле,
			НСтр("ru = 'раза'"),
			НСтр("ru = 'раз'"),
			НСтр("ru = 'раз'"),
			"м");
			
			СтрокаРесуров.РасшифровкаСчетчика = РасшифровкаСчетчика;
		Иначе
			СтрокаРесуров.РасшифровкаСчетчика = "";
		КонецЕсли;
		
	КонецЦикла;
	
	ПланированиеРесурсовУНФКлиент.ЗаполнитьДлительностьВТаблицеВыбранныхРесурсов(Объект.РесурсыПредприятия);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьОкончаниеПериода()
	
	ТекущиеДанные = Элементы.РесурсыПредприятия.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	СекундыПоВремени = ТекущиеДанные.Время - Дата(1,1,1);
	СекундыПоДням = ?(ЗначениеЗаполнено(ТекущиеДанные.Дни), ТекущиеДанные.Дни*1440*60, 0);
	
	ТекущиеДанные.Финиш = ТекущиеДанные.Старт + СекундыПоДням + СекундыПоВремени;
	ТекущиеДанные.Финиш = ?(Не СекундыПоДням = 0 и ТекущиеДанные.Финиш = НачалоДня(ТекущиеДанные.Финиш)
										, ТекущиеДанные.Финиш - 1, ТекущиеДанные.Финиш);
	
	ПланированиеРесурсовУНФКлиент.ПроконтролироватьШагПланирования(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОкончанияРедактированияРасписания(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения = Неопределено Тогда Возврат КонецЕсли;
	
	ТекущиеДанные = Элементы.РесурсыПредприятия.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	ВидПовтора = РезультатВыполнения.ВидПовтора;
	
	ТекущиеДанные.ВидПовтора = ВидПовтора;
	
	Если ВидПовтора = ПредопределенноеЗначение("Перечисление.ВидыПовторовРасписания.НеПовторять") Тогда
		 ПланированиеРесурсовУНФКлиент.ОчиститьДанныеСтроки(ТекущиеДанные, Ложь);
		 ЗаполнитьДанныеТаблицыРесурсовНаФорме();
		 Возврат;
	 КонецЕсли;
	
	ТекущиеДанные.ИнтервалПовторения = РезультатВыполнения.ИнтервалПовторения;
	ТекущиеДанные.Пн = РезультатВыполнения.Пн;
	ТекущиеДанные.Вт = РезультатВыполнения.Вт;
	ТекущиеДанные.Ср = РезультатВыполнения.Ср;
	ТекущиеДанные.Чт = РезультатВыполнения.Чт;
	ТекущиеДанные.Пт = РезультатВыполнения.Пт;
	ТекущиеДанные.Сб = РезультатВыполнения.Сб;
	ТекущиеДанные.Вс = РезультатВыполнения.Вс;
	ТекущиеДанные.ПоследнийДеньМесяца = РезультатВыполнения.ПоследнийДеньМесяца;
	ТекущиеДанные.ДатаПовторения = РезультатВыполнения.ДатаПовторения;
	ТекущиеДанные.ДеньНеделиМесяца = РезультатВыполнения.ДеньНеделиМесяца;
	ТекущиеДанные.НомерНеделиМесяца = РезультатВыполнения.НомерНеделиМесяца;
	ТекущиеДанные.НомерМесяца = РезультатВыполнения.НомерМесяца;
	
	ЗаполнитьДанныеТаблицыРесурсовНаФорме();
	
КонецПроцедуры

&НаКлиенте
Процедура РесурсыПредприятияВидЗавершенияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.РесурсыПредприятия.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	Если ТекущиеДанные.ВидЗавершения = ПредопределенноеЗначение("Перечисление.ВидыЗавершенияПовторовРасписания.ПоДате") Тогда
		ТекущиеДанные.ЗавершатьПосле = НачалоДня(ТекущиеДанные.Финиш+86400);
		ТекущиеДанные.РасшифровкаСчетчика = "";
	ИначеЕсли ТекущиеДанные.ВидЗавершения = ПредопределенноеЗначение("Перечисление.ВидыЗавершенияПовторовРасписания.ПоСчетчику") Тогда
		ТекущиеДанные.ЗавершатьПосле = 1;
		ТекущиеДанные.РасшифровкаСчетчика = "раза";
	Иначе
		ТекущиеДанные.РасшифровкаСчетчика = "";
		ТекущиеДанные.ЗавершатьПосле = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РесурсыПредприятияЗавершатьПослеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РесурсыПредприятия.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	Если ТекущиеДанные.ВидЗавершения = ПредопределенноеЗначение("Перечисление.ВидыЗавершенияПовторовРасписания.ПоСчетчику")
										и ЗначениеЗаполнено(ТекущиеДанные.ЗавершатьПосле) Тогда 
		
		ФорматнаяСтрока = "Л = ru_RU";
		
		РасшифровкаСчетчика = ПланированиеРесурсовУНФКлиент.ПредметИсчисления(
		ТекущиеДанные.ЗавершатьПосле,
		НСтр("ru = 'раза'"),
		НСтр("ru = 'раз'"),
		НСтр("ru = 'раз'"),
		"м");
		
		ТекущиеДанные.РасшифровкаСчетчика = РасшифровкаСчетчика;
	Иначе
		ТекущиеДанные.РасшифровкаСчетчика = "";
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.ЗавершатьПосле) = Тип("Дата")
		и ЗначениеЗаполнено(ТекущиеДанные.ЗавершатьПосле)
		и ЗначениеЗаполнено(ТекущиеДанные.Старт)
		и ТекущиеДанные.ЗавершатьПосле<НачалоДня(ТекущиеДанные.Старт)
		Тогда
		ТекущиеДанные.ЗавершатьПосле=НачалоДня(ТекущиеДанные.Старт)
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРесурсыИзПланировщика(ВыбранныеРесурсы)
	
	Для Каждого СтрокаРесурсов из ВыбранныеРесурсы Цикл
		
		
		НоваяСтрока = Объект.РесурсыПредприятия.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРесурсов);
		
		НоваяСтрока.РесурсПредприятия = СтрокаРесурсов.Ресурс;
		НоваяСтрока.Старт = СтрокаРесурсов.НачалоПериода;
		НоваяСтрока.Финиш = СтрокаРесурсов.ОкончаниеПериода;
		НоваяСтрока.Мощность = СтрокаРесурсов.Загрузка;
		НоваяСтрока.Длительность = Дата(1,1,1)+(НоваяСтрока.Финиш - НоваяСтрока.Старт); 
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоСпецификацииНаСервере()
	
	Документ = РеквизитФормыВЗначение("Объект");
	СтекСпецификацийУзлов = Новый Массив;
	Документ.ЗаполнитьЗапасыПоСпецификации(СтекСпецификацийУзлов);
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
	ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	ОбновитьКэшиДанныхСервер();
	ЗаполнитьСлужебныеДанныеПослеЧтенияОбъекта(Объект);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	ЗапасыНеЗаполнены = Ложь;
	ВывестиОтметкиКонтроля(ЭтотОбъект);

КонецПроцедуры // ЗаполнитьПоСпецификацииНаСервере()

&НаСервереБезКонтекста
Функция ПолучитьДанныеДатаПриИзменении(ДокументСсылка, ДатаНовая, ДатаПередИзменением)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("РазностьДат", УправлениеНебольшойФирмойСервер.ПроверитьНомерДокумента(ДокументСсылка, ДатаНовая, ДатаПередИзменением));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

&НаСервереБезКонтекста
Функция ПолучитьДанныеОрганизацияПриИзменении(Организация)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Компания", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

&НаСервереБезКонтекста
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные, ИмяТЧ = "Запасы")
	
	СтруктураДанные.Вставить("ЕдиницаИзмерения", СтруктураДанные.Номенклатура.ЕдиницаИзмерения);
	// Наборы
	СтруктураДанные.Вставить("ЭтоНабор", СтруктураДанные.Номенклатура.ЭтоНабор);
	
	
	СтруктураДанные.Вставить("ТипНоменклатуры", СтруктураДанные.Номенклатура.ТипНоменклатуры);
	
	СтруктураДанные.Вставить("Склад", СтруктураДанные.Номенклатура.Склад);
	
	// Характеристики
	СтруктураДанные.Вставить("ИспользоватьХарактеристики",Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики",Ложь);
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) 
		и СтруктураДанные.Номенклатура.ИспользоватьХарактеристики Тогда
		
		ЗначенияПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура);
		
		Если Не ЗначенияПоУмолчанию = Неопределено
			Тогда
			ХарактеристикаПоУмолчанию = ЗначенияПоУмолчанию;
		КонецЕсли;
		
		Если НЕ СтруктураДанные.Свойство("Характеристика") Тогда
			СтруктураДанные.Вставить("Характеристика",ХарактеристикаПоУмолчанию);
		Иначе
			СтруктураДанные.Характеристика = ?(ЗначениеЗаполнено(СтруктураДанные.Характеристика), СтруктураДанные.Характеристика, ХарактеристикаПоУмолчанию);
		КонецЕсли;
		
		СтруктураДанные.Вставить("ИспользоватьХарактеристики",Истина);
		СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики",СтруктураДанные.Номенклатура.ПроверятьЗаполнениеХарактеристики);
	КонецЕсли; 
	// Конец Характеристики
	
	//Партии
	СтруктураДанные.Вставить("ИспользоватьПартии",Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеПартий",Ложь);
	
	Если НЕ СтруктураДанные.Свойство("Партия") Тогда
		СтруктураДанные.Вставить("Партия", Неопределено);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) и СтруктураДанные.Номенклатура.ИспользоватьПартии
		Тогда
		
		Если СтруктураДанные.Свойство("СтатусПартии")
			Тогда
			ЗначенияПартииПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура,СтруктураДанные.СтатусПартии);
		Иначе
			Если Не СтруктураДанные.Свойство("ВидОперации")
				Тогда
				ВидОперации = Неопределено
			Иначе
				ВидОперации = СтруктураДанные.ВидОперации
			КонецЕсли;
			
			СтатусПартии = НоменклатураВДокументахСервер.СоответствиеВидаОпреацииИлиХозОперацииСтатусуПартии(, ВидОперации);
			ЗначенияПартииПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура,СтатусПартии);
		КонецЕсли;
		
		ПартияПоУмолчанию = Справочники.ПартииНоменклатуры.ПустаяСсылка();
		
		Если Не ЗначенияПартииПоУмолчанию = Неопределено
			Тогда
			ПартияПоУмолчанию = ЗначенияПартииПоУмолчанию;
		КонецЕсли;
		
		СтруктураДанные.Партия = ?(ЗначениеЗаполнено(СтруктураДанные.Партия), СтруктураДанные.Партия, ПартияПоУмолчанию);
		
		СтруктураДанные.ПроверятьЗаполнениеПартий = СтруктураДанные.Номенклатура.ПроверятьЗаполнениеПартий;
		СтруктураДанные.ИспользоватьПартии = Истина;
		
	КонецЕсли;
	// Конец Партии
	
	Если СтруктураДанные.Свойство("Характеристика") Тогда
		СтруктураДанные.Вставить("Спецификация", УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	Иначе
		СтруктураДанные.Вставить("Спецификация", УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтруктураДанные.Номенклатура));
	КонецЕсли;
	СтруктураДанные = ПолучитьДанныеСпецификацияПриИзменении(СтруктураДанные);
	
	Если ИмяТЧ="Операции" Тогда
		
		Если ЗначениеЗаполнено(СтруктураДанные.Спецификация) И ТипЗнч(СтруктураДанные.Спецификация)=Тип("СправочникСсылка.Спецификации") Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", СтруктураДанные.Спецификация);
			Если СтруктураДанные.Свойство("Операция") Тогда
				Запрос.УстановитьПараметр("Операция", СтруктураДанные.Операция);
			Иначе
				Запрос.УстановитьПараметр("Операция", Неопределено);
			КонецЕсли; 
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СпецификацииОперации.Ссылка КАК Спецификация,
			|	СпецификацииОперации.НомерСтроки КАК НомерСтроки,
			|	СпецификацииОперации.Операция КАК Операция,
			|	СпецификацииОперации.НормаВремени КАК НормаВремени,
			|	СпецификацииОперации.КоличествоПродукции КАК КоличествоПродукции
			|ПОМЕСТИТЬ Операции
			|ИЗ
			|	Справочник.Спецификации.Операции КАК СпецификацииОперации
			|ГДЕ
			|	СпецификацииОперации.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СпецификацииОперации.Операция КАК Операция,
			|	СпецификацииОперации.Операция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ВЫБОР
			|		КОГДА СпецификацииОперации.КоличествоПродукции = 0
			|			ТОГДА 0
			|		ИНАЧЕ СпецификацииОперации.НормаВремени / СпецификацииОперации.КоличествоПродукции
			|	КОНЕЦ КАК НормаВремени
			|ИЗ
			|	Операции КАК СпецификацииОперации
			|ГДЕ
			|	СпецификацииОперации.Операция В
			|			(ВЫБРАТЬ
			|				СпецификацииОперации.Операция
			|			ИЗ
			|				Операции КАК СпецификацииОперации
			|			ГДЕ
			|				(СпецификацииОперации.Операция = &Операция
			|					ИЛИ &Операция = НЕОПРЕДЕЛЕНО)
			|			СГРУППИРОВАТЬ ПО
			|				СпецификацииОперации.Операция
			|			ИМЕЮЩИЕ
			|				КОЛИЧЕСТВО(СпецификацииОперации.НомерСтроки) = 1)";
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				// Норма времени берется из спецификации
				СтруктураДанные.Вставить("Операция", Выборка.Операция);
				СтруктураДанные.Вставить("НормаВремени", Выборка.НормаВремени);
				СтруктураДанные.Вставить("ЕдиницаИзмерения", Выборка.ЕдиницаИзмерения);
			КонецЕсли;
		КонецЕсли;
		
		Если СтруктураДанные.Свойство("Операция") 
			И ЗначениеЗаполнено(СтруктураДанные.Операция)
			И ТипЗнч(СтруктураДанные.Операция)=Тип("СправочникСсылка.Номенклатура") Тогда
			Если НЕ СтруктураДанные.Свойство("НормаВремени") Тогда
				// Норма времени берется из карточки операции
				ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктураДанные.Операция, "НормаВремени, ЕдиницаИзмерения");
				СтруктураДанные.Вставить("НормаВремени", ЗначенияРеквизитов.НормаВремени);
				СтруктураДанные.Вставить("ЕдиницаИзмерения", ЗначенияРеквизитов.ЕдиницаИзмерения);
			КонецЕсли; 
		Иначе
			// Не удалось определить операцию
			СтруктураДанные.Вставить("Операция", Неопределено);
			СтруктураДанные.Вставить("НормаВремени", 0);
			СтруктураДанные.Вставить("ЕдиницаИзмерения", Неопределено);
		КонецЕсли;
	
	КонецЕсли; 
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()

&НаСервереБезКонтекста
Функция ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("Спецификация", УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	СтруктураДанные = ПолучитьДанныеСпецификацияПриИзменении(СтруктураДанные);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеХарактеристикаПриИзменении()

&НаСервереБезКонтекста
Функция ПолучитьДанныеСтруктурнаяЕдиницаПриИзменении(Склад)
	
	Результат = Новый Структура;
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Склад, "ТипСтруктурнойЕдиницы, ИсточникПеремещения, ИсточникПеремещения.ТипСтруктурнойЕдиницы");
	
	Если ЗначенияРеквизитов.ИсточникПеремещенияТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Склад
		ИЛИ ЗначенияРеквизитов.ИсточникПеремещенияТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
		Результат.Вставить("ИсточникПеремещения", ЗначенияРеквизитов.ИсточникПеремещения);
	Иначе
		Результат.Вставить("ИсточникПеремещения", Неопределено);
	КонецЕсли;
	Результат.Вставить("ТипСтруктурнойЕдиницы", ЗначенияРеквизитов.ТипСтруктурнойЕдиницы);
	Возврат Результат;
	
КонецФункции // ПолучитьДанныеСтруктурнаяЕдиницаПриИзменении()	

&НаСервереБезКонтекста
Функция ПолучитьДанныеСтруктурнаяЕдиницаРезервПриИзменении(Склад)
	
	Если Склад.ПолучательПеремещения.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Склад
		ИЛИ Склад.ПолучательПеремещения.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
		
		Возврат Склад.ПолучательПеремещения;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат Склад.ПолучательПеремещения;
	
КонецФункции // ПолучитьДанныеСтруктурнаяЕдиницаРезервПриИзменении()

&НаСервере
Процедура ЗаполнитьПоДокументу(Реквизит = "ЗаказНаПроизводствоОснование")
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Заполнить(Объект[Реквизит]);
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	
	ОбновитьКэшиДанныхСервер();
	ЗаполнитьСлужебныеДанныеПослеЧтенияОбъекта(Объект);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	
	Если Объект.РучноеРаспределение Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли; 
	ОбновитьАвтоОтметкиПриИзмененииОпераций(ЭтотОбъект);
	ТипСтруктурнойЕдиницы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы");
	
КонецПроцедуры // ЗаполнитьПоДокументу()

&НаСервере
Процедура УстановитьРежимИСписокВыбора()
	
	Если НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимПодразделениям.Получить()
		И НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимСкладам.Получить() Тогда
		
		Элементы.СтруктурнаяЕдиница.РежимВыбораИзСписка = Истина;
		Элементы.СтруктурнаяЕдиница.КнопкаСоздания = Ложь;
		Элементы.СтруктурнаяЕдиница.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		Элементы.СтруктурнаяЕдиница.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		
		Элементы.ПродукцияСтруктурнаяЕдиницаРезерв.РежимВыбораИзСписка = Истина;
		Элементы.ПродукцияСтруктурнаяЕдиницаРезерв.КнопкаСоздания = Ложь;
		Элементы.ПродукцияСтруктурнаяЕдиницаРезерв.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		Элементы.ПродукцияСтруктурнаяЕдиницаРезерв.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		
		Элементы.ЗапасыСтруктурнаяЕдиницаРезерв.РежимВыбораИзСписка = Истина;
		Элементы.ЗапасыСтруктурнаяЕдиницаРезерв.КнопкаСоздания = Ложь;
		Элементы.ЗапасыСтруктурнаяЕдиницаРезерв.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		Элементы.ЗапасыСтруктурнаяЕдиницаРезерв.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		
		Элементы.ПродукцияСтруктурнаяЕдиницаТабличнаяЧасть.РежимВыбораИзСписка = Истина;
		Элементы.ПродукцияСтруктурнаяЕдиницаТабличнаяЧасть.КнопкаСоздания = Ложь;
		Элементы.ПродукцияСтруктурнаяЕдиницаТабличнаяЧасть.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		Элементы.ПродукцияСтруктурнаяЕдиницаТабличнаяЧасть.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		
		Элементы.ЗапасыСтруктурнаяЕдиницаТабличнаяЧасть.РежимВыбораИзСписка = Истина;
		Элементы.ЗапасыСтруктурнаяЕдиницаТабличнаяЧасть.КнопкаСоздания = Ложь;
		Элементы.ЗапасыСтруктурнаяЕдиницаТабличнаяЧасть.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		Элементы.ЗапасыСтруктурнаяЕдиницаТабличнаяЧасть.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьРежимИСписокВыбора()

// ПодключаемоеОборудование
&НаСервереБезКонтекста
Процедура ПолучитьДанныеПоШтрихКодам(СтруктураДанные)
	
	// Преобразование весовых штрихкодов.
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		
		РегистрыСведений.ШтрихкодыНоменклатуры.ПреобразоватьВесовойШтрихкод(ТекШтрихкод);
		
	КонецЦикла;
	
	ДанныеПоШтрихКодам = РегистрыСведений.ШтрихкодыНоменклатуры.ПолучитьДанныеПоШтрихкодам(СтруктураДанные.МассивШтрихкодов);
	
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		
		ДанныеШтрихкода = ДанныеПоШтрихкодам[ТекШтрихкод.Штрихкод];
		
		Если ДанныеШтрихкода <> Неопределено
		   И ДанныеШтрихкода.Количество() <> 0 Тогда
			
			СтруктураДанныеНоменклатуры = Новый Структура();
			СтруктураДанныеНоменклатуры.Вставить("Номенклатура", ДанныеШтрихкода.Номенклатура);
			СтруктураДанныеНоменклатуры.Вставить("ТипНоменклатуры", ДанныеШтрихкода.Номенклатура.ТипНоменклатуры);
			СтруктураДанныеНоменклатуры.Вставить("Характеристика", ДанныеШтрихкода.Характеристика);
			ДанныеШтрихкода.Вставить("СтруктураДанныеНоменклатуры", ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанныеНоменклатуры));
			
			Если НЕ ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения) Тогда
				ДанныеШтрихкода.ЕдиницаИзмерения  = ДанныеШтрихкода.Номенклатура.ЕдиницаИзмерения;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДанные.Вставить("ДанныеПоШтрихКодам", ДанныеПоШтрихКодам);
	
	Для каждого парам Из Метаданные.Документы.ЗаказНаПроизводство.ТабличныеЧасти.Запасы.Реквизиты.Номенклатура.ПараметрыВыбора Цикл
		Если парам.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(парам.Значение)=Тип("ФиксированныйМассив") Тогда
				СтруктураДанные.Вставить("ОтборТипНоменклатуры", парам.Значение);
			Иначе
			    МассивТипов = Новый Массив;
				МассивТипов.Добавить(парам.Значение);
				СтруктураДанные.Вставить("ОтборТипНоменклатуры", МассивТипов);
			КонецЕсли;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПолучитьДанныеПоШтрихКодам()

&НаКлиенте
Функция ЗаполнитьПоДаннымШтрихкодов(ДанныеШтрикодов)
	
	ИмяТекущейСтраницы = Элементы.Страницы.ТекущаяСтраница.Имя;
	Если ИмяТекущейСтраницы = "ТЧПродукция" Тогда
		ИмяТабличнойЧасти = "Продукция";
	ИначеЕсли ИмяТекущейСтраницы = "ТЧЗапасы" Тогда
		ИмяТабличнойЧасти = "Запасы";
	Иначе
		Возврат Новый Структура("НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа", Новый Массив, Новый Массив);
	КонецЕсли;
	
	НеизвестныеШтрихкоды = Новый Массив;
	ШтрихкодыНекорректногоТипа = Новый Массив;
	
	Если ТипЗнч(ДанныеШтрикодов) = Тип("Массив") Тогда
		МассивШтрихкодов = ДанныеШтрикодов;
	Иначе
		МассивШтрихкодов = Новый Массив;
		МассивШтрихкодов.Добавить(ДанныеШтрикодов);
	КонецЕсли;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("МассивШтрихкодов", МассивШтрихкодов);
	ПолучитьДанныеПоШтрихКодам(СтруктураДанные);
	
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		ДанныеШтрихкода = СтруктураДанные.ДанныеПоШтрихкодам[ТекШтрихкод.Штрихкод];
		
		Если ДанныеШтрихкода <> Неопределено
		   И ДанныеШтрихкода.Количество() = 0 Тогда
			НеизвестныеШтрихкоды.Добавить(ТекШтрихкод);
		ИначеЕсли СтруктураДанные.ОтборТипНоменклатуры.Найти(ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ТипНоменклатуры) = Неопределено ИЛИ ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЭтоНабор Тогда
			ШтрихкодыНекорректногоТипа.Добавить(Новый Структура("Штрихкод,Номенклатура,ТипНоменклатуры,ЭтоНабор", ТекШтрихкод.Штрихкод, ДанныеШтрихкода.Номенклатура, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ТипНоменклатуры, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЭтоНабор));
		Иначе
			МассивСтрокТЧ = Объект[ИмяТабличнойЧасти].НайтиСтроки(Новый Структура("Номенклатура,Характеристика,ЕдиницаИзмерения",ДанныеШтрихкода.Номенклатура,ДанныеШтрихкода.Характеристика,ДанныеШтрихкода.ЕдиницаИзмерения));
			Если МассивСтрокТЧ.Количество() = 0 Тогда
				НоваяСтрока = Объект[ИмяТабличнойЧасти].Добавить();
				НоваяСтрока.Номенклатура = ДанныеШтрихкода.Номенклатура;
				НоваяСтрока.Характеристика = ДанныеШтрихкода.Характеристика;
				НоваяСтрока.Партия = ДанныеШтрихкода.Партия;
				НоваяСтрока.Количество = ТекШтрихкод.Количество;
				НоваяСтрока.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения), ДанныеШтрихкода.ЕдиницаИзмерения, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЕдиницаИзмерения);
				Если НоваяСтрока.Свойство("Спецификация") Тогда
					НоваяСтрока.Спецификация = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.Спецификация;
				КонецЕсли;
				Если НоваяСтрока.Свойство("ИспользоватьЭтапыПроизводства") Тогда
					НоваяСтрока.ИспользоватьЭтапыПроизводства = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ИспользоватьЭтапыПроизводства;
					Если НоваяСтрока.Свойство("ПодразделениеЗавершающегоЭтапа")
						И НоваяСтрока.ИспользоватьЭтапыПроизводства 
						И ПараметрыФормы.ИспользоватьЭтапыПроизводства 
						И ТипСтруктурнойЕдиницы=ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") Тогда
						НоваяСтрока.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
					КонецЕсли; 
				КонецЕсли;
				УправлениеНебольшойФирмойКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект, НоваяСтрока);
				Элементы[ИмяТабличнойЧасти].ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
				Если ИмяТабличнойЧасти = "Запасы" Тогда
					НоваяСтрока.ДоляСтоимости = 1;
				КонецЕсли; 
			Иначе
				НайденнаяСтрока = МассивСтрокТЧ[0];
				НайденнаяСтрока.Количество = НайденнаяСтрока.Количество + ТекШтрихкод.Количество;
				Элементы[ИмяТабличнойЧасти].ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа",НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа);
	
КонецФункции // ЗаполнитьПоДаннымШтрихкодов()

&НаКлиенте
Процедура ПолученыШтрихкоды(ДанныеШтрикодов) Экспорт
	
	Модифицированность = Истина;
	
	НедобавленныеШтрихкоды		= ЗаполнитьПоДаннымШтрихкодов(ДанныеШтрикодов);
	НеизвестныеШтрихкоды		= НедобавленныеШтрихкоды.НеизвестныеШтрихкоды;
	ШтрихкодыНекорректногоТипа	= НедобавленныеШтрихкоды.ШтрихкодыНекорректногоТипа;
	
	ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа);
	
	Если НеизвестныеШтрихкоды.Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПолученыШтрихкодыЗавершение", ЭтотОбъект, НеизвестныеШтрихкоды);
		
		ОткрытьФорму(
			"РегистрСведений.ШтрихкодыНоменклатуры.Форма.РегистрацияШтрихкодовНоменклатуры",
			Новый Структура("НеизвестныеШтрихкоды", НеизвестныеШтрихкоды), ЭтотОбъект,,,,Оповещение
		);
		
		Возврат;
		
	КонецЕсли;
	
	ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды);
	
КонецПроцедуры // ПолученыШтрихкоды()

&НаКлиенте
Процедура ПолученыШтрихкодыЗавершение(ВозвращаемыеПараметры, Параметры) Экспорт
	
	НеизвестныеШтрихкоды = Параметры;
	
	Если ВозвращаемыеПараметры <> Неопределено Тогда
		
		МассивШтрихкодов = Новый Массив;
		
		Для каждого ЭлементМассива Из ВозвращаемыеПараметры.ЗарегистрированныеШтрихкоды Цикл
			МассивШтрихкодов.Добавить(ЭлементМассива);
		КонецЦикла;
		
		Для каждого ЭлементМассива Из ВозвращаемыеПараметры.ПолученыНовыеШтрихкоды Цикл
			МассивШтрихкодов.Добавить(ЭлементМассива);
		КонецЦикла;
		
		НедобавленныеШтрихкоды		= ЗаполнитьПоДаннымШтрихкодов(МассивШтрихкодов);
		НеизвестныеШтрихкоды		= НедобавленныеШтрихкоды.НеизвестныеШтрихкоды;
		ШтрихкодыНекорректногоТипа	= НедобавленныеШтрихкоды.ШтрихкодыНекорректногоТипа;
		ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа);
	КонецЕсли;
	
	ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды);

КонецПроцедуры

&НаКлиенте
Процедура ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды) Экспорт
	
	Для каждого ТекНеизвестныйШтрихкод Из НеизвестныеШтрихкоды Цикл
		
		СтрокаСообщения = НСтр("ru = 'Данные по штрихкоду не найдены: %1%; количество: %2%'");
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1%", ТекНеизвестныйШтрихкод.Штрихкод);
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2%", ТекНеизвестныйШтрихкод.Количество);
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрокаСообщения);
		
	КонецЦикла;
	
	ЗаполнитьПризнакиИспользованияХарактеристикСервер();

	Если ИмяТабличнойЧасти="Продукция" Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
		ОбновитьСпискиВыбораПродукции();
	ИначеЕсли ИмяТабличнойЧасти="Запасы" Тогда
		ВывестиОтметкиКонтроля(ЭтотОбъект);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа) Экспорт
	
	Для каждого ТекНекорректныйШтрихкод Из ШтрихкодыНекорректногоТипа Цикл
		
		СтрокаСообщения = НСтр("ru = 'Найденная по штрихкоду %1% номенклатура -%2%- имеет тип %3%, который не подходит для этой табличной части'");
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1%", ТекНекорректныйШтрихкод.Штрихкод);
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2%", ТекНекорректныйШтрихкод.Номенклатура);
		Если ТекНекорректныйШтрихкод.Свойство("ЭтоНабор") И ТекНекорректныйШтрихкод.ЭтоНабор Тогда
			// Наборы
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%3%", НСтр("ru = 'Набор'"));
		Иначе
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%3%", ТекНекорректныйШтрихкод.ТипНоменклатуры);
		КонецЕсли; 
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрокаСообщения);
		
	КонецЦикла;
	
КонецПроцедуры
// Конец ПодключаемоеОборудование

//Проверяет,в зависимости от использования, заполнение характеристик номенклатуры
//
&НаСервере
Процедура ПроверитьЗаполнениеХарактеристикРаспределениеЗапасов(Отказ)
	
	ТаблицаНоменклатуры = РаспределениеЗапасов.Выгрузить(,"Номенклатура");
	ТаблицаНоменклатуры.Свернуть("Номенклатура");

	СписокНоменклатуры = СписокНоменклатурыСХарактеристикамиРаспределениеЗапасов(ТаблицаНоменклатуры);
	СписокНоменклатурыСПартями = СписокНоменклатурыСПартиямиРаспределениеЗапасов(ТаблицаНоменклатуры);

	Для Каждого СтрокаЗапасы из РаспределениеЗапасов Цикл
		
		Если Не СписокНоменклатуры.НайтиПоЗначению(СтрокаЗапасы.Номенклатура) = Неопределено и Не ЗначениеЗаполнено(СтрокаЗапасы.Характеристика)
			Тогда
			ТекстСообщения = НСтр("ru = 'В таблице %Таблица%, для номенклатуры %1% в строке %Номер%, заполнение поля ""характеристика"" является обязательным. '");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1%", Строка(СтрокаЗапасы.Номенклатура));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", СтрокаЗапасы.НомерСтроки);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Таблица%", "РаспределениеЗапасов");
			
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
			Объект,
			ТекстСообщения,
			"РаспределениеЗапасов",
			СтрокаЗапасы.НомерСтроки,
			"Характеристика",
			Отказ
			);
		КонецЕсли;
		
		Если Не СписокНоменклатурыСПартями.НайтиПоЗначению(СтрокаЗапасы.Номенклатура) = Неопределено
			и Не ЗначениеЗаполнено(СтрокаЗапасы.Партия)
			Тогда
			ТекстСообщения = НСтр("ru = 'В таблице %Таблица%, для номенклатуры %1% в строке %Номер%, заполнение поля ""партия"" является обязательным. '");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1%", Строка(СтрокаЗапасы.Номенклатура));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", СтрокаЗапасы.НомерСтроки);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Таблица%", "РаспределениеЗапасов");
			
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
			Объект,
			ТекстСообщения,
			"РаспределениеЗапасов",
			СтрокаЗапасы.НомерСтроки,
			"Партия",
			Отказ
			);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

//Получает список номенклатуры для которой ведется учет по характеристикам таб. части "РаспределениеЗапасов"
//
&НаСервере
Функция СписокНоменклатурыСХарактеристикамиРаспределениеЗапасов(ТаблицаНоменклатуры)
	
	СписокНоменклатуры = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ТаблицаНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|ГДЕ
	|	ТаблицаНоменклатуры.Номенклатура.ИспользоватьХарактеристики
	|	И ТаблицаНоменклатуры.Номенклатура.ПроверятьЗаполнениеХарактеристики";
	
	МассивНоменклатуры = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");
	СписокНоменклатуры.ЗагрузитьЗначения(МассивНоменклатуры);
	
	Возврат СписокНоменклатуры;
	
КонецФункции

//Получает список номенклатуры для которой ведется учет по партиям таб. части "РаспределениеЗапасов"
//
&НаСервере
Функция СписокНоменклатурыСПартиямиРаспределениеЗапасов(ТаблицаНоменклатуры)
	
	СписокНоменклатуры = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ТаблицаНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|ГДЕ
	|	ТаблицаНоменклатуры.Номенклатура.ИспользоватьПартии 
	|	И ТаблицаНоменклатуры.Номенклатура.ПроверятьЗаполнениеПартий";
	
	МассивНоменклатуры = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");
	СписокНоменклатуры.ЗагрузитьЗначения(МассивНоменклатуры);
	
	Возврат СписокНоменклатуры;

КонецФункции

&НаСервере
Процедура ЗаполнитьКолонкуРезервПоОстаткамНаСервере()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьКолонкуРезервПоОстаткам();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
	ОбновитьКэшиДанныхСервер();
	ЗаполнитьСлужебныеДанныеПослеЧтенияОбъекта(Объект);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	Если Объект.РучноеРаспределение Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли; 
	
КонецПроцедуры // ЗаполнитьКолонкуРезервПоОстаткамНаСервере()

&НаСервереБезКонтекста
Функция ИспользуетсяРезервирование(ВидОперацииОбъекта, ЗаказПокупателяОбъекта, ПоложениеЗаказаПокупателя, ИмяТабличнойЧасти)
	
	Если Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить()
		И ЗначениеЗаполнено(ЗаказПокупателяОбъекта)
		И ПоложениеЗаказаПокупателя <> ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		
		Если ИмяТабличнойЧасти = "Запасы" И ВидОперацииОбъекта = Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка Тогда
			Возврат Истина;
		ИначеЕсли ИмяТабличнойЧасти = "Продукция" И ВидОперацииОбъекта = Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции // ИспользуетсяРезервирование()

&НаКлиенте
Процедура ВыборТипаОснованияЗавершение(ВыбИмяФормы, Параметры) Экспорт
	
	Если ВыбИмяФормы<>Неопределено Тогда
		
		СтруктураПараметровОтбора = Новый Структура();
		_ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыбратьОснованиеЗавершение", ЭтотОбъект);
		ОткрытьФорму(ВыбИмяФормы.Значение, СтруктураПараметровОтбора, ЭтотОбъект, ,,,_ОповещениеОЗакрытии);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОснованиеЗавершение(ВыбЗначение, Параметры) Экспорт

	Если ВыбЗначение<>Неопределено Тогда
		Объект.ДокументОснование = ВыбЗначение;
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(ВыбЗначение);
		Модифицированность = Истина;
		
		ЗаполнитьПоОснованиюНачало();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованиюНачало() Экспорт

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоДокументуОснованиюЗавершение", ЭтотОбъект);
	ПоказатьВопрос(
		ОписаниеОповещения, 
		НСтр("ru = 'Заполнить документ по выбранному основанию?'"), 
		РежимДиалогаВопрос.ДаНет, 0);		

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДокументуОснованиюЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    Если Ответ = КодВозвратаДиалога.Да Тогда
        ЗаполнитьПоДокументу("ДокументОснование");
		ОбновитьСпискиВыбораПродукции();
    КонецЕсли;

КонецПроцедуры // ЗаполнитьПоЗаказуПокупателя()

&НаКлиенте
Процедура УстановитьТекущейСтраницейЗавершениеЗаказа()
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЗавершениеЗаказа;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаказПокупателяЗаполнен(Объект)
	
	Возврат ЗначениеЗаполнено(Объект.ЗаказПокупателя) 
	ИЛИ Объект.ПоложениеЗаказаПокупателя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти");	
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыТЧПоШапке(Объект, Реквизит = "")

	// Заполнение склада
	ЗаполнятьСтруктурнуюЕдиницу = (ПустаяСтрока(Реквизит) ИЛИ Реквизит="СтруктурнаяЕдиница");
	Если ЗаполнятьСтруктурнуюЕдиницу И Объект.ПоложениеСклада<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		ТЧ = ?(Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка"), Объект.Продукция, Объект.Запасы);
		Для каждого СтрокаТабличнойЧасти Из ТЧ Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаРезерв;
		КонецЦикла; 
		ТЧ = ?(Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка"), Объект.Запасы, Объект.Продукция);
		Для каждого СтрокаТабличнойЧасти Из ТЧ Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
		КонецЦикла; 
	КонецЕсли;
	// Заполнение заказа покупателя
	ЗаполнятьЗаказПокупателя = ((ПустаяСтрока(Реквизит) ИЛИ Реквизит="ЗаказПокупателя"));
	Если ЗаполнятьЗаказПокупателя И Объект.ПоложениеЗаказаПокупателя<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
		КонецЦикла; 
		Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
		КонецЦикла; 
		Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаПоКлючу(Таблица, КлючСвязи, ИмяПоля = "КлючСвязи")
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить(ИмяПоля, КлючСвязи);
	Строки = Таблица.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат Строки[0];
	КонецЕсли; 
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПризнакиИспользованияХарактеристикСервер()
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	Если Объект.РучноеРаспределение Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли; 
	
КонецПроцедуры
 
&НаКлиенте
Процедура ЗаполнитьДанныеШапки(СтрокаТЧ, ИмяТЧ = "Запасы")
	
	ЭтоСборка = (Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка"));
	ЭтоРазборка = (Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка"));
	
	СтруктураПолей = Новый Структура;
	Если Объект.ПоложениеЗаказаПокупателя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") Тогда
		СтруктураПолей.Вставить("ЗаказПокупателя", Объект.ЗаказПокупателя);
	КонецЕсли;
	Если Объект.ПоложениеСклада=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") 
		И ЭтоСборка Тогда
		Если ИмяТЧ="Продукция" Тогда
			СтруктураПолей.Вставить("СтруктурнаяЕдиница", ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка"));
		Иначе
			СтруктураПолей.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиницаРезерв);
		КонецЕсли; 
	КонецЕсли; 
	Если Объект.ПоложениеСклада=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") 
		И ЭтоРазборка Тогда
		Если ИмяТЧ="Продукция" Тогда
			СтруктураПолей.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиницаРезерв);
		Иначе
			СтруктураПолей.Вставить("СтруктурнаяЕдиница", ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка"));
		КонецЕсли; 
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтруктураПолей);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПродукциюИзЗаказов(Заказы)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заказы", Заказы);
	Запрос.УстановитьПараметр("ВидОперации", Объект.ВидОперации);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|	ЗаказПокупателяЗапасы.СерийныеНомера КАК СерийныеНомера,
	|	ЗаказПокупателяЗапасы.Количество - ЗаказПокупателяЗапасы.Резерв КАК Количество,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяЗапасы.Спецификация КАК Спецификация,
	|	ЗаказПокупателяЗапасы.Партия КАК Партия,
	|	ЗаказПокупателяЗапасы.Ссылка КАК ЗаказПокупателя,
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ЗаказПокупателяЗапасы.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ Запасы
	|ИЗ
	|	Документ.ЗаказПокупателя.Запасы КАК ЗаказПокупателяЗапасы
	|ГДЕ
	|	ЗаказПокупателяЗапасы.Количество - ЗаказПокупателяЗапасы.Резерв > 0
	|	И (ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ИЛИ ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|	И (ЗаказПокупателяЗапасы.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ИЛИ ЗаказПокупателяЗапасы.Номенклатура.СпособПополнения = ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство))
	|	И ЗаказПокупателяЗапасы.Ссылка В(&Заказы)
	|	И ЗаказПокупателяЗапасы.Ссылка.ОсновнойВариантКП = ЗаказПокупателяЗапасы.НомерВариантаКП
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказПокупателяМатериалы.Номенклатура,
	|	ЗаказПокупателяМатериалы.Характеристика,
	|	ЗаказПокупателяМатериалы.СерийныеНомера,
	|	ЗаказПокупателяМатериалы.Количество - ЗаказПокупателяМатериалы.Резерв,
	|	ЗаказПокупателяМатериалы.ЕдиницаИзмерения,
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
	|	ЗаказПокупателяМатериалы.Партия,
	|	ЗаказПокупателяМатериалы.Ссылка,
	|	ЗаказПокупателяМатериалы.Номенклатура.ТипНоменклатуры,
	|	ЗаказПокупателяМатериалы.КлючСвязи
	|ИЗ
	|	Документ.ЗаказПокупателя.Материалы КАК ЗаказПокупателяМатериалы
	|ГДЕ
	|	ЗаказПокупателяМатериалы.Количество - ЗаказПокупателяМатериалы.Резерв > 0
	|	И (ЗаказПокупателяМатериалы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ИЛИ ЗаказПокупателяМатериалы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|	И ЗаказПокупателяМатериалы.Номенклатура.СпособПополнения = ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
	|	И ЗаказПокупателяМатериалы.Ссылка В(&Заказы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.Характеристика КАК Характеристика,
	|	Запасы.СерийныеНомера КАК СерийныеНомера,
	|	СУММА(Запасы.Количество) КАК Количество,
	|	Запасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Запасы.Спецификация КАК Спецификация,
	|	Запасы.Партия КАК Партия,
	|	Запасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	Запасы.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА Запасы.Спецификация.ВидПроизводства <> ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьЭтапыПроизводства
	|ИЗ
	|	Запасы КАК Запасы
	|
	|СГРУППИРОВАТЬ ПО
	|	Запасы.Спецификация,
	|	Запасы.ЕдиницаИзмерения,
	|	Запасы.СерийныеНомера,
	|	Запасы.Характеристика,
	|	Запасы.ЗаказПокупателя,
	|	Запасы.Партия,
	|	Запасы.ТипНоменклатуры,
	|	Запасы.Номенклатура,
	|	ВЫБОР
	|		КОГДА Запасы.Спецификация.ВидПроизводства <> ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Продукция.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Если ПараметрыФормы.ИспользоватьЭтапыПроизводства 
			И Выборка.ИспользоватьЭтапыПроизводства 
			И ТипСтруктурнойЕдиницы=ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") Тогда
			НоваяСтрока.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
		КонецЕсли; 
	КонецЦикла; 
	
	ЗаполнитьПризнакиИспользованияХарактеристикСервер();
	ЗаполнитьПризнакиИспользованияЭтапов();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыФормы()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РезервированиеЗапасов", ПолучитьФункциональнуюОпцию("РезервированиеЗапасов"));
	ПараметрыФормы.Вставить("УчетПоНесколькимПодразделениям", ПолучитьФункциональнуюОпцию("УчетПоНесколькимПодразделениям"));
	ПараметрыФормы.Вставить("ИспользоватьТехоперации", ПолучитьФункциональнуюОпцию("ИспользоватьТехоперации"));
	ПараметрыФормы.Вставить("ИспользоватьЭтапыПроизводства", ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства"));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОперацииНаСервере()
	
	Документ = РеквизитФормыВЗначение("Объект");
	СтекСпецификацийУзлов = Новый Массив;
	Документ.ЗаполнитьОперацииПоСпецификации();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
	ОбновитьКэшиДанныхСервер();
	ЗаполнитьСлужебныеДанныеПослеЧтенияОбъекта(Объект);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	ВывестиОтметкиКонтроля(ЭтотОбъект);
	ОбновитьАвтоОтметкиПриИзмененииОпераций(ЭтотОбъект);
	
КонецПроцедуры // ЗаполнитьПоСпецификацииНаСервере()

&НаСервереБезКонтекста
Функция ПолучитьДанныеЕдиницаИзмеренияПриИзменении(ТекущаяЕдиницаИзмерения = Неопределено, ЕдиницаИзмерения = Неопределено)
	
	СтруктураДанные = Новый Структура;
	
	Если ТекущаяЕдиницаИзмерения = Неопределено Тогда
		СтруктураДанные.Вставить("ТекущийКоэффициент", 1);
	Иначе
		СтруктураДанные.Вставить("ТекущийКоэффициент", ТекущаяЕдиницаИзмерения.Коэффициент);
	КонецЕсли;
	
	Если ЕдиницаИзмерения = Неопределено Тогда
		СтруктураДанные.Вставить("Коэффициент", 1);
	Иначе
		СтруктураДанные.Вставить("Коэффициент", ЕдиницаИзмерения.Коэффициент);
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеЕдиницаИзмеренияПриИзменении()

&НаКлиенте
Процедура ОбновитьКэшиДанных()
	
	СотрудникиКОбновлению = Новый Массив;
	Если Объект.ПоложениеИсполнителя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Исполнитель) ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Исполнитель)<>Тип("СправочникСсылка.Сотрудники") Тогда
				Продолжить;
			КонецЕсли; 
			Если КэшПодразделения.Получить(СтрокаТабличнойЧасти.Исполнитель)=Неопределено Тогда
				СотрудникиКОбновлению.Добавить(СтрокаТабличнойЧасти.Исполнитель);
			КонецЕсли; 
		КонецЦикла;
		Для каждого СтрокаТабличнойЧасти Из Объект.СоставБригады Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Сотрудник) Тогда
				Продолжить;
			КонецЕсли; 
			Если КэшПодразделения.Получить(СтрокаТабличнойЧасти.Сотрудник)=Неопределено Тогда
				СотрудникиКОбновлению.Добавить(СтрокаТабличнойЧасти.Сотрудник);
			КонецЕсли; 
		КонецЦикла;
	ИначеЕсли Объект.ПоложениеИсполнителя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") Тогда
		Если ЗначениеЗаполнено(Объект.Исполнитель) И ТипЗнч(Объект.Исполнитель)=Тип("СправочникСсылка.Сотрудники") Тогда
			Если КэшПодразделения.Получить(Объект.Исполнитель)=Неопределено Тогда
				СотрудникиКОбновлению.Добавить(Объект.Исполнитель);
			КонецЕсли;
		ИначеЕсли ТипЗнч(Объект.Исполнитель)=Тип("СправочникСсылка.Бригады") Тогда
			Для каждого СтрокаТабличнойЧасти Из Объект.СоставБригады Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Сотрудник) Тогда
					Продолжить;
				КонецЕсли; 
				Если КэшПодразделения.Получить(СтрокаТабличнойЧасти.Сотрудник)=Неопределено Тогда
					СотрудникиКОбновлению.Добавить(СтрокаТабличнойЧасти.Сотрудник);
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли; 
	КонецЕсли;
	
	Если СотрудникиКОбновлению.Количество()>0 Тогда
		ОбновитьКэшиДанныхСервер(СотрудникиКОбновлению);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКэшиДанныхСервер(МассивСотрудников = Неопределено)
	
	Если МассивСотрудников=Неопределено Тогда
		МассивСотрудников = Новый Массив;
		Если Объект.ПоложениеИсполнителя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
			Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Исполнитель) ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Исполнитель)<>Тип("СправочникСсылка.Сотрудники") Тогда
					Продолжить;
				КонецЕсли; 
				МассивСотрудников.Добавить(СтрокаТабличнойЧасти.Исполнитель);
			КонецЦикла;
			Для каждого СтрокаТабличнойЧасти Из Объект.СоставБригады Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Сотрудник) Тогда
					Продолжить;
				КонецЕсли; 
				МассивСотрудников.Добавить(СтрокаТабличнойЧасти.Сотрудник);
			КонецЦикла;
		ИначеЕсли Объект.ПоложениеИсполнителя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") Тогда
			Если ЗначениеЗаполнено(Объект.Исполнитель) И ТипЗнч(Объект.Исполнитель)=Тип("СправочникСсылка.Сотрудники") Тогда
				МассивСотрудников.Добавить(Объект.Исполнитель);
			ИначеЕсли ТипЗнч(Объект.Исполнитель)=Тип("СправочникСсылка.Бригады") Тогда
				Для каждого СтрокаТабличнойЧасти Из Объект.СоставБригады Цикл
					Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Сотрудник) Тогда
						Продолжить;
					КонецЕсли; 
					МассивСотрудников.Добавить(СтрокаТабличнойЧасти.Сотрудник);
				КонецЦикла;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьЗапрос = ПараметрыФормы.ИспользоватьТехоперации И МассивСотрудников.Количество()>0;
	
	Если ВыполнитьЗапрос Тогда
		
		МассивСотрудников = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивСотрудников);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
		Запрос.УстановитьПараметр("ПериодСреза", ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, КонецДня(ТекущаяДата())));
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	ЕСТЬNULL(СотрудникиСрезПоследних.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК СтруктурнаяЕдиница
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники.СрезПоследних(&ПериодСреза, Организация = &Организация) КАК СотрудникиСрезПоследних
		|		ПО (СотрудникиСрезПоследних.Сотрудник = Сотрудники.Ссылка)
		|ГДЕ
		|	Сотрудники.Ссылка В(&МассивСотрудников)";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если ТипЗнч(КэшПодразделения)=Тип("ФиксированноеСоответствие") Тогда
			СоответствиеПодразделений = Новый Соответствие(КэшПодразделения);
		Иначе
			СоответствиеПодразделений = Новый Соответствие;
		КонецЕсли; 
		
		Пока Выборка.Следующий() Цикл
			СоответствиеПодразделений.Вставить(Выборка.Сотрудник, Выборка.СтруктурнаяЕдиница);	
		КонецЦикла;
		
		КэшПодразделения = Новый ФиксированноеСоответствие(СоответствиеПодразделений);
		
	Иначе
		
		КэшПодразделения = Новый ФиксированноеСоответствие(Новый Соответствие);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьДлительностьОперации()

	ТекущаяСтрока = Элементы.Операции.ТекущиеДанные;
	ТекущаяСтрока.Нормочасы = ТекущаяСтрока.НормаВремени * ТекущаяСтрока.КоличествоПлан;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоставБригадыНаСервере(Бригада, КлючСвязи = Неопределено)

	Если КлючСвязи=Неопределено Тогда
		Объект.СоставБригады.Очистить();
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Бригада) И ТипЗнч(Бригада) = Тип("СправочникСсылка.Бригады") Тогда
		
		ТаблицаСостава = Справочники.Бригады.СоставБригады(Бригада, Объект.Организация, Объект.Дата);
		
		Для каждого СтрокаТабличнойЧасти Из ТаблицаСостава Цикл
			НоваяСтрока = Объект.СоставБригады.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
			НоваяСтрока.КТУ = 1;
			Если Объект.ПоложениеСтруктурнойЕдиницыОпераций=Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
				НоваяСтрока.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиница;
			КонецЕсли;
			Если КлючСвязи<>Неопределено Тогда
				НоваяСтрока.КлючСвязи = КлючСвязи;
			КонецЕсли; 
		КонецЦикла; 
		
	КонецЕсли;	
	
	Модифицированность = Истина;	

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСлужебныеДанныеПослеЧтенияОбъекта(Объект)
	
	Если Объект.ПоложениеИсполнителя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		Для каждого ТекущаяСтрока Из Объект.Операции Цикл
			ТекущаяСтрока.ИсполнительБригада = (ТипЗнч(ТекущаяСтрока.Исполнитель)=Тип("СправочникСсылка.Бригады"));
			Если ТекущаяСтрока.ИсполнительБригада Тогда
				ТекущаяСтрока.ИзменитьСостав = НСтр("ru = 'Изменить состав и КТУ'");
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для каждого ТекущаяСтрока Из Объект.Операции Цикл
			ТекущаяСтрока.ИсполнительБригада = Ложь;
		КонецЦикла; 
	КонецЕсли; 
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
		СтрокаПродукции = СтрокаПоКлючу(Объект.Продукция, СтрокаТабличнойЧасти.КлючСвязиПродукция);
		ЗаполнитьДанныеПродукцииВСтрокеОперации(Объект, СтрокаТабличнойЧасти, СтрокаПродукции);
	КонецЦикла; 	
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеСпецификацияПриИзменении(СтруктураДанные)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства")
		И ЗначениеЗаполнено(СтруктураДанные.Спецификация)
		И ТипЗнч(СтруктураДанные.Спецификация)=Тип("СправочникСсылка.Спецификации") Тогда
		ВидПроизводства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураДанные.Спецификация, "ВидПроизводства");
		СтруктураДанные.Вставить("ИспользоватьЭтапыПроизводства", ЗначениеЗаполнено(ВидПроизводства));
	Иначе
		СтруктураДанные.Вставить("ИспользоватьЭтапыПроизводства", Ложь);
	КонецЕсли; 
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеХарактеристикаПриИзменении()

&НаКлиенте
Процедура ОбновитьСкладВТЧ()
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка") Тогда
		ИмяТЧ = "Продукция";
		ИмяТЧОчистка = "Запасы";
	Иначе
		ИмяТЧ = "Запасы";
		ИмяТЧОчистка = "Продукция";
	КонецЕсли;
	Для каждого СтрокаТабличнойЧасти Из Объект[ИмяТЧ] Цикл
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаРезерв;
	КонецЦикла; 
	Для каждого СтрокаТабличнойЧасти Из Объект[ИмяТЧОчистка] Цикл
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область Подбор

&НаКлиенте
Процедура Подбор(Команда)
	
	ИмяТабличнойЧасти 	= "Запасы";
	ЕстьХарактеристики 	= Истина;
	ЕстьПартии 			= Ложь;
	
	ПараметрыПодбора = Новый Структура;
	
	ПараметрыПодбора.Вставить("Период", 			Объект.Дата);
	ПараметрыПодбора.Вставить("Организация", 		Компания);
	ПараметрыПодбора.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиницаРезерв);
	
	ПараметрыПодбора.Вставить("ИспользуютсяСпецификации",	Истина);
	ПараметрыПодбора.Вставить("ИспользуютсяПартии", 		Ложь);
	ПараметрыПодбора.Вставить("ОтображатьКолонкуЦена", 		Ложь);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Сборка") Тогда
		ПараметрыПодбора.Вставить("ЭтоПриходныйДокумент", Ложь);
		ПараметрыПодбора.Вставить("ДоступноРедактированиеСтруктурнойЕдиницы", Истина);
	Иначе
		ПараметрыПодбора.Вставить("ЭтоПриходныйДокумент", Истина);
		ПараметрыПодбора.Вставить("ДоступноРедактированиеСтруктурнойЕдиницы", Ложь);
	КонецЕсли;
	ПараметрыПодбора.Вставить("ИспользуетсяРезервирование", ИспользуетсяРезервирование(Объект.ВидОперации, Объект.ЗаказПокупателя, Объект.ПоложениеЗаказаПокупателя, ИмяТабличнойЧасти));
	
	ТипНоменклатуры = Новый СписокЗначений;
	Для каждого ЭлементМассива Из Элементы[ИмяТабличнойЧасти + "Номенклатура"].ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
				Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
					ТипНоменклатуры.Добавить(ЭлементФиксМассива);
				КонецЦикла; 
			Иначе
				ТипНоменклатуры.Добавить(ЭлементМассива.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыПодбора.Вставить("ТипНоменклатуры", ТипНоменклатуры);
	// Наборы
	ПараметрыПодбора.Вставить("ЭтоНабор", Ложь);
	
	СтатусПартии = Новый СписокЗначений;
	Для каждого ЭлементМассива Из Элементы[ИмяТабличнойЧасти + "Партия"].ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя = "Отбор.Статус" Тогда
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
				Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
					СтатусПартии.Добавить(ЭлементФиксМассива);
				КонецЦикла;
			Иначе
				СтатусПартии.Добавить(ЭлементМассива.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыПодбора.Вставить("СтатусПартии", СтатусПартии);
	
	ПараметрыПодбора.Вставить("УникальныйИдентификаторФормыВладельца", УникальныйИдентификатор);
	
	ОткрытьФорму("ОбщаяФорма.ФормаПодбора", ПараметрыПодбора, ЭтотОбъект);
	
КонецПроцедуры // Подбор()

&НаКлиенте
Процедура ПодборПродукция(Команда)
	
	ИмяТабличнойЧасти 	= "Продукция";
	ЕстьХарактеристики 	= Истина;
	ЕстьПартии 			= Ложь;
	
	ПараметрыПодбора = Новый Структура;
	
	ПараметрыПодбора.Вставить("Период", 			Объект.Дата);
	ПараметрыПодбора.Вставить("Организация",		Компания);
	ПараметрыПодбора.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиницаРезерв);
	
	ПараметрыПодбора.Вставить("ИспользуютсяСпецификации",	Истина);
	ПараметрыПодбора.Вставить("ИспользуютсяПартии", 		Ложь);
	ПараметрыПодбора.Вставить("ОтображатьКолонкуЦена",		Ложь);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Сборка") Тогда
		ПараметрыПодбора.Вставить("ЭтоПриходныйДокумент", Истина);
		ПараметрыПодбора.Вставить("ДоступноРедактированиеСтруктурнойЕдиницы", Ложь);
	Иначе
		ПараметрыПодбора.Вставить("ЭтоПриходныйДокумент", Ложь);
		ПараметрыПодбора.Вставить("ДоступноРедактированиеСтруктурнойЕдиницы", Истина);
	КонецЕсли;
	ПараметрыПодбора.Вставить("ИспользуетсяРезервирование", ИспользуетсяРезервирование(Объект.ВидОперации, Объект.ЗаказПокупателя, Объект.ПоложениеЗаказаПокупателя, ИмяТабличнойЧасти));
	
	ТипНоменклатуры = Новый СписокЗначений;
	Для каждого ЭлементМассива Из Элементы[ИмяТабличнойЧасти + "Номенклатура"].ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
				Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
					ТипНоменклатуры.Добавить(ЭлементФиксМассива);
				КонецЦикла;
			Иначе
				ТипНоменклатуры.Добавить(ЭлементМассива.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыПодбора.Вставить("ТипНоменклатуры", ТипНоменклатуры);
	// Наборы
	ПараметрыПодбора.Вставить("ЭтоНабор", Ложь);
	
	СтатусПартии = Новый СписокЗначений;
	Для каждого ЭлементМассива Из Элементы[ИмяТабличнойЧасти + "Партия"].ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя = "Отбор.Статус" Тогда
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
				Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
					СтатусПартии.Добавить(ЭлементФиксМассива);
				КонецЦикла;
			Иначе
				СтатусПартии.Добавить(ЭлементМассива.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыПодбора.Вставить("СтатусПартии", СтатусПартии);
	
	ПараметрыПодбора.Вставить("УникальныйИдентификаторФормыВладельца", УникальныйИдентификатор);
	
	ОткрытьФорму("ОбщаяФорма.ФормаПодбора", ПараметрыПодбора, ЭтотОбъект);
	
КонецПроцедуры // ПодборПродукция()

&НаСервере
Процедура ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТЧ, ЕстьХарактеристики, ЕстьПартии)
	
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	
	Для каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		
		НоваяСтрока = Объект[ИмяТЧ].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗагрузки);
		
		Если НЕ СкладВШапке = Истина Тогда
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.СтруктурнаяЕдиница) Тогда
				
				НоваяСтрока.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаРезерв;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Характеристики
		СтруктураДанныеНоменклатуры = Новый Структура();
		СтруктураДанныеНоменклатуры.Вставить("Номенклатура", СтрокаЗагрузки.Номенклатура);
		СтруктураДанныеНоменклатуры.Вставить("Характеристика", СтрокаЗагрузки.Характеристика);
		
		СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанныеНоменклатуры);
		
		Если ИмяТабличнойЧасти = "Продукция" Тогда
			
			Если ЗначениеЗаполнено(СтрокаЗагрузки.Номенклатура) 
				Тогда
				НоваяСтрока.ТипНоменклатуры = СтрокаЗагрузки.Номенклатура.ТипНоменклатуры;
			КонецЕсли;
			СтатусПартии = Новый СписокЗначений;
			СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
			СтруктураДанныеНоменклатуры.Вставить("СтатусПартии", СтатусПартии)
		ИначеЕсли ИмяТабличнойЧасти = "Запасы"
			Тогда
			СтатусПартии = Новый СписокЗначений;
			СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
			СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
			СтруктураДанныеНоменклатуры.Вставить("СтатусПартии", СтатусПартии)
		КонецЕсли;
		
		НоваяСтрока.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
		НоваяСтрока.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
		НоваяСтрока.ЗаполнениеХарактеристикиПроверено = Истина;
		
		Если СтруктураДанные.ИспользоватьХарактеристики
			Тогда
			НоваяСтрока.Характеристика = СтруктураДанные.Характеристика;
		КонецЕсли;
		// Конец Характеристики
		
		//Партии
		НоваяСтрока.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
		НоваяСтрока.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
		
		НоваяСтрока.Партия = ?(ЗначениеЗаполнено(СтрокаЗагрузки.Партия), СтрокаЗагрузки.Партия, СтруктураДанные.Партия);
		// Конец Партии
		
		НоваяСтрока.КлючСвязи = УправлениеНебольшойФирмойСервер.СоздатьНовыйКлючСвязи(ЭтотОбъект);
		
		Если НоваяСтрока.Свойство("Спецификация") Тогда
			НоваяСтрока.Спецификация = СтруктураДанные.Спецификация;
		КонецЕсли;
		Если НоваяСтрока.Свойство("ИспользоватьЭтапыПроизводства") Тогда
			НоваяСтрока.ИспользоватьЭтапыПроизводства = СтруктураДанные.ИспользоватьЭтапыПроизводства;
			Если НоваяСтрока.Свойство("ПодразделениеЗавершающегоЭтапа")
				И НоваяСтрока.ИспользоватьЭтапыПроизводства 
				И ПараметрыФормы.ИспользоватьЭтапыПроизводства 
				И ТипСтруктурнойЕдиницы=ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") Тогда
				НоваяСтрока.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
			КонецЕсли; 
		КонецЕсли;

	КонецЦикла;
	
	Если ИмяТабличнойЧасти="Запасы" Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли; 
	
КонецПроцедуры // ПолучитьЗапасыИзХранилища()

// ПодключаемоеОборудование
&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ТекШтрихкод = "";
	ПоказатьВводЗначения(Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект, Новый Структура("ТекШтрихкод", ТекШтрихкод)), ТекШтрихкод, НСтр("ru = 'Введите штрихкод'"));

КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ТекШтрихкод = ?(Результат = Неопределено, ДополнительныеПараметры.ТекШтрихкод, Результат);
    
    
    Если НЕ ПустаяСтрока(ТекШтрихкод) Тогда
        ПолученыШтрихкоды(Новый Структура("Штрихкод, Количество, ДоляСтоимости", ТекШтрихкод, 1, 1));
    КонецЕсли;

КонецПроцедуры // ПоискПоШтрихкоду()

&НаКлиенте
Процедура ПолучитьВес(Команда)
	
	ПодключаемоеОборудованиеУНФКлиент.ПолучениеВесаСЭлектронныхВесовДляТабличнойЧасти(ЭтотОбъект, "Запасы", Ложь);
	
КонецПроцедуры // ПолучитьВес()

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	ПодключаемоеОборудованиеУНФКлиент.ПолучитьДанныеИзТСД(ЭтотОбъект);
	
КонецПроцедуры // ЗагрузитьДанныеИзТСД()
// Конец ПодключаемоеОборудование

#КонецОбласти

#Область ЗаполнениеОбъектов

&НаКлиенте
Процедура СохранитьДокументКакШаблон(Параметр) Экспорт
	
	ЗаполнениеОбъектовУНФКлиент.СохранитьДокументКакШаблон(Объект, ОтображаемыеРеквизиты(), Параметр);
	
КонецПроцедуры

&НаСервере
Функция ОтображаемыеРеквизиты()
	
	Возврат ЗаполнениеОбъектовУНФ.ОтображаемыеРеквизиты(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область КопированиеСтрокТабличныхЧастей

&НаКлиенте
Процедура ПродукцияКопироватьСтроки(Команда)
	
	КопироватьСтроки("Продукция");
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияВставитьСтроки(Команда)
	
	ИмяТабличнойЧасти = "Продукция";
	ВставитьСтроки(ИмяТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКопироватьСтроки(Команда)
	
	КопироватьСтроки("Запасы");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыВставитьСтроки(Команда)
	
	ИмяТабличнойЧасти = "Запасы";
	ВставитьСтроки(ИмяТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьСтроки(ИмяТЧ)
	
	Если КопированиеТабличнойЧастиКлиент.МожноКопироватьСтроки(Объект[ИмяТЧ], Элементы[ИмяТЧ].ТекущиеДанные) Тогда
		КоличествоСкопированных = 0;
		КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных);
		КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОКопированииСтрок(КоличествоСкопированных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(ИмяТЧ)
	
	КоличествоСкопированных = 0;
	КоличествоВставленных = 0;
	ВставитьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных, КоличествоВставленных);
	ОбработатьВставленныеСтроки(ИмяТЧ, КоличествоВставленных);
	КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоСкопированных, КоличествоВставленных);
	
КонецПроцедуры

&НаСервере
Процедура КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных)
	
	КопированиеТабличнойЧастиСервер.Копировать(Объект[ИмяТЧ], Элементы[ИмяТЧ].ВыделенныеСтроки, КоличествоСкопированных);
	
КонецПроцедуры

&НаСервере
Процедура ВставитьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных, КоличествоВставленных)
	
	КопированиеТабличнойЧастиСервер.Вставить(Объект, ИмяТЧ, Элементы, КоличествоСкопированных, КоличествоВставленных);
	ОбработатьВставленныеСтрокиНаСервере(ИмяТЧ, КоличествоВставленных);
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	Если Объект.РучноеРаспределение И ИмяТЧ="Запасы" Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВставленныеСтрокиНаСервере(ИмяТЧ, КоличествоВставленных)
	
	Количество = Объект[ИмяТЧ].Количество();
	
	Для Итератор = 1 По КоличествоВставленных Цикл
		
		СтрокаТабличнойЧасти = Объект[ИмяТЧ][Количество - Итератор];
		
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
		СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
		
		СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
			СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
		КонецЕсли;
		
		Если ИмяТЧ = "Продукция" Тогда
			
			СтрокаТабличнойЧасти.ТипНоменклатуры = СтруктураДанные.ТипНоменклатуры;
			
		ИначеЕсли ИмяТЧ="Операции" Тогда
			
			СтруктураДанные = Новый Структура;
			СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
			СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
				СтруктураДанные.Вставить("Спецификация", СтрокаТабличнойЧасти.Спецификация);
			КонецЕсли; 
			
			СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные, ИмяТЧ);
			
			СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
			Если ЗначениеЗаполнено(СтруктураДанные.Операция) Тогда
				СтрокаТабличнойЧасти.Операция 			= СтруктураДанные.Операция;
				СтрокаТабличнойЧасти.ЕдиницаИзмерения 	= СтруктураДанные.ЕдиницаИзмерения;
				СтрокаТабличнойЧасти.НормаВремени 		= СтруктураДанные.НормаВремени;
			Иначе
				СтрокаТабличнойЧасти.НормаВремени 		= 0;
			КонецЕсли;
			СтрокаТабличнойЧасти.Нормочасы = СтрокаТабличнойЧасти.НормаВремени * СтрокаТабличнойЧасти.КоличествоПлан;
			
			Если Объект.ПоложениеИсполнителя=Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
				СтрокаТабличнойЧасти.Исполнитель = Объект.Исполнитель;
			КонецЕсли; 
			Если Объект.ПоложениеСтруктурнойЕдиницыОпераций=Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиница;
			КонецЕсли;
			
		КонецЕсли; 
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЕдиницаИзмерения) Тогда
			СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИмяТЧ="Операции" Тогда
		ОбновитьКэшиДанныхСервер();
		ЗаполнитьСлужебныеДанныеПослеЧтенияОбъекта(Объект);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВставленныеСтроки(ИмяТЧ, КоличествоВставленных)
	
	Количество = Объект[ИмяТЧ].Количество();
	
	ЕстьСпецификации = Ложь;
	Для Итератор = 1 По КоличествоВставленных Цикл
		СтрокаТабличнойЧасти = Объект[ИмяТЧ][Количество - Итератор];
		УправлениеНебольшойФирмойКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект, СтрокаТабличнойЧасти);
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
			ЕстьСпецификации = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ИмяТЧ="Продукция" Тогда
		Если ЕстьСпецификации Тогда
			ЗапасыНеЗаполнены = Истина;
		КонецЕсли; 
		УстановитьКартинкиЗакладок(ЭтотОбъект);
		ОбновитьСпискиВыбораПродукции();
	ИначеЕсли ИмяТЧ="Запасы" Тогда
		ВывестиОтметкиКонтроля(ЭтотОбъект);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииКопироватьСтроки(Команда)
	
	КопироватьСтроки("Операции");
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииВставитьСтроки(Команда)
	
	ВставитьСтроки("Операции");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область РаспределениеЗапасов

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКартинкиЗакладок(Форма, Заполнено = Неопределено, Распределено = Неопределено)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Заполнено <> (НЕ Форма.ЗапасыНеЗаполнены) Тогда
		Если Заполнено <> Неопределено Тогда
			Форма.ЗапасыНеЗаполнены = НЕ Заполнено;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧЗапасы", "Картинка", ?(Форма.ЗапасыНеЗаполнены, БиблиотекаКартинок.ВниманиеВВидеТреугольника, Новый Картинка));
	КонецЕсли; 	
	
	Если НЕ Объект.РучноеРаспределение Тогда
		Форма.ЗапасыНеРаспределены = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧРаспределениеЗапасов", "Картинка", Новый Картинка);
	ИначеЕсли Распределено <> (НЕ Форма.ЗапасыНеРаспределены) Тогда
		Если Распределено <> Неопределено Тогда
			Форма.ЗапасыНеРаспределены = НЕ Распределено;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧРаспределениеЗапасов", "Картинка", ?(Форма.ЗапасыНеРаспределены, БиблиотекаКартинок.ВниманиеВВидеТреугольника, Новый Картинка));
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПредупрежденияЗапасы", "Видимость", Форма.ЗапасыНеЗаполнены);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПредупрежденияРаспределение", "Видимость", Форма.ЗапасыНеРаспределены);
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьСервер()
	
	Если НЕ Объект.РучноеРаспределение Тогда
		Возврат;
	КонецЕсли; 
	
	ЗаполнитьРеквизитыТЧПоШапке(Объект);
	ПроизводствоСервер.РаспределитьМатериалы(Объект.Продукция, Объект.Запасы, Объект.РаспределениеЗапасов);
	ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	УстановитьКартинкиЗакладок(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРаспределениеЗапасовНаФорме()
	
	Если НЕ Объект.РучноеРаспределение Тогда
		Возврат;
	КонецЕсли; 
	
	ТекущаяСтрока = Элементы.СписокПродукции.ТекущиеДанные;
	БезОтбора = (ТекущаяСтрока=Неопределено ИЛИ ТекущаяСтрока.Значение=0);
	Элементы.РаспределениеЗапасовГруппаПродукция.Видимость = БезОтбора;
	
	Если БезОтбора Тогда
		Строки = Объект.РаспределениеЗапасов;
		ТекущаяПродукция = 0;
	Иначе
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязиПродукция", ТекущаяСтрока.Значение);
		Строки = Объект.РаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
		ТекущаяПродукция = ТекущаяСтрока.Значение;
	КонецЕсли; 
	
	СоответствияПродукции = Новый Соответствие;
	ИмяТабличнойЧасти = "Продукция";
	Для каждого СтрокаПродукция Из Объект.Продукция Цикл
		Если СтрокаПродукция.КлючСвязи=0 И НЕ ТолькоПросмотр Тогда
			СтрокаПродукция.КлючСвязи = УправлениеНебольшойФирмойКлиент.СоздатьНовыйКлючСвязи(ЭтотОбъект);
		КонецЕсли; 
		СоответствияПродукции.Вставить(СтрокаПродукция.КлючСвязи, СтрокаПродукция);
	КонецЦикла; 
	
	РаспределениеЗапасов.Очистить();
	Для каждого СтрокаТабличнойЧасти Из Строки Цикл
		Если НЕ БезОтбора И ТекущаяСтрока.Значение<>СтрокаТабличнойЧасти.КлючСвязиПродукция Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаПродукция = СоответствияПродукции.Получить(СтрокаТабличнойЧасти.КлючСвязиПродукция);
		НоваяСтрока = РаспределениеЗапасов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		ЗаполнитьДанныеПродукцииВСтрокеРаспределения(НоваяСтрока, СтрокаПродукция);
	КонецЦикла;
	
	ПеренумероватьРаспределение(РаспределениеЗапасов);
	ВывестиОтметкиКонтроля(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция Представление(Значение1 = Неопределено, Значение2 = Неопределено, Значение3 = Неопределено)
	
	Результат = "";
	МассивЗначенией = Новый Массив;
	МассивЗначенией.Добавить(Значение1);
	МассивЗначенией.Добавить(Значение2);
	МассивЗначенией.Добавить(Значение3);
	
	Для каждого Значение Из МассивЗначенией Цикл
		Если НЕ ЗначениеЗаполнено(Значение) Тогда
			Продолжить;
		КонецЕсли;
		Результат = Результат + ?(ПустаяСтрока(Результат), "", ", ")+Строка(Значение);
	КонецЦикла; 
	
	Возврат Результат; 
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДанныеТЧРаспределениеЗапасов(Форма)
	
	Если НЕ Форма.Объект.РучноеРаспределение Тогда
		Возврат;
	КонецЕсли; 
	
	РаспределениеНаФорме = Форма.РаспределениеЗапасов;
	РаспределениеТЧ = Форма.Объект.РаспределениеЗапасов;
	ТекущаяПродукция = Форма.ТекущаяПродукция;
	
	Если ТекущаяПродукция>0 Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязиПродукция", ТекущаяПродукция);
		Строки = РаспределениеТЧ.НайтиСтроки(СтруктураОтбора);
		Если Строки.Количество()=0 Тогда
			ИндексВставки = РаспределениеТЧ.Количество();
		Иначе
			ИндексВставки = РаспределениеТЧ.Индекс(Строки[0]);
		КонецЕсли; 
	Иначе
		Строки = Новый Массив;
		РаспределениеТЧ.Очистить();
		ИндексВставки = 0;
	КонецЕсли; 
	Для каждого СтрокаФормы Из РаспределениеНаФорме Цикл
		Если СтрокаФормы.Количество=0 Тогда
			Продолжить;
		КонецЕсли; 
		Если ИндексВставки>=РаспределениеТЧ.Количество() Тогда
			НоваяСтрока = РаспределениеТЧ.Добавить();
		Иначе
			НоваяСтрока = РаспределениеТЧ.Вставить(ИндексВставки);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаФормы);
		ИндексВставки = ИндексВставки + 1;
	КонецЦикла; 
	Для каждого СтрокаТабличнойЧасти Из Строки Цикл
		РаспределениеТЧ.Удалить(СтрокаТабличнойЧасти);
	КонецЦикла;
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДанныеПродукцииВСтрокеРаспределения(СтрокаРаспределения, СтрокаПродукции = Неопределено)
	
	Если СтрокаПродукции=Неопределено Тогда
		СтрокаРаспределения.НоменклатураПродукция = Неопределено;
		СтрокаРаспределения.ХарактеристикаПродукция = Неопределено;
		СтрокаРаспределения.СпецификацияПродукция = Неопределено;
		СтрокаРаспределения.ЕдиницаИзмеренияПродукция = Неопределено;
		СтрокаРаспределения.КоличествоПродукция = 0;
		СтрокаРаспределения.РезервПродукция = 0;
		СтрокаРаспределения.ПартияПродукция = Неопределено;
		СтрокаРаспределения.СтруктурнаяЕдиницаПродукция = Неопределено;
		СтрокаРаспределения.КлючСвязиПродукция = 0;
		СтрокаРаспределения.ЗаказПокупателя = Неопределено; 
	Иначе
		СтрокаРаспределения.НоменклатураПродукция = СтрокаПродукции.Номенклатура;
		СтрокаРаспределения.ХарактеристикаПродукция = СтрокаПродукции.Характеристика;
		СтрокаРаспределения.СпецификацияПродукция = СтрокаПродукции.Спецификация;
		СтрокаРаспределения.ЕдиницаИзмеренияПродукция = СтрокаПродукции.ЕдиницаИзмерения;
		СтрокаРаспределения.КоличествоПродукция = СтрокаПродукции.Количество;
		СтрокаРаспределения.РезервПродукция = СтрокаПродукции.Резерв;
		СтрокаРаспределения.ПартияПродукция = СтрокаПродукции.Партия;
		СтрокаРаспределения.СтруктурнаяЕдиницаПродукция = СтрокаПродукции.СтруктурнаяЕдиница;
		СтрокаРаспределения.КлючСвязиПродукция = СтрокаПродукции.КлючСвязи;
		СтрокаРаспределения.ЗаказПокупателя = СтрокаПродукции.ЗаказПокупателя; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НайтиСуществующуюСтрокуРаспределения(Таблица, КлючСвязиПродукция, ДанныеЗапасов)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("КлючСвязиПродукция", КлючСвязиПродукция);
	СтруктураОтбора.Вставить("Номенклатура", ДанныеЗапасов.Номенклатура);
	СтруктураОтбора.Вставить("Характеристика", ДанныеЗапасов.Характеристика);
	СтруктураОтбора.Вставить("Партия", ДанныеЗапасов.Партия);
	СтруктураОтбора.Вставить("Спецификация", ДанныеЗапасов.Спецификация);
	СтруктураОтбора.Вставить("ЕдиницаИзмерения", ДанныеЗапасов.ЕдиницаИзмерения);
	СтруктураОтбора.Вставить("СтруктурнаяЕдиница", ДанныеЗапасов.СтруктурнаяЕдиница);
	СтруктураОтбора.Вставить("ЗаказПокупателя", ДанныеЗапасов.ЗаказПокупателя);
	Строки = Таблица.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат Строки[0];
	КонецЕсли; 
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПеренумероватьРаспределение(РаспределениеЗапасов)
	
	Ном = 1;
	Для каждого СтрокаТабличнойЧасти Из РаспределениеЗапасов Цикл
		СтрокаТабличнойЧасти.НомерСтроки = Ном;
		Ном = Ном+1;
	КонецЦикла; 	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоРаспределениюСервер()
	
	Если НЕ Объект.РучноеРаспределение Тогда
		Возврат;
	КонецЕсли; 
	
	ПроизводствоСервер.ЗаполнитьПоРаспределению(Объект.Запасы, Объект.РаспределениеЗапасов);
	ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	ЗапасыНеЗаполнены = Ложь;
	ВывестиОтметкиКонтроля(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДанныеЗапасовВСтрокеРаспределения(Объект, СтрокаРаспределения, СтрокаЗапасов)
	
	ЗаполнитьЗначенияСвойств(СтрокаРаспределения, СтрокаЗапасов, ИменаКолонокЗапасов(Объект))
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиОтметкиКонтроля(Форма)
	
	Объект = Форма.Объект;
	Если НЕ Объект.РучноеРаспределение Тогда
		УстановитьКартинкиЗакладок(Форма);
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаРаспределения Из Форма.РаспределениеЗапасов Цикл
		СтрокаРаспределения.ОшибкаКоличество = Ложь;
	КонецЦикла;
	
	ЕстьОшибкиРаспределения = Ложь;
	Для каждого СтрокаКонтроля Из Форма.КэшКонтроляРаспределения Цикл
		Если СтрокаКонтроля.КоличествоЗапасы=СтрокаКонтроля.КоличествоРаспределение Тогда
			Продолжить;
		КонецЕсли;
		ЕстьОшибкиРаспределения = Истина;
		СтруктураОтбора = Новый Структура(ИменаКолонокЗапасов(Объект));
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаКонтроля);
		СтрокиРаспределения = Форма.РаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
			Если (СтрокаКонтроля.КоличествоЗапасы<>СтрокаКонтроля.КоличествоРаспределение) Тогда
				СтрокаРаспределения.ОшибкаКоличество = Истина;
			КонецЕсли; 
		КонецЦикла;  
	КонецЦикла;
	
	УстановитьКартинкиЗакладок(Форма, , НЕ ЕстьОшибкиРаспределения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКэшКонтроляПриИзмененииДанныхСтроки(НовыеДанныеСтроки = Неопределено)
	
	Если НЕ Объект.РучноеРаспределение Тогда
		Возврат;
	КонецЕсли; 
	
	Для ии = 1 По 2 Цикл
		Если ии=1 Тогда
			Если ТипЗнч(СтарыеДанныеСтроки)<>Тип("ФиксированнаяСтруктура") Тогда
				Продолжить;
			КонецЕсли; 
			СтруктураОтбора = Новый Структура(СтарыеДанныеСтроки);
			СтарыеДанныеСтроки = Неопределено;
		Иначе
			Если НовыеДанныеСтроки=Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			СтруктураОтбора = Новый Структура(ИменаКолонокЗапасов(Объект));
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, НовыеДанныеСтроки);
		КонецЕсли;
		СтруктураИтогов = ИтогиПоЗапасам(СтруктураОтбора);
		СтрокиКонтроля = КэшКонтроляРаспределения.НайтиСтроки(СтруктураОтбора);
		Если СтрокиКонтроля.Количество()=0 Тогда
			СтрокаКонтроля = КэшКонтроляРаспределения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКонтроля, СтруктураОтбора);
		Иначе
			СтрокаКонтроля = СтрокиКонтроля[0];
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаКонтроля, СтруктураИтогов);
		ЕстьОшибки = (СтрокаКонтроля.КоличествоЗапасы<>СтрокаКонтроля.КоличествоРаспределение);
		СтрокиНаФорме = РаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрокаНаФорме Из СтрокиНаФорме Цикл
			СтрокаНаФорме.ОшибкаКоличество = ЕстьОшибки;
		КонецЦикла; 
	КонецЦикла;
	
	ЕстьОшибкиРаспределения = Ложь;
	Для каждого СтрокаКонтроля Из КэшКонтроляРаспределения Цикл
		Если СтрокаКонтроля.КоличествоЗапасы<>СтрокаКонтроля.КоличествоРаспределение Тогда
			ЕстьОшибкиРаспределения = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	УстановитьКартинкиЗакладок(ЭтотОбъект, , НЕ ЕстьОшибкиРаспределения);
	ОбновитьТекстПодсказкиРаспределения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекстПодсказкиРаспределения()
	
	ПоясняющийТекстОшибкиРаспределение = "";
	
	ТекущаяСтрока = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	Если ТекущаяСтрока=Неопределено Тогда
		ПоясняющийТекстОшибкиРаспределение = НСтр("ru = 'Результат распределения не соответствует данным о материалах и/или продукции.'");
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(ТекущаяСтрока.НоменклатураПродукция) И НЕ ТекущаяСтрока.ОшибкаКоличество Тогда
		ПоясняющийТекстОшибкиРаспределение = НСтр("ru = 'Результат распределения не соответствует данным о материалах и/или продукции. Для подробной информации выделите строку с предупреждением.'");
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура(ИменаКолонокЗапасов(Объект));
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, ТекущаяСтрока);
	СтруктураИтогов = ИтогиПоЗапасам(СтруктураОтбора);
	ЕдиницаИзмерения = Строка(СтруктураОтбора.ЕдиницаИзмерения);
	
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.НоменклатураПродукция) Тогда
		ПоясняющийТекстОшибкиРаспределение = ПоясняющийТекстОшибкиРаспределение 
		+ НСтр("ru = 'Не указана продукция распределения. '"); 
	КонецЕсли; 
	
	Если ТекущаяСтрока.ОшибкаКоличество Тогда
		ПоясняющийТекстОшибкиРаспределение = ПоясняющийТекстОшибкиРаспределение 
		+ СтрШаблон(НСтр("ru = 'Отличается количество материалов (%1 %2) и распределения (%3 %4). '"), СтруктураИтогов.КоличествоЗапасы, ЕдиницаИзмерения, СтруктураИтогов.КоличествоРаспределение, ЕдиницаИзмерения); 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Функция ИтогиПоЗапасам(СтруктураОтбора)
	
	СтруктураИтогов = Новый Структура("КоличествоЗапасы, КоличествоРаспределение", 0, 0, 0, 0);
	СтрокиЗапасы = Объект.Запасы.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаЗапасы Из СтрокиЗапасы Цикл
		СтруктураИтогов.КоличествоЗапасы = СтруктураИтогов.КоличествоЗапасы + СтрокаЗапасы.Количество;
	КонецЦикла; 
	СтрокиРаспределения = Объект.РаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
		СтруктураИтогов.КоличествоРаспределение = СтруктураИтогов.КоличествоРаспределение + СтрокаРаспределения.Количество;
	КонецЦикла;
	Возврат СтруктураИтогов;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИменаКолонокЗапасов(Объект)
	
	Возврат "Этап, Номенклатура, Характеристика, Партия, Спецификация, ЕдиницаИзмерения"
	+?(Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Сборка") 
	И Объект.ПоложениеСклада=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти"), ", СтруктурнаяЕдиница", "")
	+?(Объект.ПоложениеЗаказаПокупателя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти"), ", ЗаказПокупателя", "");	
	
КонецФункции 

&НаКлиенте
Процедура ОбновитьСпискиВыбораПродукции()
	
	ТекущаяСтрока = Элементы.СписокПродукции.ТекущиеДанные;
	Если ТекущаяСтрока=Неопределено Тогда
		КлючСвязи = 0;
	Иначе
		КлючСвязи = ТекущаяСтрока.Значение;
	КонецЕсли;
	
	ИмяТабличнойЧасти = "Продукция";
	
	СписокПродукции.Очистить();
	Элементы.РаспределениеЗапасовНоменклатураПродукция.СписокВыбора.Очистить();
	Элементы.ЗапасыЗаказПокупателя.СписокВыбора.Очистить();
	Элементы.ОперацииЗаказПокупателя.СписокВыбора.Очистить();
	Элементы.ОперацииНоменклатура.СписокВыбора.Очистить();
	СписокПродукции.Добавить(0, НСтр("ru = 'Вся продукция'"));
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		Если СтрокаТабличнойЧасти.КлючСвязи=0 И НЕ ТолькоПросмотр Тогда
			СтрокаТабличнойЧасти.КлючСвязи = УправлениеНебольшойФирмойКлиент.СоздатьНовыйКлючСвязи(ЭтотОбъект);
		КонецЕсли; 
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		ПредставлениеПродукции = Представление(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика, СтрокаТабличнойЧасти.Спецификация);
		СписокПродукции.Добавить(СтрокаТабличнойЧасти.КлючСвязи, ПредставлениеПродукции);
		Элементы.РаспределениеЗапасовНоменклатураПродукция.СписокВыбора.Добавить(СтрокаТабличнойЧасти.КлючСвязи, ПредставлениеПродукции);
		Если Элементы.ЗапасыЗаказПокупателя.СписокВыбора.НайтиПоЗначению(СтрокаТабличнойЧасти.ЗаказПокупателя)=Неопределено Тогда
			ПредставлениеЗаказа = ?(ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя), Строка(СтрокаТабличнойЧасти.ЗаказПокупателя), НСтр("ru = '<Не указан>'"));
			Элементы.ЗапасыЗаказПокупателя.СписокВыбора.Добавить(СтрокаТабличнойЧасти.ЗаказПокупателя, ПредставлениеЗаказа);
		КонецЕсли; 
	КонецЦикла;
	Элементы.ЗапасыЗаказПокупателя.СписокВыбора.СортироватьПоПредставлению();
	Для каждого ЭлементСписка Из Элементы.ЗапасыЗаказПокупателя.СписокВыбора Цикл
		Элементы.ОперацииЗаказПокупателя.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	Для каждого ЭлементСписка Из Элементы.РаспределениеЗапасовНоменклатураПродукция.СписокВыбора Цикл
		Элементы.ОперацииНоменклатура.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла; 
	
	ТекущаяСтрока = СписокПродукции.НайтиПоЗначению(КлючСвязи);
	Если ТекущаяСтрока=Неопределено Тогда
		ТекущаяСтрока = СписокПродукции[0];
	КонецЕсли; 
	Элементы.СписокПродукции.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
	
	Если Объект.РучноеРаспределение Тогда
		ОбновитьРаспределениеЗапасовНаФорме();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПодсказкуРаспределение(ПрочитатьЗначение = Ложь)
	
	Если ПрочитатьЗначение Тогда
		ОбновитьСпособРаспределенияПоУмолчанию();
	КонецЕсли; 
	
	ЭлементыЗаголовка = Новый Массив;
	ЭлементыЗаголовка.Добавить(
	СтрШаблон(НСтр("ru = 'Правила автоматического распределения материалов: по спецификациям и (или) 
	|пропорционально количеству выпускаемой продукции. Для ручного режима первичное 
	|распределение выполняется по тем же правилам, но есть возможность откорректировать
	|результат. Режим по умолчанию: %1 '"),
	?(РучноеРаспределениеПоУмолчанию, НСтр("ru = 'ручное распределение'"), НСтр("ru = 'автоматическое распределение'"))));
	ЭлементыЗаголовка.Добавить(Новый ФорматированнаяСтрока("изменить", , , , "ИзменитьРежимПоУмолчанию"));
	Элементы.РучноеРаспределениеРасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ЭлементыЗаголовка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСпособРаспределенияПоУмолчанию()
	
	РучноеРаспределениеПоУмолчанию = (Константы.ИспользоватьРучноеРаспределениеМатериаловПоУмолчанию.Получить()=Перечисления.ДаНет.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСпособаРаспределенияПоУмолчанию(Значение, ДополнительныеПараметры) Экспорт
	
	ОбновитьПодсказкуРаспределение(Истина);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОперации

&НаКлиенте
Процедура ОперацииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	
	Если НоваяСтрока И Копирование Тогда
		ТекущаяСтрока.КлючСвязи = 0;
	КонецЕсли;	
	
	Если НоваяСтрока ИЛИ ТекущаяСтрока.КлючСвязи=0 Тогда
		ИмяТабличнойЧасти = "Операции";
		УправлениеНебольшойФирмойКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя="ОперацииИзменитьСостав" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуИзмененияСостава(ВыбраннаяСтрока);
	КонецЕсли; 	
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПередУдалением(Элемент, Отказ)
	
	УправлениеНебольшойФирмойКлиент.УдалитьСтрокиПодчиненнойТабличнойЧасти(ЭтотОбъект, "СоставБригады");
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПриИзменении(Элемент)
	
	ОбновитьАвтоОтметкиПриИзмененииОпераций(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение)<>Тип("Число") Тогда
		Возврат;
	КонецЕсли; 	
	
	СтрокаПродукция = СтрокаПоКлючу(Объект.Продукция, ВыбранноеЗначение);
	Если СтрокаПродукция=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ТекущаяСтрока = Элементы.Операции.ТекущиеДанные;
	
	ЗаполнитьДанныеПродукцииВСтрокеОперации(Объект, ТекущаяСтрока, СтрокаПродукция);
	ПриИзмененииНоменклатурыХарактеристикиСпецификацииОпераций();
	ВыбранноеЗначение = СтрокаПродукция.Номенклатура;
	
	Если Элементы.Найти("ОперацииОперация")<>Неопределено Тогда
		Элементы.Операции.ТекущийЭлемент = Элементы.ОперацииОперация;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНоменклатурыХарактеристикиСпецификацииОпераций()
	
	СтрокаТабличнойЧасти = Элементы.Операции.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	СтруктураДанные.Вставить("Спецификация", СтрокаТабличнойЧасти.Спецификация);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные, "Операции");
	
	Если ЗначениеЗаполнено(СтруктураДанные.Операция) Тогда
		СтрокаТабличнойЧасти.Операция 			= СтруктураДанные.Операция;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения 	= СтруктураДанные.ЕдиницаИзмерения;
		СтрокаТабличнойЧасти.НормаВремени 		= СтруктураДанные.НормаВремени;
		РассчитатьДлительностьОперации();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииОперацияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Операции.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	СтруктураДанные.Вставить("Спецификация", СтрокаТабличнойЧасти.Спецификация);
	СтруктураДанные.Вставить("Операция", 	СтрокаТабличнойЧасти.Операция);
	
	СтруктураРезультат = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные, "Операции");
	
	Если ЗначениеЗаполнено(СтруктураДанные.Операция) Тогда
		СтрокаТабличнойЧасти.ЕдиницаИзмерения 	= СтруктураРезультат.ЕдиницаИзмерения;
		СтрокаТабличнойЧасти.НормаВремени 		= СтруктураРезультат.НормаВремени;
	Иначе
		СтрокаТабличнойЧасти.НормаВремени 		= 0;
	КонецЕсли; 
	РассчитатьДлительностьОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииКоличествоПланПриИзменении(Элемент)
	
	РассчитатьДлительностьОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииНормаВремениПриИзменении(Элемент)
	
	РассчитатьДлительностьОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииЕдиницаИзмеренияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.Операции.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти.ЕдиницаИзмерения = ВыбранноеЗначение Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийКоэффициент = 0;
	Если ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		ТекущийКоэффициент = 1;
	КонецЕсли;
	
	Коэффициент = 0;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		Коэффициент = 1;
	КонецЕсли;
	
	Если ТекущийКоэффициент = 0 И Коэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.ЕдиницаИзмерения, ВыбранноеЗначение);
	ИначеЕсли ТекущийКоэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	ИначеЕсли Коэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(,ВыбранноеЗначение);
	ИначеЕсли ТекущийКоэффициент = 1 И Коэффициент = 1 Тогда
		СтруктураДанные = Новый Структура("ТекущийКоэффициент, Коэффициент", 1, 1);
	КонецЕсли;
	
	Если СтруктураДанные.ТекущийКоэффициент <> 0 Тогда
		СтрокаТабличнойЧасти.НормаВремени = СтрокаТабличнойЧасти.НормаВремени * СтруктураДанные.Коэффициент / СтруктураДанные.ТекущийКоэффициент;
		РассчитатьДлительностьОперации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииИсполнительПриИзменении(Элемент)
	
	ОбновитьКэшиДанных();
	СтрокаТабличнойЧасти = Элементы.Операции.ТекущиеДанные;
	ЗаполнитьСлужебныеДанныеВСтрокеОпераций(Объект, СтрокаТабличнойЧасти);
	Если Объект.ПоложениеСтруктурнойЕдиницыОпераций=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") И НЕ СтрокаТабличнойЧасти.ИсполнительБригада Тогда
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = КэшПодразделения.Получить(СтрокаТабличнойЧасти.Исполнитель);
	ИначеЕсли Объект.ПоложениеСтруктурнойЕдиницыОпераций=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") Тогда
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаОпераций;
	КонецЕсли;
	Если СтрокаТабличнойЧасти.ИсполнительБригада Тогда
		Если СтрокаТабличнойЧасти.КлючСвязи=0 Тогда
			УправлениеНебольшойФирмойКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект, СтрокаТабличнойЧасти);
		КонецЕсли;
		УправлениеНебольшойФирмойКлиентСервер.УдалитьСтрокиПоКлючуСвязи(Объект.СоставБригады, СтрокаТабличнойЧасти);
		ЗаполнитьСоставБригадыНаСервере(СтрокаТабличнойЧасти.Исполнитель, СтрокаТабличнойЧасти.КлючСвязи);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуИзмененияСостава(Идентификатор)
	
	СтрокаТабличнойЧасти = Объект.Операции.НайтиПоИдентификатору(Идентификатор);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязи);
	ПараметрыОткрытия.Вставить("Идентификатор", СтрокаТабличнойЧасти.ПолучитьИдентификатор());
	ПараметрыОткрытия.Вставить("ПоложениеСтруктурнойЕдиницы", Объект.ПоложениеСтруктурнойЕдиницыОпераций);
	ПараметрыОткрытия.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиницаОпераций);
	ПараметрыОткрытия.Вставить("СоставБригады", Новый Массив);
	ПараметрыОткрытия.Вставить("Бригада", СтрокаТабличнойЧасти.Исполнитель);
	ПараметрыОткрытия.Вставить("Организация", Объект.Организация);
	ПараметрыОткрытия.Вставить("Дата", Объект.Дата);
	ПараметрыОткрытия.Вставить("СкрытьТабельныйНомер", Истина);
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязи);
	СтрокиСостава = Объект.СоставБригады.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаТабличнойЧасти Из СтрокиСостава Цикл
		ОписаниеСтроки = Новый Структура("Сотрудник, КТУ, СтруктурнаяЕдиница");
		ЗаполнитьЗначенияСвойств(ОписаниеСтроки, СтрокаТабличнойЧасти);
		ПараметрыОткрытия.СоставБригады.Добавить(ОписаниеСтроки);
	КонецЦикла;
	
	ОткрытьФорму("Документ.СдельныйНаряд.Форма.ФормаИзмененияСоставаБригады", ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставБригадыСотрудникПриИзменении(Элемент)
	
	ОбновитьКэшиДанных();
	СтрокаТабличнойЧасти = Элементы.СоставБригады.ТекущиеДанные;
	СтрокаТабличнойЧасти.КТУ = 1;
	Если Объект.ПоложениеСтруктурнойЕдиницыОпераций=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = КэшПодразделения.Получить(СтрокаТабличнойЧасти.Исполнитель);
	Иначе
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиница;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииНоменклатураОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииЭтапНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииЭтапАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаТабличнойЧасти = Элементы.Операции.ТекущиеДанные;
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.ЗагрузитьЗначения(ЭтапыПроизводства(СтрокаТабличнойЧасти.Спецификация));
		ОбновитьПредставлениеПустогоЭтапа(ДанныеВыбора); 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеВнешнимВидомФормы

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Колонка "Спецификация"
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Услуга);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ПродукцияСпецификация.Имя);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ТекстВторостепеннойНадписи);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Текст", НСтр("ru = '<Не используются>'"));
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	// Резервирование
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ЗаказПокупателя", Документы.ЗаказПокупателя.ПустаяСсылка());
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.ПоложениеЗаказаПокупателя", Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ПродукцияРезерв.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ПродукцияСтруктурнаяЕдиницаТабличнаяЧасть.Имя);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ЗаказПокупателя", Неопределено, ВидСравненияКомпоновкиДанных.Заполнено);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.Резерв", 0, ВидСравненияКомпоновкиДанных.Больше);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.СтруктурнаяЕдиница", Справочники.СтруктурныеЕдиницы.ПустаяСсылка());
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ПродукцияСтруктурнаяЕдиницаТабличнаяЧасть.Имя);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Истина);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЗаказПокупателя", Документы.ЗаказПокупателя.ПустаяСсылка());
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.ПоложениеЗаказаПокупателя", Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыРезерв.Имя);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыСтруктурнаяЕдиницаТабличнаяЧасть.Имя);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЗаказПокупателя", Неопределено, ВидСравненияКомпоновкиДанных.Заполнено);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.Резерв", 0, ВидСравненияКомпоновкиДанных.Больше);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.СтруктурнаяЕдиница", Справочники.СтруктурныеЕдиницы.ПустаяСсылка());
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыСтруктурнаяЕдиницаТабличнаяЧасть.Имя);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Истина);
	
	// Операции
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Операции.ИсполнительБригада", Ложь);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ОперацииИзменитьСостав");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Видимость", Ложь);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Операции.ИсполнительБригада", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.ПоложениеСтруктурнойЕдиницыОпераций", Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ОперацииСтруктурнаяЕдиница");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Видимость", Ложь);
	
	// Этапы производства
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ИспользоватьЭтапыПроизводства", Ложь);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ПродукцияПодразделениеЗавершающегоЭтапа");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ИспользоватьЭтапыПроизводства", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ПодразделениеЗавершающегоЭтапа", Справочники.СтруктурныеЕдиницы.ПустаяСсылка());
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ПродукцияПодразделениеЗавершающегоЭтапа");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Истина);
	
	//Ресурсы
	ПланированиеРесурсовУНФ.УстановитьУсловноеОформлениеРесурсы("РесурсыПредприятия", ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтметкуСтруктурнойЕдиницыРезерва() 
	
	ЕстьРезерв = (Объект.Продукция.Итог("Резерв")>0);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияСтруктурнаяЕдиницаРезерв", "АвтоОтметкаНезаполненного", ЕстьРезерв);
	Если НЕ ЕстьРезерв Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияСтруктурнаяЕдиницаРезерв", "ОтметкаНезаполненного", Ложь);
	КонецЕсли;
	
	ЕстьРезерв = (Объект.Запасы.Итог("Резерв")>0);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыСтруктурнаяЕдиницаРезерв", "АвтоОтметкаНезаполненного", ЕстьРезерв);
	Если НЕ ЕстьРезерв Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыСтруктурнаяЕдиницаРезерв", "ОтметкаНезаполненного", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьУсловноеОформлениеТабличнойЧастиДляХарактеристик()
			
	НаименованиеПоляХарактеристика = "ОперацииХарактеристика";
	
	ЗначениеПоиска = ЭтаФорма.Элементы.Найти(НаименованиеПоляХарактеристика);
	
	Если НЕ ЗначениеПоиска = Неопределено
		Тогда
		ИмяПоляПроверятьЗаполнениеХарактеристики = "Объект.Операции.ПроверятьЗаполнениеХарактеристики";
		ИмяПоляИспользоватьХарактеристики = "Объект.Операции.ИспользоватьХарактеристики";
		ИмяПоляХарактеристики = НаименованиеПоляХарактеристика; 
		
		НовоеУсловноеОформление = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляИспользоватьХарактеристики, Ложь, ВидСравненияКомпоновкиДанных.Равно);

		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляХарактеристики);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Текст", НСтр("ru = '<Не используется>'"));
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти);
		
		НовоеУсловноеОформление = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляПроверятьЗаполнениеХарактеристики, Истина, ВидСравненияКомпоновкиДанных.Равно);
		
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляХарактеристики);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Истина);
		
		НовоеУсловноеОформление = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Операции.Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка(), ВидСравненияКомпоновкиДанных.НеРавно);
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляХарактеристики);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
	КонецЕсли;
	НаименованиеПоляПартия= "ОперацииПартия";
	
	ЗначениеПоискаПартия = ЭтаФорма.Элементы.Найти(НаименованиеПоляПартия);
	
	Если НЕ ЗначениеПоискаПартия = Неопределено
		Тогда
		ИмяПоляПроверятьЗаполнениеПартии = "Объект.Операции.ПроверятьЗаполнениеПартий";
		ИмяПоляИспользоватьПартии = "Объект.Операции.ИспользоватьПартии";
		ИмяПоляПартии = НаименованиеПоляПартия;
		
		НовоеУсловноеОформление = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляИспользоватьПартии, Ложь, ВидСравненияКомпоновкиДанных.Равно);
		
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляПартии);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Текст", НСтр("ru = '<Не используется>'"));
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти);
		
		НовоеУсловноеОформление = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляПроверятьЗаполнениеПартии, Истина, ВидСравненияКомпоновкиДанных.Равно);
		
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляПартии);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Истина);
		
		НовоеУсловноеОформление = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Операции.Партия", Справочники.ПартииНоменклатуры.ПустаяСсылка(), ВидСравненияКомпоновкиДанных.НеРавно);
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляПартии);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	ПараметрыФормы = Форма.ПараметрыФормы;
	
	ЭтоРазборка = (Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка"));
	ЗаказЗавершен = (Объект.СостояниеЗаказа=ПредопределенноеЗначение("Справочник.СостоянияЗаказовНаПроизводство.Завершен"));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыДоляСтоимости", "Видимость", ЭтоРазборка);
	
	УстановитьВидимостьКолонокРезерв(Форма);
	
	// Заказ покупателя
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаказПокупателя", "ТолькоПросмотр", ЗначениеЗаполнено(Объект.ЗаказНаПроизводствоОснование));	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияЗаказПокупателя", "ТолькоПросмотр", ЗначениеЗаполнено(Объект.ЗаказНаПроизводствоОснование));	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЗаказПокупателя", "ТолькоПросмотр", ЗначениеЗаполнено(Объект.ЗаказНаПроизводствоОснование));	
	
	// Распределение
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасов", "ТолькоПросмотр", Форма.ТолькоПросмотр);	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РучноеРаспределение", "ТолькоПросмотр", Форма.ТолькоПросмотр);	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧРаспределениеЗапасов", "Видимость", Объект.РучноеРаспределение);	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЗаполнитьПоРаспределению", "Видимость", Объект.РучноеРаспределение);	
	
	// Параметры выбора
	Если ЭтоРазборка Тогда
		
		// Статус партии.
		ДоступныеСтатусы = Новый Массив;
		ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
		ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
		МассивЗапасРабота = Новый ФиксированныйМассив(ДоступныеСтатусы);
		НовыйМассив = Новый Массив;
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Статус", МассивЗапасРабота));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Дополнительно.ОграничениеСтатуса", МассивЗапасРабота));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ПродукцияПартия.ПараметрыВыбора = НовыеПараметры;
		
		НовыйМассив = Новый Массив;
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Статус", ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы")));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Дополнительно.ОграничениеСтатуса", ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы")));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ЗапасыПартия.ПараметрыВыбора = НовыеПараметры;
		Элементы.РаспределениеЗапасовПартия.ПараметрыВыбора = НовыеПараметры;
		
		Если НЕ Форма.ТолькоПросмотр Тогда
			Для каждого СтрокаЗапасы Из Объект.Запасы Цикл
				Если СтрокаЗапасы.Резерв=0 Тогда
					Продолжить;
				КонецЕсли; 
				СтрокаЗапасы.Резерв = 0;
			КонецЦикла;
		КонецЕсли; 
		
		// Тип номенклатуры.
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ТипНоменклатуры", ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Запас")));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ЭтоНабор", Ложь));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ПродукцияНоменклатура.ПараметрыВыбора = НовыеПараметры;
		
	Иначе
		
		// Статус партии.
		НовыйМассив = Новый Массив;
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Статус", ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы")));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Дополнительно.ОграничениеСтатуса", ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы")));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ПродукцияПартия.ПараметрыВыбора = НовыеПараметры;
		
		ДоступныеСтатусы = Новый Массив;
		ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
		ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
		МассивЗапасРабота = Новый ФиксированныйМассив(ДоступныеСтатусы);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Статус", МассивЗапасРабота));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Дополнительно.ОграничениеСтатуса", МассивЗапасРабота));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ЗапасыПартия.ПараметрыВыбора = НовыеПараметры;
		Элементы.РаспределениеЗапасовПартия.ПараметрыВыбора = НовыеПараметры;
		
		Если НЕ Форма.ТолькоПросмотр Тогда
			Для каждого СтрокаПродукция Из Объект.Продукция Цикл
				Если СтрокаПродукция.Резерв=0 Тогда
					Продолжить;
				КонецЕсли; 
				СтрокаПродукция.Резерв = 0;
			КонецЦикла;
		КонецЕсли; 
		
		// Тип номенклатуры.
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Запас"));
		НовыйМассив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа"));
		НовыйМассив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга"));
		МассивЗапасРабота = Новый ФиксированныйМассив(НовыйМассив);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ТипНоменклатуры", МассивЗапасРабота));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ЭтоНабор", Ложь));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Дополнительно.ОграничениеТипа", МассивЗапасРабота));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ПродукцияНоменклатура.ПараметрыВыбора = НовыеПараметры;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтраницаЗавершениеЗаказа", "Видимость", ЗаказЗавершен);	
	
	// Операции
	ОперацииЗаполнены = (Объект.Операции.Количество()>0);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧОперации", "Видимость", ПараметрыФормы.ИспользоватьТехоперации);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧСоставБригады", "Видимость", ПараметрыФормы.ИспользоватьТехоперации И ОтображатьЗакладкуБригады(Объект));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаполнитьСоставБригады", "Видимость", ПараметрыФормы.ИспользоватьТехоперации И ОтображатьЗакладкуБригады(Объект));
	ОбновитьАвтоОтметкиПриИзмененииОпераций(Форма);
	
	// Этапы
	УправлениеВидимостьюЭтапов(Форма);
	
КонецПроцедуры // УстановитьВидимостьИДоступность()

&НаСервере
Процедура УстановитьВидимостьЭлементовДляМобильногоКлиента()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	ЭтоМобильныйКлиент = Истина;
	РаботаСОтборами.НастроитьПанельОтборовМобильныйКлиент(ЭтотОбъект);
	УправлениеНебольшойФирмойСервер.НастроитьФормуОбъектаМобильныйКлиент(Элементы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьОтПользовательскихНастроек(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	ЭтоРазборка = (Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка"));
	ЭтоСборка = (Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Сборка"));
	
	ЗаказПокупателяЗаполнен = ЗаказПокупателяЗаполнен(Объект);
	
	// Склад
	Если Объект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияСтруктурнаяЕдиницаРезерв", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыСтруктурнаяЕдиницаРезерв", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияСтруктурнаяЕдиницаТабличнаяЧасть", "Видимость", ЭтоРазборка И ЗаказПокупателяЗаполнен);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыСтруктурнаяЕдиницаТабличнаяЧасть", "Видимость", ЭтоСборка И ЗаказПокупателяЗаполнен);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасовСтруктурнаяЕдиница", "Видимость", ЭтоСборка И ЗаказПокупателяЗаполнен);
		Форма.СкладВШапке = Ложь;
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияСтруктурнаяЕдиницаРезерв", "Видимость", ЭтоРазборка И ЗаказПокупателяЗаполнен);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыСтруктурнаяЕдиницаРезерв", "Видимость", ЭтоСборка И ЗаказПокупателяЗаполнен);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияСтруктурнаяЕдиницаТабличнаяЧасть", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыСтруктурнаяЕдиницаТабличнаяЧасть", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасовСтруктурнаяЕдиница", "Видимость", Ложь);
		Форма.СкладВШапке = Истина;
	КонецЕсли;
	
	// Заказ покупателя
	ЗаказВТабличнойЧасти = (Объект.ПоложениеЗаказаПокупателя = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти"));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияЗаказПокупателя", "Видимость", ЗаказВТабличнойЧасти);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЗаказПокупателя", "Видимость", ЗаказВТабличнойЧасти);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасовЗаказПокупателя", "Видимость", ЗаказВТабличнойЧасти);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОперацииЗаказПокупателя", "Видимость", ЗаказВТабличнойЧасти);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЗаказПокупателя", "Видимость", НЕ ЗаказВТабличнойЧасти);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияДобавитьИзЗаказов", "Видимость", ЭтоСборка И ЗаказВТабличнойЧасти);
	УстановитьВидимостьКолонокРезерв(Форма);
	
	// Исполнитель
	Если Объект.ПоложениеИсполнителя = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаИсполнитель", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИсполнительТНСостав", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧСоставБригады", "Видимость", ОтображатьЗакладкуБригады(Объект));
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаИсполнитель", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИсполнительТНСостав", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧСоставБригады", "Видимость", ОтображатьЗакладкуБригады(Объект));
	КонецЕсли;
	
	// Подразделение выполнения операций
	Если Объект.ПоложениеСтруктурнойЕдиницыОпераций = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтруктурнаяЕдиницаОпераций", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОперацииСтруктурнаяЕдиница", "Видимость", НЕ ОтображатьЗакладкуБригады(Объект));
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СоставБригадыСтруктурнаяЕдиница", "Видимость", ОтображатьЗакладкуБригады(Объект));
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтруктурнаяЕдиницаОпераций", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОперацииСтруктурнаяЕдиница", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СоставБригадыСтруктурнаяЕдиница", "Видимость", Ложь);
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимостьОтПользовательскихНастроек()

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьКолонокРезерв(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	ЭтоРазборка = (Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка"));
	ЗаказПокупателяЗаполнен = ЗаказПокупателяЗаполнен(Объект);
	
	// Резерв
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияРезерв", "Видимость", ЗаказПокупателяЗаполнен И ЭтоРазборка);	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыРезерв", "Видимость", ЗаказПокупателяЗаполнен И НЕ ЭтоРазборка);	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыИзменитьРезерв", "Видимость", ЗаказПокупателяЗаполнен И НЕ ЭтоРазборка);	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОтображатьЗакладкуБригады(Объект)
	
	Возврат ЗначениеЗаполнено(Объект.Исполнитель) 
		И ТипЗнч(Объект.Исполнитель)=Тип("СправочникСсылка.Бригады") 
		И Объект.ПоложениеИсполнителя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке");
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостьюЭтапов(Форма)
	
	Элементы = Форма.Элементы;
	
	ЕстьЭтапы = ПроизводствоСЭтапами(Форма);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЭтап", "Видимость", 								ЕстьЭтапы);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасовЭтап", "Видимость", 				ЕстьЭтапы);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОперацииЭтап", "Видимость", 							ЕстьЭтапы);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияПодразделениеЗавершающегоЭтапа", "Видимость", 	ЕстьЭтапы);
	
КонецПроцедуры

#КонецОбласти 

#Область Операции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСлужебныеДанныеОперации(Объект)
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Операции Цикл
		ЗаполнитьСлужебныеДанныеВСтрокеОпераций(Объект, СтрокаТабличнойЧасти);
	КонецЦикла; 	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСлужебныеДанныеВСтрокеОпераций(Объект, СтрокаТабличнойЧасти)
	
	СтрокаТабличнойЧасти.ИсполнительБригада = (ТипЗнч(СтрокаТабличнойЧасти.Исполнитель)=Тип("СправочникСсылка.Бригады"));
	Если СтрокаТабличнойЧасти.ИсполнительБригада Тогда
		СтрокаТабличнойЧасти.ИзменитьСостав = НСтр("ru = 'Изменить состав и КТУ'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.КлючСвязиПродукция) Тогда
		СтрокаПродукции = СтрокаПоКлючу(Объект.Продукция, СтрокаТабличнойЧасти.КлючСвязиПродукция);
		ЗаполнитьДанныеПродукцииВСтрокеОперации(Объект, СтрокаТабличнойЧасти, СтрокаПродукции);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДанныеПродукцииВСтрокеОперации(Объект, СтрокаОперации, СтрокаПродукции)
	
	Если СтрокаПродукции=Неопределено Тогда
		СтруктураИзменяемыхПолей = Новый Структура;
		СтруктураИзменяемыхПолей.Вставить("КлючСвязиПродукция", 0);
		СтруктураИзменяемыхПолей.Вставить("Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
		СтруктураИзменяемыхПолей.Вставить("Характеристика", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));
		СтруктураИзменяемыхПолей.Вставить("Партия", ПредопределенноеЗначение("Справочник.ПартииНоменклатуры.ПустаяСсылка"));
		СтруктураИзменяемыхПолей.Вставить("Спецификация", ПредопределенноеЗначение("Справочник.Спецификации.ПустаяСсылка"));
	Иначе
		СтруктураИзменяемыхПолей = Новый Структура("Номенклатура, Характеристика, Партия, Спецификация, КлючСвязиПродукция");
		ЗаполнитьЗначенияСвойств(СтруктураИзменяемыхПолей, СтрокаПродукции);
		СтруктураИзменяемыхПолей.КлючСвязиПродукция = СтрокаПродукции.КлючСвязи;
	КонецЕсли;
	
	Если Объект.ПоложениеЗаказаПокупателя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		Если СтрокаПродукции=Неопределено Тогда
			СтруктураИзменяемыхПолей.Вставить("ЗаказПокупателя", ПредопределенноеЗначение("Документ.ЗаказПокупателя.ПустаяСсылка"));
		Иначе
			СтруктураИзменяемыхПолей.Вставить("ЗаказПокупателя", СтрокаПродукции.ЗаказПокупателя);
		КонецЕсли; 
	Иначе
		СтруктураИзменяемыхПолей.Вставить("ЗаказПокупателя", Объект.ЗаказПокупателя);
	КонецЕсли;
	
	Для каждого КлючИЗначение Из СтруктураИзменяемыхПолей Цикл
		Если СтрокаОперации[КлючИЗначение.Ключ]=КлючИЗначение.Значение Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаОперации[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьАвтоОтметкиПриИзмененииОпераций(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ОперацииЗаполнены = (Объект.Операции.Количество()>0);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Исполнитель", "ТолькоПросмотр", НЕ ОперацииЗаполнены);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтруктурнаяЕдиницаОпераций", "ТолькоПросмотр", НЕ ОперацииЗаполнены);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Исполнитель", "АвтоОтметкаНезаполненного", ОперацииЗаполнены);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтруктурнаяЕдиницаОпераций", "АвтоОтметкаНезаполненного", ОперацииЗаполнены);
	Если НЕ ОперацииЗаполнены Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Исполнитель", "ОтметкаНезаполненного", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтруктурнаяЕдиницаОпераций", "ОтметкаНезаполненного", Ложь);
	КонецЕсли;
	
	Если НЕ Форма.ТолькоПросмотр И Объект.Исполнитель=Неопределено Тогда
		Объект.Исполнитель = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ЭтапыПроизводства

&НаКлиентеНаСервереБезКонтекста
Функция ПроизводствоСЭтапами(Форма)
	
	Объект = Форма.Объект;
	ПараметрыФормы = Форма.ПараметрыФормы;
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка") Тогда
		Возврат Ложь;
	КонецЕсли;
	Если Форма.ТипСтруктурнойЕдиницы<>ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ПараметрыФормы.ИспользоватьЭтапыПроизводства Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	ЕстьЭтапы = Ложь;
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		Если СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства Тогда
			ЕстьЭтапы = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат ЕстьЭтапы;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтапыПроизводства(Спецификации)
	
	Возврат ПроизводствоСервер.ЭтапыПроизводстваСпецификаций(Спецификации);	
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПредставлениеПустогоЭтапа(Список)
	
	Если Список.Количество()>0 И НЕ ЗначениеЗаполнено(Список[0].Значение) Тогда
		Список[0].Представление = НСтр("ru = '<Без этапов>'");
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакиИспользованияЭтапов()
	
	Если НЕ ПараметрыФормы.ИспользоватьЭтапыПроизводства Тогда
		Возврат;
	КонецЕсли;
	Если Объект.ВидОперации=Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
		Возврат;
	КонецЕсли;
	Если ТипСтруктурнойЕдиницы<>Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
		Возврат;
	КонецЕсли;
	
	МассивСпецификаций = Новый Массив;
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства = Ложь;
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) И МассивСпецификаций.Найти(СтрокаТабличнойЧасти.Спецификация)=Неопределено Тогда
			МассивСпецификаций.Добавить(СтрокаТабличнойЧасти.Спецификация);
		КонецЕсли; 
	КонецЦикла;
	
	Если МассивСпецификаций.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	СпецификацииСПоэтапнымПроизводством = ПроизводствоСервер.СпецификацииСПоэтапнымПроизводством(МассивСпецификаций);
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства = (СпецификацииСПоэтапнымПроизводством.Найти(СтрокаТабличнойЧасти.Спецификация)<>Неопределено);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 
