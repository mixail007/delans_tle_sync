#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает текст запроса информация о сопоставлении номенклатуры
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаИнформацияОСопоставлении() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПродукцияВЕТИС.Ссылка КАК Продукция,
	|	КОЛИЧЕСТВО(ПродукцияВЕТИС.Производитель) КАК КоличествоПроизводителей,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ПродукцияВЕТИС.НомерСтроки = 1
	|				ТОГДА ПродукцияВЕТИС.Производитель
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК Производитель
	|ПОМЕСТИТЬ ВтПроизводители
	|ИЗ
	|	Справочник.ПродукцияВЕТИС.Производители КАК ПродукцияВЕТИС
	|ГДЕ
	|	ПродукцияВЕТИС.Ссылка В(&Продукция)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродукцияВЕТИС.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствиеНоменклатурыВЕТИС.Продукция КАК Продукция,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыВЕТИС.Номенклатура) КАК Количество
	|ПОМЕСТИТЬ СопоставленоПозиций
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
	|ГДЕ
	|	СоответствиеНоменклатурыВЕТИС.Продукция В(&Продукция)
	|
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеНоменклатурыВЕТИС.Продукция
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.ЭтоГруппа КАК ЭтоГруппа,
	|	ЕСТЬNULL(СопоставленоПозиций.Количество, 0) КАК Количество,
	|	СоответствиеНоменклатурыВЕТИС.Номенклатура КАК Номенклатура,
	|	СоответствиеНоменклатурыВЕТИС.Характеристика КАК Характеристика,
	|	ПРЕДСТАВЛЕНИЕ(СоответствиеНоменклатурыВЕТИС.Номенклатура) КАК НоменклатураПредставление,
	|	ЕСТЬNULL(ВтПроизводители.КоличествоПроизводителей, 0) КАК КоличествоПроизводителей,
	|	ВтПроизводители.Производитель КАК Производитель,
	|	ПРЕДСТАВЛЕНИЕ(ВтПроизводители.Производитель) КАК ПроизводительПредставление
	|ИЗ
	|	Справочник.ПродукцияВЕТИС КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленоПозиций КАК СопоставленоПозиций
	|		ПО (СопоставленоПозиций.Продукция = Товары.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПроизводители КАК ВтПроизводители
	|		ПО (ВтПроизводители.Продукция = Товары.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
	|		ПО Товары.Ссылка = СоответствиеНоменклатурыВЕТИС.Продукция
	|			И (СопоставленоПозиций.Количество = 1
	|				ИЛИ СоответствиеНоменклатурыВЕТИС.Порядок = 1)
	|ГДЕ
	|	Товары.Ссылка В(&Продукция)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта" Тогда
		Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
			ПроизвольнаяПродукция = ИнтеграцияВЕТИСВызовСервера.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "Произвольная");
			Если ПроизвольнаяПродукция Тогда
				ВыбраннаяФорма = "ПроизвольнаяПродукция";
				СтандартнаяОбработка = Ложь;
			КонецЕсли;
		Иначе
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ПомощникСоздания";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Наименование");
	
	Поля.Добавить("ЭтоГруппа");
	Поля.Добавить("ТребуетсяЗагрузка");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если Данные.ТребуетсяЗагрузка = Истина Тогда
		СтандартнаяОбработка = Ложь;
		Представление = НСтр("ru = '<не загружено>'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
