
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ЗначениеЗаполнено(Параметры.Ключ) Тогда
		
		Если ТипЗнч(Параметры.Ключ.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		
			КатегорияНоменклатуры = Параметры.Ключ.Владелец.КатегорияНоменклатуры;
			Номенклатура = Параметры.Ключ.Владелец;
			
		ИначеЕсли ТипЗнч(Параметры.Ключ.Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
			
			КатегорияНоменклатуры = Параметры.Ключ.Владелец;
			Номенклатура = Неопределено;
			
		КонецЕсли;
		
	// Копирование объекта.
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования")
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		Если ТипЗнч(Параметры.ЗначениеКопирования.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
			
			Номенклатура = Параметры.ЗначениеКопирования.Владелец;
			КатегорияНоменклатуры = Номенклатура.КатегорияНоменклатуры;
			
		ИначеЕсли ТипЗнч(Параметры.ЗначениеКопирования.Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
			
			КатегорияНоменклатуры = Параметры.ЗначениеКопирования.Владелец;
			Номенклатура = Неопределено;
			
		КонецЕсли;
		
		Объект.ФайлКартинки = Неопределено;
		
	// Заполнение.
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") Тогда
		
		Если Параметры.ЗначенияЗаполнения.Свойство("Владелец") Тогда
			
			Если ТипЗнч(Параметры.ЗначенияЗаполнения.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
				
				Номенклатура = Параметры.ЗначенияЗаполнения.Владелец;
				КатегорияНоменклатуры = Номенклатура.КатегорияНоменклатуры;
				
			ИначеЕсли ТипЗнч(Параметры.ЗначенияЗаполнения.Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
				
				КатегорияНоменклатуры = Параметры.ЗначенияЗаполнения.Владелец;
				Номенклатура = Неопределено;
				
			ИначеЕсли ТипЗнч(Параметры.ЗначенияЗаполнения.Владелец) = Тип("СписокЗначений") Тогда
				
				Если Параметры.ЗначенияЗаполнения.Владелец.Количество() = 1 и ТипЗнч(Параметры.ЗначенияЗаполнения.Владелец[0].Значение) = Тип("Массив")
					Тогда
					
					Для каждого ЭлСписка Из Параметры.ЗначенияЗаполнения.Владелец[0].Значение Цикл
						
						Если ТипЗнч(ЭлСписка) = Тип("СправочникСсылка.Номенклатура") Тогда
							Объект.Владелец = ЭлСписка;
							Номенклатура = ЭлСписка;
							КатегорияНоменклатуры = Номенклатура.КатегорияНоменклатуры;
							Прервать;
						Иначе
							Объект.Владелец = ЭлСписка;
							КатегорияНоменклатуры = ЭлСписка;
						КонецЕсли;
						
					КонецЦикла;
						
                Иначе
					Для каждого ЭлСписка Из Параметры.ЗначенияЗаполнения.Владелец Цикл
						
						Если ТипЗнч(ЭлСписка.Значение) = Тип("СправочникСсылка.Номенклатура") Тогда
							Объект.Владелец = ЭлСписка.Значение;
							Номенклатура = ЭлСписка.Значение;
							КатегорияНоменклатуры = Номенклатура.КатегорияНоменклатуры;
							Прервать;
						Иначе
							Объект.Владелец = ЭлСписка.Значение;
							КатегорияНоменклатуры = ЭлСписка.Значение;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			ИначеЕсли ТипЗнч(Параметры.ЗначенияЗаполнения.Владелец) = Тип("Массив") Тогда
				Для каждого ЭлСписка Из Параметры.ЗначенияЗаполнения.Владелец Цикл
					
					Если ТипЗнч(ЭлСписка) = Тип("СправочникСсылка.Номенклатура") Тогда
						Объект.Владелец = ЭлСписка;
						Номенклатура = ЭлСписка;
						КатегорияНоменклатуры = Номенклатура.КатегорияНоменклатуры;
						Прервать;
					Иначе
						Объект.Владелец = ЭлСписка;
						КатегорияНоменклатуры = ЭлСписка;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		// Элемент создается из формы выбора; заполним значения дополнительных реквизитов значениями отбора.
		Если Параметры.ЗначенияЗаполнения.Свойство("ЗначенияДополнительныхРеквизитов") Тогда
			
			Для каждого Эл Из Параметры.ЗначенияЗаполнения.ЗначенияДополнительныхРеквизитов Цикл
				
				НоваяСтрока = Объект.ДополнительныеРеквизиты.Добавить();
				НоваяСтрока.Свойство = Эл.Ключ;
				НоваяСтрока.Значение = Эл.Значение;
				
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		КатегорияНоменклатуры = Неопределено;
		Номенклатура = Неопределено;
		
	КонецЕсли;
	
	ФормироватьНаименованиеДляПечатиАвтоматически = УстановитьФлагФормироватьНаименованиеДляПечатиАвтоматически(
		Объект.Наименование,
		Объект.НаименованиеДляПечати
	);
	
	Если Не Отказ Тогда
		ЗаполнитьСписокВыбораВладельца();
	КонецЕсли;
	
	РазрешеноРедактированиеЦенДокументов = УправлениеНебольшойФирмойУправлениеДоступомПовтИсп.ЕстьПравоДоступа(
		"Изменение",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.РегистрыСведений.ЦеныНоменклатуры)
	);
	ОбъектМетаданных = Объект.Ссылка.Метаданные();
	РазрешеноРедактированиеХарактеристик =
		УправлениеНебольшойФирмойУправлениеДоступомПовтИсп.ЕстьПравоДоступа(
			"Добавление",
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных))
		ИЛИ УправлениеНебольшойФирмойУправлениеДоступомПовтИсп.ЕстьПравоДоступа(
			"Изменение", 
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных));
	
	ВидимостьКопированияЦен = НЕ ЗначениеЗаполнено(Объект.Ссылка) И (ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Номенклатура")) И РазрешеноРедактированиеЦенДокументов;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрименитьЦеныНоменклатуры", "Видимость", ВидимостьКопированияЦен);
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// УНФ СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиПереопределяемый.ТаблицаСвойствПриСозданииНаСервере(ЭтотОбъект);
	
	Если Не Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.Найти("РедактироватьСоставДополнительныхРеквизитов") = Неопределено
		Тогда
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.РедактироватьСоставДополнительныхРеквизитов.Видимость = Ложь;
	КонецЕсли;
	
	// Шаблоны наименований
	ШаблоныНаименований.ИнициализироватьНастройкиФормированияНаименований(ЭтотОбъект, ШаблоныНаименованийНастройки);
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И НЕ ЗначениеЗаполнено(Объект.Наименование) И Объект.ДополнительныеРеквизиты.Количество() <> 0 Тогда
		// Создается новая номенклатура с передачей значений дополнительных реквизитов.
		СформироватьНаименования();
	КонецЕсли;
	// Конец Шаблоны наименований
	
	ДобавитьПросмотрИзображений();
	
	//+ ГИСМ
	
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		ВидМаркировкиВладельца = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ВидМаркировки");
		Если  ВидМаркировкиВладельца =  Перечисления.ВидыМаркировки.МаркируемаяПродукция
			ИЛИ ВидМаркировкиВладельца = Перечисления.ВидыМаркировки.КонтрольныйИдентификационныйЗнак Тогда
			Элементы.КиЗГИСМGTIN.Видимость = Истина;
		Иначе
			Элементы.КиЗГИСМGTIN.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Элементы.КиЗГИСМGTIN.Видимость = Ложь;
	КонецЕсли;
	
	//-
	
	ЭтоНовый = (Объект.Ссылка.Пустая());
	ЭтоКопия = ЗначениеЗаполнено(Параметры.ЗначениеКопирования);
	
	// Изображения
	Элементы.ПереключениеИзображений.Видимость = ЭтоЗапас();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Шаблоны наименований
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И НЕ ЗначениеЗаполнено(Объект.Наименование) И Объект.ДополнительныеРеквизиты.Количество() <> 0 Тогда
		// Создается новая номенклатура с передачей значений дополнительных реквизитов.
		ОбновитьНаименования();
	КонецЕсли;
	// Конец Шаблоны наименований
	
	// УНФ СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиентПереопределяемыйУНФ.ТаблицаСвойствОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект, Объект); 
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ТипЗнч(ЭтаФорма.ВладелецФормы) = Тип("ТаблицаФормы") и НЕ ЭтаФорма.ВладелецФормы.ТекущиеДанные = Неопределено
		Тогда
		ЭтаФорма.ТолькоПросмотр = ?(ЭтаФорма.ВладелецФормы.ТекущиеДанные.Свойство("ЭтоКатегория") и Не ЭтоНовый, ЭтаФорма.ВладелецФормы.ТекущиеДанные.ЭтоКатегория, Ложь);
		Элементы.СвойстваИЗначения.ТолькоПросмотр = ЭтаФорма.ТолькоПросмотр;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "АдресКартинкиКонтекстноеМенюДобавитьИзображение", "Доступность", Не ЭтаФорма.ТолькоПросмотр);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "АдресКартинкиКонтекстноеМенюУстановитьИзображениеОсновным", "Доступность", Не ЭтаФорма.ТолькоПросмотр);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "АдресКартинкиКонтекстноеМенюУдалитьИзображение", "Доступность", Не ЭтаФорма.ТолькоПросмотр);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаСкопировать", "Доступность", Не ЭтаФорма.ТолькоПросмотр);
		
		Отказ = ЭтоКопия и ЭтаФорма.ТолькоПросмотр;
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// УНФ СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиПереопределяемый.ТаблицаСвойствОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Объект, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// УНФ СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиПереопределяемый.ТаблицаСвойствПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	ИзмененаОсновнаяКартинка = ТекущийОбъект.ФайлКартинки<>Объект.Ссылка.ФайлКартинки;
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПрименитьЦеныНоменклатуры Тогда
		
		ДеталиЗаписи = Новый Структура; 
		ДеталиЗаписи.Вставить("Период", ТекущаяДата());
		ДеталиЗаписи.Вставить("Номенклатура", Объект.Владелец);
		ДеталиЗаписи.Вставить("Характеристика", Объект.Ссылка);
		ДеталиЗаписи.Вставить("Автор", Пользователи.АвторизованныйПользователь());
		
		УправлениеНебольшойФирмойСервер.КопироватьЦеныНоменклатурыВНовуюХарактеристику(ДеталиЗаписи);
		
	КонецЕсли;
	
	// Шаблоны наименований
	ШаблоныНаименований.ЗапомнитьШаблоныДляВидовНаименований(ЭтотОбъект, КатегорияНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПрименитьЦеныНоменклатуры Тогда
		
		Оповестить("ОбновитьСписокЦенНоменклатуры");
		
	КонецЕсли;
	
	Если ЭтоНовый Тогда
		Оповестить("ДобавленаХарактеристика", Объект.Ссылка, Объект.Владелец);
		ЭтоНовый = Ложь;
	КонецЕсли;
	
	Если ИзмененаОсновнаяКартинка Тогда
	
		Оповестить("ОбновленаОсновнаяКартинка", Объект.Ссылка);
	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Шаблоны наименований
	Если ИмяСобытия = "Запись_ШаблоныНаименований" Тогда
		
		ШаблоныНаименованийКлиент.ОбработкаОповещенияШаблоныНаименованийПослеЗаписи(ЭтотОбъект);
		
	ИначеЕсли ИмяСобытия = "Запись_Файл" Тогда
		
		Если Параметр.Свойство("ВладелецФайла") И Параметр.ВладелецФайла = Объект.Ссылка
			ИЛИ НЕ Параметр.Свойство("ВладелецФайла") Тогда
			
			ОбновитьПросмотрИзображений(Источник);
			
			Если Параметр.Свойство("ЭтоНовый") И Параметр.ЭтоНовый
				И НЕ ЗначениеЗаполнено(Объект.ФайлКартинки) Тогда
				
				Если ТипЗнч(Источник) = Тип("Массив") Тогда
					ИзображениеДляПроверки = Источник[0];
				Иначе
					ИзображениеДляПроверки = Источник;
				КонецЕсли;
				Строки = Изображения.НайтиСтроки(Новый Структура("Ссылка", ИзображениеДляПроверки));
				Если Строки.Количество() <> 0 Тогда
					УстановитьИзображениеОсновнымСервер(ИзображениеДляПроверки);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		КатегорияНоменклатуры = ПолучитьНоменклатурнуюГруппуВладельца(Объект.Владелец);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрименитьЦеныНоменклатуры", "Видимость", Истина);
		
	ИначеЕсли ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
		
		КатегорияНоменклатуры = Объект.Владелец;
		
	Иначе
		
		КатегорияНоменклатуры = Неопределено;
		
	КонецЕсли;
	
	// УНФ СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиПереопределяемый.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
	
КонецПроцедуры // ВладелецПриИзменении()

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если ФормироватьНаименованиеДляПечатиАвтоматически Тогда
		
		Объект.НаименованиеДляПечати = Объект.Наименование;
		ШаблоныНаименованийКлиент.НаименованиеПриИзменении(
			ЭтотОбъект,
			Объект.НаименованиеДляПечати,
			ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаДляПечати")
		);
		
	КонецЕсли;
	
	ШаблоныНаименованийКлиент.ОбновитьСписокСвойствОбъектаПриИзмененииСтандартногоРеквизита(ЭтотОбъект, "Наименование");
	
	ШаблоныНаименованийКлиент.НаименованиеПриИзменении(
		ЭтотОбъект,
		Объект.Наименование,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаРабочее")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДляПечатиПриИзменении(Элемент)
	
	Объект.НаименованиеДляПечати = СтрЗаменить(Объект.НаименованиеДляПечати, Символы.ПС, " ");
	ФормироватьНаименованиеДляПечатиАвтоматически = УстановитьФлагФормироватьНаименованиеДляПечатиАвтоматически(Объект.Наименование, Объект.НаименованиеДляПечати);
	
	ШаблоныНаименованийКлиент.НаименованиеПриИзменении(
		ЭтотОбъект,
		Объект.НаименованиеДляПечати,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.НоменклатураДляПечати")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Наименование <> Элементы.Наименование.ТекстРедактирования Тогда
		
		Объект.Наименование = Элементы.Наименование.ТекстРедактирования;
		НаименованиеПриИзменении(Элементы.Наименование);
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_НаименованиеНачалоВыбора", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДляПечатиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.НаименованиеДляПечати <> Элементы.НаименованиеДляПечати.ТекстРедактирования Тогда
		
		Объект.НаименованиеДляПечати = Элементы.НаименованиеДляПечати.ТекстРедактирования;
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_НаименованиеДляПечатиНачалоВыбора", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПрокруткаИзображенийВправоНажатие(Элемент)
	
	СдвигИзображения(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПрокруткаИзображенийВлевоНажатие(Элемент)
	
	СдвигИзображения(-1);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьИзображение(Команда)
	
	ДобавитьИзображениеНаКлиенте();
	
КонецПроцедуры

// Устанавливает основным выбранное изображение (будет показываться первым).
//
&НаКлиенте
Процедура УстановитьИзображениеОсновным(Команда)
	
	Если ТекущееИзображение < 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьИзображениеОсновнымСервер(Изображения[ТекущееИзображение].Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьИзображение(Команда)
	
	ПрисоединенныйФайл = Изображения[ТекущееИзображение].Ссылка;
	Если НЕ ЗначениеЗаполнено(ПрисоединенныйФайл) Тогда
		Возврат;
	КонецЕсли;
	
	ПометитьНаУдалениеПрисоединенныйФайл(ПрисоединенныйФайл);
	
	ОповеститьОбИзменении(ПрисоединенныйФайл);
	Оповестить("Запись_Файл", Новый Структура, ПрисоединенныйФайл);
	
КонецПроцедуры

&НаКлиенте
Процедура ПросмотретьИзображение(Команда)
	
	ПросмотретьПрисоединенныйФайл();
	
КонецПроцедуры // ПросмотретьИзображение()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает данные файла
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеФайла(ФайлКартинки, УникальныйИдентификатор)
	
	ДополнительныеПараметры = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ДополнительныеПараметры.ИдентификаторФормы = УникальныйИдентификатор;
	
	Возврат РаботаСФайлами.ДанныеФайла(ФайлКартинки, ДополнительныеПараметры);
	
КонецФункции // ПолучитьДанныеФайла()

&НаСервере
Функция ЭтоЗапас()
	
	Если ТипЗнч(Объект.Владелец)=Тип("СправочникСсылка.Номенклатура") Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ТипНоменклатуры") = Перечисления.ТипыНоменклатуры.Запас;
	ИначеЕсли ТипЗнч(Объект.Владелец)=Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ТипНоменклатурыПоУмолчанию") = Перечисления.ТипыНоменклатуры.Запас;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
// Функция возвращает номенклутарную группу владельца.
//
Функция ПолучитьНоменклатурнуюГруппуВладельца(НоменклатураВладелец)
	
	Возврат НоменклатураВладелец.КатегорияНоменклатуры;
	
КонецФункции // ПолучитьНоменклатурнуюГруппуВладельца()

&НаСервере
// Процедура - заполняет список выбора для реквизита Владелец.
//
Процедура ЗаполнитьСписокВыбораВладельца()
	
	Элементы.Владелец.СписокВыбора.Очистить();
	Если ЗначениеЗаполнено(КатегорияНоменклатуры) Тогда
		Элементы.Владелец.СписокВыбора.Добавить(КатегорияНоменклатуры);
	КонецЕсли;
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		Элементы.Владелец.СписокВыбора.Добавить(Номенклатура);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСписокВыбораВладельца()

// Присваивает соответствующее значение переменной ФормироватьНаименованиеДляПечатиАвтоматически.
//
&НаКлиентеНаСервереБезКонтекста
Функция УстановитьФлагФормироватьНаименованиеДляПечатиАвтоматически(Наименование, НаименованиеДляПечати)
	
	Возврат (Наименование = НаименованиеДляПечати ИЛИ ПустаяСтрока(НаименованиеДляПечати));
	
КонецФункции // УстановитьФлагФормироватьНаименованиеДляПечатиАвтоматически()

#КонецОбласти

#Область Изображение

// Процедура - обработчик события Нажатие поля АдресКартинки.
//
&НаКлиенте
Процедура Подключаемый_АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ РазрешеноРедактированиеХарактеристик Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущееИзображение >= 0 Тогда
		ПросмотретьПрисоединенныйФайл();
	ИначеЕсли Не ЭтаФорма.ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
		ДобавитьИзображениеНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры // АдресКартинкиНажатие()

&НаСервере
Функция ОтображатьФайлНаФорме(ПрисоединенныйФайл, ПроверятьПометкуУдаления = Истина)
	
	ДопустимыеРасширения = Новый Массив;
	ДопустимыеРасширения.Добавить("png");
	ДопустимыеРасширения.Добавить("jpeg");
	ДопустимыеРасширения.Добавить("jpg");
	
	СвойстваФайла = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПрисоединенныйФайл, "ВладелецФайла,ПометкаУдаления,Расширение");
	
	Если ПроверятьПометкуУдаления И СвойстваФайла.ПометкаУдаления
		ИЛИ СвойстваФайла.ВладелецФайла <> Объект.Ссылка
		ИЛИ ДопустимыеРасширения.Найти(СвойстваФайла.Расширение) = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Вызывается при изменении присоединенного файла.
//
&НаСервере
Процедура ОбновитьПросмотрИзображений(Знач ИзмененныеФайлы = Неопределено)
	
	ОбновитьПросмотрИзображений = (ИзмененныеФайлы = Неопределено);
	
	Если ИзмененныеФайлы <> Неопределено Тогда
		Если ТипЗнч(ИзмененныеФайлы) <> Тип("Массив") Тогда
			ИзмененныеФайлы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИзмененныеФайлы);
		КонецЕсли;
		
		Для каждого Файл Из ИзмененныеФайлы Цикл
			ОбновитьПросмотрИзображений = ОтображатьФайлНаФорме(Файл, Ложь);
			Если ОбновитьПросмотрИзображений Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ОбновитьПросмотрИзображений Тогда
		Возврат;
	КонецЕсли;
	
	ИзмененоОсновноеИзображение = Ложь;
	Если ЗначениеЗаполнено(Объект.ФайлКартинки)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ФайлКартинки, "ПометкаУдаления") Тогда
		Объект.ФайлКартинки = Неопределено;
		Модифицированность = Истина;
	КонецЕсли;
	
	ДобавитьПросмотрИзображений();
	СдвигИзображения(0);
	
КонецПроцедуры

// Выводит на форму картинки из присоединенных файлов.
//
&НаСервере
Процедура ДобавитьПросмотрИзображений()
	
	Изображения.Очистить();
	
	// Присоединенный файл записанный в Объект.ФайлКартинки показываем первым.
	Если НЕ Объект.ФайлКартинки.Пустая() Тогда
		
		ДвоичныеДанныеКартинки = УправлениеНебольшойФирмойСервер.СсылкаНаДвоичныеДанныеФайла(Объект.ФайлКартинки, УникальныйИдентификатор);
		
		Если ДвоичныеДанныеКартинки <> Неопределено Тогда
			НоваяСтрока = Изображения.Вставить(0);
			НоваяСтрока.Ссылка = Объект.ФайлКартинки;
			НоваяСтрока.Адрес = ДвоичныеДанныеКартинки;
		КонецЕсли;
		
	КонецЕсли;
	
	// После присоединенного файла из Объект.ФайлКартинки показываем все остальные подходящие.
	Файлы = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Объект.Ссылка, Файлы);
	Для каждого Файл Из Файлы Цикл
		
		Если НЕ ОтображатьФайлНаФорме(Файл)
			ИЛИ Файл = Объект.ФайлКартинки Тогда
			Продолжить;
		КонецЕсли;
		ДвоичныеДанныеКартинки = УправлениеНебольшойФирмойСервер.СсылкаНаДвоичныеДанныеФайла(Файл, УникальныйИдентификатор);
		
		Если ДвоичныеДанныеКартинки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Изображения.Добавить();
		НоваяСтрока.Ссылка = Файл;
		НоваяСтрока.Адрес = ДвоичныеДанныеКартинки;
		
	КонецЦикла;
	
	КоличествоИзображений = Элементы.Изображение.ПодчиненныеЭлементы.Количество();
	ОтображаемоеИзображение = Элементы.Изображение.ПодчиненныеЭлементы[КоличествоИзображений - 1];
	ОтображаемоеИзображение.ТекстНевыбраннойКартинки = Элементы.АдресКартинки.ТекстНевыбраннойКартинки;
	Если Изображения.Количество() = 0 Тогда
		ТекущееИзображение = -1;
		ОтображаемоеИзображение.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Одинарная);
	Иначе
		ТекущееИзображение = 0;
		ОтображаемоеИзображение.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	КонецЕсли;
	
	УстановитьПрокруткуИзображения();
	ИзображениеВидимостьКоманднойПанели();
	
КонецПроцедуры

// Показывает соседнее с текущим изображение.
// 
// Параметры:
//  Направление - Число - Если = -1 - сдивиг влево; Если = 1 - сдвиг вправо.
//
&НаСервере
Процедура СдвигИзображения(Направление)
	
	НомерЭлемента = ТекущееИзображение + Направление;
	Если НомерЭлемента < 0 ИЛИ НомерЭлемента >= Изображения.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКДанным = СтрШаблон("Изображения[%1].Адрес", НомерЭлемента);
	
	КоличествоИзображений = Элементы.Изображение.ПодчиненныеЭлементы.Количество();
	Если КоличествоИзображений = 1 Тогда
		ПредыдущееИзображение = Элементы.АдресКартинки;
		НовоеИзображениеНомер = 1;
	Иначе
		ПредыдущееИзображение = Элементы.Изображение.ПодчиненныеЭлементы[КоличествоИзображений - 1];
		НовоеИзображениеНомер = Число(СтрЗаменить(ПредыдущееИзображение.Имя, "АдресКартинки", "")) + 1;
	КонецЕсли;
	
	НовоеИзображение = Элементы.Добавить("АдресКартинки" + НовоеИзображениеНомер, Тип("ПолеФормы"), Элементы.Изображение);
	ЗаполнитьЗначенияСвойств(НовоеИзображение, Элементы.АдресКартинки,, "Видимость,Рамка,ПутьКДанным");
	НовоеИзображение.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	НовоеИзображение.ПутьКДанным = ПутьКДанным;
	НовоеИзображение.УстановитьДействие("Нажатие", "Подключаемый_АдресКартинкиНажатие");
	ТекущееИзображение = НомерЭлемента;
	
	Элементы.Переместить(ПредыдущееИзображение.КонтекстноеМеню.ПодчиненныеЭлементы["АдресКартинкиКонтекстноеМенюПросмотретьИзображение"],
	                     НовоеИзображение.КонтекстноеМеню);
	Элементы.Переместить(ПредыдущееИзображение.КонтекстноеМеню.ПодчиненныеЭлементы["АдресКартинкиКонтекстноеМенюУдалитьИзображение"],
	                     НовоеИзображение.КонтекстноеМеню, НовоеИзображение.КонтекстноеМеню.ПодчиненныеЭлементы[0]);
	Элементы.Переместить(ПредыдущееИзображение.КонтекстноеМеню.ПодчиненныеЭлементы["АдресКартинкиКонтекстноеМенюУстановитьИзображениеОсновным"],
	                     НовоеИзображение.КонтекстноеМеню, НовоеИзображение.КонтекстноеМеню.ПодчиненныеЭлементы[0]);
	Элементы.Переместить(ПредыдущееИзображение.КонтекстноеМеню.ПодчиненныеЭлементы["АдресКартинкиКонтекстноеМенюДобавитьИзображение"],
	                     НовоеИзображение.КонтекстноеМеню, НовоеИзображение.КонтекстноеМеню.ПодчиненныеЭлементы[0]);
	
	Если КоличествоИзображений = 1 Тогда
		ПредыдущееИзображение.Видимость = Ложь;
	Иначе
		Элементы.Удалить(ПредыдущееИзображение);
	КонецЕсли;
	УстановитьПрокруткуИзображения();
	ИзображениеВидимостьКоманднойПанели();
	
КонецПроцедуры

// Процедура просмотра картинки
//
&НаКлиенте
Процедура ПросмотретьПрисоединенныйФайл()
	
	ОчиститьСообщения();
	
	Файл = Изображения[ТекущееИзображение].Ссылка;
	ДанныеФайла = ПолучитьДанныеФайла(Файл, УникальныйИдентификатор);
	РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
	
КонецПроцедуры // ПросмотретьПрисоединенныйФайл()

&НаСервере
Процедура ПометитьНаУдалениеПрисоединенныйФайл(ПрисоединенныйФайл)
	
	Если НЕ ЗначениеЗаполнено(ПрисоединенныйФайл) Тогда
		Возврат;
	КонецЕсли;
	
	ПрисоединенныйФайлОбъект = ПрисоединенныйФайл.ПолучитьОбъект();
	ПрисоединенныйФайлОбъект.УстановитьПометкуУдаления(Истина);
	ПрисоединенныйФайлОбъект.Записать();
	
КонецПроцедуры

// Процедура добавления изображения для номенклатуры
//
&НаКлиенте
Процедура ДобавитьИзображениеНаКлиенте()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ТекстВопроса = НСтр("ru='Для выбора изображения необходимо записать объект. Записать?'");
		Ответ = Неопределено;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьИзображениеНаКлиентеЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ДобавитьИзображениеНаКлиентеФрагмент();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеНаКлиентеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Записать();
	Иначе
		Возврат;
	КонецЕсли;
	
	ДобавитьИзображениеНаКлиентеФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеНаКлиентеФрагмент()
	
	Перем ИдентификаторФайла, Фильтр;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ИдентификаторФайла = Новый УникальныйИдентификатор;
		
		Фильтр = НСтр("ru = 'Все картинки (*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf)|*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf"
		+ "|Все файлы(*.*)|*.*"
		+ "|Формат bmp(*.bmp*;*.dib;*.rle)|*.bmp;*.dib;*.rle"
		+ "|Формат GIF(*.gif*)|*.gif"
		+ "|Формат JPEG(*.jpeg;*.jpg)|*.jpeg;*.jpg"
		+ "|Формат PNG(*.png*)|*.png"
		+ "|Формат TIFF(*.tif)|*.tif"
		+ "|Формат icon(*.ico)|*.ico"
		+ "|Формат метафайл(*.wmf;*.emf)|*.wmf;*.emf'");
		
		РаботаСФайламиКлиент.ДобавитьФайлы(Объект.Ссылка, ИдентификаторФайла, Фильтр);
		
		СдвигИзображения(Изображения.Количество()-ТекущееИзображение-1);
	КонецЕсли;
	
КонецПроцедуры // ДобавитьИзображениеНаКлиенте()

// Устанавливает видимость и доступность кнопок перелистывания изображения.
//
&НаСервере
Процедура УстановитьПрокруткуИзображения()
	
	Если Изображения.Количество() <= 1 Тогда
		Элементы.ДекорацияПрокруткаИзображенийВлево.Видимость = Ложь;
		Элементы.ДекорацияПрокруткаИзображенийВправо.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.ДекорацияПрокруткаИзображенийВлево.Видимость = Истина;
		Элементы.ДекорацияПрокруткаИзображенийВправо.Видимость = Истина;
	КонецЕсли;
	
	Если ТекущееИзображение = 0 Тогда
		Элементы.ДекорацияПрокруткаИзображенийВлево.Доступность = Ложь;
		Элементы.ДекорацияПрокруткаИзображенийВправо.Доступность = Истина;
	ИначеЕсли ТекущееИзображение = Изображения.Количество() - 1 Тогда
		Элементы.ДекорацияПрокруткаИзображенийВлево.Доступность = Истина;
		Элементы.ДекорацияПрокруткаИзображенийВправо.Доступность = Ложь;
	Иначе
		Элементы.ДекорацияПрокруткаИзображенийВлево.Доступность = Истина;
		Элементы.ДекорацияПрокруткаИзображенийВправо.Доступность = Истина
	КонецЕсли;
	
	ЭлементАдресКартинки = Элементы.Найти("АдресКартинки1");
	Если ЭлементАдресКартинки <> Неопределено Тогда
		ТекущийЭлемент = ЭлементАдресКартинки;
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает видимость кнопок контекстного меню изображения.
//
&НаСервере
Процедура ИзображениеВидимостьКоманднойПанели()
	
	ЕстьИзображения = Изображения.Количество();
	
	Элементы.АдресКартинкиКонтекстноеМенюУстановитьИзображениеОсновным.Видимость = ЕстьИзображения;
	Элементы.АдресКартинкиКонтекстноеМенюУдалитьИзображение.Видимость = ЕстьИзображения;
	Элементы.АдресКартинкиКонтекстноеМенюПросмотретьИзображение.Видимость = ЕстьИзображения;
	
	ЭтоОсновноеИзображение = Ложь;
	Если ЕстьИзображения Тогда
		ЭтоОсновноеИзображение = (Изображения[ТекущееИзображение].Ссылка = Объект.ФайлКартинки);
	КонецЕсли;
	
	Элементы.АдресКартинкиКонтекстноеМенюУстановитьИзображениеОсновным.Пометка = ЭтоОсновноеИзображение;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИзображениеОсновнымСервер(ПрисоединенныйФайл)
	
	ФайлКартинки = Неопределено;
	
	Если ТипЗнч(ПрисоединенныйФайл) = Тип("Массив") Тогда
		Для каждого Эл Из ПрисоединенныйФайл Цикл
			Если ОтображатьФайлНаФорме(Эл, Ложь) Тогда
				ФайлКартинки = Эл;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ОтображатьФайлНаФорме(ПрисоединенныйФайл, Ложь) Тогда
		ФайлКартинки = ПрисоединенныйФайл;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ФайлКартинки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ФайлКартинки = Неопределено ИЛИ Объект.ФайлКартинки = ФайлКартинки Тогда
		Объект.ФайлКартинки = Неопределено;
	Иначе
		Объект.ФайлКартинки = ФайлКартинки;
	КонецЕсли;
	Модифицированность = Истина;
	
	ИзображениеВидимостьКоманднойПанели();
	
КонецПроцедуры

#КонецОбласти

#Область ШаблоныНаименований

&НаКлиенте
Процедура Подключаемый_НаименованиеНачалоВыбора()
	
	Если ШаблоныНаименованийНастройки.ТребуетсяОбновитьНаименования Тогда
		СформироватьНаименования();
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_НаименованиеОбработкаВыбора", ЭтотОбъект);
	
	ПоказатьВыборИзМеню(
		ОписаниеОповещения,
		ШаблоныНаименованийНастройки.СформированныеНаименования,
		Элементы.Наименование
	);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НаименованиеДляПечатиНачалоВыбора()
	
	Если ШаблоныНаименованийНастройки.ТребуетсяОбновитьНаименования Тогда
		СформироватьНаименования();
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_НаименованиеДляПечатиОбработкаВыбора", ЭтотОбъект);
	
	ПоказатьВыборИзМеню(
		ОписаниеОповещения,
		ШаблоныНаименованийНастройки.СформированныеНаименования,
		Элементы.НаименованиеДляПечати
	);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НаименованиеОбработкаВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранноеЗначение = ОбработатьРезультатВыбора(Результат.Значение, Результат.Представление);
	ШаблоныНаименованийКлиент.НаименованиеОбработкаВыбора(
		ЭтотОбъект,
		ВыбранноеЗначение,
		КатегорияНоменклатуры,
		Объект.Наименование,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаРабочее")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НаименованиеДляПечатиОбработкаВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранноеЗначение = ОбработатьРезультатВыбора(Результат.Значение, Результат.Представление);
	ШаблоныНаименованийКлиент.НаименованиеОбработкаВыбора(
		ЭтотОбъект,
		ВыбранноеЗначение,
		КатегорияНоменклатуры,
		Объект.НаименованиеДляПечати,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаДляПечати")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаименования()
	
	ШаблоныНаименованийКлиент.ОбновитьНаименование(
		ЭтотОбъект,
		Объект.Наименование,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаРабочее")
	);
	
	ШаблоныНаименованийКлиент.ОбновитьНаименование(
		ЭтотОбъект,
		Объект.НаименованиеДляПечати,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаДляПечати")
	);
	
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатВыбора(РезультатЗначение, РезультатПредставление)
	
	Возврат ШаблоныНаименований.ОбработатьРезультатВыбора(РезультатЗначение, РезультатПредставление);
	
КонецФункции

&НаСервере
Процедура СформироватьНаименования()
	
	ШаблоныНаименований.СформироватьНаименования(ЭтотОбъект, КатегорияНоменклатуры);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// УНФ СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры // Подключаемый_РедактироватьСоставСвойств()

&НаКлиенте
Процедура Свойства_ТаблицаСвойстваИЗначенияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	УправлениеСвойствамиКлиентПереопределяемыйУНФ.ТаблицаСвойствОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект, Объект);
	
	// УНФ. Шаблоны наименований
	ШаблоныНаименованийКлиент.ОбновитьСписокСвойствОбъектаПриИзмененииТаблицыДопРеквизитов(ЭтотОбъект, Свойства_ТаблицаСвойстваИЗначения);
	ОбновитьНаименования();
	
КонецПроцедуры

&НаКлиенте
Процедура Свойства_ТаблицаСвойстваИЗначенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	УправлениеСвойствамиКлиентПереопределяемыйУНФ.ТаблицаСвойствПередНачаломДобавления(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура Свойства_ТаблицаСвойстваИЗначенияПередУдалением(Элемент, Отказ)
	
	Модифицированность = Истина;
	
	УправлениеСвойствамиКлиентПереопределяемыйУНФ.ТаблицаСвойствПередУдалением(Элемент, Отказ, Модифицированность);
	
	УправлениеСвойствамиКлиентПереопределяемыйУНФ.ТаблицаСвойствОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект, Объект);
	
	// УНФ. Шаблоны наименований
	ШаблоныНаименованийКлиент.ОбновитьСписокСвойствОбъектаПриИзмененииТаблицыДопРеквизитов(ЭтотОбъект, Свойства_ТаблицаСвойстваИЗначения);
	ОбновитьНаименования();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФормы(Парметр1, Параметр2) Экспорт 

	УправлениеСвойствамиПереопределяемый.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект, , , Истина);  	

КонецПроцедуры


// Конец УНФ СтандартныеПодсистемы.Свойства

#КонецОбласти

