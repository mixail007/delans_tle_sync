
#Область ПеременныеФормы

&НаКлиенте
Перем ФормаДлительнойОперации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат; // Возврат при получении формы для анализа.
	КонецЕсли;
	
	Элементы.СписокЗапасы.РежимВыбора			= Параметры.РежимВыбора;
	Элементы.СписокЗапасыВНаличии.РежимВыбора	= Параметры.РежимВыбора;
	Если Параметры.Свойство("МножественныйВыбор") И ЗначениеЗаполнено(Параметры.МножественныйВыбор) Тогда
		ВыборНесколькихЗначений = Параметры.МножественныйВыбор;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
	// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	ЗагрузкаДанныхИзВнешнегоИсточника.ПриСозданииНаСервере(Метаданные.Справочники.Номенклатура, НастройкиЗагрузкиДанных, ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	
	// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
	Если Элементы.Найти("СписокГрупповоеИзменениеОбъектов") <> Неопределено Тогда
		
		МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.Номенклатура);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокГрупповоеИзменениеОбъектов", "Видимость", МожноРедактировать);
		
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	РаботаСНоменклатурой.ПриСозданииНаСервереФормаСпискаНоменклатуры(ЭтотОбъект, Элементы.ОсновныеКоманды);
	КнопкаПодобратьНоменклатуруИзСервиса = Элементы.Найти("ПодобратьНоменклатуруИзСервиса");
	Если КнопкаПодобратьНоменклатуруИзСервиса <> Неопределено Тогда
		Элементы.Переместить(КнопкаПодобратьНоменклатуруИзСервиса, Элементы.ОсновныеКоманды, Элементы.СкопироватьНоменклатуру);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
	//Определение вида цен и периода для списков
	Если Параметры.Свойство("Период") И ЗначениеЗаполнено(Параметры.Период) Тогда
		ПериодЦен = Параметры.Период;
	КонецЕсли;
	
	
	ЕстьДоступКЦенам = ПравоДоступа("Просмотр", Метаданные.РегистрыСведений.ЦеныНоменклатуры);
	ВосстановитьНастройки();
	ПоказыватьЦены = ПоказыватьЦены И ЕстьДоступКЦенам;
	//Колонка Количество нужна, если включен отбор "В наличии", даже если сама колонка Количество не показывается
	ИспользоватьКоличествоВСписке = ПоказыватьОстатки ИЛИ ОтборОстатки=2;
	
	ПереопределитьТекстЗапросаВСписках();
	
	УстановитьНастройкиФормыПриСоздании(Параметры);
	УстановитьОтборНедействительнаяНоменклатура(ЭтотОбъект);
	
	Элементы.ОтборИерархияКонтекстноеМенюИерархияВключаяВложенные.Пометка = ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить(
		ИмяФормы,
		"ВключаяВложенные",
		Ложь
	);
	
	ЗаполнитьДеревоКатегорий();
	ЗаполнитьДеревоИерархии();
	
	Если ЕстьДоступКЦенам Тогда
		УстановитьВидЦенДляСписков(Ложь);
	КонецЕсли;
	УстановитьПараметрыЗапросовСписков();
	
	Параметры.Свойство("ЭтоЗагрузкаИзВнешнегоИсточника", ЭтоЗагрузкаИзВнешнегоИсточника);
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() Тогда
		Элементы.ПраваяПанель.Видимость = Ложь;
		Элементы.ЕдиницаИзмерения.Видимость = Ложь;
		Элементы.ДокументСчетНаОплатуСоздатьИзНоменклатуры.Видимость = Ложь;
		Элементы.ДокументЗаказНарядСоздатьИзНоменклатуры.Видимость = Ложь;
		Элементы.ДокументАктВыполненныхРаботСоздатьИзНоменклатуры.Видимость = Ложь;
		Элементы.ДокументЗаказПоставщикуСоздатьИзНоменклатуры.Видимость = Ложь;
		Элементы.СписокЗапасыОбработкаНастройкаПрограммыБольшеВозможностейКонтекст.Видимость = Ложь;
	КонецЕсли;
	// Конец МобильноеПриложение
	
	ИнформационныйЦентрСервер.ВывестиКонтекстныеСсылки(ЭтаФорма, Элементы.ИнформационныеСсылки);
	
	// ПодключаемоеОборудование
	ИспользоватьПодключаемоеОборудование = УправлениеНебольшойФирмойПовтИсп.ИспользоватьПодключаемоеОборудование();
	
	Если Параметры.РежимВыбора И Параметры.Свойство("ТекущаяСтрока") И ЗначениеЗаполнено(Параметры.ТекущаяСтрока) 
		И ОтборКатегорииИерархияПереключатель = Перечисления.ВидыОтборовНоменклатуры.ГруппыНоменклатуры
		Тогда
		ГруппаТекущейНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ТекущаяСтрока, "Родитель");
		Если ЗначениеЗаполнено(ГруппаТекущейНоменклатуры) Тогда
			ИдентификаторСтроки = 0;
			ОбщегоНазначенияКлиентСервер.ПолучитьИдентификаторСтрокиДереваПоЗначениюПоля("Значение", ИдентификаторСтроки, ОтборИерархия.ПолучитьЭлементы(), ГруппаТекущейНоменклатуры, Ложь);
			Элементы.ОтборИерархия.ТекущаяСтрока = ИдентификаторСтроки;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("НоменклатураИсточник") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокЗапасы, "Ссылка", Параметры.Отбор.НоменклатураИсточник, ВидСравненияКомпоновкиДанных.НеРавно);
	КонецЕсли; 
	
	// МобильныйКлиент
	УстановитьВидимостьЭлементовДляМобильногоКлиента();
	// КонецМобильныйКлиент
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода");
	// Конец ПодключаемоеОборудование
	
	Если ЭтоЗагрузкаИзВнешнегоИсточника = Истина Тогда
		
		ПодключитьОбработчикОжидания("ПоказатьПомощникЗагрузкиДанныхИзВнешнегоИсточника", 0.2, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройки();
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы ИЛИ РазрешитьЗакрытие ИЛИ Корзина.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВопросПеренестиТовары",
		ЭтотОбъект);
		
	ПоказатьВопрос(Оповещение,
		НСтр("ru = 'В корзине остались несохраненные товары. Продолжить закрытие формы?'"),
		РежимДиалогаВопрос.ОКОтмена,
		0,
		КодВозвратаДиалога.ОК,
		НСтр("ru = 'Подбор товаров'")
	);
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НоменклатураГруппа" И ЗначениеЗаполнено(Параметр) Тогда
		
		НоваяГруппа = Неопределено;
		Если ТипЗнч(Параметр) = Тип("Массив") И Параметр.Количество() <> 0 Тогда
			НоваяГруппа = Параметр[0];
		ИначеЕсли ЗначениеЗаполнено(Параметр) Тогда
			НоваяГруппа = Параметр;
		КонецЕсли;
		
		ЗаполнитьДеревоИерархии(НоваяГруппа);
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_НаборКонстант" Тогда
		
		ПеречитатьЗначенияКонстантОбновитьФорму();
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_КатегорииНоменклатуры" Тогда
		
		НоваяКатегория = Неопределено;
		Если ТипЗнч(Параметр) = Тип("Массив") И Параметр.Количество() <> 0 Тогда
			НоваяКатегория = Параметр[0];
		ИначеЕсли ЗначениеЗаполнено(Параметр) Тогда
			НоваяКатегория = Параметр;
		КонецЕсли;
		
		ЗаполнитьДеревоКатегорий(НоваяКатегория);
		
	КонецЕсли;
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование"
	   И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			//Преобразуем предварительно к ожидаемому формату
			Если Параметр[1] = Неопределено Тогда
				Данные = Новый Структура("Штрихкод, Количество", Параметр[0],1); // Достаем штрихкод из основных данных
			Иначе
				Данные = Новый Структура("Штрихкод, Количество", Параметр[1][1],1); // Достаем штрихкод из дополнительных данных
			КонецЕсли;
			
			ПолученШтрихкод(Данные);
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	ТекущийЭлемент = Элементы[ТекущаяСтраницаНоменклатуры];
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Подключаемый_НажатиеНаИнформационнуюСсылку(Элемент)
	
	ИнформационныйЦентрКлиент.НажатиеНаИнформационнуюСсылку(ЭтаФорма, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеНаСсылкуВсеИнформационныеСсылки(Элемент)
	
	ИнформационныйЦентрКлиент.НажатиеНаСсылкуВсеИнформационныеСсылки(ЭтаФорма.ИмяФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИерархияПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ОтборИерархия.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовоеЗначение = Элементы.ОтборИерархия.ТекущиеДанные.Значение;
	НовоеПредставление = Элементы.ОтборИерархия.ТекущиеДанные.Представление;
	
	Для каждого элОтбора Из ЭтотОбъект[ТекущаяСтраницаНоменклатуры].КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы Цикл
		Если Строка(элОтбора.ЛевоеЗначение) = "Родитель" Тогда
			
			Если элОтбора.ПравоеЗначение = НовоеЗначение И элОтбора.Представление = НовоеПредставление И элОтбора.Использование Тогда
				Возврат;
			Иначе
				УстановитьОтборПоИерархии();
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	//отбор не был установлен ранее, и текущее значение отбора = <Все группы>
	Если НЕ Элементы.ОтборИерархия.ТекущиеДанные.Представление = "<Все группы>" Тогда
		УстановитьОтборПоИерархии();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИерархияНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)

	Если Элемент.ТекущаяСтрока = Неопределено Тогда
		Выполнение = Ложь;
		Возврат;
	КонецЕсли;
	
	СтрокаИерархии = ОтборИерархия.НайтиПоИдентификатору(Элемент.ТекущаяСтрока);
	Если СтрокаИерархии = Неопределено
		Или СтрокаИерархии.Представление = "<Все группы>"
		Или СтрокаИерархии.Представление = "<Нет группы>" Тогда
		
		Выполнение = Ложь;
		Возврат;
	КонецЕсли;
	
	ПараметрыПеретаскивания.Значение = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаИерархии.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИерархияПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив")
		Или ПараметрыПеретаскивания.Значение.Количество() = 0
		Тогда
		
		Возврат;
	КонецЕсли;
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//Группа, в которую переносим
	СтрокаИерархии = ОтборИерархия.НайтиПоИдентификатору(Строка);
	Если СтрокаИерархии = Неопределено Или СтрокаИерархии.Представление = "<Все группы>" Тогда
		Возврат;
	КонецЕсли;
	
	//Строки, которые переносим, в т.ч. это может быть группа из списка иерархии
	спПеренести = Новый Массив;
	Для каждого ВыделеннаяСтрока Из ПараметрыПеретаскивания.Значение Цикл
		
		Если Элементы.СтраницыНоменклатуры.ТекущаяСтраница = Элементы.СтраницаЗапасыВНаличии 
			И ТипЗнч(ВыделеннаяСтрока)=Тип("Число") Тогда
			
			ДанныеСтроки = Элементы[ТекущаяСтраницаНоменклатуры].ДанныеСтроки(ВыделеннаяСтрока);
			спПеренести.Добавить(ДанныеСтроки.Ссылка);
		ИначеЕсли Элементы.СтраницыНоменклатуры.ТекущаяСтраница = Элементы.СтраницаЗапасы И 
			ТипЗнч(ВыделеннаяСтрока) = Тип("СправочникСсылка.Номенклатура") Тогда
			
			спПеренести.Добавить(ВыделеннаяСтрока);
		Иначе
			Возврат;
		КонецЕсли;
		
	КонецЦикла; 
	
	ПеренестиВГруппу(спПеренести, СтрокаИерархии.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИерархияПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	СтандартнаяОбработка = Ложь;
	
	Если Строка = Неопределено Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	СтрокаИерархии = ОтборИерархия.НайтиПоИдентификатору(Строка);
	Если СтрокаИерархии = Неопределено Или СтрокаИерархии.Представление = "<Все группы>" Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	ПараметрыПеретаскивания.ДопустимыеДействия	= ДопустимыеДействияПеретаскивания.Перемещение;
	ПараметрыПеретаскивания.Действие			= ДействиеПеретаскивания.Перемещение;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКатегорииПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	//Категории не перетаскиваем
	ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы[ТекущаяСтраницаНоменклатуры]);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьНедействительную(Команда)
	
	Элементы.ПоказыватьНедействительную.Пометка = Не Элементы.ПоказыватьНедействительную.Пометка;
	
	УстановитьОтборНедействительнаяНоменклатура(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИерархияВключаяВложенные(Команда)
	
	Элементы.ОтборИерархияКонтекстноеМенюИерархияВключаяВложенные.Пометка = Не Элементы.ОтборИерархияКонтекстноеМенюИерархияВключаяВложенные.Пометка;
	УстановитьОтборПоИерархии();
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитДопУпорядочиванияУвеличить(Команда)
	
	ИзменитьПорядокГруппыНоменклатуры(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитДопУпорядочиванияУменьшить(Команда)
	
	ИзменитьПорядокГруппыНоменклатуры(+1);
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитДопУпорядочиванияКатегорииУвеличить(Команда)
	
	ИзменитьПорядокКатегорийНоменклатуры(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитДопУпорядочиванияКатегорииУменьшить(Команда)
	
	ИзменитьПорядокКатегорийНоменклатуры(+1);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеОбработчики

&НаКлиенте
Процедура ПослеВыполненияФоновогоЗадания(Прогресс, ДополнительныеПараметры) Экспорт
	
	Элементы[ТекущаяСтраницаНоменклатуры].Обновить();
	ПоказатьПредупреждение(,Нстр("ru ='Загрузка данных завершена.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНеобходимостьПодключенияОбработчикаОжидания(ДлительнаяОперация)
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ПослеВыполненияФоновогоЗадания(ДлительнаяОперация, Неопределено);
		
	Иначе
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеВыполненияФоновогоЗадания", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Идет загрузка данных.'");
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОтборовСписков

&НаСервере
Процедура УстановитьФиксированныйОтборПоТипамНоменклатуры(ОграничениеТипов)
	
	ВсеТипыНоменклатуры = Новый Соответствие;
	Для каждого ТипНоменклатуры Из Метаданные.Перечисления.ТипыНоменклатуры.ЗначенияПеречисления Цикл
		ВсеТипыНоменклатуры.Вставить(ТипНоменклатуры.Синоним, ТипНоменклатуры.Имя);
	КонецЦикла;
	
	//Установим ограничение по типам на форме
	Если ТипЗнч(ОграничениеТипов) = Тип("Массив") ИЛИ ТипЗнч(ОграничениеТипов) = Тип("ФиксированныйМассив") Тогда
		Для каждого ТипНоменклатуры Из ОграничениеТипов Цикл
			ЭтаФорма["ОтборТип"+ВсеТипыНоменклатуры.Получить(Строка(ТипНоменклатуры))] = Истина;
		КонецЦикла;
	ИначеЕсли ТипЗнч(ОграничениеТипов) = Тип("СписокЗначений") Тогда
		ОграничениеТиповМассив = ОграничениеТипов.ВыгрузитьЗначения();
		Для каждого ТипНоменклатуры Из ОграничениеТиповМассив Цикл
			ЭтаФорма["ОтборТип"+ВсеТипыНоменклатуры.Получить(Строка(ТипНоменклатуры))] = Истина;
		КонецЦикла;
	Иначе
		ЭтаФорма["ОтборТип"+ВсеТипыНоменклатуры.Получить(Строка(ОграничениеТипов))] = Истина;
	КонецЕсли; 
	
	УстановитьОтборПоТипуНоменклатуры();
	
	Элементы.ОтборТипЗапас.Доступность					= ОтборТипЗапас;
	Элементы.ОтборТипУслуга.Доступность					= ОтборТипУслуга;
	Элементы.ОтборТипРабота.Доступность					= ОтборТипРабота;
	Элементы.ОтборТипОперация.Доступность				= ОтборТипОперация;
	Элементы.ОтборТипВидРабот.Доступность				= ОтборТипВидРабот;
	Элементы.ОтборТипСертификат.Доступность				= ОтборТипПодарочныйСертификат;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоИерархии(ГруппаТекущейСтроки = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Номенклатура.Ссылка КАК Значение,
	|	Номенклатура.Наименование КАК Представление,
	|	Номенклатура.ПометкаУдаления КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА Номенклатура.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Картинка,
	|	Номенклатура.РеквизитДопУпорядочиванияУНФ КАК Порядок
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ЭтоГруппа
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок ИЕРАРХИЯ,
	|	Номенклатура.Наименование ИЕРАРХИЯ";
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	СтрокаНетГруппы = Дерево.Строки.Вставить(0);
	СтрокаНетГруппы.Значение	 = Справочники.Номенклатура.ПустаяСсылка();
	СтрокаНетГруппы.Представление= "<Все группы>";
	СтрокаНетГруппы.Картинка = -1;
	
	СтрокаНетГруппы = Дерево.Строки.Добавить();
	СтрокаНетГруппы.Значение	 = Справочники.Номенклатура.ПустаяСсылка();
	СтрокаНетГруппы.Представление= "<Нет группы>";
	СтрокаНетГруппы.Картинка = -1;
	
	ЗначениеВРеквизитФормы(Дерево,"ОтборИерархия");
	
	ИдентификаторСтроки = Неопределено;
	Если ГруппаТекущейСтроки <> Неопределено Тогда
		ИдентификаторСтроки = ИдентификаторСтрокиДереваПоЗначению(ОтборИерархия, ГруппаТекущейСтроки);
	КонецЕсли;
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		Элементы.ОтборИерархия.ТекущаяСтрока = ИдентификаторСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборТипПриИзменении(Элемент)
	
	УстановитьОтборПоТипуНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦеныОтборПриИзменении(Элемент)
	
	УстановитьОтборЦенСервер(ЦенаОт, ЦенаДо);

КонецПроцедуры

&НаСервере
Процедура УстановитьСтраницуСпискаНоменклатуры(НоваяСтраницаНоменклатуры)
	
	Если НоваяСтраницаНоменклатуры <> ТекущаяСтраницаНоменклатуры Тогда
		ПеренестиНаложенныеОтборы(НоваяСтраницаНоменклатуры);
		
		Если НоваяСтраницаНоменклатуры = "СписокЗапасы" Тогда
			Элементы.СтраницыНоменклатуры.ТекущаяСтраница = Элементы.СтраницаЗапасы;
		ИначеЕсли НоваяСтраницаНоменклатуры = "СписокЗапасыВНаличии" Тогда
			Элементы.СтраницыНоменклатуры.ТекущаяСтраница = Элементы.СтраницаЗапасыВНаличии;
		КонецЕсли;
		
		ТекущаяСтраницаНоменклатуры = НоваяСтраницаНоменклатуры;

	КонецЕсли;
	
	УстановитьОтборСпискаОстаткиСервер(ЭтаФорма[ТекущаяСтраницаНоменклатуры], ОтборОстатки);
	
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборЦенСервер(ЦенаОт, ЦенаДо)
	
	Если ЦенаОт<>0 ИЛИ ЦенаДо<>0 Тогда
		ОтборПоЦенам = Истина;
	Иначе
		ОтборПоЦенам = Ложь;
	КонецЕсли;
	
	ПереопределитьТекстЗапросаВСписках();
	УстановитьПараметрыЗапросовСписков();
	
	// Отбор на список товаров.
	ГруппаОтборПоКоличеству = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ЭтаФорма[ТекущаяСтраницаНоменклатуры].КомпоновщикНастроек.Настройки.Отбор.Элементы,
		"ОтборПоЦене",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборПоКоличеству,
		"Цена",
		ВидСравненияКомпоновкиДанных.БольшеИлиРавно,
		ЦенаОт,
		"ЦенаОт",
		ЦенаОт<>0);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборПоКоличеству,
		"Цена",
		ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
		ЦенаДо,
		"ЦенаДо",
		ЦенаДо<>0);
	
	Элементы.ЦеныОтбор.Заголовок = НСтр("ru = 'Цены'")+
		?(ЦенаОт<>0, НСтр("ru = ' от '")+ЦенаОт,"")+
		?(ЦенаДо<>0, НСтр("ru = ' до '")+ЦенаДо,"")
		;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоТипуНоменклатуры()
	
	ОтборТипНоменклатуры = Новый Массив;
	Если ОтборТипЗапас Тогда
		ОтборТипНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Запас"));
	КонецЕсли;
	Если ОтборТипУслуга Тогда
		ОтборТипНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга"));
	КонецЕсли; 
	Если ОтборТипРабота Тогда
		ОтборТипНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа"));
	КонецЕсли; 
	Если ОтборТипВидРабот Тогда
		ОтборТипНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ВидРабот"));
	КонецЕсли; 
	Если ОтборТипОперация Тогда
		ОтборТипНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Операция"));
	КонецЕсли;
	Если ОтборТипПодарочныйСертификат И ПолучитьФункциональнуюОпцию("ИспользоватьПодарочныеСертификаты") Тогда
		ОтборТипНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ПодарочныйСертификат"));
	КонецЕсли;
	
	Использование = ОтборТипНоменклатуры.Количество()>0 ИЛИ Параметры.РежимВыбора;
	
	// Отбор на список номенклатуры
	УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(ЭтаФорма[ТекущаяСтраницаНоменклатуры], 
		"ТипНоменклатуры", ОтборТипНоменклатуры, Использование, ВидСравненияКомпоновкиДанных.ВСписке);
		
	Если Элементы.ОтборНабор.Доступность Тогда
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(ЭтаФорма[ТекущаяСтраницаНоменклатуры], 
			"ЭтоНабор", Истина, ОтборНабор, ВидСравненияКомпоновкиДанных.Равно);
	Иначе
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(ЭтаФорма[ТекущаяСтраницаНоменклатуры], 
			"ЭтоНабор", ОтборНабор, Истина, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли; 

	УстановитьЗаголовокОтборТипыНоменклатуры(ОтборТипНоменклатуры);
	
	НастроитьПанельОтборовМобильныйКлиент();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокОтборТипыНоменклатуры(ОтборТипНоменклатуры)
	
	СтрокаТипов = НСтр("ru = 'Тип:'");
	Для каждого стрТип Из ОтборТипНоменклатуры Цикл
		СтрокаТипов = СтрокаТипов + " " + НРег(стрТип)+",";
	КонецЦикла;
	Если ОтборНабор Тогда
		СтрокаТипов = СтрокаТипов + " Набор,";
	КонецЕсли; 
	СтрокаТипов = Лев(СтрокаТипов,СтрДлина(СтрокаТипов)-1);

	Элементы.ТипНоменклатурыОтбор.ЗаголовокСвернутогоОтображения = СтрокаТипов;
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиНаложенныеОтборы(НоваяСтраницаНоменклатуры)

	Если ТекущаяСтраницаНоменклатуры="" Тогда
		Возврат;
	КонецЕсли;
	
	МассивДляУдаления = Новый Массив;
	НаборЭлементовТекущаяСтраницаОтбор	= ЭтаФорма[ТекущаяСтраницаНоменклатуры].КомпоновщикНастроек.Настройки.Отбор;
	НаборЭлементовНоваяСтраницаОтбор	= ЭтаФорма[НоваяСтраницаНоменклатуры].КомпоновщикНастроек.Настройки.Отбор;
	Для каждого элОтбора Из ЭтаФорма[ТекущаяСтраницаНоменклатуры].КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		
		МассивДляУдаления.Добавить(элОтбора);
		
		Если ТипЗнч(элОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			НовыйЭлОтбораГруппа = НаборЭлементовНоваяСтраницаОтбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НовыйЭлОтбораГруппа, элОтбора);
			Для каждого ПодчиненныйЭлГруппы Из элОтбора.Элементы Цикл
				МассивДляУдаления.Добавить(ПодчиненныйЭлГруппы);
				НовыйЭлОтбора = НовыйЭлОтбораГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(НовыйЭлОтбора, ПодчиненныйЭлГруппы);
			КонецЦикла;
			
			Продолжить;
		КонецЕсли;
		
		//скопируем элементы из текущего отбора в отбор открываемого динамического списка
		НовыйЭлОтбора = НаборЭлементовНоваяСтраницаОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЗаполнитьЗначенияСвойств(НовыйЭлОтбора, элОтбора);
	КонецЦикла;
	
	Для каждого элОтбораУдалить Из МассивДляУдаления Цикл
		Если элОтбораУдалить.Родитель = Неопределено Тогда
			НаборЭлементовТекущаяСтраницаОтбор.Элементы.Удалить(элОтбораУдалить);
		Иначе
			элОтбораУдалить.Родитель.Элементы.Удалить(элОтбораУдалить);
		КонецЕсли;
	КонецЦикла;
	
	//ВЕТИС
	НаборЭлементовНоваяСтраницаОтбор	= ЭтаФорма[НоваяСтраницаНоменклатуры].Отбор.Элементы;
	ОтборВЕТИС = Новый ПолеКомпоновкиДанных("ПодконтрольнаяПродукцияВЕТИС");
	
	ЕстьОтборПоВЕТИС = Ложь;
	
	Для каждого элОтбора Из НаборЭлементовНоваяСтраницаОтбор Цикл
		
		Если элОтбора.ЛевоеЗначение = ОтборВЕТИС Тогда
			ЕстьОтборПоВЕТИС = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЕстьОтборПоВЕТИС Тогда
		
		Для каждого элОтбора Из ЭтаФорма[ТекущаяСтраницаНоменклатуры].Отбор.Элементы Цикл
			
			Если элОтбора.ЛевоеЗначение = ОтборВЕТИС Тогда 
				НовыйЭлОтбора = НаборЭлементовНоваяСтраницаОтбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(НовыйЭлОтбора, элОтбора);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСкладПриИзменении(Элемент)
	
	ВНаличииНаСкладе = ОтборОстатки=1;
	
	УстановитьПараметрыЗапросовСписков();
	
	НоваяСтраницаНоменклатуры = ?(ВНаличииНаСкладе, "СписокЗапасыВНаличии",  "СписокЗапасы");
	УстановитьСтраницуСпискаНоменклатуры(НоваяСтраницаНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидЦенПриИзменении(Элемент)
	
	ОтборВидЦенСервер();
	ОбновитьНадписьПодобраноТоваров();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидЦенСервер()
	
	УстановитьВидЦенДляСписков();
	УстановитьПараметрыЗапросовСписков();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидЦенДляСписков(ОбновлятьВидимость=Истина)
	
	Если НЕ ЗначениеЗаполнено(ОтборВидЦен) Тогда
		ОтборВидЦен = Справочники.ВидыЦен.ПолучитьОсновнойВидЦенПродажи();
	КонецЕсли;
	
	Если ОтборВидЦен.ВалютаЦены<>ВалютаПодбора Тогда
		ПересчитатьЦеныСуммыКорзиныВВалюту(ОтборВидЦен.ВалютаЦены);
	КонецЕсли; 
	
	ВалютаПодбора = ОтборВидЦен.ВалютаЦены;
	ВалютаПредставление = УправлениеНебольшойФирмойПовтИсп.ПолучитьСимвольноеПредставлениеВалюты(ВалютаПодбора);
	СимвольноеПредставление = ?(ЗначениеЗаполнено(ВалютаПредставление), " " + ВалютаПредставление, "");
	ЗаголовокЦен = Строка(ОтборВидЦен) + СимвольноеПредставление;
	Элементы.Цена.Заголовок 					= ЗаголовокЦен;
	Элементы.СписокЗапасыВНаличииЦена.Заголовок = ЗаголовокЦен;
	
	Если ОбновлятьВидимость Тогда
		УстановитьВидимостьИДоступность();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьЦеныСуммыКорзиныВВалюту(НоваяВалюта)
	
	КурсВалюты = УправлениеНебольшойФирмойСервер.ПолучитьКурсыВалют(ВалютаПодбора,НоваяВалюта, ТекущаяДатаСеанса());
	
	Для каждого СтрокаКорзины Из Корзина Цикл
	
		СтрокаКорзины.Цена = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(СтрокаКорзины.Цена, КурсВалюты.КурсНач, КурсВалюты.Курс,	
			КурсВалюты.КратностьНач, КурсВалюты.Кратность);
			
		НоменклатураВДокументахКлиентСервер.РассчитатьСуммуВСтрокеТабличнойЧасти(ЭтотОбъект, СтрокаКорзины, "Корзина");
	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОстаткиПриИзменении(Элемент)
	
	ПереформироватьЗапросыСписков();
	
КонецПроцедуры

&НаСервере
Процедура ПереформироватьЗапросыСписков()
	
	ВНаличииНаСкладе = (ОтборОстатки = 1);
	ИспользоватьКоличествоВСписке = ПоказыватьОстатки ИЛИ ОтборОстатки=2;
	ОтборПоКоличеству = (ОтборОстатки > 0); //ОтборПоКоличеству = Истина при вариантах "В наличии" и "Отсутствуют"
	Если ЦенаОт<>0 ИЛИ ЦенаДо<>0 Тогда
		ОтборПоЦенам = Истина;
	Иначе
		ОтборПоЦенам = Ложь;
	КонецЕсли;
	
	ПереопределитьТекстЗапросаВСписках();
	УстановитьПараметрыЗапросовСписков();
	
	Если ВНаличииНаСкладе Тогда
		НоваяСтраницаНоменклатуры = "СписокЗапасыВНаличии";
	Иначе
		НоваяСтраницаНоменклатуры = "СписокЗапасы";
	КонецЕсли;
	
	УстановитьСтраницуСпискаНоменклатуры(НоваяСтраницаНоменклатуры);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСпискаОстаткиСервер(СписокДляОтбора, ОтборОстатки)
	
	ВидСравненияОтбора	= ?(ОтборОстатки=2, ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, ВидСравненияКомпоновкиДанных.Больше);
	ИспользованиеОтбора = ОтборОстатки<>0;
	
	УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(ЭтаФорма[ТекущаяСтраницаНоменклатуры], 
		"КоличествоОстаток", 0, ИспользованиеОтбора, ВидСравненияОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьКоличествоВСписке(Команда)
	
	ПоказыватьКоличествоВСпискеСервер();
	
КонецПроцедуры

Процедура ПоказыватьКоличествоВСпискеСервер()
	
	ИспользоватьКоличествоВСписке = ПоказыватьОстатки ИЛИ ОтборОстатки=2;
	ПереопределитьТекстЗапросаВСписках();
	
	УстановитьПараметрыЗапросовСписков();
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьЦеныВСписке(Команда)
	
	ПоказыватьЦеныВСпискеСервер();
	
КонецПроцедуры

Процедура ПоказыватьЦеныВСпискеСервер()
	
	ИспользоватьКоличествоВСписке = ПоказыватьОстатки ИЛИ ОтборОстатки=2;
	
	ПереопределитьТекстЗапросаВСписках();
	УстановитьПараметрыЗапросовСписков();

	Если ПоказыватьЦены Тогда
		УстановитьОтборЦеныНаСервере(ЦенаОт, ЦенаДо);
	Иначе
		УстановитьОтборЦеныНаСервере(0, 0);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьПанельОтборов(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	
	СтруктураИменЭлементов = Новый Структура("ФильтрыНастройкиИДопИнфо, ДекорацияРазвернутьОтборы, ПраваяПанель",
	    "ФильтрыНастройкиИДопИнфо","ДекорацияРазвернутьОтборы","ПраваяПанель"
		);
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость, СтруктураИменЭлементов);
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоИерархии()
	
	Если Элементы.ОтборИерархия.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоОтборПоГруппе = ЗначениеЗаполнено(Элементы.ОтборИерархия.ТекущиеДанные.Значение);
	
	Элементы.ОтборИерархияКонтекстноеМенюИзменить.Доступность							= ЭтоОтборПоГруппе;
	Элементы.ОтборИерархияКонтекстноеМенюСкопировать.Доступность						= ЭтоОтборПоГруппе;
	Элементы.ОтборИерархияКонтекстноеМенюУстановитьПометкуУдаленияГруппы.Доступность	= ЭтоОтборПоГруппе;
	
	ПравоеЗначение	= Неопределено;
	Сравнение		= ВидСравненияКомпоновкиДанных.Равно;
	Использование	= Истина;
	Представление	= Элементы.ОтборИерархия.ТекущиеДанные.Представление;
	
	Если ЭтоОтборПоГруппе Тогда
		
		Если Элементы.ОтборИерархияКонтекстноеМенюИерархияВключаяВложенные.Пометка Тогда
			Сравнение = ВидСравненияКомпоновкиДанных.ВИерархии;
		КонецЕсли;
		ПравоеЗначение = Элементы.ОтборИерархия.ТекущиеДанные.Значение;
		
	ИначеЕсли Элементы.ОтборИерархия.ТекущиеДанные.Представление = "<Все группы>" Тогда
		
		Использование = Ложь;
		
	ИначеЕсли Элементы.ОтборИерархия.ТекущиеДанные.Представление = "<Нет группы>" Тогда
		
		ПравоеЗначение = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ЭтотОбъект[ТекущаяСтраницаНоменклатуры],
		"Родитель",
		ПравоеЗначение,
		Сравнение,
		Представление,
		Использование
	);
	
	ОтборИерархияТекущая = ПравоеЗначение;
	
	Если ЭтоМобильныйКлиент Тогда
		НастроитьПанельОтборовМобильныйКлиент();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОтборПоИерархии()
	
	ПравоеЗначение = Элементы.ОтборИерархия.ТекущиеДанные.Значение;
	Сравнение		= ВидСравненияКомпоновкиДанных.Равно;
	Использование	= Ложь;
	Представление	= Элементы.ОтборИерархия.ТекущиеДанные.Представление;
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ЭтотОбъект[ТекущаяСтраницаНоменклатуры],
		"Родитель",
		ПравоеЗначение,
		Сравнение,
		Представление,
		Использование
	);
	
	Если ЭтоМобильныйКлиент Тогда
		НастроитьПанельОтборовМобильныйКлиент();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФиксированныйОтборПоНаборам(ОграничениеЭтоНабор)
	
	Элементы.ОтборНабор.Доступность = Ложь;
	
	УстановитьОтборПоТипуНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область Подбор

&НаСервере
// Функция помещает результаты подбора в хранилище
//
// Возвращает структуру:
//	Структура
//		- Адрес в хранилище, куда помещена выбранная номенклатура (корзина);
//		- Уникальный идентификатор формы владельца, необходим для идентификации при обработке результатов подбора;
//
Функция ЗаписатьПодборВХранилище() 
	
	ПодобранныеТовары = Корзина.Выгрузить();
	АдресКорзиныВХранилище = ПоместитьВоВременноеХранилище(ПодобранныеТовары, УникальныйИдентификаторФормыВладельца);
	Возврат Новый Структура("АдресКорзиныВХранилище, УникальныйИдентификаторФормыВладельца", АдресКорзиныВХранилище, УникальныйИдентификаторФормыВладельца);
	
КонецФункции // ЗаписатьПодборВХранилище()

&НаСервере
Процедура СохранитьНастройки()
	
	СтрокаНастроек = "ПоказыватьЦены,ПоказыватьОстатки";
	ИмяКлючаОбъекта = "СписокНоменклатура";
	
	МассивНастроек = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаНастроек, , Истина, Истина);
	Для каждого Элемент Из МассивНастроек Цикл
		ХранилищеСистемныхНастроек.Сохранить(ИмяКлючаОбъекта,
		ИмяКлючаОбъекта+"_" + Элемент, ЭтаФорма[Элемент]);
	КонецЦикла;
	
	Если Элементы.ОтборКатегорииИерархияПереключатель.Видимость Тогда
		КатегорииНоменклатурыСервер.УстановитьНастройкуВидОтбораНоменклатуры(ОтборКатегорииИерархияПереключатель);
	КонецЕсли; 
	
	ОбщегоНазначения.ХранилищеНастроекДанныхФормСохранить(
		ИмяФормы,
		"ВключаяВложенные",
		Элементы.ОтборИерархияКонтекстноеМенюИерархияВключаяВложенные.Пометка
	);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройки()
	
	СтрокаНастроек = "ПоказыватьЦены,ПоказыватьОстатки";
	ИмяКлючаОбъекта = "СписокНоменклатура";
	
	МассивНастроек = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаНастроек);
	Для каждого Элемент Из МассивНастроек Цикл
		Значение = ХранилищеСистемныхНастроек.Загрузить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+"_" + Элемент);
		Если Значение=Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		ЭтаФорма[Элемент] = Значение;
	КонецЦикла;
	
	ПоказыватьЦены = ПоказыватьЦены И ЕстьДоступКЦенам;
	
	ОтборКатегорииИерархияПереключатель = КатегорииНоменклатурыСервер.ПолучитьНастройкуВидОтбораНоменклатуры();
	
	// Форма открыта с отбором по кокретной категории номенклатуры. 
	// При использовании интерактивного отбора по категории в форме происходит ошибка пересечения отборов 
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("КатегорияНоменклатуры") Тогда
		ОтборКатегорииИерархияПереключатель = Перечисления.ВидыОтборовНоменклатуры.ГруппыНоменклатуры;
		Элементы.ОтборКатегорииИерархияПереключатель.Видимость = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборЦеныНаСервере(ЦенаОт, ЦенаДо)
	
	УстановитьВидимостьИДоступность();
	УстановитьОтборЦенСервер(ЦенаОт, ЦенаДо);
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураВыбора()
	
	Возврат 
		Новый Структура(
		"Номенклатура,
		|Характеристика,
		|ХарактеристикиИспользуются,
		|ТипНоменклатуры,
		|ЭтоНабор,
		|ЕдиницаИзмерения,
		|Цена,
		|СтавкаНДС,
		|Количество,
		|СтранаПроисхождения");
		
КонецФункции

&НаКлиенте
Процедура ДобавитьНоменклатуруВКорзину(СтруктураВыбора)
	
	СтруктураПоискаВКорзине = Новый Структура;
	СтруктураПоискаВКорзине.Вставить("Номенклатура", СтруктураВыбора.Номенклатура);
	Если СтруктураВыбора.Характеристика=Неопределено ИЛИ СтруктураВыбора.Характеристика="<Без характеристики>" Тогда
		СтруктураПоискаВКорзине.Вставить("Характеристика", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));
	Иначе
		СтруктураПоискаВКорзине.Вставить("Характеристика", СтруктураВыбора.Характеристика);
	КонецЕсли;
	НайденныеСтроки = Корзина.НайтиСтроки(СтруктураПоискаВКорзине);
	Если НайденныеСтроки.Количество()=0 Тогда
		СтрокаКорзины = Корзина.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКорзины, СтруктураВыбора);
	Иначе
		СтрокаКорзины = НайденныеСтроки[0];
		СтрокаКорзины.Количество = СтрокаКорзины.Количество + СтруктураВыбора.Количество;
		//Если цена изменилась - перезаполняем новым значением
		СтрокаКорзины.Цена = СтруктураВыбора.Цена;
	КонецЕсли;
	
	Если НЕ ПоказыватьЦены Тогда
		СтруктураВыбора.Вставить("ВидЦен",			ОтборВидЦен);
		СтруктураВыбора.Вставить("ВалютаДокумента", ВалютаПодбора);
		СтруктураВыбора.Вставить("Коэффициент",		1);
		Если СтруктураВыбора.Свойство("Характеристика") И СтруктураВыбора.Характеристика=Неопределено Тогда
			СтруктураВыбора.Вставить("Характеристика", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));			
		КонецЕсли;
		СтрокаКорзины.Цена = ПолучитьЦенуНоменклатурыПоВидуЦенНаСервере(СтруктураВыбора);
	КонецЕсли;
	
	НоменклатураВДокументахКлиентСервер.РассчитатьСуммуВСтрокеТабличнойЧасти(ЭтотОбъект, СтрокаКорзины, "Корзина");
	
	ОбновитьНадписьПодобраноТоваров();

	ПоказатьОповещениеПользователя(НСтр("ru = 'Подбор товаров'")
		,
		,
		НСтр("ru = 'Товар "+СтруктураВыбора.Номенклатура+","+СтруктураВыбора.Характеристика+" добавлен в корзину'"), БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЦенуНоменклатурыПоВидуЦенНаСервере(СтруктураВыбора)
	
	//Текущую дату получаем на сервере, используя ТекущаяДатаСеанса, чтобы учесть разницу часовых поясов
	СтруктураВыбора.Вставить("ДатаОбработки", ТекущаяДатаСеанса());
	
	Возврат УправлениеНебольшойФирмойСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураВыбора);
	
КонецФункции

&НаКлиенте
Процедура Восстановить(Команда)
	
	СписокОтложенных = ПолучитьСписокОтложенных();
	Для каждого стр Из СписокОтложенных Цикл
		стр.Представление = СтрПолучитьСтроку(стр.Значение,1);
	КонецЦикла;
	
	Если СписокОтложенных.Количество()=0 Тогда
	    ОткрытьКорзинуПродолжить(Неопределено);
	Иначе
		ПоказатьВыборИзМеню(Новый ОписаниеОповещения("ВосстановитьЗавершение",ЭтотОбъект), СписокОтложенных);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьЗавершение(ВыбранныйЭлемент, Параметры) Экспорт

	ПараметрыКорзины=Неопределено;
	Если ВыбранныйЭлемент<>Неопределено И ВыбранныйЭлемент.Значение<>Неопределено Тогда
		ПараметрыКорзины = ВосстановитьНаСервере(ВыбранныйЭлемент.Значение);
		ОткрытьКорзинуПродолжить(ПараметрыКорзины);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВосстановитьНаСервере(КлючНастроек)
	
	СтрокаНастроек = ХранилищеНастроекДанныхФорм.Загрузить("КорзинаНоменклатура", КлючНастроек);
	Корзина.Загрузить(ЗначениеИзСтрокиВнутр(СтрокаНастроек));
	ХранилищеНастроекДанныхФорм.Удалить("КорзинаНоменклатура", КлючНастроек, ПользователиИнформационнойБазы.ТекущийПользователь().Имя);
	
	ЧислоОтложенныхКорзин = ЧислоОтложенныхКорзин - 1;
	Если ЧислоОтложенныхКорзин>0 Тогда
		Элементы.ДекорацияКорзина.Картинка = БиблиотекаКартинок.ТележкаОтложена;
	Иначе
		Элементы.ДекорацияКорзина.Картинка = БиблиотекаКартинок.ТележкаПустая;
	КонецЕсли;
	
	ПараметрыКорзины = ЗаписатьПодборВХранилище();
	Возврат ПараметрыКорзины;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокОтложенных()
	
	СписокОтложенных = ХранилищеНастроекДанныхФорм.ПолучитьСписок("КорзинаНоменклатура");
	ЧислоОтложенныхКорзин = СписокОтложенных.Количество();
	Возврат СписокОтложенных;
	
КонецФункции

&НаКлиенте
Процедура СписокЗапасыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ВыбратьВСписке(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВСписке(СтандартнаяОбработка)
	
	Если Элементы[ТекущаяСтраницаНоменклатуры].ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.РежимВыбора Тогда
		Если ЭтотОбъект.ЗакрыватьПриВыборе = Ложь И Тип(ЭтотОбъект.ВладелецФормы) = Тип("ТаблицаФормы") 
			И ЭтотОбъект.ВладелецФормы.Имя = "ValueList" Тогда //подбор в платформенный список значений
			
			ОповеститьОВыборе(Элементы[ТекущаяСтраницаНоменклатуры].ТекущиеДанные.Номенклатура);
			
		ИначеЕсли ВыборНесколькихЗначений Тогда
			
			МассивВыбранныхЗначений = Новый Массив;
			Для каждого ВыделеннаяСтрока Из Элементы[ТекущаяСтраницаНоменклатуры].ВыделенныеСтроки Цикл
				Если ТипЗнч(ВыделеннаяСтрока) = Тип("СправочникСсылка.Номенклатура") Тогда
					
					МассивВыбранныхЗначений.Добавить(ВыделеннаяСтрока);
					
				ИначеЕсли ТипЗнч(ВыделеннаяСтрока) = Тип("Число") Тогда
					
					МассивВыбранныхЗначений.Добавить(Элементы[ТекущаяСтраницаНоменклатуры].ДанныеСтроки(ВыделеннаяСтрока).Номенклатура);
					
				КонецЕсли;
			КонецЦикла;
			
			ОповеститьОВыборе(МассивВыбранныхЗначений);
		Иначе
			ОповеститьОВыборе(Элементы[ТекущаяСтраницаНоменклатуры].ТекущиеДанные.Номенклатура);
		КонецЕсли;
	Иначе
		ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", Новый Структура("Ключ", Элементы[ТекущаяСтраницаНоменклатуры].ТекущиеДанные.Номенклатура));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СтандартныеКомандыСпискаНоменклатуры

&НаКлиенте
Процедура СписокЗапасыВНаличииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьВСписке(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНоменклатуру()
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "СозданиеФормыНоменклатура");
	
	Если Элементы.ОтборИерархия.ТекущиеДанные<>Неопределено Тогда
		РодительЗначение = Элементы.ОтборИерархия.ТекущиеДанные.Значение;
	Иначе
		РодительЗначение = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
	КонецЕсли; 
	ЗначенияЗаполнения = Новый Структура("Родитель", РодительЗначение);
		
	ЗаполнениеТип = Новый Массив;
	Если ОтборТипЗапас Тогда
	    ЗаполнениеТип.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Запас"));
	КонецЕсли;
	Если ОтборТипУслуга Тогда
		ЗаполнениеТип.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга"));
	КонецЕсли;
	Если ОтборТипРабота Тогда
		ЗаполнениеТип.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа"));
	КонецЕсли;
	Если ОтборТипВидРабот Тогда
		ЗаполнениеТип.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ВидРабот"));
	КонецЕсли;
	Если ОтборТипОперация Тогда
		ЗаполнениеТип.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Операция"));
	КонецЕсли;
	Если ОтборТипПодарочныйСертификат Тогда
		ЗаполнениеТип.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ПодарочныйСертификат"));
	КонецЕсли;
	Если ЗаполнениеТип.Количество()>0 Тогда
		ЗначенияЗаполнения.Вставить("ТипНоменклатуры", ЗаполнениеТип);
	КонецЕсли;
	Если ОтборНабор Тогда
		ЗначенияЗаполнения.Вставить("ЭтоНабор", Истина);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ОтборКатегорииТекущая) Тогда
		ЗначенияЗаполнения.Вставить("КатегорияНоменклатуры", ОтборКатегорииТекущая);
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	СтруктураПараметров.Вставить("Отбор", СписокЗапасы.Отбор);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", СтруктураПараметров, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНоменклатуру()
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыНоменклатура");
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", Новый Структура("Ключ", Элементы[ТекущаяСтраницаНоменклатуры].ТекущиеДанные.Ссылка));
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьГруппу()
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаГруппы", Новый Структура("Ключ", Элементы.ОтборИерархия.ТекущиеДанные.Значение));
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьГруппу()
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаГруппы", Новый Структура("ЗначениеКопирования", Элементы.ОтборИерархия.ТекущиеДанные.Значение));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьГруппу()
	
	РодительЗначение = ?(Элементы.ОтборИерархия.ТекущиеДанные = Неопределено, ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"),
		Элементы.ОтборИерархия.ТекущиеДанные.Значение);
		
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ЗначенияЗаполнения",  Новый Структура("Родитель, ЭтоГруппа", РодительЗначение, Истина));
	ПараметрыЗаполнения.Вставить("ЭтоГруппа",  Истина);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаГруппы", ПараметрыЗаполнения, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьНоменклатуру()
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", Новый Структура("ЗначениеКопирования", Элементы[ТекущаяСтраницаНоменклатуры].ТекущиеДанные.Ссылка));
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеНоменклатуру()
	
	ТекущаяНоменклатура = Элементы[ТекущаяСтраницаНоменклатуры].ТекущиеДанные.Ссылка;
	НовоеЗначениеПометкиУдаления = ИзменитьПометкуУдаленияСервер(ТекущаяНоменклатура);
	
	ПоказатьОповещениеПользователя(
		СтрШаблон(НСтр("ru='Пометка удаления %1'"), ?(НовоеЗначениеПометкиУдаления, НСтр("ru='установлена'"), НСтр("ru='снята'"))),
		ПолучитьНавигационнуюСсылку(ТекущаяНоменклатура),
		ТекущаяНоменклатура,
		БиблиотекаКартинок.Информация32);
		
	Элементы[ТекущаяСтраницаНоменклатуры].Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьПометкуУдаленияСервер(Номенклатура)
	
	НоменклатураОбъект = Номенклатура.ПолучитьОбъект();
	НоменклатураОбъект.УстановитьПометкуУдаления(Не НоменклатураОбъект.ПометкаУдаления, Истина);
	
	Возврат НоменклатураОбъект.ПометкаУдаления;
	
КонецФункции

&НаКлиенте
Процедура УстановитьПометкуУдаленияГруппы(Команда)
	
	Если Элементы.ОтборИерархия.ТекущиеДанные = Неопределено Или Не ЗначениеЗаполнено(Элементы.ОтборИерархия.ТекущиеДанные.Значение) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрокаДерева = ОтборИерархия.НайтиПоИдентификатору(Элементы.ОтборИерархия.ТекущиеДанные.ПолучитьИдентификатор());
	ТекущаяСтрокаДерева.ПометкаУдаления = ИзменитьПометкуУдаленияСервер(ТекущаяСтрокаДерева.Значение);
	ТекущаяСтрокаДерева.Картинка= ?(ТекущаяСтрокаДерева.ПометкаУдаления,1,0);
	НоменклатураВДокументахКлиентСервер.УстановитьФлагУПодчиненных(ТекущаяСтрокаДерева.ПолучитьЭлементы(), "ПометкаУдаления", ТекущаяСтрокаДерева.ПометкаУдаления);
	НоменклатураВДокументахКлиентСервер.УстановитьФлагУПодчиненных(ТекущаяСтрокаДерева.ПолучитьЭлементы(), "Картинка", ТекущаяСтрокаДерева.Картинка);
	
	ПоказатьОповещениеПользователя(
		СтрШаблон(НСтр("ru='Пометка удаления %1'"), ?(ТекущаяСтрокаДерева.ПометкаУдаления, НСтр("ru='установлена'"), НСтр("ru='снята'"))),
		ПолучитьНавигационнуюСсылку(ТекущаяСтрокаДерева.Значение),
		ТекущаяСтрокаДерева.Значение,
		БиблиотекаКартинок.Информация32);
		
	Элементы[ТекущаяСтраницаНоменклатуры].Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ПереместитьВГруппуНаСервере(Результат, ДополнительныеПараметры)

	Об = ДополнительныеПараметры.ТекущийЭлементСписка.ПолучитьОбъект();
	Об.Родитель = Результат;
	Об.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВГруппуПродолжить(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) И ДополнительныеПараметры.Свойство("ТекущийЭлементСписка") Тогда
		ПереместитьВГруппуНаСервере(Результат, ДополнительныеПараметры);
	КонецЕсли; 
	
КонецПроцедуры	

&НаКлиенте
Процедура ПереместитьВГруппу(Команда)
	
	ТекЭлементСписка = Новый Структура("ТекущийЭлементСписка",Элементы[ТекущаяСтраницаНоменклатуры].ТекущиеДанные.Ссылка);
	ОписаниеОповещенияПереместить = Новый ОписаниеОповещения("ПереместитьВГруппуПродолжить", ЭтотОбъект, ТекЭлементСписка);
		
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбораГруппы",,,,,, 
		ОписаниеОповещенияПереместить, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если НЕ Группа Тогда
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "СозданиеФормы" + РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	КонецЕсли;
	
	Если Не Копирование И Не Группа Тогда
		Отказ = Истина;
		СоздатьНоменклатуру();
	ИначеЕсли Группа Тогда
		Отказ = Истина;
		СоздатьГруппу();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Если НоменклатураВДокументахКлиентСервер.ЕстьРеквизитОбъекта("ЭтоГруппа", Элемент.ТекущиеДанные) И НЕ Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормы" + РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	ИначеЕсли НоменклатураВДокументахКлиентСервер.ЕстьРеквизитОбъекта("ГруппировкаСтроки", Элемент.ТекущиеДанные) Тогда
		//для группировки по группам или категориям
		Если ТипЗнч(Элемент.ТекущиеДанные.ГруппировкаСтроки.Ключ)=Тип("СправочникСсылка.Номенклатура") Тогда
			Отказ = Истина;
			ОткрытьФорму("Справочник.Номенклатура.ФормаГруппы", Новый Структура("Ключ",Элемент.ТекущиеДанные.ГруппировкаСтроки.Ключ));
		ИначеЕсли ТипЗнч(Элемент.ТекущиеДанные.ГруппировкаСтроки.Ключ)=Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
			Отказ = Истина;
			ОткрытьФорму("Справочник.КатегорииНоменклатуры.ФормаОбъекта", Новый Структура("Ключ",Элемент.ТекущиеДанные.ГруппировкаСтроки.Ключ));
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗапасыПриАктивизацииСтроки(Элемент)

	Элементы.СкопироватьНоменклатуру.Доступность = НЕ Элементы[ТекущаяСтраницаНоменклатуры].ТекущиеДанные=Неопределено;
	
	Если НЕ ЭтоМобильныйКлиент Тогда
	
		// СтандартныеПодсистемы.ПодключаемыеКоманды
		ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗапасыВНаличииПриАктивизацииСтроки(Элемент)

	Элементы.СкопироватьНоменклатуру.Доступность = НЕ Элементы[ТекущаяСтраницаНоменклатуры].ТекущиеДанные=Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКорзиной

&НаКлиенте
Процедура ДекорацияКорзинаНажатие(Элемент)
	
	ОткрытьКорзину();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписьПодобраноТоваров() Экспорт
	
	КоличествоТоваров	= Корзина.Итог("Количество");
	СуммаТоваров		= Корзина.Итог("Сумма");
	
	Если Корзина.Количество()=0 Тогда
		НадписьПодобраноТоваров = НСтр("ru = 'перетащите товары в корзину'");
	Иначе	
		НоменклатураВДокументахКлиентСервер.ОбновитьИтогиПодобранныхТоваров(ЭтаФорма);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКорзинаПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДобавитьВКорзину(ПараметрыПеретаскивания.Значение);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКорзинаПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКорзину()
	
	Если Корзина.Количество()>0 Тогда
		ПараметрыКорзины = ЗаписатьПодборВХранилище();
		ОткрытьКорзинуПродолжить(ПараметрыКорзины);
	Иначе
		Восстановить(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКорзинуПродолжить(ПараметрыКорзины)
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("НалогообложениеНДС", НалогообложениеНДС);
	ПередаваемыеПараметры.Вставить("ИспользуетсяНДС", ИспользуетсяНДС);
	ПередаваемыеПараметры.Вставить("АдресКорзиныВХранилище", ?(ЗначениеЗаполнено(ПараметрыКорзины), ПараметрыКорзины.АдресКорзиныВХранилище, Неопределено));
	ПередаваемыеПараметры.Вставить("ОтборВидЦен", ОтборВидЦен);
	ПередаваемыеПараметры.Вставить("ОтборСклад", ОтборСклад);
	ПередаваемыеПараметры.Вставить("ИспользуютсяХарактеристики", ИспользуютсяХарактеристики);
	ПередаваемыеПараметры.Вставить("ЕстьДоступКЦенам", ЕстьДоступКЦенам);
	
	ПередаваемыеПараметры.Вставить("УникальныйИдентификаторФормыВладельца", УникальныйИдентификаторФормыВладельца);
	
	ОповещениеКорзинаЗакрытие = Новый ОписаниеОповещения("КорзинаЗакрытие",ЭтотОбъект);
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаКорзина",ПередаваемыеПараметры, ЭтаФорма,,,,ОповещениеКорзинаЗакрытие,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КорзинаЗакрытие(ПараметрЗакрытия, Параметры) Экспорт

	Корзина.Очистить();
	
	Если ПараметрЗакрытия = Неопределено Тогда
		//Закрытие без сохранения
	ИначеЕсли ПараметрЗакрытия="КорзинаОтложена" Тогда
		ЧислоОтложенныхКорзин = ЧислоОтложенныхКорзин + 1;
		Элементы.ДекорацияКорзина.Картинка = БиблиотекаКартинок.ТележкаОтложена;
	ИначеЕсли ПараметрЗакрытия="ПеренестиВДокумент" Тогда
		//Закрытие без сохранения
	Иначе 
		//Закрытие с сохранением
		Для каждого стр Из ПараметрЗакрытия.Корзина Цикл
			НовСтр = Корзина.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, стр);
		КонецЦикла;
	КонецЕсли;
	
	ОбновитьНадписьПодобраноТоваров();
	
КонецПроцедуры	

&НаКлиенте
Процедура ДобавитьВКорзину(ВыбранныеСтроки)
	
	Если ТипЗнч(ВыбранныеСтроки) = Тип("Массив") Тогда
		Для каждого ВыделеннаяСтрока Из ВыбранныеСтроки Цикл
			ДобавитьСтрокуВКорзину(ВыделеннаяСтрока);
		КонецЦикла; 
	ИначеЕсли ТипЗнч(ВыбранныеСтроки) = Тип("СправочникСсылка.Номенклатура") Тогда
		ДобавитьСтрокуВКорзину(ВыбранныеСтроки);
	КонецЕсли;
	
	ОбновитьНадписьПодобраноТоваров();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуВКорзину(ВыделеннаяСтрока)

	Если Элементы.СтраницыНоменклатуры.ТекущаяСтраница = Элементы.СтраницаЗапасыВНаличии 
		И ТипЗнч(ВыделеннаяСтрока)<>Тип("Число") Тогда
		возврат;
	ИначеЕсли Элементы.СтраницыНоменклатуры.ТекущаяСтраница = Элементы.СтраницаЗапасы И 
		ТипЗнч(ВыделеннаяСтрока) <> Тип("СправочникСсылка.Номенклатура") Тогда
		возврат;
	КонецЕсли;
	
	ДанныеСтроки = Элементы[ТекущаяСтраницаНоменклатуры].ДанныеСтроки(ВыделеннаяСтрока);
	
	СтруктураВыбора = СтруктураВыбора();
	ЗаполнитьЗначенияСвойств(СтруктураВыбора, ДанныеСтроки);
	
	Если Не ДанныеСтроки.Свойство("Характеристика")
		Тогда
		СтруктураВыбора.Характеристика = НоменклатураВДокументахСервер.ЗначенияНоменклатурыПоУмолчанию(СтруктураВыбора.Номенклатура);
	КонецЕсли;
		
	СтруктураВыбора.Количество = 1;
	ДобавитьНоменклатуруВКорзину(СтруктураВыбора);
	
КонецПроцедуры	

&НаКлиенте
Процедура ВыбратьИзСпискаЗавершение(ВыбранныйЭлемент, Параметры) Экспорт

	Если ВыбранныйЭлемент<>Неопределено И ВыбранныйЭлемент.Значение<>Неопределено Тогда
		РазрешитьЗакрытие = 1;
		НоменклатураВДокументахКлиент.ОформитьДокументСТоварамиИзКорзины(ЭтаФорма, ВыбранныйЭлемент.Значение);
		
		Корзина.Очистить();
		ОбновитьНадписьПодобраноТоваров();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВКорзинуИзСписка(Команда)
	
	ДобавитьВКорзину(Элементы[ТекущаяСтраницаНоменклатуры].ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьКорзинаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДекорацияКорзинаНажатие(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКатегориями

&НаКлиенте
Процедура ОтборКатегорииИерархияПереключательПриИзменении(Элемент)
	
	Если ОтборКатегорииИерархияПереключатель = ПредопределенноеЗначение("Перечисление.ВидыОтборовНоменклатуры.КатегорииНоменклатуры") Тогда
		
		Элементы.СтраницыКатегорииИерархия.ТекущаяСтраница = Элементы.КатегорииОтбор;
		ОтключитьОтборПоИерархии();
		ПодключитьОбработчикОжидания("ОтборКатегорииПриАктивизацииСтрокиНаКлиенте", 0.2, Истина);
		
	ИначеЕсли ОтборКатегорииИерархияПереключатель = ПредопределенноеЗначение("Перечисление.ВидыОтборовНоменклатуры.ГруппыНоменклатуры") Тогда
		
		Элементы.СтраницыКатегорииИерархия.ТекущаяСтраница = Элементы.ИерархияГруппыОтбор;
		
		Если Элементы.ОтборИерархия.ТекущиеДанные <> Неопределено Тогда
		    Если Элементы.ОтборИерархия.ТекущаяСтрока = Неопределено Тогда //отбор на новом созданном элементе
				ИдентификаторСтроки = 0;
				ОбщегоНазначенияКлиентСервер.ПолучитьИдентификаторСтрокиДереваПоЗначениюПоля("Значение", ИдентификаторСтроки, ОтборИерархия.ПолучитьЭлементы(), Элементы.ОтборИерархия.ТекущиеДанные.Значение, Ложь);
				Элементы.ОтборИерархия.ТекущаяСтрока = ИдентификаторСтроки;
			Иначе //восстановить ранее сделанный, отключенный отбор по иерархии
				УстановитьОтборПоИерархии();
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ОтборКатегории.ТекущаяСтрока = 0;
		ПодключитьОбработчикОжидания("ОтборКатегорииПриАктивизацииСтрокиНаКлиенте", 0.1, Истина);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКатегорииПриАктивизацииСтроки(Элемент)
	
	Если ОтборКатегорииИерархияПереключатель <> ПредопределенноеЗначение("Перечисление.ВидыОтборовНоменклатуры.КатегорииНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ОтборКатегории.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтборКатегорииТекущая = Элементы.ОтборКатегории.ТекущиеДанные.Значение Тогда
		Возврат;
	КонецЕсли;
	
	ДоступныКонтекстныеКоманды = ЗначениеЗаполнено(Элементы.ОтборКатегории.ТекущиеДанные.Значение);
	Элементы.ОтборКатегорииКонтекстноеМенюИзменить.Доступность = ДоступныКонтекстныеКоманды;
	
	ПодключитьОбработчикОжидания("ОтборКатегорииПриАктивизацииСтрокиНаКлиенте", 0.2, Истина);
	
	ТекущийЭлемент = Элементы.ОтборКатегории;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКатегорииПриАктивизацииСтрокиНаКлиенте()
	
	Если ОтборКатегорииИерархияПереключатель = ПредопределенноеЗначение("Перечисление.ВидыОтборовНоменклатуры.ГруппыНоменклатуры") Тогда
		
		ОтборКатегорииТекущая = ПредопределенноеЗначение("Справочник.КатегорииНоменклатуры.ПустаяСсылка");
		
	ИначеЕсли ОтборКатегорииИерархияПереключатель = ПредопределенноеЗначение("Перечисление.ВидыОтборовНоменклатуры.КатегорииНоменклатуры") Тогда
		
		Если Элементы.ОтборКатегории.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ОтборКатегорииТекущая = Элементы.ОтборКатегории.ТекущиеДанные.Значение;
		
	КонецЕсли;
	
	ОтборКатегорииПриАктивизацииСтрокиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОтборКатегорииПриАктивизацииСтрокиНаСервере()
	
	КатегорииНоменклатурыСервер.УстановитьОтборПоКатегории(
		ЭтотОбъект,
		ЭтаФорма[ТекущаяСтраницаНоменклатуры], 
		ОтборКатегорииТекущая
	);
	
	КатегорииНоменклатурыСервер.ПоказатьСвойстваКатегории(
		ЭтотОбъект,
		ЭтаФорма[ТекущаяСтраницаНоменклатуры],
		ОтборКатегорииТекущая,
		"КатегорииОтборСвойства",
		Ложь
	);
	
	Если ТекущаяСтраницаНоменклатуры = "СписокЗапасыВНаличии" И ВНаличииНаСкладе Тогда
		
		КатегорииНоменклатурыСервер.ПоказатьСвойстваКатегории(
			ЭтотОбъект,
			ЭтаФорма[ТекущаяСтраницаНоменклатуры],
			ОтборКатегорииТекущая,
			"КатегорииОтборСвойства",
			Истина
		);
		
	КонецЕсли;
	
	НастроитьПанельОтборовМобильныйКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКатегорииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если УправлениеНебольшойФирмойУправлениеДоступомПовтИсп.ЕстьПрофильРабочееМестоКассира() Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура;
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ЗначенияЗаполнения.Вставить("Родитель", РодительКатегории(Элемент.ТекущиеДанные.Значение));
	КонецЕсли;
	
	ОткрытьФорму("Справочник.КатегорииНоменклатуры.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСправочникКатегории(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	Если Элементы.ОтборКатегории.ТекущиеДанные <> Неопределено Тогда
		ПараметрыФормы.Вставить("ТекущаяСтрока", Элементы.ОтборКатегории.ТекущиеДанные.Значение);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.КатегорииНоменклатуры.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКатегорию(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	Если Элементы.ОтборКатегории.ТекущиеДанные <> Неопределено Тогда
		ПараметрыФормы.Вставить("Ключ", Элементы.ОтборКатегории.ТекущиеДанные.Значение);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.КатегорииНоменклатуры.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачалоВыбораДополнительногоРеквизита(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Элемент.СписокВыбора.Количество() = 0 Тогда
		
		ОписаниеСвойства = Новый Структура("Свойство,СписокВыбораЗначенийСсылка", Неопределено, Неопределено);
		ОписаниеСвойства(Элемент.Имя, ОписаниеСвойства);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Свойство", ОписаниеСвойства.Свойство);
		ПараметрыФормы.Вставить("СписокВыбораЗначенийСсылка", ОписаниеСвойства.СписокВыбораЗначенийСсылка);
		ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаВыбораЗначенияСвойства", ПараметрыФормы, Элемент);
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаВыбораДополнительногоРеквизита(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	УстановитьОтборПоСвойству(Элемент.Имя, ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	
	УстановитьОтборПоСвойству(Элемент.Имя);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоКатегорий(КатегорияТекущейСтроки = Неопределено)
	
	Дерево = КатегорииНоменклатурыСервер.ДеревоКатегорий();
	ЗначениеВРеквизитФормы(Дерево, "ОтборКатегории");
	
	ИдентификаторСтроки = Неопределено;
	Если КатегорияТекущейСтроки <> Неопределено Тогда
		ИдентификаторСтроки = ИдентификаторСтрокиДереваПоЗначению(ОтборКатегории, КатегорияТекущейСтроки);
	КонецЕсли;
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		Элементы.ОтборКатегории.ТекущаяСтрока = ИдентификаторСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОписаниеСвойства(ЭлементИмя, ОписаниеСвойства)
	
	Строки = СвойстваОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("ИмяРеквизитаЗначение", ЭлементИмя));
	Если Строки.Количество() <> 0 Тогда
		
		ЗаполнитьЗначенияСвойств(ОписаниеСвойства, Строки[0]);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоСвойству(ЭлементИмя, Значение = Неопределено)
	
	Если Значение = Неопределено Тогда
		Значение = ЭтотОбъект[Элементы[ЭлементИмя].ПутьКДанным];
	КонецЕсли;
	
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Значение)) Тогда
		КатегорииНоменклатурыСервер.УстановитьОтборПоДополнительномуРеквизиту(
			ЭтотОбъект,
			ЭтаФорма[ТекущаяСтраницаНоменклатуры],
			ЭлементИмя,
			ОтборКатегорииТекущая);
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Возврат;
	КонецЕсли;
	
	ПолеОтбора = КатегорииНоменклатурыСервер.ПолеОтбораДополнительногоРеквизита(ЭтотОбъект, ЭлементИмя, ОтборКатегорииТекущая);
	УстановитьМеткуИОтборСписка(ПолеОтбора, Элементы[ЭлементИмя].Родитель.Имя, Значение);
	
	ЭтотОбъект[Элементы[ЭлементИмя].ПутьКДанным] = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьНастройкиФормыПриСоздании(Параметры)
	
	Если Параметры.Свойство("ВидЦен") И ЗначениеЗаполнено(Параметры.ВидЦен) Тогда
		Элементы.ОтборВидЦен.ТолькоПросмотр = Истина;
		ОтборВидЦен = Параметры.ВидЦен;
	Иначе
		ОтборВидЦен = Справочники.ВидыЦен.ПолучитьОсновнойВидЦенПродажи();
	КонецЕсли;
	
	Если Параметры.Свойство("Валюта") И ЗначениеЗаполнено(Параметры.Валюта) Тогда
		ВалютаПодбора = Параметры.Валюта;
	ИначеЕсли ЗначениеЗаполнено(ОтборВидЦен) Тогда
		ВалютаПодбора = ОтборВидЦен.ВалютаЦены;
	Иначе
		ВалютаПодбора = УправлениеНебольшойФирмойПовтИсп.ПолучитьВалютуУчета();
	КонецЕсли;
	Если ЗначениеЗаполнено(ВалютаПодбора) Тогда
		ВалютаПредставление = УправлениеНебольшойФирмойПовтИсп.ПолучитьСимвольноеПредставлениеВалюты(ОтборВидЦен.ВалютаЦены);
	КонецЕсли;
	
	ТекущаяСтраницаНоменклатуры = "СписокЗапасы";
	Если Параметры.Свойство("ТипНоменклатуры") И ЗначениеЗаполнено(Параметры.ТипНоменклатуры) Тогда
		УстановитьФиксированныйОтборПоТипамНоменклатуры(Параметры.ТипНоменклатуры);
	ИначеЕсли Параметры.Отбор.Свойство("ТипНоменклатуры") И ЗначениеЗаполнено(Параметры.Отбор.ТипНоменклатуры) Тогда
		УстановитьФиксированныйОтборПоТипамНоменклатуры(Параметры.Отбор.ТипНоменклатуры);
		Параметры.Отбор.Удалить("ТипНоменклатуры");
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ЭтоНабор") Тогда
		ОтборНабор = Параметры.Отбор.ЭтоНабор;
		УстановитьФиксированныйОтборПоНаборам(Параметры.Отбор.ЭтоНабор);
		Параметры.Отбор.Удалить("ЭтоНабор");
	КонецЕсли; 
	
	Если Параметры.Свойство("СтруктурнаяЕдиница") И ЗначениеЗаполнено(Параметры.СтруктурнаяЕдиница) Тогда
		ОтборСклад = Параметры.СтруктурнаяЕдиница; 
	ИначеЕсли Параметры.Свойство("Склад") И ЗначениеЗаполнено(Параметры.Склад) Тогда
		ОтборСклад = Параметры.Склад;
	КонецЕсли;
	
	// Налогообложение НДС и Сумма для Корзины
	НалогообложениеНДС 		= ?(Параметры.Свойство("НалогообложениеНДС"), Параметры.НалогообложениеНДС, Неопределено);
	Если Параметры.Свойство("СуммаВключаетНДС") Тогда
		СуммаВключаетНДС = Параметры.СуммаВключаетНДС;
	Иначе
		СуммаВключаетНДС = Истина;
	КонецЕсли;
	
	// Установим видимость подменю в зависимости от наличия прав
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		ДоступныЗакупки = РольДоступна("ДобавлениеИзменениеПодсистемыЗакупки");
		ДоступныПродажи = РольДоступна("ДобавлениеИзменениеПодсистемыПродажи");
		
		Элементы.ПодменюКупить.Видимость		= ДоступныЗакупки;
		Элементы.ПодменюКупитьКорзина.Видимость	= ДоступныЗакупки;
		Элементы.ПодменюПродать.Видимость		= ДоступныПродажи;
		Элементы.ПодменюПродатьКорзина.Видимость= ДоступныПродажи;
	КонецЕсли;
	
	// РМК
	Если УправлениеНебольшойФирмойУправлениеДоступомПовтИсп.ЕстьПрофильРабочееМестоКассира() Тогда
		Элементы.ОтборКатегорииКонтекстноеМенюДобавить.Видимость = Ложь;
		Элементы.ОтборКатегорииКонтекстноеМенюИзменить.Видимость = Ложь;
		Элементы.ОтборКатегории.ИзменятьСоставСтрок = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьИДоступностьПриСоздании();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступностьПриСоздании()
	
	ЧислоДоступныхКоманд = Элементы.ПодменюПродать.ПодчиненныеЭлементы.Количество() + Элементы.ПодменюКупить.ПодчиненныеЭлементы.Количество();
	
	Элементы.Корзина.Видимость        = ЧислоДоступныхКоманд > 0 И НЕ Параметры.РежимВыбора;
	Элементы.ПодменюПродать.Видимость = НЕ Параметры.РежимВыбора;
	Элементы.ПодменюКупить.Видимость  = НЕ Параметры.РежимВыбора;
	Элементы.Выбрать.Видимость        = Параметры.РежимВыбора;
	Элементы.ЦеныОтбор.Видимость      = ЕстьДоступКЦенам;
	ПеречитатьЗначенияКонстантОбновитьФорму();
	
	//Обновить состояние отложенных товаров в корзине
	СписокОтложенныхТоваров = ПолучитьСписокОтложенных();
	Если СписокОтложенныхТоваров.Количество()=0 Тогда
		Элементы.ДекорацияКорзина.Картинка = БиблиотекаКартинок.ТележкаПустая;
	Иначе
		Элементы.ДекорацияКорзина.Картинка = БиблиотекаКартинок.ТележкаОтложена;
	КонецЕсли;
	
	НадписьПодобраноТоваров = НСтр("ru = 'перетащите товары в корзину'");
	Элементы.СписокЗапасыКонтекстноеМенюДобавитьВКорзину.Видимость			= НЕ Параметры.РежимВыбора;
	Элементы.СписокЗапасыВНаличииКонтекстноеМенюДобавитьВКорзину.Видимость	= НЕ Параметры.РежимВыбора;
	
	Если ОтборКатегорииИерархияПереключатель = Перечисления.ВидыОтборовНоменклатуры.КатегорииНоменклатуры Тогда
		Элементы.СтраницыКатегорииИерархия.ТекущаяСтраница = Элементы.КатегорииОтбор;
	ИначеЕсли ОтборКатегорииИерархияПереключатель = Перечисления.ВидыОтборовНоменклатуры.ГруппыНоменклатуры Тогда
		Элементы.СтраницыКатегорииИерархия.ТекущаяСтраница = Элементы.ИерархияГруппыОтбор;
	КонецЕсли;
	
	ИспользоватьНаборы = ПолучитьФункциональнуюОпцию("ИспользоватьНаборы");
	Элементы.ОтборНабор.Видимость = ИспользоватьНаборы;
	Элементы.ДекорацияНаборыОтступ.Видимость = ИспользоватьНаборы;
	
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПеречитатьЗначенияКонстантОбновитьФорму()

	//Отображение типов номенклатуры от настроек
	Элементы.ОтборТипОперация.Видимость = Константы.ФункциональнаяОпцияИспользоватьТехоперации.Получить();
	
	//Отображение колонки Характеристики
	ИспользуютсяХарактеристики 	= Константы.ФункциональнаяОпцияИспользоватьХарактеристики.Получить();
	Элементы.СписокЗапасыВНаличииХарактеристика.Видимость	= ИспользуютсяХарактеристики;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыЗапросовСписков()
	
	Если ЗначениеЗаполнено(ПериодЦен) Тогда
		ПараметрПериод = ПериодЦен;
	Иначе
		ПараметрПериод = ТекущаяДатаСеанса();
	КонецЕсли;
	
	//запретим делать отборы по цене и количеству, если эти поля заполняются через СписокЗапасыПриПолученииДанныхНаСервере
	УстановитьОграничениеПорядокОтборГруппировка(СписокЗапасы);
	УстановитьОграничениеПорядокОтборГруппировка(СписокЗапасыВНаличии);

	УстановитьПараметрСписка(СписокЗапасы, "ВидЦен", ОтборВидЦен);
	УстановитьПараметрСписка(СписокЗапасы, "Период", ПараметрПериод);
	УстановитьПараметрСписка(СписокЗапасы, "ДатаПереходаНаНДС20", Дата('20190101'));
	
	СписокЗапасы.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ПоказыватьЦены", ПоказыватьЦены);
	СписокЗапасы.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ПоказыватьОстатки", ПоказыватьОстатки);
	СписокЗапасы.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ОтборПоКоличеству", ОтборПоКоличеству);
	СписокЗапасы.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ОтборПоЦенам", ОтборПоЦенам);
	СписокЗапасы.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ТекущаяСтраницаНоменклатуры", 
		?(ОтборОстатки=1, "СписокЗапасыВНаличии", "СписокЗапасы"));
	СписокЗапасы.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("СоответствиеСтавокНДС",
		УправлениеНебольшойФирмойПовтИсп.СоответствиеСтавокНДС(?(ЗначениеЗаполнено(ПериодЦен),ПериодЦен, ТекущаяДатаСеанса())));
	
	УстановитьПараметрСписка(СписокЗапасыВНаличии, "ВидЦен", ОтборВидЦен);
	УстановитьПараметрСписка(СписокЗапасыВНаличии, "Период", ПараметрПериод);
	УстановитьПараметрСписка(СписокЗапасыВНаличии, "ДатаПереходаНаНДС20", Дата('20190101'));
	
	Если ЗначениеЗаполнено(ОтборСклад) Тогда
		УстановитьПараметрСписка(СписокЗапасыВНаличии, "ВсеСклады", Ложь);
		УстановитьПараметрСписка(СписокЗапасыВНаличии, "Склад", ОтборСклад);
	Иначе
		УстановитьПараметрСписка(СписокЗапасыВНаличии, "ВсеСклады", Истина);
		УстановитьПараметрСписка(СписокЗапасыВНаличии, "Склад", Справочники.СтруктурныеЕдиницы.ПустаяСсылка());
	КонецЕсли; 
	
	СписокЗапасыВНаличии.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ПоказыватьЦены", ПоказыватьЦены);
	СписокЗапасыВНаличии.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ПоказыватьОстатки", ПоказыватьОстатки);
	СписокЗапасыВНаличии.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ОтборПоЦенам", ОтборПоЦенам);
	СписокЗапасыВНаличии.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ОтборПоКоличеству", ОтборПоКоличеству);
	СписокЗапасыВНаличии.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ТекущаяСтраницаНоменклатуры", 
		?(ОтборОстатки=1, "СписокЗапасыВНаличии", "СписокЗапасы"));
	СписокЗапасыВНаличии.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("СоответствиеСтавокНДС",
		УправлениеНебольшойФирмойПовтИсп.СоответствиеСтавокНДС(?(ЗначениеЗаполнено(ПериодЦен),ПериодЦен, ТекущаяДатаСеанса())));
	
	НастроитьПанельОтборовМобильныйКлиент();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрСписка(Список, ИмяПараметра, ЗначениеПараметра)
	
	Если Список.Параметры.Элементы.Найти(ИмяПараметра)<>Неопределено Тогда
		Список.Параметры.УстановитьЗначениеПараметра(ИмяПараметра, ЗначениеПараметра);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	//Остатки и склад
	ИспользоватьКоличествоВСписке = ПоказыватьОстатки ИЛИ ОтборОстатки=2;
	
	Элементы.ГруппаСтрокаПоискаЗапасы.Видимость			= НЕ ВНаличииНаСкладе;
	Элементы.ГруппаСтрокаПоискаЗапасыВНаличии.Видимость	= ВНаличииНаСкладе;
	Элементы.ОтборСклад.Видимость = (ОтборОстатки=1);
	Элементы.Остаток.Видимость						= ПоказыватьОстатки;
	Элементы.СписокЗапасыВНаличииОстаток.Видимость	= ПоказыватьОстатки;
	
	//Цены
	Элементы.ОтборВидЦен.Доступность			= ПоказыватьЦены;
	Элементы.ЦеныДиапазон.Доступность			= ПоказыватьЦены;
	Элементы.Цена.Видимость						= ПоказыватьЦены;
	Элементы.СписокЗапасыВНаличииЦена.Видимость	= ПоказыватьЦены;
	
	// Категории, иерархия
	Если ЗначениеЗаполнено(ОтборКатегорииТекущая) Тогда
		Если ТекущаяСтраницаНоменклатуры = "СписокЗапасыВНаличии" И ВНаличииНаСкладе Тогда
			
			ПоказатьСвойстваХарактеристик = Истина;
			
		Иначе
			
			ПоказатьСвойстваХарактеристик = Ложь;
			
			УдаляемыеМетки = Новый СписокЗначений;
			Для каждого Метка Из ДанныеМеток Цикл
				Если СтрНачинаетсяС(Метка.ИмяПоляОтбора, "Характеристика.") Тогда
					УдаляемыеМетки.Добавить(Метка.ПолучитьИдентификатор());
				КонецЕсли;
			КонецЦикла;
			
			УдалитьМеткиОтбора(УдаляемыеМетки);
			
		КонецЕсли;
		
		КатегорииНоменклатурыСервер.ПоказатьСвойстваКатегории(
			ЭтотОбъект,
			ЭтаФорма[ТекущаяСтраницаНоменклатуры],
			ОтборКатегорииТекущая,
			"КатегорииОтборСвойстваХарактеристики",
			ПоказатьСвойстваХарактеристик);
		
		ОбновитьЭлементыМеток();
		
	КонецЕсли;
	
КонецПроцедуры

//Перед закрытием, если были подобраны товары, предлагает пользователю 
// оформить подобранные товары в документе или продолжить выбор
&НаКлиенте
Процедура ВопросПеренестиТовары(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		РазрешитьЗакрытие = Истина;
	Иначе
		РазрешитьЗакрытие = Ложь;
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// 1. Недействительная номенклатура отображается серым
	НовоеУсловноеОформление = СписокЗапасы.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Добавить();
	
	Оформление = НовоеУсловноеОформление.Оформление.Элементы.Найти("ЦветТекста");
	Оформление.Значение 		= ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;
	Оформление.Использование 	= Истина;
	
	Отбор = НовоеУсловноеОформление.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование 	= Истина;
	Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Недействителен");
	Отбор.ПравоеЗначение 	= Истина;
	
	НовоеУсловноеОформление = СписокЗапасыВНаличии.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Добавить();
	
	Оформление = НовоеУсловноеОформление.Оформление.Элементы.Найти("ЦветТекста");
	Оформление.Значение 		= ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;
	Оформление.Использование 	= Истина;
	
	Отбор = НовоеУсловноеОформление.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование 	= Истина;
	Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Недействителен");
	Отбор.ПравоеЗначение 	= Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборНедействительнаяНоменклатура(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СписокЗапасы,
		"Недействителен",
		Ложь,
		,
		,
		Не Форма.Элементы.ПоказыватьНедействительную.Пометка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СписокЗапасыВНаличии,
		"Недействителен",
		Ложь,
		,
		,
		Не Форма.Элементы.ПоказыватьНедействительную.Пометка);
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиВГруппу(спПеренести, НоваяГруппа)

	Для каждого элНоменклатура Из спПеренести Цикл
		Если элНоменклатура.Родитель<>НоваяГруппа Тогда
			Об = элНоменклатура.ПолучитьОбъект();
			Об.Родитель = НоваяГруппа;
			Об.Записать();
		КонецЕсли;
	КонецЦикла;
	
	Если спПеренести[0].ЭтоГруппа Тогда
		
		ЗаполнитьДеревоИерархии();
		
		ИдентификаторСтроки = 0;
		ОбщегоНазначенияКлиентСервер.ПолучитьИдентификаторСтрокиДереваПоЗначениюПоля(
			"Значение",
			ИдентификаторСтроки,
			ОтборИерархия.ПолучитьЭлементы(),
			спПеренести[0],
			Ложь
		);
		Элементы.ОтборИерархия.ТекущаяСтрока = ИдентификаторСтроки;
		
	Иначе
		
		Элементы[ТекущаяСтраницаНоменклатуры].Обновить();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ИдентификаторСтрокиДереваПоЗначению(Коллекция, ИскомоеЗначение)
	
	КоллекцияЭлементов = Коллекция.ПолучитьЭлементы();
	
	Для каждого Элемент Из КоллекцияЭлементов Цикл
		
		Если Элемент.Значение = ИскомоеЗначение Тогда
			Возврат Элемент.ПолучитьИдентификатор();
		КонецЕсли;
		
		Идентификатор = ИдентификаторСтрокиДереваПоЗначению(Элемент, ИскомоеЗначение);
		
		Если Идентификатор <> Неопределено Тогда
			Возврат Идентификатор;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

&НаСервереБезКонтекста
Функция РодительКатегории(Категория)
	
	Если НЕ ЗначениеЗаполнено(Категория) Тогда
		Возврат Справочники.КатегорииНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	ДанныеКатегории = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Категория, "Родитель,ЭтоГруппа");
	
	Если ДанныеКатегории.ЭтоГруппа Тогда
		Возврат Категория;
	КонецЕсли;
	
	Возврат ДанныеКатегории.Родитель;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьЭлементовДляМобильногоКлиента()
	
	Если НЕ ОбщегоНазначенияКлиентСервер.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоМобильныйКлиент = Истина;
	
	Элементы.ПродатьКупить.Видимость = Ложь;
	Элементы.СписокЗапасыПродатьКупить.Видимость = Истина;
	Элементы.СписокЗапасыВНаличииПродатьКупить.Видимость = Истина;
	
	Элементы.Корзина.Видимость = Ложь;
	Элементы.СписокЗапасыКонтекстноеМенюДобавитьВКорзину.Видимость = Ложь;
	Элементы.СписокЗапасыВНаличииКонтекстноеМенюДобавитьВКорзину.Видимость = Ложь;
	
	НастроитьПанельОтборовМобильныйКлиент();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПанельОтборовМобильныйКлиент()
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСОтборами.НастроитьПанельОтборовМобильныйКлиент(ЭтотОбъект, "ПраваяПанель", "ДанныеМеток", "ОтборОстатки,ПоказыватьЦены,ОтборТипВидРабот,ОтборТипЗапас,ОтборТипОперация,ОтборТипПодарочныйСертификат,ОтборТипРабота,ОтборТипУслуга,ОтборНабор,ОтборИерархияТекущая,ОтборКатегорииТекущая");
	
КонецПроцедуры

&НаСервере
Процедура ПереопределитьТекстЗапросаВСписках()
	
	ПереопределитьТекстЗапросаСписокЗапасы();
	ПереопределитьТекстЗапросаСписокЗапасыВНаличии();
	
КонецПроцедуры

&НаСервере
Процедура ПереопределитьТекстЗапросаСписокЗапасы()
	
	СписокЗапасы.ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СправочникНоменклатура.Ссылка КАК Ссылка,
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	СправочникНоменклатура.ПометкаУдаления КАК ПометкаУдаления,
	|	СправочникНоменклатура.Родитель КАК Родитель,
	|	СправочникНоменклатура.ЭтоГруппа КАК ЭтоГруппа,
	|	СправочникНоменклатура.Код КАК Код,
	|	СправочникНоменклатура.Наименование КАК Наименование,
	|	СправочникНоменклатура.Артикул КАК Артикул,
	|	СправочникНоменклатура.ДатаИзменения КАК ДатаИзменения,
	|	СправочникНоменклатура.МетодОценки КАК МетодОценки,
	|	ВЫРАЗИТЬ(СправочникНоменклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК НаименованиеПолное,
	|	СправочникНоменклатура.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СправочникНоменклатура.КатегорияНоменклатуры КАК КатегорияНоменклатуры,
	|	СправочникНоменклатура.Поставщик КАК Поставщик,
	|	СправочникНоменклатура.Склад КАК Склад,
	|	СправочникНоменклатура.СпособПополнения КАК СпособПополнения,
	|	СправочникНоменклатура.СрокПополнения КАК СрокПополнения,
	|	СправочникНоменклатура.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|	ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка) КАК СтавкаНДСНоменклатуры,
	|	СправочникНоменклатура.СчетУчетаЗапасов КАК СчетУчетаЗапасов,
	|	СправочникНоменклатура.СчетУчетаЗатрат КАК СчетУчетаЗатрат,
	|	СправочникНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	СправочникНоменклатура.Ячейка КАК Ячейка,
	|	СправочникНоменклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	СправочникНоменклатура.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики,
	|	СправочникНоменклатура.ИспользоватьПартии КАК ИспользуетсяРезервирование,
	|	СправочникНоменклатура.ИспользоватьПартии КАК ИспользоватьПартии,
	|	СправочникНоменклатура.ЭтоНабор КАК ЭтоНабор,
	|	СправочникНоменклатура.Недействителен КАК Недействителен,
	|	СправочникНоменклатура.СрокИсполненияЗаказа КАК СрокИсполненияЗаказа,
	|	СправочникНоменклатура.НормаВремени КАК НормаВремени,
	|	СправочникНоменклатура.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	СправочникНоменклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СправочникНоменклатура.ВидАлкогольнойПродукции КАК ВидАлкогольнойПродукции,
	|	СправочникНоменклатура.ИмпортнаяАлкогольнаяПродукция КАК ИмпортнаяАлкогольнаяПродукция,
	|	СправочникНоменклатура.ПроизводительИмпортерАлкогольнойПродукции КАК ПроизводительИмпортерАлкогольнойПродукции,
	|	СправочникНоменклатура.Вес КАК Вес,
	|	СправочникНоменклатура.Объем КАК Объем,
	|	СправочникНоменклатура.ОбъемДАЛ КАК ОбъемДАЛ,
	|	СправочникНоменклатура.ИспользоватьСерийныеНомера КАК ИспользоватьСерийныеНомера,
	|	СправочникНоменклатура.ГарантийныйСрок КАК ГарантийныйСрок,
	|	СправочникНоменклатура.ВыписыватьГарантийныйТалон КАК ВыписыватьГарантийныйТалон,
	|	ПОДСТРОКА(СправочникНоменклатура.Комментарий, 1, 1000) КАК Комментарий,
	|	&Период КАК Период,
	|	&ВидЦен КАК ВидЦен,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА 0
	|		КОГДА СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|			ТОГДА 4
	|		ИНАЧЕ 2
	|	КОНЕЦ + ВЫБОР
	|		КОГДА СправочникНоменклатура.ИспользоватьХарактеристики
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА СправочникНоменклатура.ПометкаУдаления
	|			ТОГДА 6
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА СправочникНоменклатура.ЭтоНабор
	|			ТОГДА 12
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВариантКартинки,
	|	ВЫБОР
	|		КОГДА НаличиеФайлов.ЕстьФайлы ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА НаличиеФайлов.ЕстьФайлы
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЕстьФайлы,
	|	&ДопПоляШаблон КАК ДопПоляШаблон
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеФайлов КАК НаличиеФайлов
	|		ПО СправочникНоменклатура.Ссылка = НаличиеФайлов.ОбъектСФайлами}
	|";
	
	Если ОтборПоЦенам И ОтборПоКоличеству Тогда //Медленный способ с двумя левыми соединениями
		
		СписокЗапасы.ТекстЗапроса = СтрЗаменить(СписокЗапасы.ТекстЗапроса,
		"&ДопПоляШаблон КАК ДопПоляШаблон", 
		"ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения, СправочникНоменклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК Коэффициент,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена,
		|	ВЫБОР
		|		КОГДА ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
		|			ТОГДА ЕСТЬNULL(ОстаткиТоваров.Количество, 0) / ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент
		|		ИНАЧЕ ЕСТЬNULL(ОстаткиТоваров.Количество, 0)
		|	КОНЕЦ КАК КоличествоОстаток");
		
		СписокЗапасы.ТекстЗапроса = СтрЗаменить(СписокЗапасы.ТекстЗапроса,
		"ПО СправочникНоменклатура.Ссылка = НаличиеФайлов.ОбъектСФайлами",
		"ПО СправочникНоменклатура.Ссылка = НаличиеФайлов.ОбъектСФайлами
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, Актуальность И ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО (ЦеныНоменклатурыСрезПоследних.Номенклатура = СправочникНоменклатура.Ссылка)
		|			И (ЦеныНоменклатурыСрезПоследних.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОстаткиТоваров КАК ОстаткиТоваров
		|		ПО (ОстаткиТоваров.Номенклатура = СправочникНоменклатура.Ссылка)");
		
	ИначеЕсли ОтборПоЦенам Тогда //без отбора по количеству
		
		СписокЗапасы.ТекстЗапроса = СтрЗаменить(СписокЗапасы.ТекстЗапроса,
		"&ДопПоляШаблон КАК ДопПоляШаблон", 
		"ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения, СправочникНоменклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК Коэффициент,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена,
		|	0 КАК КоличествоОстаток");
		
		СписокЗапасы.ТекстЗапроса = СтрЗаменить(СписокЗапасы.ТекстЗапроса,
		"ПО СправочникНоменклатура.Ссылка = НаличиеФайлов.ОбъектСФайлами",
		"ПО СправочникНоменклатура.Ссылка = НаличиеФайлов.ОбъектСФайлами
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, Актуальность И ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО (ЦеныНоменклатурыСрезПоследних.Номенклатура = СправочникНоменклатура.Ссылка)
		|			И (ЦеныНоменклатурыСрезПоследних.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))");
		
	ИначеЕсли ОтборПоКоличеству Тогда //без отбора по ценам
		
		СписокЗапасы.ТекстЗапроса = СтрЗаменить(СписокЗапасы.ТекстЗапроса,
		"&ДопПоляШаблон КАК ДопПоляШаблон",
		"ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения, Значение(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	1 КАК Коэффициент,
		|	0 КАК Цена,
		|	ЕСТЬNULL(ОстаткиТоваров.Количество, 0) КАК КоличествоОстаток");
		
		СписокЗапасы.ТекстЗапроса = СтрЗаменить(СписокЗапасы.ТекстЗапроса,
		"ПО СправочникНоменклатура.Ссылка = НаличиеФайлов.ОбъектСФайлами",
		"ПО СправочникНоменклатура.Ссылка = НаличиеФайлов.ОбъектСФайлами
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОстаткиТоваров КАК ОстаткиТоваров
		|		ПО (ОстаткиТоваров.Номенклатура = СправочникНоменклатура.Ссылка)");
		
	Иначе
		
		СписокЗапасы.ТекстЗапроса = СтрЗаменить(СписокЗапасы.ТекстЗапроса,
		"&ДопПоляШаблон КАК ДопПоляШаблон", 
		"ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения, Значение(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	1 КАК Коэффициент,
		|	0 КАК Цена,
		|	0 КАК КоличествоОстаток");
		
	КонецЕсли;
	
	СписокЗапасы.ТекстЗапроса =	СписокЗапасы.ТекстЗапроса +"
	|ГДЕ
	|	НЕ СправочникНоменклатура.ЭтоГруппа;";
	
КонецПроцедуры

&НаСервере
Процедура ПереопределитьТекстЗапросаСписокЗапасыВНаличии()
	
	СписокЗапасыВНаличии.ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапасыНаСкладах.Номенклатура.Ссылка КАК Ссылка,
	|	ЗапасыНаСкладах.Номенклатура.Ссылка КАК Номенклатура,
	|	ЗапасыНаСкладах.Номенклатура.ПометкаУдаления КАК ПометкаУдаления,
	|	ЗапасыНаСкладах.Номенклатура.ЭтоГруппа КАК ЭтоГруппа,
	|	ЗапасыНаСкладах.Номенклатура.Код КАК Код,
	|	ЗапасыНаСкладах.Номенклатура.Родитель КАК Родитель,
	|	ЗапасыНаСкладах.Номенклатура.Наименование КАК Наименование,
	|	ЗапасыНаСкладах.Номенклатура.Артикул КАК Артикул,
	|	ВЫРАЗИТЬ(ЗапасыНаСкладах.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК НаименованиеПолное,
	|	ЗапасыНаСкладах.Номенклатура.КатегорияНоменклатуры КАК КатегорияНоменклатуры,
	|	ЗапасыНаСкладах.Номенклатура.Поставщик КАК Поставщик,
	|	ЗапасыНаСкладах.Номенклатура.Склад КАК Склад,
	|	ЗапасыНаСкладах.Номенклатура.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|	ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка) КАК СтавкаНДСНоменклатуры,
	|	ЗапасыНаСкладах.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ЗапасыНаСкладах.Номенклатура.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики,
	|	ЗапасыНаСкладах.Номенклатура.ЭтоНабор КАК НоменклатураЭтоНабор,
	|	ЗапасыНаСкладах.Номенклатура.ИспользоватьПартии КАК ИспользуетсяРезервирование,
	|	ЗапасыНаСкладах.Номенклатура.ИспользоватьПартии КАК ИспользоватьПартии,
	|	ЗапасыНаСкладах.Номенклатура.ЭтоНабор КАК ЭтоНабор,
	|	ЗапасыНаСкладах.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗапасыНаСкладах.Номенклатура.Недействителен КАК Недействителен,
	|	ПОДСТРОКА(ЗапасыНаСкладах.Номенклатура.Комментарий, 1, 1000) КАК Комментарий,
	|	ЗапасыНаСкладах.Номенклатура.ДатаИзменения КАК ДатаИзменения,
	|	ЗапасыНаСкладах.Номенклатура.МетодОценки КАК МетодОценки,
	|	ЗапасыНаСкладах.Номенклатура.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗапасыНаСкладах.Номенклатура.СпособПополнения КАК СпособПополнения,
	|	ЗапасыНаСкладах.Номенклатура.СрокПополнения КАК СрокПополнения,
	|	ЗапасыНаСкладах.Номенклатура.СчетУчетаЗапасов КАК СчетУчетаЗапасов,
	|	ЗапасыНаСкладах.Номенклатура.СчетУчетаЗатрат КАК СчетУчетаЗатрат,
	|	ЗапасыНаСкладах.Номенклатура.Ячейка КАК Ячейка,
	|	ЗапасыНаСкладах.Номенклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	ЗапасыНаСкладах.Номенклатура.СрокИсполненияЗаказа КАК СрокИсполненияЗаказа,
	|	ЗапасыНаСкладах.Номенклатура.НормаВремени КАК НормаВремени,
	|	ЗапасыНаСкладах.Номенклатура.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	ЗапасыНаСкладах.Номенклатура.ВидАлкогольнойПродукции КАК ВидАлкогольнойПродукции,
	|	ЗапасыНаСкладах.Номенклатура.ИмпортнаяАлкогольнаяПродукция КАК ИмпортнаяАлкогольнаяПродукция,
	|	ЗапасыНаСкладах.Номенклатура.ПроизводительИмпортерАлкогольнойПродукции КАК ПроизводительИмпортерАлкогольнойПродукции,
	|	ЗапасыНаСкладах.Номенклатура.Вес КАК Вес,
	|	ЗапасыНаСкладах.Номенклатура.Объем КАК Объем,
	|	ЗапасыНаСкладах.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ,
	|	ЗапасыНаСкладах.Номенклатура.ИспользоватьСерийныеНомера КАК НоменклатураИспользоватьСерийныеНомера,
	|	ЗапасыНаСкладах.Номенклатура.ГарантийныйСрок КАК НоменклатураГарантийныйСрок,
	|	ЗапасыНаСкладах.Номенклатура.ПодконтрольнаяПродукцияВЕТИС КАК ПодконтрольнаяПродукцияВЕТИС,
	|	ЗапасыНаСкладах.Номенклатура.ВыписыватьГарантийныйТалон КАК НоменклатураВыписыватьГарантийныйТалон,
	|	ЗапасыНаСкладах.Характеристика КАК Характеристика,
	|	&Период КАК Период,
	|	&ВидЦен КАК ВидЦен,
	|	ВЫБОР
	|		КОГДА ЗапасыНаСкладах.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА 0
	|		КОГДА ЗапасыНаСкладах.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|			ТОГДА 4
	|		ИНАЧЕ 2
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ЗапасыНаСкладах.Номенклатура.ИспользоватьХарактеристики
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ЗапасыНаСкладах.Номенклатура.ПометкаУдаления
	|			ТОГДА 6
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ЗапасыНаСкладах.Номенклатура.ЭтоНабор
	|			ТОГДА 12
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВариантКартинки,
	|	ЕСТЬNULL(ЗапасыНаСкладах.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ВЫБОР
	|		КОГДА НаличиеФайлов.ЕстьФайлы ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА НаличиеФайлов.ЕстьФайлы
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЕстьФайлы,
	|	&ДопПоляШаблон КАК ДопПоляШаблон
	|ИЗ
	|	РегистрНакопления.ЗапасыНаСкладах.Остатки(
	|			,
	|			(&ВсеСклады
	|				ИЛИ СтруктурнаяЕдиница = &Склад)
	|				И НЕ Номенклатура.ЭтоНабор
	|				И НЕ Номенклатура.ЭтоГруппа) КАК ЗапасыНаСкладах
	|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеФайлов КАК НаличиеФайлов
	|		ПО ЗапасыНаСкладах.Номенклатура = НаличиеФайлов.ОбъектСФайлами}";
	
	СписокЗапасыВНаличии.ТекстЗапроса = СтрЗаменить(СписокЗапасыВНаличии.ТекстЗапроса, "ПО (&ОкончаниеШаблона)", "");
	
	Если ОтборПоЦенам Тогда
		
		СписокЗапасыВНаличии.ТекстЗапроса = СтрЗаменить(СписокЗапасыВНаличии.ТекстЗапроса,
		"&ДопПоляШаблон КАК ДопПоляШаблон",
		"ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения, ЗапасыНаСкладах.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК Коэффициент,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена");
		
		СписокЗапасыВНаличии.ТекстЗапроса = СтрЗаменить(СписокЗапасыВНаличии.ТекстЗапроса,
		"ПО ЗапасыНаСкладах.Номенклатура = НаличиеФайлов.ОбъектСФайлами",
		"ПО ЗапасыНаСкладах.Номенклатура = НаличиеФайлов.ОбъектСФайлами
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, Актуальность И ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО (ЦеныНоменклатурыСрезПоследних.Номенклатура = ЗапасыНаСкладах.Номенклатура)
		|			И (ЦеныНоменклатурыСрезПоследних.Характеристика = ЗапасыНаСкладах.Характеристика)");
		
	Иначе
		
		СписокЗапасыВНаличии.ТекстЗапроса = СтрЗаменить(СписокЗапасыВНаличии.ТекстЗапроса,
		"&ДопПоляШаблон КАК ДопПоляШаблон",
		"ЕСТЬNULL(ЗапасыНаСкладах.Номенклатура.ЕдиницаИзмерения, Значение(справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	1 КАК Коэффициент,
		|	0 КАК Цена");
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОграничениеПорядокОтборГруппировка(Список)
	
	ОграничениеПорядок = Список.ПолучитьОграниченияИспользованияВПорядке();
	ОграничениеОтбор = Список.ПолучитьОграниченияИспользованияВОтборе();
	ОграничениеГруппировка = Список.ПолучитьОграниченияИспользованияВГруппировке();
	
	ИндексПорядокКоличество = ОграничениеПорядок.Найти("КоличествоОстаток");
	ИндексОтборКоличество = ОграничениеОтбор.Найти("КоличествоОстаток");
	ИндексГруппировкаКоличество = ОграничениеГруппировка.Найти("КоличествоОстаток");
	Если ОтборПоКоличеству ИЛИ Список = СписокЗапасыВНаличии Тогда //можно использовать количество
		Если ИндексПорядокКоличество <> Неопределено Тогда
			ОграничениеПорядок.Удалить(ИндексПорядокКоличество);
		КонецЕсли;
		Если ИндексОтборКоличество <> Неопределено Тогда
			ОграничениеОтбор.Удалить(ИндексОтборКоличество);
		КонецЕсли;
		Если ИндексГруппировкаКоличество <> Неопределено Тогда
			ОграничениеГруппировка.Удалить(ИндексГруппировкаКоличество);
		КонецЕсли;
	Иначе
		Если ИндексПорядокКоличество = Неопределено Тогда
			ОграничениеПорядок.Добавить("КоличествоОстаток");
		КонецЕсли;
		Если ИндексОтборКоличество = Неопределено Тогда
			ОграничениеОтбор.Добавить("КоличествоОстаток");
		КонецЕсли;
		Если ИндексГруппировкаКоличество = Неопределено Тогда
			ОграничениеГруппировка.Добавить("КоличествоОстаток");
		КонецЕсли;
	КонецЕсли;
	
	ИндексПорядокЦена = ОграничениеПорядок.Найти("Цена");
	ИндексОтборЦена = ОграничениеОтбор.Найти("Цена");
	ИндексГруппировкаЦена = ОграничениеГруппировка.Найти("Цена");
	Если ОтборПоЦенам Тогда //можно использовать цену
		Если ИндексПорядокЦена <> Неопределено Тогда
			ОграничениеПорядок.Удалить(ИндексПорядокЦена);
		КонецЕсли;
		Если ИндексОтборЦена <> Неопределено Тогда
			ОграничениеОтбор.Удалить(ИндексОтборЦена);
		КонецЕсли;
		Если ИндексГруппировкаЦена <> Неопределено Тогда
			ОграничениеГруппировка.Удалить(ИндексГруппировкаЦена);
		КонецЕсли;
	Иначе
		Если ИндексПорядокЦена = Неопределено Тогда
			ОграничениеПорядок.Добавить("Цена");
		КонецЕсли;
		Если ИндексОтборЦена = Неопределено Тогда
			ОграничениеОтбор.Добавить("Цена");
		КонецЕсли;
		Если ИндексГруппировкаЦена = Неопределено Тогда
			ОграничениеГруппировка.Добавить("Цена");
		КонецЕсли;
	КонецЕсли;
	
	Список.УстановитьОграниченияИспользованияВПорядке(ОграничениеПорядок);
	Список.УстановитьОграниченияИспользованияВОтборе(ОграничениеОтбор);
	Список.УстановитьОграниченияИспользованияВГруппировке(ОграничениеГруппировка);
	
	массивУдалить = Новый Массив;
	Для каждого элКомпоновки Из Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если (НЕ ОтборПоЦенам) И Лев(элКомпоновки, 4) = "Цена" И
			(ТипЗнч(элКомпоновки) = Тип("ПорядокКомпоновкиДанных")
				ИЛИ ТипЗнч(элКомпоновки) = Тип("ОтборКомпоновкиДанных")
				ИЛИ ТипЗнч(элКомпоновки) = Тип("ГруппировкаКомпоновкиДанных"))
			Тогда
			массивУдалить.Добавить(элКомпоновки);
		КонецЕсли;
		Если (НЕ ОтборПоКоличеству) И Лев(элКомпоновки, 17) = "КоличествоОстаток" И
			(ТипЗнч(элКомпоновки) = Тип("ПорядокКомпоновкиДанных")
				ИЛИ ТипЗнч(элКомпоновки) = Тип("ОтборКомпоновкиДанных")
				ИЛИ ТипЗнч(элКомпоновки) = Тип("ГруппировкаКомпоновкиДанных"))
			Тогда
			массивУдалить.Добавить(элКомпоновки);
		КонецЕсли;		
	КонецЦикла;
	Для каждого элУдалить Из массивУдалить Цикл
		Для каждого элНастройки Из элУдалить.Элементы Цикл
			элНастройки.Использование = Ложь;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область МеткиОтборов

&НаКлиенте
Процедура Подключаемый_МеткаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МеткаИД = Сред(Элемент.Имя, СтрДлина("Метка_")+1);
	УдалитьМеткуОтбора(МеткаИД);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьМеткуИОтборСписка(ИмяПоляОтбораСписка, ГруппаРодительМетки, ВыбранноеЗначение, ПредставлениеЗначения="")
	
	Если ПредставлениеЗначения="" Тогда
		ПредставлениеЗначения=Строка(ВыбранноеЗначение);
	КонецЕсли; 
	
	РаботаСОтборами.ПрикрепитьМеткуОтбора(ЭтотОбъект, ИмяПоляОтбораСписка, ГруппаРодительМетки, ВыбранноеЗначение, ПредставлениеЗначения);
	РаботаСОтборами.УстановитьОтборСписка(ЭтотОбъект, ЭтаФорма[ТекущаяСтраницаНоменклатуры], ИмяПоляОтбораСписка);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьМеткуОтбора(МеткаИД)
	
	РаботаСОтборами.УдалитьМеткуОтбораСервер(ЭтотОбъект, ЭтаФорма[ТекущаяСтраницаНоменклатуры], МеткаИД);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьМеткиОтбора(Метки)
	
	РаботаСОтборами.УдалитьМеткиОтбораСервер(ЭтотОбъект, ЭтаФорма[ТекущаяСтраницаНоменклатуры], Метки);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыМеток()
	
	РаботаСОтборами.ОбновитьЭлементыМеток(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемоеОборудование

// Процедура обрабатывает полученные штрихкоды.
//
&НаКлиенте
Процедура ПолученШтрихкод(ДанныеШтрихкода) Экспорт
	
	Штрихкод = ДанныеШтрихкода.Штрихкод;
	
	Номенклатура = ПолучитьНоменклатуруПоШтрихкоду(ДанныеШтрихкода);
	
	Если Номенклатура <> Неопределено Тогда
		
		Элементы[ТекущаяСтраницаНоменклатуры].ТекущаяСтрока = Номенклатура;
		ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта",Новый Структура("Ключ",Номенклатура),ЭтотОбъект);
		Возврат;
		
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	Если РаботаСНоменклатуройКлиент.ДоступенФункционалПодсистемы() Тогда
		РаботаСНоменклатуройКлиент.НайтиНоменклатуруПоШтрихкодуВСервисе(ШтрихКод);
		Возврат;
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	
	СтрокаСообщения = НСтр("ru = 'Данные по штрихкоду не найдены: %1%'");
	СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1%", Штрихкод);
	ОбщегоНазначенияКлиент.СообщитьПользователю(СтрокаСообщения);
	
КонецПроцедуры // ПолученыШтрихкоды()

Функция ПолучитьНоменклатуруПоШтрихкоду(ДанныеШтрихкода)

	возврат РегистрыСведений.ШтрихкодыНоменклатуры.ПолучитьНоменклатуруПоШтрихкоду(ДанныеШтрихкода);
	
КонецФункции

// Процедура - обработчик команды командной панели табличной части.
&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ТекШтрихкод = "";
	ПоказатьВводЗначения(Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект, Новый Структура("ТекШтрихкод", ТекШтрихкод)), ТекШтрихкод, НСтр("ru = 'Введите штрихкод'"));

КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ТекШтрихкод = ?(Результат = Неопределено, ДополнительныеПараметры.ТекШтрихкод, Результат);
    
    
    Если НЕ ПустаяСтрока(ТекШтрихкод) Тогда
        ПолученШтрихкод(Новый Структура("Штрихкод, Количество", ТекШтрихкод, 1));
    КонецЕсли;

КонецПроцедуры // ПоискПоШтрихкоду()

&НаКлиенте
Процедура ОтборВидЦенОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти //ПодключаемоеОборудование

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
&НаКлиенте
Процедура ПоказатьПомощникЗагрузкиДанныхИзВнешнегоИсточника()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата", ЭтотОбъект, НастройкиЗагрузкиДанных);
	
	НастройкиЗагрузкиДанных.Вставить("ИмяМакетаСШаблоном", "ЗагрузкаИзФайла");
	НастройкиЗагрузкиДанных.Вставить("ОписаниеСтрокиВыбора", Новый Структура("ПолноеИмяОбъектаМетаданных, Тип", "Номенклатура", "ПрикладнаяЗагрузка"));
	
	ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных, ОписаниеОповещения, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДанныхИзВнешнегоИсточника(Команда)
	
	ПоказатьПомощникЗагрузкиДанныхИзВнешнегоИсточника();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата(РезультатЗагрузки, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗагрузки) = Тип("Структура") Тогда
		
		Если РезультатЗагрузки.ОписаниеДействия = "ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника" Тогда
		
			ЗагрузкаДанныхИзВнешнегоИсточника.ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных.ИмяФормыЗагрузкиДанныхИзВнешнихИсточников);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата", ЭтотОбъект, НастройкиЗагрузкиДанных);
			ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных, ОписаниеОповещения, ЭтотОбъект);
			
		ИначеЕсли РезультатЗагрузки.ОписаниеДействия = "ОбработатьПодготовленныеДанные" Тогда
			
			ДлительнаяОперация = ОбработатьПодготовленныеДанные(РезультатЗагрузки);
			ПроверитьНеобходимостьПодключенияОбработчикаОжидания(ДлительнаяОперация);
			
		КонецЕсли;
		
	ИначеЕсли РезультатЗагрузки = Неопределено Тогда
		
		Элементы[ТекущаяСтраницаНоменклатуры].Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
		
	Если Элементы.СтраницыНоменклатуры.ТекущаяСтраница.Имя = "СтраницаЗапасы" Тогда
		УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Элементы.СписокЗапасы);
	Иначе
		УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Элементы.СписокЗапасыВНаличии);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокЗапасыПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Если НЕ Настройки.ДополнительныеСвойства.ТекущаяСтраницаНоменклатуры = "СписокЗапасы" Тогда
		Возврат;
	КонецЕсли;
	
	ПоказыватьЦены = Настройки.ДополнительныеСвойства.ПоказыватьЦены;
	ПоказыватьОстатки = Настройки.ДополнительныеСвойства.ПоказыватьОстатки;
	
	ОтборПоКоличеству = Настройки.ДополнительныеСвойства.ОтборПоКоличеству;
	ОтборПоЦенам = Настройки.ДополнительныеСвойства.ОтборПоЦенам;
	ВидЦен = Неопределено;
	Период = ТекущаяДата();
	СоответствиеСтавокНДС = Настройки.ДополнительныеСвойства.СоответствиеСтавокНДС;
	
	Если ОтборПоЦенам И ОтборПоКоличеству Тогда
		//ничего не делать: данные получены запросом в динамическом списке
		Возврат;
	КонецЕсли;
	
	Ссылки = Новый Массив;
	Для Каждого КлючИЗначение Из Строки Цикл
		Ссылки.Добавить(КлючИЗначение.Ключ);
		
		// Заполняем ставки НДС
		СтрокаСписка =  КлючИЗначение.Значение.Данные;
		СтрокаСписка.СтавкаНДСНоменклатуры = СоответствиеСтавокНДС.Получить(СтрокаСписка.ВидСтавкиНДС);
	КонецЦикла;
	
	Для Каждого элОтбора Из Настройки.ПараметрыДанных.Элементы Цикл
		
		Если Строка(элОтбора.Параметр) = "ВидЦен" Тогда
			ВидЦен = элОтбора.Значение;
		КонецЕсли;
		Если Строка(элОтбора.Параметр) = "Период" Тогда
			Период = элОтбора.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОтборПоЦенам Тогда
		ВывестиКоличество(Строки, Ссылки, ПоказыватьОстатки);
		Возврат;
	КонецЕсли;
	
	Если ОтборПоКоличеству Тогда
		ВывестиЦены(Строки, Ссылки, ВидЦен, ПоказыватьЦены);
		Возврат;
	КонецЕсли;
	
	Если ПоказыватьЦены И ПоказыватьОстатки Тогда
		ВывестиЦеныИКоличество(Строки, Ссылки, ВидЦен);
		Возврат
	КонецЕсли;
	
	ВывестиЦены(Строки, Ссылки, ВидЦен, ПоказыватьЦены);
	ВывестиКоличество(Строки, Ссылки, ПоказыватьОстатки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокЗапасыВНаличииПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)

	Если НЕ Настройки.ДополнительныеСвойства.ТекущаяСтраницаНоменклатуры = "СписокЗапасыВНаличии" Тогда
		Возврат;
	КонецЕсли;
	
	ПоказыватьЦены = Настройки.ДополнительныеСвойства.ПоказыватьЦены;
	ПоказыватьОстатки = Настройки.ДополнительныеСвойства.ПоказыватьОстатки;
	
	ОтборПоКоличеству = Настройки.ДополнительныеСвойства.ОтборПоКоличеству;
	ОтборПоЦенам = Настройки.ДополнительныеСвойства.ОтборПоЦенам;
	СоответствиеСтавокНДС = Настройки.ДополнительныеСвойства.СоответствиеСтавокНДС;
	
	ВидЦен = Неопределено;
	Период = ТекущаяДата();

	Для каждого элОтбора Из Настройки.ПараметрыДанных.Элементы Цикл
	
		Если Строка(элОтбора.Параметр) = "ВидЦен" Тогда
			ВидЦен = элОтбора.Значение;
		КонецЕсли;
		Если Строка(элОтбора.Параметр) = "Период" Тогда
			Период = элОтбора.Значение;
		КонецЕсли;
	
	КонецЦикла;
	
	Номенклатура = Новый Массив;
	Характеристики = Новый Массив;
	
	ТЗСтрок = Новый ТаблицаЗначений;
	ТЗСтрок.Колонки.Добавить("Ключ", Новый ОписаниеТипов("Число"));
	ТЗСтрок.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТЗСтрок.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Для Каждого КлючИЗначение Из Строки Цикл
		СтрокаСписка =  КлючИЗначение.Значение.Данные;
		НовСтр = ТЗСтрок.Добавить();
		НовСтр.Ключ = КлючИЗначение.Ключ;
		НовСтр.Номенклатура = СтрокаСписка.Номенклатура;
		НовСтр.Характеристика = СтрокаСписка.Характеристика;
		
		// Заполняем ставки НДС
		СтрокаСписка.СтавкаНДСНоменклатуры = СоответствиеСтавокНДС.Получить(СтрокаСписка.ВидСтавкиНДС);
	КонецЦикла;
	
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Если ПоказыватьЦены Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТЗСтрок.Ключ КАК Ключ,
		|	ТЗСтрок.Номенклатура КАК Номенклатура,
		|	ТЗСтрок.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ ВТСтрок
		|ИЗ
		|	&ТЗСтрок КАК ТЗСтрок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТСтрок.Ключ КАК КлючСтроки,
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатурыСрезПоследних.Характеристика КАК Характеристика,
		|	ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК Коэффициент,
		|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
		|ИЗ
		|	ВТСтрок КАК ВТСтрок
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО ВТСтрок.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
		|			И ВТСтрок.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
		|ГДЕ
		|	НЕ ЦеныНоменклатурыСрезПоследних.Номенклатура ЕСТЬ NULL";
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("Характеристики", Характеристики);
		Запрос.УстановитьПараметр("ТЗСтрок", ТЗСтрок);
		
		Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаСписка = Строки.Получить(Выборка.КлючСтроки).Данные;
			
			Если ЗначениеЗаполнено(Выборка.ЕдиницаИзмерения) Тогда
				СтрокаСписка.Цена = Выборка.Цена;
				СтрокаСписка.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
				СтрокаСписка.КоличествоОстаток = СтрокаСписка.КоличествоОстаток / Выборка.Коэффициент;
			
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника

// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
&НаКлиенте
Процедура Подключаемый_ПодобратьНоменклатуруИзСервиса(Команда)
		
	РаботаСНоменклатуройКлиент.ПодобратьНоменклатуруИзСервиса();
	
КонецПроцедуры	
// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ИмяСписка = ТекущаяСтраницаНоменклатуры;
	
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы[ИмяСписка]);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	
	ИмяСписка = ТекущаяСтраницаНоменклатуры;
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы[ИмяСписка], Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.СписокЗапасы);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПорядокГруппыНоменклатуры(Шаг)
	
	ДанныеТекущейСтроки = Элементы.ОтборИерархия.ТекущиеДанные;
	Если ДанныеТекущейСтроки = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ГруппаНоменклатурыСсылка = ДанныеТекущейСтроки.Значение;
	Если ЗначениеЗаполнено(ГруппаНоменклатурыСсылка) Тогда
		
		ИзменитьПорядокГруппыНоменклатурыНаСервере(ГруппаНоменклатурыСсылка, Шаг);
		ЗаполнитьДеревоИерархии();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПорядокКатегорийНоменклатуры(Шаг)
	
	ДанныеТекущейСтроки = Элементы.ОтборКатегории.ТекущиеДанные;
	Если ДанныеТекущейСтроки = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ИзменитьПорядокКатегорииНоменклатурыНаСервере(ДанныеТекущейСтроки.Значение, Шаг) = Истина Тогда
		
		ЗаполнитьДеревоКатегорий();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьПорядокГруппыНоменклатурыНаСервере(ГруппаНоменклатурыСсылка, Шаг)
	
	ГруппаНоменклатурыОбъект = ГруппаНоменклатурыСсылка.ПолучитьОбъект();
	ГруппаНоменклатурыОбъект.РеквизитДопУпорядочиванияУНФ = ГруппаНоменклатурыОбъект.РеквизитДопУпорядочиванияУНФ + Шаг;
	ГруппаНоменклатурыОбъект.Записать();
	
КонецПроцедуры

&НаСервере
Функция ИзменитьПорядокКатегорииНоменклатурыНаСервере(КатегорияНоменклатуры, Шаг)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КатегорияНоменклатуры",			КатегорияНоменклатуры);
	Запрос.УстановитьПараметр("ЭтоГруппа", 						КатегорияНоменклатуры.ЭтоГруппа);
	Запрос.УстановитьПараметр("РодительКатегорииНоменклатуры",	КатегорияНоменклатуры.Родитель);
	Запрос.УстановитьПараметр("РеквизитДопУпорядочивания",		КатегорияНоменклатуры.РеквизитДопУпорядочивания);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	КатегорииНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КатегорииНоменклатуры КАК КатегорииНоменклатуры
	|ГДЕ
	|	КатегорииНоменклатуры.Ссылка <> &КатегорияНоменклатуры
	|	И КатегорииНоменклатуры.ЭтоГруппа = &ЭтоГруппа
	|	И КатегорииНоменклатуры.Родитель = &РодительКатегорииНоменклатуры
	|	И КатегорииНоменклатуры.РеквизитДопУпорядочивания <= &РеквизитДопУпорядочивания
	|
	|УПОРЯДОЧИТЬ ПО
	|	КатегорииНоменклатуры.РеквизитДопУпорядочивания УБЫВ";
	
	// Вверх	Шаг	-1
	// Вниз		Шаг	+1
	
	Если Шаг > 0 Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,	"<=",	">=");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,	"Убыв",	"Возр");
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		
		КатегорияНоменклатурыСмещения = Выборка.Ссылка.ПолучитьОбъект();
		КатегорияНоменклатураИсходная = КатегорияНоменклатуры.ПолучитьОбъект();
		
		КэшРеквизита = КатегорияНоменклатурыСмещения.РеквизитДопУпорядочивания;
		КатегорияНоменклатурыСмещения.РеквизитДопУпорядочивания = КатегорияНоменклатураИсходная.РеквизитДопУпорядочивания;
		КатегорияНоменклатураИсходная.РеквизитДопУпорядочивания = КэшРеквизита;
		
		КатегорияНоменклатураИсходная.Записать();
		КатегорияНоменклатурыСмещения.Записать();
		
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОбновитьВидыСтавокНДСНаСервере()
	Справочники.Номенклатура.ЗаполнитьВидыСтавокНДС();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидыСтавокНДС(Команда)
	ОбновитьВидыСтавокНДСНаСервере();
КонецПроцедуры

&НаСервере
Функция ОбработатьПодготовленныеДанные(РезультатЗагрузки)
	
	ПараметрыВызоваСервера = Новый Структура;
	ПараметрыВызоваСервера.Вставить("НастройкиЗагрузкиДанных", РезультатЗагрузки.НастройкиЗагрузкиДанных);
	ПараметрыВызоваСервера.Вставить("ТаблицаСопоставленияДанных", ДанныеФормыВЗначение(РезультатЗагрузки.ТаблицаСопоставленияДанных, Тип("ТаблицаЗначений")));
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Подсистема ЗагрузкаДанныхИзВнешнегоИсточника: Выполнение серверного метода загрузки результата'");
	ПараметрыВыполнения.ЗапуститьНеВФоне    = Ложь;
	ПараметрыВыполнения.ЗапуститьВФоне      = Истина;	
	
	ИмяМетода = "Справочники.Номенклатура.ОбработатьПодготовленныеДанные";
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыВызоваСервера, ПараметрыВыполнения);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ВывестиЦены(Строки, Ссылки, ВидЦен, ПоказыватьЦены)
	
	Если Не ПоказыватьЦены Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК Коэффициент,
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			,
	|			ВидЦен = &ВидЦен
	|				И Номенклатура В (&Ссылки)) КАК ЦеныНоменклатурыСрезПоследних
	|ГДЕ
	|	НЕ ЦеныНоменклатурыСрезПоследних.Номенклатура ЕСТЬ NULL
	|	И ЦеныНоменклатурыСрезПоследних.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)");
	
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаСписка = Строки.Получить(Выборка.Номенклатура).Данные;
		
		Если Выборка.Коэффициент <> 1 Тогда
			СтрокаСписка.КоличествоОстаток = СтрокаСписка.КоличествоОстаток / Выборка.Коэффициент;
		КонецЕсли; 
		СтрокаСписка.Цена = Выборка.Цена;
		СтрокаСписка.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВывестиКоличество(Строки, Ссылки, ПоказыватьОстатки)
	
	Если Не ПоказыватьОстатки Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТоварныеЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ТоварныеЗапасыОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТоварныеЗапасыОстатки.Количество КАК КоличествоОстаток,
	|	0 КАК Цена
	|ИЗ
	|	РегистрСведений.ОстаткиТоваров КАК ТоварныеЗапасыОстатки
	|ГДЕ
	|	ТоварныеЗапасыОстатки.Номенклатура В(&Ссылки)");
	
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаСписка = Строки.Получить(Выборка.Номенклатура).Данные;
		СтрокаСписка.КоличествоОстаток = Выборка.КоличествоОстаток / СтрокаСписка.Коэффициент;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВывестиЦеныИКоличество(Строки, Ссылки, ВидЦен)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(ОстаткиТоваров.Номенклатура, ЦеныНоменклатурыСрезПоследних.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения, ОстаткиТоваров.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ОстаткиТоваров.Количество, 0) / ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ ЕСТЬNULL(ОстаткиТоваров.Количество, 0)
	|	КОНЕЦ КАК КоличествоОстаток,
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
	|ИЗ
	|	РегистрСведений.ОстаткиТоваров КАК ОстаткиТоваров
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				,
	|				ВидЦен = &ВидЦен
	|					И Номенклатура В (&Ссылки)
	|					И Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ОстаткиТоваров.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|ГДЕ
	|	(ОстаткиТоваров.Номенклатура В (&Ссылки)
	|			ИЛИ НЕ ЦеныНоменклатурыСрезПоследних.Номенклатура ЕСТЬ NULL
	|				И ЦеныНоменклатурыСрезПоследних.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))");
	
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаСписка = Строки.Получить(Выборка.Номенклатура).Данные;
		
		СтрокаСписка.КоличествоОстаток = Выборка.КоличествоОстаток;
		СтрокаСписка.Цена = Выборка.Цена;
		СтрокаСписка.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
		
	КонецЦикла;

КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
