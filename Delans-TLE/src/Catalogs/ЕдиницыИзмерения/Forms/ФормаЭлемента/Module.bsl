
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьВесИОбъем()
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	НастроитьВесИОбъем();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("УказатьВесИОбъемДляЕдиницыТовара", ЗначениеЗаполнено(Вес) ИЛИ ЗначениеЗаполнено(Объем));
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Вес", Вес);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Объем", Объем);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	НастроитьВесИОбъем();
	
КонецПроцедуры

&НаКлиенте
Процедура ВесПриИзменении(Элемент)
	
	Элементы.ГруппаВесИОбъем.ЗаголовокСвернутогоОтображения = ЗаголовокСвернутойГруппыВесИОбъем(Вес, Объем);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъемПриИзменении(Элемент)
	
	Элементы.ГруппаВесИОбъем.ЗаголовокСвернутогоОтображения = ЗаголовокСвернутойГруппыВесИОбъем(Вес, Объем);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьВесИОбъем()
	
	Если ТипЗнч(Объект.Владелец) <> Тип("СправочникСсылка.Номенклатура") Тогда
		Элементы.ГруппаВесИОбъем.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаВесИОбъем.Видимость = Истина;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ВесИОбъемЕдиницТоваров.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Номенклатура = Объект.Владелец;
	МенеджерЗаписи.ЕдиницаИзмерения = Объект.Ссылка;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		Вес = МенеджерЗаписи.Вес;
		Объем = МенеджерЗаписи.Объем;
		Элементы.ГруппаВесИОбъем.ЗаголовокСвернутогоОтображения = ЗаголовокСвернутойГруппыВесИОбъем(Вес, Объем);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокСвернутойГруппыВесИОбъем(Знач Вес, Знач Объем)
	
	КомпонентыЗаголовка = Новый Массив;
	Если ЗначениеЗаполнено(Вес) Тогда
		КомпонентыЗаголовка.Добавить(СтрШаблон("Вес: %1 кг", Вес));
	КонецЕсли;
	Если ЗначениеЗаполнено(Объем) Тогда
		КомпонентыЗаголовка.Добавить(СтрШаблон("Объем: %1 м³", Объем));
	КонецЕсли;
	
	Возврат СтрСоединить(КомпонентыЗаголовка, ", ");
	
КонецФункции

#КонецОбласти