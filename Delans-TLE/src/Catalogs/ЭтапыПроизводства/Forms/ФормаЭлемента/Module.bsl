
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеФормы();	
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СтруктутрныеЕдиницыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элементы.СтруктутрныеЕдиницы.ТекущиеДанные;
	Если НоваяСтрока И Объект.СтруктутрныеЕдиницы.Количество()=1 Тогда
		ТекущаяСтрока.ПоУмолчанию = Истина;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктутрныеЕдиницыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение)=Тип("Массив") Тогда
		МассивСтруктурныхЕдиниц = ВыбранноеЗначение;
	Иначе
		МассивСтруктурныхЕдиниц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбранноеЗначение);
	КонецЕсли; 
	
	Для каждого СтруктурнаяЕдиница Из МассивСтруктурныхЕдиниц Цикл
		
		Если ТипЗнч(СтруктурнаяЕдиница)<>Тип("СправочникСсылка.СтруктурныеЕдиницы") ИЛИ НЕ ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
			Возврат;
		КонецЕсли; 
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
		Если Объект.СтруктутрныеЕдиницы.НайтиСтроки(СтруктураОтбора).Количество()=0 Тогда
			НоваяСтрока = Объект.СтруктутрныеЕдиницы.Добавить();
			НоваяСтрока.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
			НоваяСтрока.ПоУмолчанию = (Объект.СтруктутрныеЕдиницы.Количество()=1);
			Модифицированность = Истина;
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подобрать(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("Отбор", Новый Структура);
	ПараметрыФормы.Отбор.Вставить("ТипСтруктурнойЕдиницы", ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение"));
	ОткрытьФорму("Справочник.СтруктурныеЕдиницы.ФормаВыбора", ПараметрыФормы, Элементы.СтруктутрныеЕдиницы, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьКакОсновное(Команда)
	
	ТекущаяСтрока = Элементы.СтруктутрныеЕдиницы.ТекущиеДанные;
	ТекущаяСтрока.ПоУмолчанию = НЕ ТекущаяСтрока.ПоУмолчанию;
	Если ТекущаяСтрока.ПоУмолчанию Тогда
		Для каждого СтрокаТабличнойЧасти Из Объект.СтруктутрныеЕдиницы Цикл
			Если ТекущаяСтрока=СтрокаТабличнойЧасти ИЛИ НЕ СтрокаТабличнойЧасти.ПоУмолчанию Тогда
				Продолжить;
			КонецЕсли; 
			СтрокаТабличнойЧасти.ПоУмолчанию = Ложь;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "СтруктутрныеЕдиницыСтруктурнаяЕдиница");
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.СтруктутрныеЕдиницы.ПоУмолчанию", Истина);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Шрифт", Новый Шрифт(Новый Шрифт, , , Истина));
	
КонецПроцедуры

#КонецОбласти
 
