
&НаСервере
Процедура ПолучитьПараметрыЗаполнения(СтруктураЗаполнения)
	
	ЗаголовокПредставления = "";
	Если Константы.ДобавитьСловоДоговорВПредставлениеДоговораОснованияПечати.Получить() Тогда
		
		ЗаголовокПредставления = Нстр("ru = 'Договор: '");
		
	КонецЕсли;
	
	ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ДоговорПоУмолчанию(СтруктураЗаполнения.Контрагент);
	СтруктураЗаполнения.Вставить("Договор", ДоговорПоУмолчанию);
	СтруктураЗаполнения.Вставить("ОснованиеПечатиСсылка", ДоговорПоУмолчанию);
	СтруктураЗаполнения.Вставить("ОснованиеПечати", ?(ЗначениеЗаполнено(ДоговорПоУмолчанию), ЗаголовокПредставления + ДоговорПоУмолчанию.Наименование, ""));
	СтруктураЗаполнения.Вставить("ВидОперации", Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд);
	СтруктураЗаполнения.Вставить("ЭтоГруппа", СтруктураЗаполнения.Контрагент.ЭтоГруппа);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЗаполнения = Новый Структура();
	СтруктураЗаполнения.Вставить("Контрагент", ПараметрКоманды);
	ПолучитьПараметрыЗаполнения(СтруктураЗаполнения);
	
	Если СтруктураЗаполнения.ЭтоГруппа Тогда
		ВызватьИсключение НСтр("ru = 'Нельзя выбирать группу контрагентов.'");
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЗаказПокупателя.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", СтруктураЗаполнения));
	
КонецПроцедуры
