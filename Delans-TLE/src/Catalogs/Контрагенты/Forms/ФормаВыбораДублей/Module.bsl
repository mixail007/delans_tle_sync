
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ИНН,КПП");
	ИНН = СокрЛП(ИНН);
	КПП = СокрЛП(КПП);
	
	Если НЕ Параметры.ЭтоЮрЛицо Тогда
		Элементы.ТаблицаДублейКПП.Видимость = Ложь;
	КонецЕсли;
	
	ОбновитьТаблицуДублей();
	
	Заголовок = ?(ЗначениеЗаполнено(КПП),
					НСтр("ru = 'Список дублей по ИНН и КПП'"),
					НСтр("ru = 'Список дублей по ИНН'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Контрагент" Тогда
		ОбновитьТаблицуДублей();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаДублейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.ТаблицаДублей.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПередачи = Новый Структура("Ключ", Элементы.ТаблицаДублей.ТекущиеДанные.Ссылка);
	ПараметрыПередачи.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта",
		ПараметрыПередачи, 
		Элемент,
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДублейПриАктивизацииСтроки(Элемент)
	
	Если НЕ Элементы.ТаблицаДублей.ТекущиеДанные = Неопределено Тогда
		
		ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиСписка", 0.2, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьДокументыПоКонтрагенту(Команда)
	
	Если Элементы.ТаблицаДублей.ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Команда не может быть выполнена для указанного объекта!'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Контрагент", Элементы.ТаблицаДублей.ТекущиеДанные.Ссылка);	
	ПараметрыФормы = Новый Структура(
			"Отбор, КлючНастроек, СформироватьПриОткрытии",
			СтруктураОтбора,
			"Контрагент",
			Истина);
	
	ОткрытьФорму("Обработка.ДокументыПоКонтрагенту.Форма.СписокДокументов",
		ПараметрыФормы,
		ЭтотОбъект,
		,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьТаблицуДублей()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 20
		|	Контрагенты.Ссылка КАК Ссылка,
		|	Контрагенты.Наименование КАК Наименование,
		|	Контрагенты.ИНН КАК ИНН,
		|	Контрагенты.КПП КАК КПП,
		|	ВЫБОР
		|		КОГДА Контрагенты.ПометкаУдаления
		|			ТОГДА 4
		|		КОГДА Контрагенты.Предопределенный
		|			ТОГДА 5
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеЭлементов,
		|	Контрагенты.Код КАК Код,
		|	Контрагенты.Ответственный.Наименование КАК ОтветственныйНаименование
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И Контрагенты.КПП = &КПП
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаДублей.Загрузить(Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьАктивизациюСтрокиСписка()
	
	Если Элементы.ТаблицаДублей.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ОткрытьДокументыПоКонтрагенту.Заголовок = СтрШаблон(НСтр("ru='Документы по контрагенту (%1)'"),
		ПолучитьКоличествоДокументовКонтрагента(Элементы.ТаблицаДублей.ТекущиеДанные.Ссылка)
	);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКоличествоДокументовКонтрагента(Контрагент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументыПоКонтрагенту.Ссылка) КАК КоличествоДокументов
		|ИЗ
		|	КритерийОтбора.ДокументыПоКонтрагенту(&Контрагент) КАК ДокументыПоКонтрагенту";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.КоличествоДокументов;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

&НаКлиенте
Процедура Изменить(Команда)
	
	Если Элементы.ТаблицаДублей.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПередачи = Новый Структура("Ключ", Элементы.ТаблицаДублей.ТекущиеДанные.Ссылка);
	ПараметрыПередачи.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта",
		ПараметрыПередачи,
		Элементы.ТаблицаДублей,
	);

КонецПроцедуры

#КонецОбласти
