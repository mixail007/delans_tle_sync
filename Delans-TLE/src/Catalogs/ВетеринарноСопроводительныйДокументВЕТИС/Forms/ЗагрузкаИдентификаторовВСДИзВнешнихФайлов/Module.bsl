
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИнициализироватьТабличныйДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	Закрыть(Идентификаторы());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьТабличныйДокумент()
	
	Макет = Справочники.ВетеринарноСопроводительныйДокументВЕТИС.ПолучитьМакет("ЗагрузкаИдентификаторовВСДИзВнешнихФайлов");
	
	ТабличныйДокумент.Очистить();
	
	ОбластьТовар = Макет.ПолучитьОбласть("КодыВСД");
	ТабличныйДокумент.Присоединить(ОбластьТовар);
	
	ТабличныйДокумент.ФиксацияСверху = 1;
	
КонецПроцедуры

&НаСервере
Функция Идентификаторы()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Идентификатор", Метаданные.ОпределяемыеТипы.УникальныйИдентификаторВЕТИС.Тип);
	
	КоличествоСтрок = ТабличныйДокумент.ВысотаТаблицы;
	
	Для НомерСтроки = 2 По КоличествоСтрок Цикл
		
		Идентификатор = СокрЛП(ТабличныйДокумент.Область("R" + НомерСтроки + "C2").Текст);
		
		Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(Идентификатор) Тогда
			
			НоваяСтрока = Таблица.Добавить();
			НоваяСтрока.Идентификатор = Идентификатор;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Таблица.ВыгрузитьКолонку("Идентификатор");
	
КонецФункции

#КонецОбласти
