
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды.Количество() = 1
		И ЭтоПредметШаблона(ПараметрКоманды[0])
		И ЕстьДоступныеШаблоны(ПараметрКоманды[0]) Тогда
		
		ШаблоныСообщенийКлиент.СформироватьСообщение(ПараметрКоманды[0], "Письмо",,, Новый Структура("ИмяФормыИсточникаСообщения", ПараметрыВыполненияКоманды.Источник.ИмяФормы));
		
	Иначе
		
		ДополнительныеПараметрыПечати = Неопределено;
		ЗаполнитьДополнительныеПараметрыПечати(ДополнительныеПараметрыПечати, ПараметрыВыполненияКоманды);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИмяФормыОбъектаПечати",	ПараметрыВыполненияКоманды.Источник.ИмяФормы);
		ПараметрыФормы.Вставить("ОбъектыОтправки",			ПараметрКоманды);
		Если ДополнительныеПараметрыПечати <> Неопределено Тогда
			ПараметрыФормы.Вставить("ДополнительныеПараметрыПечати", ДополнительныеПараметрыПечати);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.ВыборПечатныхФормДляОтправки",
			ПараметрыФормы,
			ПараметрыВыполненияКоманды.Источник,
			ПараметрыВыполненияКоманды.Уникальность,
			ПараметрыВыполненияКоманды.Окно,
			ПараметрыВыполненияКоманды.НавигационнаяСсылка
		);
		
	КонецЕсли;
	
	СтатистикаИспользованияФормКлиент.ПроверитьЗаписатьСтатистикуКоманды(
		"ОтправитьПоЭлектроннойПочте",
		ПараметрыВыполненияКоманды.Источник
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДополнительныеПараметрыПечати(ДополнительныеПараметры, ПараметрыВыполненияКоманды)
	
	Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Справочник.ДоговорыКонтрагентов.Форма.ФормаЭлемента" Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ДокументОткрытия", ПараметрыВыполненияКоманды.Источник.ДокументОткрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоПредметШаблона(ПараметрКоманды)
	
	ТипыПредметовШаблонов = Новый Массив;
	ТипыПредметовШаблонов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	ТипыПредметовШаблонов.Добавить(Тип("СправочникСсылка.КонтактныеЛица"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаданиеНаРаботу"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ПриемИПередачаВРемонт"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.Событие"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.СчетНаОплату"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
	
	Возврат ТипыПредметовШаблонов.Найти(ТипЗнч(ПараметрКоманды)) <> Неопределено;
	
КонецФункции

&НаСервере
Функция ЕстьДоступныеШаблоны(ПредметШаблона)
	Возврат ШаблоныСообщенийПереопределяемый.ЕстьДоступныеШаблоны(Истина, ПредметШаблона);
КонецФункции
