
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Пользователь") Тогда
		
		Пользователь = Параметры.Пользователь;
		
		Если ЗначениеЗаполнено(Пользователь) Тогда
			
			ЗаполнитьДерево();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		ОбновитьНастройки();
	КонецЕсли;
	
КонецПроцедуры // ПриЗакрытии() 

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДеревоНастроекПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные = Неопределено ИЛИ Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		
		Отказ = Истина;
		Возврат;
		
	ИначеЕсли Элемент.ТекущиеДанные.Настройка = ПредопределенноеЗначение("ПланВидовХарактеристик.НастройкиПользователей.ОсновноеПодразделение") Тогда
		
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ТипСтруктурнойЕдиницы", ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение")));
		Элементы.ДеревоНастроекЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);;
		
	ИначеЕсли Элемент.ТекущиеДанные.Настройка = ПредопределенноеЗначение("ПланВидовХарактеристик.НастройкиПользователей.ОсновнойСклад") Тогда
		
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Склад"));
		НовыйМассив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Розница"));
		НовыйМассив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.РозницаСуммовойУчет"));
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипСтруктурнойЕдиницы", Новый ФиксированныйМассив(НовыйМассив));
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		Элементы.ДеревоНастроекЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
		
	ИначеЕсли Элемент.ТекущиеДанные.Настройка = ПредопределенноеЗначение("ПланВидовХарактеристик.НастройкиПользователей.НоменклатураДоставки") Тогда
		
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ТипНоменклатуры", ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга")));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ЭтоНабор", Ложь));
		Элементы.ДеревоНастроекЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Процедура обновляет информацию в таблице настроек.
//
&НаСервере
Процедура ЗаполнитьДерево()

	НастройкиЭлементы = ДеревоНастроек.ПолучитьЭлементы();
	НастройкиЭлементы.Очистить();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.Текст=
	"ВЫБРАТЬ
	|	Настройки.Родитель КАК Родитель,
	|	Настройки.Ссылка КАК Настройка,
	|	Настройки.ЭтоГруппа КАК ЭтоГруппа,
	|	НЕ Настройки.ЭтоГруппа КАК НомерКартинки,
	|	ЗначениеНастроек.Значение КАК Значение,
	|	Константы.ИспользоватьНесколькоОрганизаций КАК ИспользоватьНесколькоОрганизаций,
	|	Константы.ФункциональнаяОпцияУчетПоНесколькимСкладам КАК ФункциональнаяОпцияУчетПоНесколькимСкладам,
	|	Константы.ФункциональнаяОпцияУчетПоНесколькимПодразделениям КАК ФункциональнаяОпцияУчетПоНесколькимПодразделениям
	|ИЗ
	|	ПланВидовХарактеристик.НастройкиПользователей КАК Настройки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователей КАК ЗначениеНастроек
	|		ПО (ЗначениеНастроек.Настройка = Настройки.Ссылка)
	|			И (ЗначениеНастроек.Пользователь = &Пользователь),
	|	Константы КАК Константы
	|ГДЕ
	|	НЕ Настройки.ПометкаУдаления
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация)
	|				И НЕ Константы.ИспользоватьНесколькоОрганизаций)
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ОсновнойСклад)
	|				И НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимСкладам)
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ОсновноеПодразделение)
	|				И НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимПодразделениям)
	|	И (Настройки.Родитель <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.НастройкаПодбора)
	|			ИЛИ Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ИспользоватьНовыйМеханизмПодбора))
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ИспользоватьЗарплатаИсполнителейВЗаказНаряде)
	|				И НЕ Константы.ФункциональнаяОпцияИспользоватьПодсистемуЗарплата)
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ИспользоватьЗарплатаИсполнителейВЗаказНаряде)
	|				И НЕ Константы.ФункциональнаяОпцияИспользоватьПодсистемуРаботы)
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ИспользоватьМатериалыВЗаказНаряде)
	|				И НЕ Константы.ФункциональнаяОпцияИспользоватьПодсистемуРаботы)
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ИспользоватьМатериалыЗаказчикаВЗаказНаряде)
	|				И НЕ Константы.ФункциональнаяОпцияИспользоватьПодсистемуРаботы)
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ИспользоватьТоварыВЗаказНаряде)
	|				И НЕ Константы.ФункциональнаяОпцияИспользоватьПодсистемуРаботы)
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ПоложениеВидаРаботВЗаказНаряде)
	|				И НЕ Константы.ФункциональнаяОпцияИспользоватьПодсистемуРаботы)
	|	И НЕ Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.СпособЗагрузкиДанныхИзВнешнихИсточников)
	|	И НЕ Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ЗагрузкаДанныхИзВнешнихИсточников)
	|	И НЕ Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ЗначениеЗаполненияКатегорияНоменклатуры)
	|	И НЕ Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.Биллинг)
	|	И НЕ Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ОпросПоПодсистемеБиллингЗавершен)
	|	И НЕ Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ВыводитьСоветОплатаСертификатомВВалюте)
	|	И НЕ Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ПредложениеИспользованияСистемыВзаимодействияДляТелефонииПоказано)
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ПоложениеИсполнителяВСдельномНаряде)
	|				И НЕ Константы.ФункциональнаяОпцияИспользоватьТехоперации)
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ПоложениеСтруктурнойЕдиницыВСдельномНаряде)
	|				И НЕ Константы.ФункциональнаяОпцияИспользоватьТехоперации)
	|	И НЕ(Настройки.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.НеВыводитьОтчетыПоПродажамИЦенамВКарточкеНоменклатуры))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоГруппа ИЕРАРХИЯ,
	|	Настройки.Наименование";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ДобавитьСтрокиВДерево(Выборка, НастройкиЭлементы);
	
КонецПроцедуры // ЗаполнитьДерево()

//По переданным данным создаем дерево строк на форме
//
// Выборка - выборка запроса с данными в иерархии
// ДеревоЗначени - элементы дерева значения, для которого создаются строки
//
&НаСервере
Функция ДобавитьСтрокиВДерево(Выборка, ДеревоЗначений)
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрокаНастройки = ДеревоЗначений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаНастройки, Выборка);
		НоваяСтрокаНастройки.Значение = Выборка.Настройка.ТипЗначения.ПривестиЗначение(Выборка.Значение);
		
		СтрокиВыборки = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Если СтрокиВыборки.Количество() > 0 Тогда
			
			ДобавитьСтрокиВДерево(СтрокиВыборки, НоваяСтрокаНастройки.ПолучитьЭлементы());
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

// Процедура выполняет запись значений настроек в регистр сведений.
//
&НаСервере
Процедура ОбновитьНастройки()
	
	НаборЗаписей = РегистрыСведений.НастройкиПользователей.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Пользователь.Использование = Истина;
	НаборЗаписей.Отбор.Пользователь.Значение      = Пользователь;
	
	НастройкиГруппы = ДеревоНастроек.ПолучитьЭлементы();
	Для Каждого ГруппаНастроек Из НастройкиГруппы Цикл
		
		НастройкиЭлементы = ГруппаНастроек.ПолучитьЭлементы();
		
		Для Каждого СтрокаНастроек Из НастройкиЭлементы Цикл
			
			Запись = НаборЗаписей.Добавить();
			
			Запись.Пользователь = Пользователь;
			Запись.Настройка    = СтрокаНастроек.Настройка;
			Запись.Значение     = СтрокаНастроек.Настройка.ТипЗначения.ПривестиЗначение(СтрокаНастроек.Значение);
			
		КонецЦикла;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры // ОбновитьНастройки()

#КонецОбласти

