
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.ТорговоеПредложение.Пустая() Тогда
		
		ТекущаяЗапись = РегистрыСведений.СостоянияСинхронизацииТорговыеПредложения.СоздатьМенеджерЗаписи();
		ТекущаяЗапись.ТорговоеПредложение = Параметры.ТорговоеПредложение;
		СвойстваПредложения = Новый Структура("Организация, Валюта");
		ТорговыеПредложенияПереопределяемый.ПолучитьСвойстваТорговогоПредложения(Параметры.ТорговоеПредложение,
			СвойстваПредложения);
		ТекущаяЗапись.Организация = СвойстваПредложения.Организация;
		
		ТекущаяЗапись.Прочитать();
		
		Если ТекущаяЗапись.Выбран() Тогда
			ЗначениеВРеквизитФормы(ТекущаяЗапись, "Запись");
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьВидимостьДоступность(Запись, Элементы);
	
	// Установка видимости публикации остатков в прикладном решении.
	ТорговыеПредложенияПереопределяемый.ВыгружатьОтносительныеОстаткиТоваров(Элементы.ПубликоватьОстатки.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.УведомлятьОЗаказах Тогда
		Если ПустаяСтрока(Запись.АдресЭлектроннойПочты) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Введите адрес электронной почты'"), , "АдресЭлектроннойПочты", , Отказ);
		ИначеЕсли Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Запись.АдресЭлектроннойПочты, Истина) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Адрес электронной почты введен неверно'"), , "АдресЭлектроннойПочты", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Запись.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияСинхронизацииТорговыеПредложения.ТребуетсяСинхронизация");
	
	ЗначенияЗаписи = Новый Структура;
	ЗначенияЗаписи.Вставить("ТорговоеПредложение",      Запись.ТорговоеПредложение);
	ЗначенияЗаписи.Вставить("Организация",              Запись.Организация);
	ЗначенияЗаписи.Вставить("АдресЭлектроннойПочты",    Запись.АдресЭлектроннойПочты);
	ЗначенияЗаписи.Вставить("УведомлятьОЗаказах",       Запись.УведомлятьОЗаказах);
	ЗначенияЗаписи.Вставить("ПубликоватьОстатки",       Запись.ПубликоватьОстатки);
	ЗначенияЗаписи.Вставить("ПубликоватьСрокиПоставки", Запись.ПубликоватьСрокиПоставки);
	ЗначенияЗаписи.Вставить("ПубликоватьЦены",          Запись.ПубликоватьЦены);
	ЗначенияЗаписи.Вставить("ДополнительноеОписание",   Запись.ДополнительноеОписание);
	Оповестить("ТорговыеПредложения_ЗаписьДополнительныеНастройки", ЗначенияЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОповеститьОбИзменении(Запись.ТорговоеПредложение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УведомлятьОЗаказахПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность(Запись, Элементы)
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(Запись, Элементы)
	
	Элементы.АдресЭлектроннойПочты.Доступность = Запись.УведомлятьОЗаказах;
	Элементы.АдресЭлектроннойПочты.АвтоОтметкаНезаполненного = Элементы.АдресЭлектроннойПочты.Доступность;
	
КонецПроцедуры

#КонецОбласти
